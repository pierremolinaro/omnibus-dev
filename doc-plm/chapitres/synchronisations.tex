 %!TEX encoding = UTF-8 Unicode
%!TEX root = ../doc-plm.tex





\chapterLabel{Synchronisation et communication}{chapitreSynchros}

PLM ne définit pas d'outils de synchronisation particulier, mais propose des briques permettant de les construire. Ces briques sont des types opaques et des fonctions.

Ce chapitre présente~:
\begin{itemize}
  \item les types et fonctions constituant ces briques élémentaires (\refSection{typeFonctionsBriqueSynchro})~;
  \item comment écrire un pilote de gestion du temps sur un processeur Cortex-M4 (\refSectionPage{piloteTimeCortex})~;
  \item l'implémentation du sémaphore de Dijkstra (\refSectionPage{semaphoreDijskstra})~;
  \item l'implémentation d'un rendez-vous (\refSectionPage{structureRendezVous})~;
  \item l'implémentation d'un chronomètre permettant d'exprimer simplement une exécution périodique (\refSectionPage{chronoPeriodique})~;
  \item l'implémentation d'une porte de synchronization (\refSectionPage{synchronizationGate}).
\end{itemize}









\sectionLabel{Types et fonctions prédéfinis}{typeFonctionsBriqueSynchro}


PLM définit deux types opaques et neuf fonctions externes\footnote{Externes à PLM, car écrites en C.} qui permettent d'écrire outils de synchronisation et gardes.

\subsection{Type opaque \texttt{\$taskList}}

Le type opaque \plm=$taskList= est utilisé pour maintenir la liste des tâches bloquées sur un outil de synchronisation.


\subsection{Type opaque \texttt{\$guardList}}

Le type opaque \plm=$guardList= est utilisé pour maintenir la liste des tâches qui ont invoqué une garde d'un outil de synchronisation.



\subsection{Fonction \texttt{block(?!inList{}:)}}

\begin{PLM}
func primitive block (?!inList:ioWaitingList $taskList)
\end{PLM}

Cette fonction bloque la tâche en cours dans la liste \plm=ioWaitingList= passée en argument. Elle est appelable en mode \plm=primitive=.




\subsection{Fonction \texttt{block(?onDeadline{}:)}}

\begin{PLM}
func primitive block (?onDeadline:deadline $uint32) 
\end{PLM}

Cette fonction bloque la tâche en cours jusqu'à la date \plm=deadline=. Elle est appelable en mode \plm=primitive=.







\subsection{Fonction \texttt{block(?!inList{}:?onDeadline{}:)}}

\begin{PLM}
func primitive block (?!inList:waitingList $taskList 
                      ?onDeadline:deadline $uint32
                      !result: outResult $bool) 
\end{PLM}

Cette fonction bloque la tâche en cours dans la liste \plm=waitingList= jusqu'à la date \plm=deadline=. Elle est appelable en mode \plm=primitive=. L'argument de retour \plm=outResult= vaut \plm=yes= si la tâche a été rendue prête via la liste \plm=waitingList=, et \plm=no= si si elle a été rendue prête parce que son échéance \plm=deadline= a été atteinte.








\subsection{Fonction \texttt{makeTaskReady(?!fromList{}:)}}

\begin{PLM}
func service makeTaskReady (?!fromList:ioWaitingList $taskList
                            !found: outFound $bool)
\end{PLM}

Si la liste \plm=waitingList= est vide, la fonction renvoie \plm=outFound= à \plm=no=. Si la liste n'est pas vide, cette fonction retire de la liste la tâche de plus forte priorité, la rend prête, et la fonction renvoie \plm=outFound= à \plm=yes=.

Cette fonction est appelable en mode \plm=primitive=, c'est-à-dire par les primitives, et en mode \plm=service=, c'est-à-dire par les routines d'interruption.










\subsection{Fonction \texttt{makeTasksReady(?fromCurrentDate{}:)}}

\begin{PLM}
func service makeTasksReady (?fromCurrentDate:inCurrentDate $uint32)
\end{PLM}

Cette fonction rend prête toutes les tâches en attente d'échéance dont l'échéance est atteinte.

Cette fonction est appelable mode \plm=service=, c'est-à-dire par les routines d'interruption.






\subsection{Fonction \texttt{handleGuardedCommand}}

\begin{PLM}
func guard handle (?!guard:ioGuard $guardList)
\end{PLM}

Cette fonction enregistre la tâche courante dans la liste \plm=ioGuard=. Cette fonction est appelable mode \plm=guard=, c'est-à-dire par les gardes.








\subsection{Fonction \texttt{handle (?guardedDeadline{}:)}}

\begin{PLM}
func guard handle (?guardedDeadline:deadline $uint32)
\end{PLM}

Cette fonction enregistre la tâche courante pour une attente en garde jusqu'à la date \plm=deadline=. Cette fonction est appelable mode \plm=guard=, c'est-à-dire par les gardes.










\subsection{Fonction \texttt{notifyChange}}

\begin{PLM}
func service notifyChange (?!forGuard:ioGuard $guardList)
\end{PLM}

Cette fonction signifie aux tâches enregistrées dans \plm=ioGuard= que les gardes doivent être re-évaluées. Cette fonction est appelable en mode \plm=primitive= et en mode \plm=service=.











\subsection{Fonction \texttt{notifyChangeForGuardedWaitUntil(?withCurrentDate{}:)}}

\begin{PLM}
func service
notifyChangeForGuardedWaitUntil (?withCurrentDate:inCurrentDate $uint32)
\end{PLM}

Cette fonction signifie aux tâches en attente d'échéance en garde que l'instant \plm=inCurrentDate= est atteint, et qu'elles doivent re-évaluer leur gardes. Cette fonction est appelable en mode \plm=service= (et aussi en mode \plm=primitive=, bien qu'en pratique c'est la routine d'interruption périodique qui l'appelle).



\sectionLabel{Le pilote \texttt{time} pour un Cortex-M4}{piloteTimeCortex}

Le pilote \plm=time= décrit ici permet une gestion élémentaire du temps, c'est-à-dire :
\begin{itemize}
  \item maintenir une date courante, incrémentée sur interruption~;
  \item une fonction permettant d'acquérir la date courante~;
  \item une primitive d'attente d'échéance de la date courante~;
  \item la définition d'une garde exprimant l'attente d'échéance en garde.
\end{itemize}

Les Cortex-M4 implémentent le timer \emph{Systick} qui permet de programmer très simplement des interruptions périodiques. La période choisie est $1~ms$. Le comptage du temps est initialisé à $0$ au démarrage du micro-contrôleur. Il est mémorisé dans une variable de type \plm=$uint32=, ce qui signifie qu'il retombe à $0$ au bout de $2^{32}~ms$, c'est-à-dire un peu de $49$ jours\footnote{Il n'y a pas d'impossiblité technique de passer à une variable plus large, par exemple un entier 64 bits. Il faut uniquement que les routines concernées de l'exécutif, écrites en C, prennent en charge ce type. Ces fonctions sont~: \texttt{makeTasksReady(?fromCurrentDate{}:)}, \texttt{handle (?guardedDeadline{}:)} et \texttt{notifyChangeForGuardedWaitUntil(?withCurrentDate{}:)}.}.



\subsection{En-tête}

L'en-tête du pilote \plm=time= déclare la variable privée \plm=mUptime= qui est utilisée pour détenir la date courante.

\begin{PLM}
driver time {
  var mUptime $uint32 = 0
\end{PLM}



\subsection{Initialisation}

La routine \plm=init 0= est exécutée durant la phase d'initialisation, c'est-à-dire après les routines de \plm=boot= et l'initialisation des variables globales (\refSectionPage{sequenceDemarrage}). Elle programme simplement le timer \emph{SysTick} de façon qu'il engendre une interruption toutes les millisecondes. La constante \plm=F_CPU_MHZ= contient la fréquence du processeur, exprimée en nombre de MHz : l'horloge du processeur est aussi l'horloge du timer \emph{SysTick}. Il faut noter que les interruptions sont toujours masquées durant la phase d'initialisation, aussi, bien que le timer tourne, la première interruption sera prise en compte au moment du démarrage des tâches.

\begin{PLM}
init 0 { // Configure Systick interrupt every ms
  SYST_RVR = F_CPU_MHZ * 1000 - 1 // Interrupt every ms
  SYST_CVR = 0
  SYST_CSR = {SYST_CSR !CLKSOURCE:1 !ENABLE:1 !TICKINT:1}
}
\end{PLM}


\subsection{La routine d'interruption}

La routine d'interruption est \plm=systick=. Son nom est défini par le fichier de configuration de la cible (\refSubsectionPage{configurationInterrupts}). Le mot réservé \plm=service= dans son en-tête signifie qu'elle peut appeler les fonctions déclarées dans ce mode. L'appel de la fonction \plm=noteCurrentTaskFreeStackSize= permet de maintenir une information de remplissable de la tâche en cours d'exécution au moment où l'interruption est survenue. Ensuite, la variable \plm=self.mUptime= est incrémentée. Noter l'utilisation de l'opérateur \plm=+%=, qui réalise une incrémentation sans détection de débordement. La fonction \plm=makeTasksReady (!fromCurrentDate:)= libère toutes les tâches bloquées en attente d'échéance, dont l'échéance est atteinte (c'est-à-dire dont l'échéance est inférieure ou égale à la valeur de \plm=now=). La fonction  \plm=notifyChangeForGuardedWaitUntil (!withCurrentDate:)= fait de même pour les tâches en attente d'échéance en garde.

\begin{PLM}
isr service systick {
  noteCurrentTaskFreeStackSize ()
  let now = self.mUptime +% 1
  self.mUptime = now
  makeTasksReady (!fromCurrentDate:now)
  notifyChangeForGuardedWaitUntil (!withCurrentDate:now)
}
\end{PLM}




\subsection{Obtenir la date courante}

L'appel système \plm=now= retourne la date courante.

\begin{PLM}
public system safe now @noUnusedWarning () -> $uint32 {
  result = self.mUptime
}
\end{PLM}



\subsection{Attente d'échéance}

Cette primitive système exprime l'attente d'échéance~:
\begin{itemize}
  \item \plm=public= signifie qu'elle peut être appelée en dehors du pilote~;
  \item \plm=system= qu'elle est appelable à partir d'une tâche, et s'exécute interruptions masquées via un \emph{system call}~;
  \item \plm=primitive= que la tâche appelante peut être bloquée~;
  \item \plm=@noUnusedWarning= qu'aucune alerte ne sera engendrée par la compilation si cette fonction n'est pas appelée.
\end{itemize}

Si la date fournie par la valeur \plm=inDate= est postérieure à la date courante, la tâche appelante est bloquée.

\begin{PLM}
public system primitive
wait @noUnusedWarning (?untilDeadline: inDate $uint32) {
  if inDate > self.mUptime {
    block (!onDeadline:inDate)
  }
}
\end{PLM}

L'appel de la primitive a pour syntaxe~:
\begin{PLM}
var échéance $uint32 = ...
time.wait (!untilDeadline:échéance)
\end{PLM}




\subsection{Attente de délai}

L'implémentation de l'attente de délai est similaire à l'attente d'échéance~: le délai est ajouté à la date courante pour former une date absolue.

\begin{PLM}
public system primitive
wait @noUnusedWarning (?duringDelay: inDelay $uint32) {
  if inDelay > 0 {
    block (!onDeadline:self.mUptime +% inDelay)
  }
}
\end{PLM}


\subsection{Commande gardée d'attente d'échéance}

L'implémentation d'une commande gardée prédéfinit la variable booléenne \plm=accept=. Celle-ci doit être valuée par la liste d'instructions, et à l'issue de l'exécution de celle-ci~:
\begin{itemize}
  \item si la valeur de \plm=accept= est \plm=yes=, la condition d'attente est considérée comme satisfaite (c'est-à-dire ici, que la date d'échéance \plm=deadline= est postérieure à la date courante)~;
  \item si la valeur de \plm=accept= est \plm=no=, la condition d'attente est considérée comme non satisfaite (c'est-à-dire ici, que la date d'échéance \plm=deadline= est antérieure à la date courante), et que la tâche appelante a été enregistrée par un appel à la fonction \plm=handle (!guardedDeadline:)=. 
\end{itemize}

\begin{PLM}
public guard wait @noUnusedWarning (?untilDeadline:deadline $uint32) {
  noteCurrentTaskFreeStackSize ()
  accept = (deadline) ≤ self.mUptime
  if not accept {
    handle (!guardedDeadline:deadline)
  }
}
\end{PLM}

\subsection{Attente en mode \texttt{init}}

Le pilote \plm=time= est configuré par la routine \plm=init= de priorité $0$, elle est donc la première à s'exécuter dans ce mode. Les autres routines \plm=init= s'exécutent donc ensuite, dans le pilote \plm=time= est configuré. Dans le mode \plm=init=, l'exécutif n'est pas démarré, on ne peut pas appeler les routines d'attente d'échéance et de délai, mais uniquement exécuter que des attentes actives.

Les fonctions suivantes réalisent des attentes actives, et, comme elles sont déclarées dans le mode \plm=init=, elles ne peuvent pas être appelées par les tâches.

\begin{PLM}
public func init oneMillisecondBusyWait @noUnusedWarning () {
  while not SYST_CSR.COUNTFLAG.bool {}
}

public func panic panicOneMillisecondBusyWait @noUnusedWarning () {
  while not SYST_CSR.COUNTFLAG.bool {}
}

public func init busyWaitingDuringMS @noUnusedWarning (?inDelay $uint32) {
  for _ $uint32 in 0 ..< inDelay {
    self.oneMillisecondBusyWait ()
  }
}
\end{PLM}



\subsection{Attente en mode \texttt{panic}}

En mode \plm=panic=, l'exécutif n'est plus actif, on ne peut qu'exécuter des attentes actives.

\begin{PLM}
public func panic
panicBusyWaitingDuringMS @noUnusedWarning (?inDelay $uint32) {
  for _ $uint32 in 0 ..< inDelay {
    self.panicOneMillisecondBusyWait ()
  }
}
\end{PLM}













\sectionLabel{Sémaphore de Dijkstra}{semaphoreDijskstra}

Le sémaphore de Dijkstra est implémenté comme une structure et la synchronisation est réalisée en appelant les fonctions de l'exécutif.


\subsection{Version sans primitive d'attente en garde}


\begin{PLM}
struct $semaphore {
  var value $uint32
  var list = $taskList ()
  
  public system service signal @noUnusedWarning @mutating () {
    makeTaskReady (!?fromList:self.list ?found:let found)
    if not found {
      self.value += 1
    }
  }

  public system primitive wait @noUnusedWarning @mutating () {
    if self.value > 0 {
      self.value -= 1
    }else{
      block (!?inList:self.list)
    }
  }
}
\end{PLM}

Un sémaphore est constitué de deux propriétés~:
\begin{itemize}
  \item \plm=value=, sa valeur qui est un entier positif ou nul~;
  \item \plm=list=, la liste des tâches bloquées sur ce sémaphore.
\end{itemize}


{\bf Instanciation.} L'instanciation d'un sémaphore doit fournir sa valeur initiale~; par exemple~:
\begin{PLM}
var s = $semaphore (!value:1)
\end{PLM}

{\bf Routine \texttt{signal}.} Elle est déclarée en mode \plm=service=, c'est-à-dire qu'elle peut être appelée à partir des tâches, et des routines d'interruption. Elle appelle la routine \plm=makeTaskReady(!?fromList:?found:)= qui fait l'opération suivante :
\begin{itemize}
  \item si la liste \plm=self.list= est vide, alors le booléen \plm=found= est retourné à \plm=no=~;
  \item dans le cas contraire, la tâche la plus prioritaire est retirée de la liste \plm=self.list= et est rendue \emph{prête}, et le booléen \plm=found= est retourné à \plm=yes=.
\end{itemize}

Ensuite, la valeur du sémaphore est incrémentée si la liste \plm=self.list= était initialement vide.


{\bf Routine \texttt{wait}.} Elle est déclarée en mode \plm=primitive=, c'est-à-dire seules les tâches peuvent l'appeler. Elle commence par tester la valeur du sémaphore~; si il est strictement positif, il est décrémenté, et aucun blocage n'a lieu. Si il est nul, la tâche appelante est bloquée dans la liste \plm=self.list= par l'appel de \plm=block(!?inList:)=.


\subsection{Ajout de l'attente jusqu'à une échéance}

\begin{PLM}
public system primitive wait
@noUnusedWarning @mutating (?untilDeadline: deadline $uint32
                            !result: outResult $bool) {
  outResult = self.value > 0
  if outResult {
    self.value -= 1
  }else if deadline > time.now () { 
    block (!?inList:self.list !onDeadline:deadline !?result:outResult)
  }
}
\end{PLM}

Cette primitive permet d'attendre sur un sémaphore jusqu'à ce qu'une échéance soit atteinte. Son appel renvoie dans \plm=outResult=~:
\begin{itemize}
  \item \plm=yes= si le sémaphore a permis le passage~;
  \item \plm=no= si l'échéance a été atteinte.
\end{itemize}

L'exécution de la primitive teste successivement~:
\begin{itemize}
  \item la valeur du sémaphore~; s'il est strictement positif, il est décrémenté et la primitive retourne immédiatement avec \plm=outResult= à \plm=yes=~;
  \item la valeur de l'argument \plm=deadline=~; si l'échéance est atteinte, la primitive retourne immédiatement avec \plm=outResult= à \plm=no=.
\end{itemize}

Si la valeur du sémaphore est nulle et que l'échéance n'est pas atteinte, la tâche est bloquée par l'appel à \plm=block(!?inList:!onDeadline:!?result:)=.

{\bf Attention.} L'appel à \plm=block(!?inList:!onDeadline:!?result:)= ne modifie pas la valeur de l'argument \plm=outResult=~: seule son adresse est notée, de façon à pouvoir lui affecter ultérieurement la valeur \plm=yes= quand le déblocage intervient via le sémaphore, ou la valeur \plm=no= quand l'échéance est atteinte. La variable \plm=outResult= {\bf doit} être un alias d'une variable de la tâche, et non pas une variable locale de la primitive.




\subsection{Version avec primitive d'attente en garde}

Pour implémenter une primitive d'attente en garde, deux modifications de l'existant sont nécessaires~:
\begin{itemize}
  \item ajouter une propriété \plm=guardList= qui va détenir l'état du sémaphore par rapport aux commandes gardées~;
  \item modifier la primitive \plm=signal()= de façon à signaler aux commandes gardées l'évolution de la valeur du sémaphore.
\end{itemize}

La déclaration des propriétés des sémaphores devient donc~:

\begin{PLM}
struct $semaphore {
  var value $uint32
  var list = $taskList ()
  var guardList = $guardList ()
  ...
}
\end{PLM}

Et la primitive \plm=signal()= appelle \plm=notifyChange(!?forGuard:)= si le sémaphore est incrémenté~:

\begin{PLM}
  public system service signal @noUnusedWarning @mutating () {
    makeTaskReady (!?fromList:self.list ?found:let found)
    if not found {
      self.value += 1
      notifyChange (!?forGuard:self.guardList)
    }
  }
\end{PLM}

On peut maintenant écrire la garde \plm=wait()=. D'une manière générale, la variable \plm=accept= est implicitement déclarée, de type \plm=$bool=, et doit être valuée par la liste d'instructions, à \plm=yes= si la condition d'acceptation de la garde est statisfaite, et \plm=no= s'il ne l'est pas.

Ici, la condition d'acceptation est que le sémaphore soit strictement positif. Si il l'est, il est décrémenté, sinon, le refus est noté par l'appel de \plm=handle(!?guard:)=.

\begin{PLM}
public guard wait @noUnusedWarning () {
  accept = self.value > 0
  if accept {
    self.value -= 1
  }else{
    handle (!?guard:self.guardList)
  }
}
\end{PLM}


Les gardes ont leur propre espace de nommage. Aussi il n'y a pas d'ambiguïté entre la primitive \plm=wait()= et la garde \plm=wait()=.













\sectionLabel{Rendez-vous}{structureRendezVous}

Comme pour le sémaphore de Dijkstra, nous allons d'abord présenter une implémentation sans les attentes temporisées ni les gardes. Comme il n'y a pas de transmission de données, les noms \plm=input= et \plm=output= sont arbitraires et traduisent seulement la dissymétrie des primitives.

\subsection{Primitives de base}

\begin{PLM}
struct $rendezVous {
  var inputWaitList = $taskList ()
  var outputWaitList = $taskList ()

  public system primitive input @mutating () {
    makeTaskReady (!?fromList:self.outputWaitList ?found:let found)
    if not found {
      block (!?inList:self.inputWaitList)
    }
  }

  public system primitive output @mutating () {
    makeTaskReady (!?fromList:self.inputWaitList ?found:let found)
    if not found {
      block (!?inList:self.outputWaitList)
    }
  }
\end{PLM}

\subsection{Ajout des attentes temporisées}


\begin{PLM}
public system primitive
inputUntil @noUnusedWarning @mutating (?deadline:deadline $uint32
                                       !result: result $bool) {
  makeTaskReady (!?fromList:self.outputWaitList ?found:result)
  if (not result) and (deadline > time.now ()) { 
    block (!?inList:self.inputWaitList !onDeadline:deadline !?result:result)
  }
}

public system primitive
outputUntil @noUnusedWarning @mutating (?deadline:deadline $uint32
                                        !result: result $bool) {
  makeTaskReady (!?fromList:self.inputWaitList ?found:result)
  if (not result) and (deadline > time.now ()) { 
    block (!?inList:self.outputWaitList !onDeadline:deadline !?result:result)
  }
}
\end{PLM}

\subsection{Ajout des gardes}

\begin{PLM}
struct $rendezVous {
  var inputWaitList = $taskList ()
  var outputWaitList = $taskList ()
  var inputGuardList = $guardList ()
  var outputGuardList = $guardList ()

  public system primitive input @mutating () {
    makeTaskReady (!?fromList:self.outputWaitList ?found:let found)
    if not found {
      notifyChange (!?forGuard:self.outputGuardList)
      block (!?inList:self.inputWaitList)
    }
  }

  public system primitive output @mutating () {
    makeTaskReady (!?fromList:self.inputWaitList ?found:let found)
    if not found {
      notifyChange (!?forGuard:self.inputGuardList)
      block (!?inList:self.outputWaitList)
    }
  }

  public system primitive
  inputUntil @noUnusedWarning @mutating (?deadline:deadline $uint32
                                         !result: result $bool) {
    makeTaskReady (!?fromList:self.outputWaitList ?found:result)
    if (not result) and (deadline > time.now ()) { 
      block (!?inList:self.inputWaitList !onDeadline:deadline !?result:result)
    }
  }

  public system primitive
  outputUntil @noUnusedWarning @mutating (?deadline:deadline $uint32
                                          !result: result $bool) {
    makeTaskReady (!?fromList:self.inputWaitList ?found:result)
    if (not result) and (deadline > time.now ()) { 
      block (!?inList:self.outputWaitList !onDeadline:deadline !?result:result)
    }
  }

  public guard input @noUnusedWarning () {
    makeTaskReady (!?fromList:self.outputWaitList ?found:accept)
    if not accept {
      handle (!?guard:self.inputGuardList)
    }
  }

  guard output @noUnusedWarning () {
    makeTaskReady (!?fromList:self.inputWaitList ?found:accept)
    if not accept {
      handle (!?guard:self.outputGuardList)
    }
  }

}
\end{PLM}


\sectionLabel{Chronomètre périodique}{chronoPeriodique}

La structure \plm=$periodicTimer= est un outil de synchronisation qui permet d'exprimer simplement des exécutions périodiques.


\subsection{Implémentation}

La structure \plm=$periodicTimer= définit trois propriétés~:
\begin{itemize}
  \item \plm=deadline=, qui détient la date de la prochaine échéance~;
  \item \plm=period=, la période du chronomètre, qui est une constante fixée lors de l'initialisation~;
  \item \plm=guardList=, pour gérer l'attente en garde.
\end{itemize}

La primitive \plm=wait= teste l'échéance avec la date courante, et effectue le blocage de la tâche appelante si l'échéance n'est pas atteinte. L'échéance est toujours incrémentée de la valeur de la période dans les deux situations -- date courant atteinte ou non.


De même, la garde \plm=wait= teste l'échéance avec la date courante. Si elle est atteinte (\plm=accept= est à \plm=yes=), l'échéance est incrémentée de la valeur de la période~; si elle n'est pas atteinte, l'appel de la fonction \plm=handle(!?guard:)= enregistre la garde.
\begin{PLM}
struct $periodicTimer {
  var deadline $uint32 = 0
  let period $uint32
  var guardList = $guardList ()

  public system primitive wait @noUnusedWarning @mutating () {
    if self.deadline ≤ time.now () {
      block (!onDeadline:self.deadline)
    }
    self.deadline += self.period
  }

  public guard wait @noUnusedWarning () {
    accept = self.deadline ≤ time.now ()
    if accept {
      self.deadline += self.period
    }else{
      handle (!?guard:self.guardList)
    }
  }
  
  public system section deadline @noUnusedWarning () -> $uint32 {
    result = self.deadline
  }
}
\end{PLM}


\subsection{Exemple d'utilisation}

\begin{PLM}
task Tâche priority 1 stackSize 512 {
  var deadline = $periodicTimer (!period:500)
   

  on self.deadline.wait () {
    ...
  }
}
\end{PLM}





\sectionLabel{Porte de synchronisation}{synchronizationGate}

Une \emph{porte de synchronisation} est soit ouverte, soit fermée. Quand le porte est ouverte, la primitive \plm=wait= est non bloquante. Quand elle est fermée, la primitive \plm=wait= est systématiquement bloquante. Ouvrir la porte (service \plm=open=) libère toutes les tâches bloquées. Fermer la porte (service \plm=close=) ne provoque pas d'action sur la liste de tâches bloquées (celle-ci est forcément vide), ni sur la liste des gardes.

\begin{PLM}
struct $synchronizationGate {
  var isOpen $bool
  var taskList = $taskList ()
  var guardList = $guardList ()
  
  public system service close @noUnusedWarning @mutating () {
    self.isOpen = no
  }
  
  public system service open @noUnusedWarning @mutating () {
    if not self.isOpen {
      var continue = yes
      while continue {
        makeTaskReady (!?fromList:self.taskList ?found:continue)
      }
      notifyChange (!?forGuard:self.guardList)
      self.isOpen = yes
    }
  }
  
  public system primitive wait @noUnusedWarning @mutating () {
    if not self.isOpen {
      block (!?inList:self.taskList)
    }
  }
  
  public system primitive wait
  @noUnusedWarning @mutating (?untilDeadline:deadline $uint32
                              !result: result $bool) {
    result = self.isOpen
    if result {
    }else if deadline > time.now () {
      block (!?inList:self.taskList !onDeadline:deadline !?result:result)
    }
  }

  public guard wait @noUnusedWarning () {
    accept = self.isOpen
    if not accept {
      handle (!?guard:self.guardList)
    }
  }
}
\end{PLM}



\sectionLabel{Instruction \texttt{sync}}{instructionSync}



\section{Implémentation des commandes gardées}

\begin{figure}[htbp]
  \centering
  \small
  \begin{tikzpicture}[
      block/.style ={chamfered rectangle, draw=black, fill=green!20, align=center, minimum height=0.75cm, minimum width=4cm},
      node distance=2cm and 4cm
    ]
    \node [block] (horsGarde) at (0, 0) {Hors garde \textbar~$n == 0$} ;
    \node [block] (gardeModifie) [right=of horsGarde] {Garde modifiée \textbar~$n == 0$} ;
    \node [block] (evaluation) [below=of gardeModifie] {En évaluation \textbar~$n > 0$};
    \node [block] (attente) [below=of horsGarde] {En attente \textbar~$n > 0$};

    \draw [-stealth, thick] (-3, 0) to (horsGarde) ;

    \draw [-stealth, thick, orange] (horsGarde) to [out=300,in=135] node [below] {Garde neutre $\blacktriangleright~n := n+1$} (evaluation) ;
    \draw [-stealth, thick, orange] (evaluation) to [loop below] node [below] {Garde neutre $\blacktriangleright~n := n+1$} (evaluation) ;
    \draw [-stealth, thick, orange] (gardeModifie) to [loop above] node [above] {Garde neutre $\blacktriangleright~n := n+1$} (gardeModifie) ;

    \draw [-stealth, thick, OliveGreen] (evaluation) to [out=120,in=315] node [above] {Garde vraie $\blacktriangleright~n := 0$} (horsGarde) ;
    \draw [-stealth, thick, OliveGreen] (horsGarde) to [loop above] node [above] {Garde vraie $\blacktriangleright~n := 0$} (horsGarde) ;
    \draw [-stealth, thick, OliveGreen] (gardeModifie) to [out=175,in=5] node [above] {Garde vraie $\blacktriangleright~n := 0$} (horsGarde) ;

    \draw [-stealth, thick, blue] (evaluation) to node [below] {waitForChange} (attente) ;
    \draw [-stealth, thick, blue] (gardeModifie) to [out=185,in=355] node [below] {waitForChange} (horsGarde) ;

    \draw [-stealth, thick, Maroon] (attente) to node [left] {guardDidChange} (horsGarde) ;
    \draw [-stealth, thick, Maroon] (evaluation) to node [right] {guardDidChange} (gardeModifie) ;
  \end{tikzpicture}
  \caption{Graphe d'état des gardes d'une tâche}
  \labelFigure{commandeGardee}
%  \ligne
\end{figure}

