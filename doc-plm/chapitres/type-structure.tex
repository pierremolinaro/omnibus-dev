%!TEX encoding = UTF-8 Unicode
%!TEX root = ../doc-plm.tex





\chapter{Le type structure}


Le type structure est fondamental en PLM : comme dans bien d'autres langages, il permet de décrire des données structurées. Mais en PLM, une structure permet de décrire les outils de synchronisation tels que le sémaphore, le rendez-vous…

Un type structure a une sémantique de \emph{valeur}, c'est-à-dire qu'une affectation entre instances de structure effectue une copie de l'objet source vers l'objet destination. De plus, il est possible d'interdire la copie d'un type structure, ce qui est indispensable dans le cas où ce type décrit un outil de synchronisation.

Un type structure peut contenir les déclarations de :
\begin{itemize}
\item propriétés, \refSectionTitlePage{proprietesStructure} ;
\item fonctions, \refSectionTitlePage{fonctionsStructure} ;
\item services, \refSectionTitlePage{servicesStructure} ;
\item sections, \refSectionTitlePage{sectionsStructure} ;
\item primitives, \refSectionTitlePage{primitivesStructure} ;
\item gardes, \refSectionTitlePage{gardesStructure}.
\end{itemize}

De plus, des attributs paramètrent le comportement de ses instances.











\section{Déclaration d'un type structure}

La déclaration d'un type structure est introduite par le mot réservé \plm!struct!.

\begin{PLM}
struct $point {
  // Déclaration de propriétés
  // Déclaration de fonctions
  // Déclaration de services
  // Déclaration de sections
  // Déclaration de primitives
  // Déclaration de gardes
}
\end{PLM}

L'ordre des déclarations est sans importance.










\sectionLabel{Déclaration des propriétés}{proprietesStructure}

Un type structure peut définir, zéro, un ou plusieurs propriétés. Tout type est acceptable pour une propriété. 

Les propriétés déclarées soit présente une valeur initiale par défaut, soit sont initialisées par l'initialiseur engendré implicitement. De cette façon, quand un type structure est instancié, toutes ses propriétés sont initialisées. 

\subsection{Propriétés toutes initialisées par défaut}

Voici un exemple qui déclare une structure contenant deux propriétés initialisées par défaut :

\begin{PLM}
struct $point {
  var x $int32 = 0
  var y $int32 = 0
}
\end{PLM}

Un type structure dont toutes les propriétés sont initialisées par défaut présente un initialiseur sans argument. On pourra donc écrire :
\begin{PLM}
var pt $point = $point ()
\end{PLM}

Et l'annotation de type peut être omise :
\begin{PLM}
var pt = $point ()
\end{PLM}

\subsection{Propriétés non initialisées}

Pour chaque propriété non initialisée par défaut, l'initialiseur synthétisé présente un argument. Par exemple :

\begin{PLM}
struct $point {
  var x $int32
  var y $int32 = 0
}
\end{PLM}

La propriété \plm!x! n'étant pas initialisée par défaut, l'initialiseur synthétisé présente un argument dont la sélecteur porte le nom de la propriété non initialisée :
\begin{PLM}
var pt = $point (!x:4)
\end{PLM}

Si plusieurs propriétés ne sont pas initialisées par défaut, l'initialiseur synthétisé présente des arguments dans l'ordre de déclaration des propriétés non initialisées. Par exemple :

\begin{PLM}
struct $point {
  var x $int32
  var y $int32
}
\end{PLM}

Alors :
\begin{PLM}
var pt = $point (!x:4 !y:8)
\end{PLM}

\subsection{Propriété d'un type structure}

Tout type acceptable pour une propriété. Par exemple, le type \plm!$point3! a une propriété de type \plm!$point! :
\begin{PLM}
struct $point {
  var x $int32 = 0
  var y $int32 = 0
}

struct $point3 {
  var p $point = $point ()
  var z $int32 = 0
}
\end{PLM}

Toutes les propriétés du type \plm!$point3! étant initialisées par défaut, l'initialiseur est \plm!$point3 ()!. Si des propriétés ne ne sont pas initialisées par défaut, l'initialiseur synthétisé comporte des arguments en conséquences. Par exemple :

\begin{PLM}
struct $point {
  var x $int32
  var y $int32
}

struct $point3 {
  var p $point
  var z $int32
}
\end{PLM}

L'initialisation s'effectue par : 
\begin{PLM}
var pt3 = $point3 (!p:$point (!x:4 !y:8) !z:6)
\end{PLM}












\sectionLabel{Fonctions}{fonctionsStructure}

Les fonctions définies dans une structure sont particulières dans le sens où deux attributs permettent de contrôler l'accès aux propriétés :
\begin{itemize}
\item l'attribut \plm!@userAccess! autorise la fonction à accéder aux propriétés en mode \plm!user! ;
\item l'attribut \plm!@mutating! autorise la fonction à modifier \emph{directement} ou \emph{indirectement} les propriétés.
\end{itemize}

L'attribut \plm!@userAccess! peut paraître surprenant, mais il permet de garantir l'absence d'accès en parallèle aux propriétés par plusieurs tâches : la fonction d'une variable globale de type structure ne peut pas être appelée en mode \plm!user! si la fonction a été définie avec l'attribut  \plm!@userAccess!.

\subsection{Attribut \texttt{@userAccess}}\index{"@userAccess}

L'attribut \plm!@userAccess! autorise la fonction à lire \emph{directement} les propriétés en mode \plm!user!. Par \emph{directement}, on entend que les instructions de la fonction nomment les propriétés (notation \plm=self.=), ou appellent des fonctions qui déclarent l'attribut \plm!@userAccess!.

Ainsi, l'attribut \plm!@userAccess! est indispensable pour lire les propriétés en mode \plm!user!. Par contre, si la fonction ne s'exécute pas en mode \plm!user!, cet attribut est inutile. Il est aussi inutile si la fonction appelle des sections qui lisent des propriétés.

Si l'on veut l'accès en écriture, ajouter l'attribut \plm=@mutating= est indispensable. Comme les services, primitives et gardes ont par défaut un accès en écriture aux propriétés, cet attribut est indispensable pour que leur appel soit valide. 

Voici un code d'exemple qui illustre les différentes possibilités :
\begin{PLM}
struct $exemple {
  var x $int32
  
  func user getNok () -> $int32 {
    result = self.x // Erreur : @userAccess est nécessaire
  }
  
  func user getOk @userAccess () -> $int32 {
    result = self.x // Ok : @userAccess autorise l'accès
  }
  
  func user getNok2  () -> $int32 {
    result = self.getOk () // Erreur : @userAccess est nécessaire
  }
  
  func user getOk2 @userAccess () -> $int32 {
    result = self.getOk () // Ok : @userAccess autorise l'accès
  }
  
  func section sGet () -> $int32 {
    result = self.x // Ok : @userAccess est inutile
  }
  
  section sectionGet () -> $int32 {
    result = self.x
  }

  func user getOk3 () -> $int32 {
    result = self.sectionGet ()
  }
}
\end{PLM}

Dans le code d'exemple ci-dessus :
\begin{itemize}
  \item la fonction \plm=getNok= s'exécute en mode \plm=user=, ne nomme pas l'attribut \plm=@userAccess=, donc son instruction qui nomme une propriété déclenche une erreur de compilation ;
  \item la fonction \plm=getOk= s'exécute en mode \plm=user=, nomme l'attribut \plm=@userAccess=, donc son instruction qui nomme une propriété est acceptée par le compilateur ;
  \item la fonction \plm=getNok2= s'exécute en mode \plm=user=, ne nomme pas l'attribut \plm=@userAccess=, donc son instruction qui nomme une fonction déclarée avec cet attribut déclenche une erreur de compilation ;
  \item la fonction \plm=getOk2= s'exécute en mode \plm=user=, nomme l'attribut \plm=@userAccess=, donc son instruction qui nomme une fonction déclarée avec cet attribut est acceptée par le compilateur ;
  \item la fonction \plm=sGet= s'exécute en mode \plm=section=, l'attribut \plm=@userAccess= est inutile pour que son instruction qui nomme une propriété soit acceptée par le compilateur ;
  \item la section \plm=sectionGet= s'exécute en mode \plm=section=, son instruction qui nomme une propriété est acceptée par le compilateur ;
  \item la fonction \plm=getOk3= s'exécute en mode \plm=user=, l'attribut \plm=@userAccess= est inutile pour que l'appel à une section soit acceptée par le compilateur.
\end{itemize}



\subsection{Attribut \texttt{@mutating}}\index{"@mutating}

L'attribut \plm=@mutating= autorise une fonction de structure à modifier \emph{directement} les propriétés de la structure, du moment que l'accès ait été autorisé avec \plm=@userAccess=.

Voici un code d'exemple qui reprend le code d'exemple de la section précédente et illustre les différentes possibilités :

\begin{PLM}
struct $exemple {
  var x $int32
  
  func user setNok @userAccess (?inX $int32) {
    self.x = inX // Erreur : @mutating est nécessaire
  }
  
  func user setOk @userAccess @mutating (?inX $int32) {
    self.x = inX // Ok : @mutating autorise l'écriture
  }
  
  func user setNok2 @userAccess (?inX $int32) {
    self.setOk (!inX) // Erreur : @mutating est nécessaire
  }
  
  func user setOk2 @userAccess @mutating (?inX $int32) {
    self.setOk (!inX) // Ok : @mutating autorise l'accès
  }
  
  section sectionSet @mutating (?inX $int32) {
    self.x = inX
  }

  func user setOk3 @userAccess @mutating (?inX $int32) {
    self.sectionSet (!inX)
  }

}
\end{PLM}

Dans le code d'exemple ci-dessus :
\begin{itemize}
  \item la fonction \plm=setNok= ne nomme pas l'attribut \plm=@mutating=, donc son instruction qui écrit une propriété déclenche une erreur de compilation ;
  \item la fonction \plm=setOk= nomme l'attribut \plm=@mutating=, donc son instruction qui écrit une propriété est acceptée par le compilateur ;
  \item la fonction \plm=setNok2= ne nomme pas l'attribut \plm=@mutating=, donc son instruction qui écrit une fonction déclarée avec l'attribut \plm=@mutating= déclenche une erreur de compilation ;
  \item la fonction \plm=setOk2= nomme l'attribut \plm=@mutating=, donc son instruction qui nomme une fonction déclarée avec l'attribut \plm=@mutating= est acceptée par le compilateur ;
  \item la section \plm=sectionGet= nomme l'attribut \plm=@mutating=, son instruction qui écrit une propriété est acceptée par le compilateur ;
  \item la fonction \plm=setOk3= s'exécute en mode \plm=user=, l'attribut \plm=@mutating= est nécessaire pour que l'appel à une section déclarée avec l'attribut \plm=@mutating= soit acceptée par le compilateur.
\end{itemize}












\sectionLabel{Sections}{sectionsStructure}


Une structure peut définir des \emph{sections}. Une section s'exécute en mode \plm=section=, c'est-à-dire interruptions matérielles masquées ; aucune primitive de l'exécutif n'est accessible dans ce mode. On peut donc utiliser une section pour accéder en exclusion mutuelle aux propriétés de la structure.

Par défaut, une section définie dans une structure a accès aux propriétés en lecture ; pour pouvoir accéder en écriture, il faut ajouter l'attribut \plm=@mutating=\index{"@mutating}.

\begin{PLM}
struct $exemple {
  var x $int32
  
  section sectionGet () -> $int32 {
    result = self.x // Ok, accès en lecture par défaut
  }

  section sectionSet @mutating (?inX $int32) {
    self.x = inX // Ok, @mutating  autorise l'accès en écriture
  }

}
\end{PLM}

Par défaut, une section définie dans une structure est privée, c'est-à-dire appelable uniquement par les autres routines (fonctions, sections, services, primitives, gardes) de la structure. Pour la rendre publique, il faut la déclarer avec le mot réservé \plm=public=\index{public} :


\begin{PLM}
struct $exemple {
  var x $int32
  
  public section sectionGet () -> $int32 {
    result = self.x // Ok, accès en lecture par défaut
  }

  public section sectionSet @mutating (?inX $int32) {
    self.x = inX // Ok, @mutating  autorise l'accès en écriture
  }

}
\end{PLM}







\sectionLabel{Services}{servicesStructure}

Une structure peut définir des \emph{services}. Un service s'exécute en mode \plm=service=\index{service}, c'est-à-dire interruptions matérielles masquées ; seules les fonctions de l'exécutif pouvant libérer des tâches sont accessibles dans ce mode, qui est celui des routines d'interruption. Un exemple de service est la primitive \plm=V= du sémaphore de Dijkstra (\refSectionPage{semaphore}).

Par défaut, un service défini dans une structure a accès aux propriétés en lecture et en écriture (l'attribut \plm=@mutating= est inutile).

Par défaut, un service défini dans une structure est privé. Pour le rendre publique, il faut le déclarer avec le mot réservé \plm=public=\index{public}.









\sectionLabel{Primitives}{primitivesStructure}

Une structure peut définir des \emph{primitives}. Une primitive s'exécute en mode \plm=primitive=\index{primitive}, c'est-à-dire interruptions matérielles masquées ; les fonctions de l'exécutif pouvant bloquer ou libérer des tâches sont accessibles dans ce mode. Un exemple de service est la primitive \plm=P= du sémaphore de Dijkstra (\refSectionPage{semaphore}).

Par défaut, une primitive définie dans une structure a accès aux propriétés en lecture et en écriture (l'attribut \plm=@mutating= est inutile).

Par défaut, une primitive définie dans une structure est privée. Pour la rendre publique, il faut la déclarer avec le mot réservé \plm=public=\index{public}.









\sectionLabel{Gardes}{gardesStructure}

Une structure peut définir des \emph{gardes}. Une garde permet d'exprimer une attente élémentaire dans une instruction \plm=sync= (\refSectionPage{instructionSync}). Un exemple est la garde \plm=P= du sémaphore de Dijkstra (\refSectionPage{semaphore}).

Par défaut, une garde définie dans une structure a accès aux propriétés en lecture et en écriture (l'attribut \plm=@mutating= est inutile).

Par défaut, une garde définie dans une structure est privée. Pour la rendre publique, il faut la déclarer avec le mot réservé \plm=public=\index{public}.






\section{Extensions}

Pour tout type de structure, un nombre quelconque d'extensions peut être défini. Une extension peut déclarer propriétés, fonctions, sections, services, primitives et gardes.

Voici des exemples d'extension :

\begin{PLM}
struct $exemple {
  var x $int32
}

extension $exemple {
  func user setOk @userAccess @mutating (?inX $int32) {
    self.x = inX
  }
}

extension $exemple {
  func user setOk2 @userAccess @mutating (?inX $int32) {
    self.setOk (!inX) 
  }
  
  section sectionSet @mutating (?inX $int32) {
    self.x = inX
  }
}

extension $exemple {
  func user setOk3 @userAccess @mutating (?inX $int32) {
    self.sectionSet (!inX)
  }
}
\end{PLM}






\section{Visibilité des propriétés et des méthodes}

Par défaut, les propriétés et les routines définies dans une structure sont privées, c'est-à-dire que seules les routines définies dans la même structure peuvent y accéder. Pour rendre publique une propriété ou une routine, il faut la déclarer avec le mot réservé \plm=public=\index{public}.


