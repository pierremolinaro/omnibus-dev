#—————————————————————————————————————————————————————————————————————————————————————————————————
#   SEMANTICS                                                                                                           
#—————————————————————————————————————————————————————————————————————————————————————————————————

list @decoratedRegularRoutineList {
  public var @lstring receiverTypeName %initArgLabel # Empty string if standalone function
  public var @mode mode %initArgLabel
  public var @bool isRequired %initArgLabel
  public var @routineKind routineKind %initArgLabel
  public var @bool warnIfUnused %initArgLabel
  public var @bool exportedRoutine %initArgLabel
  public var @routineAttributes routineAttributes %initArgLabel
  public var @lstring routineMangledLLVMName %initArgLabel
  public var @routineFormalArgumentListAST formalArgumentList %initArgLabel
  public var @bool warningOnUnusedArgs %initArgLabel
  public var @instructionListAST mInstructionList
  public var @location mEndOfRoutineDeclaration
  public var @lstring returnTypeName %initArgLabel # Empty string if no returned value
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

method @decoratedRegularRoutineList routineSemanticAnalysis
  ?context:let @semanticContext inContext
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!intermediateCode:@intermediateCodeStruct ioIntermediateCodeStruct
{
  for function in self do
    [function semanticAnalysis 
      !context: inContext
      !?temporary: ioTemporaries
      !?intermediateCode: ioIntermediateCodeStruct
    ]
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

method @decoratedRegularRoutineList.@element semanticAnalysis
  ?context:let @semanticContext inContext
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!intermediateCode:@intermediateCodeStruct ioIntermediateCodeStruct
{
#--- Reset temporaries index
  ioTemporaries.mTemporaryIndex = 0
#--- Receiver type
  let receiverType = if self.receiverTypeName.string == ""
    then voidType ()
    else [[inContext.mTypeMap searchKey !self.receiverTypeName] type]
    end
#--- Variable map
  var universalMap = inContext.mValuedObjectMap
  [!?universalMap openOverrideForSelectBlock]
  [!?universalMap openBranch]
#--- Return value ?
  if self.returnTypeName.string != "" then
    let resultVarName = @lstring {!functionResultVariableName () !self.returnTypeName.location}
    let resultType = [[inContext.mTypeMap searchKey !self.returnTypeName] type]
    [!?universalMap insertLocalVariable !resultVarName !resultType !resultVarName !.noValue !true]
  end
#--- Analyse formal arguments
  var @routineFormalArgumentListIR formalArguments = @[]
  enterFormalArguments (
    !context:inContext
    !self.formalArgumentList
    !?universalMap
    !?formalArguments
    !warningOnUnusedArgs: self.warningOnUnusedArgs
  )
#--- Analyze instruction list
  var @allocaList allocaList = @[]
  var @instructionListIR instructionGenerationList = @[]
  [self.mInstructionList analyzeRoutineInstructionList
    !self:receiverType
    !routineAttributes: self.routineAttributes
    !context:inContext
    !mode:self.mode
    !?temporary:ioTemporaries
    !?staticEntityMap:ioIntermediateCodeStruct.mStaticEntityMap
    !?variableMap:universalMap
    !?alloca:allocaList
    !?generatedCode:instructionGenerationList
  ]
#--- End of variableMap
  [universalMap checkLocalVariableFinalState !?instructionGenerationList]
  [!?universalMap closeBranch !self.mEndOfRoutineDeclaration]
  [!?universalMap closeOverride !self.mEndOfRoutineDeclaration]
#--- Code generation
  let returnType = if self.returnTypeName.string == "" then
    voidType ()
  else
    [[inContext.mTypeMap searchKey !self.returnTypeName] type]
  end
  ioIntermediateCodeStruct.mRoutineListIR += !@regularRoutineIR {
    !self.routineMangledLLVMName
    !isRequired:self.isRequired
    !warnsIfUnused:self.warnIfUnused
    !receiverType
    !formalArguments
    !allocaList
    !instructionGenerationList
    !self.exportedRoutine
    !self.routineKind
    !returnType
    !false
  }
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

list @allocaList {
  public var @string mVarLLVMName
  public var @omnibusType mLLVMType
  public var @bool mFormalInputArgument
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

method @allocaList generateAllocaList ?!@string ioLLVMcode {
  for (variable type *) in self do
    ioLLVMcode += "  " + variable + " = alloca " + [type llvmTypeName] + "\n"
  end
#--- Init ARC variables
  for (variable type formalInputArgument) in self do
    if [type.kind isDynamicArrayType] && not formalInputArgument then
      ioLLVMcode += ";--- init dynamic array type '" + variable + "'\n"
      ioLLVMcode += "  store " + [type llvmTypeName] + " null, " + [type llvmTypeName] + "* " + variable + " ; init ARC value\n"
    end
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#! CODE GENERATION
#—————————————————————————————————————————————————————————————————————————————————————————————————

class @regularRoutineIR : @abstractRoutineIR {
  public var @omnibusType mReceiverType # .void if standalone function
  public var @routineFormalArgumentListIR mFormalArgumentListForGeneration
  public var @allocaList mAllocaList
  public var @instructionListIR mInstructionGenerationList
  public var @bool mExportedFunction # Function is not tagged with internal
  public var @routineKind mKind
  public var @omnibusType mReturnType # .void if no return value
  public var @bool mAppendFileAndLineArgumentForPanicLocation
}

#·······················································································································

override method @regularRoutineIR enterAccessibleEntities
   ?!@stringset ioInvokedRoutineSet
   ?!@uint ioMaxBranchOfOnInstructions
{
  for instruction in self.mInstructionGenerationList do
    [instruction.mInstructionGeneration enterAccessibleEntities
      !?ioInvokedRoutineSet
      !?ioMaxBranchOfOnInstructions
    ]
  end
}

#·······················································································································

override method @regularRoutineIR llvmCodeGeneration 
  ?!@string ioLLVMcode
  ?let @generationContext inGenerationContext
  ?!@generationAdds ioGenerationAdds
{
  ioLLVMcode += "define"
  if not self.mExportedFunction then
    ioLLVMcode += " internal"
  end
  ioLLVMcode += " " + [self.mReturnType llvmTypeName]
  ioLLVMcode += " @" + [self.mRoutineMangledName assemblerRepresentation] + " ("
  let receiverLLVMTypeName = if self.mReceiverType.kind == .void
    then ""
    else [self.mReceiverType llvmTypeName]
  end
  var first = true
  if receiverLLVMTypeName != "" then
    ioLLVMcode += receiverLLVMTypeName + "* " + llvmNameForSelf ()
    first = false
  end
  for (mFormalArgumentKind mFormalArgumentType mFormalArgumentName) in self.mFormalArgumentListForGeneration do
    if first then
      first = false
    else
      ioLLVMcode += ", "
    end
    switch mFormalArgumentKind
    case input :
      ioLLVMcode += [mFormalArgumentType llvmTypeName] + " %in." + mFormalArgumentName
    case output :
      ioLLVMcode += [mFormalArgumentType llvmTypeName] + " * " + llvmNameForLocalVariable (!mFormalArgumentName)
   case inputOutput :
      ioLLVMcode += [mFormalArgumentType llvmTypeName] + " * " + llvmNameForLocalVariable (!mFormalArgumentName)
    end
  end
  if not [option omnibus_options.noPanicGeneration value] && self.mAppendFileAndLineArgumentForPanicLocation then
    if not first then
      ioLLVMcode += ", "
    end
    ioLLVMcode += [inGenerationContext.mPanicLineLLVMType llvmTypeName] + " %LINE, i8* %FILE"
  end
  ioLLVMcode += ")" + llvmAttributeFunction () + "{\n"
#--- Add result variable ?
  var allocaList = self.mAllocaList
  if self.mReturnType.kind != .void then
    allocaList += !llvmNameForLocalVariable (!functionResultVariableName ()) !self.mReturnType !false
  end
#--- Store input arguments in memory
  for (mFormalArgumentKind mFormalArgumentType mFormalArgumentName) in self.mFormalArgumentListForGeneration do
    switch mFormalArgumentKind
    case input :
      allocaList += !llvmNameForLocalVariable (!mFormalArgumentName) !mFormalArgumentType !true
    case output, inputOutput :
    end
  end
  [allocaList generateAllocaList !?ioLLVMcode]
#--- Copy input arguments and generate retain instructions for input arguments
#    Insulate input and input/output arguments
  for (mFormalArgumentKind mFormalArgumentType mFormalArgumentName) in self.mFormalArgumentListForGeneration do
    switch mFormalArgumentKind
    case input :
      ioLLVMcode += ";--------- Input argument '" + mFormalArgumentName + "'\n"
      let llvmType = [mFormalArgumentType llvmTypeName]
      ioLLVMcode += "  store " + llvmType + " %in." + mFormalArgumentName + ", " + llvmType + " * "
                 + llvmNameForLocalVariable (!mFormalArgumentName) + "\n"
      [mFormalArgumentType generateRetain !mFormalArgumentName !?ioLLVMcode !?ioGenerationAdds]
      if self.mKind == .section then
        [mFormalArgumentType generateInsulate !mFormalArgumentName !?ioLLVMcode !?ioGenerationAdds]
      end
    case inputOutput :
      if self.mKind == .section then
       ioLLVMcode += ";--------- Insulate input/output argument '" + mFormalArgumentName + "'\n"
       [mFormalArgumentType generateInsulate !mFormalArgumentName !?ioLLVMcode !?ioGenerationAdds]
      end
    case output :
    end
  end
#--- Instruction list
  ioLLVMcode += ";--------- Function instruction list\n"
  [self.mInstructionGenerationList instructionListLLVMCode !?ioLLVMcode !inGenerationContext !?ioGenerationAdds]
#--- Generate release instructions for input arguments
#    Insulate output and input/output arguments
  for (mFormalArgumentKind mFormalArgumentType mFormalArgumentName) in self.mFormalArgumentListForGeneration do
    switch mFormalArgumentKind
    case input :
      ioLLVMcode += ";--------- Release input argument '" + mFormalArgumentName + "'\n"
     [mFormalArgumentType generateRelease !mFormalArgumentName !?ioLLVMcode !?ioGenerationAdds]
    case inputOutput :
      if self.mKind == .section then
       ioLLVMcode += ";--------- Insulate input/output argument '" + mFormalArgumentName + "'\n"
       [mFormalArgumentType generateInsulate !mFormalArgumentName !?ioLLVMcode !?ioGenerationAdds]
      end
    case output :
      if self.mKind == .section then
       ioLLVMcode += ";--------- Insulate output argument '" + mFormalArgumentName + "'\n"
       [mFormalArgumentType generateInsulate !mFormalArgumentName !?ioLLVMcode !?ioGenerationAdds]
      end
    end
  end
#--- Epilog
  ioLLVMcode += ";--------- Return\n"
  if self.mReturnType.kind == .void then
    ioLLVMcode += "  ret void\n"
  else
    let resultVarLLVMName = llvmNameForLocalVariable (!functionResultVariableName ())
    ioLLVMcode += "  %result = load " + [self.mReturnType llvmTypeName] + ", " + [self.mReturnType llvmTypeName] + " * " + resultVarLLVMName + "\n"
    ioLLVMcode += "  ret " + [self.mReturnType llvmTypeName] + " %result\n"
  end
  ioLLVMcode += "}\n\n"
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
