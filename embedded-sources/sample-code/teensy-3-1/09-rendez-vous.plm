target "teensy-3-1/unprivileged"

import "carte-tp-teensy-3-1.plm-import"

//-----------------------------------------------------------------------------*

sync $RendezVous {
  var inputWaitList = $TaskList ()
  var outputWaitList = $TaskList ()
  var inputGuardList = $GuardList ()
  var outputGuardList = $GuardList ()

  public primitive input @mutating () {
    makeTaskReady (!?fromList:self.outputWaitList ?found:let found)
    if not found {
      notifyChange (!?forGuard:self.outputGuardList)
      block (!?inList:self.inputWaitList)
    }
  }

  public primitive output @mutating () {
    makeTaskReady (!?fromList:self.inputWaitList ?found:let found)
    if not found {
      notifyChange (!?forGuard:self.inputGuardList)
      block (!?inList:self.outputWaitList)
    }
  }

  public guard input @noUnusedWarning () {
    makeTaskReady (!?fromList:self.outputWaitList ?found:accept)
    if not accept {
      handle (!?guard:self.inputGuardList)
    }
  }

  guard output @noUnusedWarning () {
    makeTaskReady (!?fromList:self.inputWaitList ?found:accept)
    if not accept {
      handle (!?guard:self.outputGuardList)
    }
  }

}

//-----------------------------------------------------------------------------*

sync rdvs = $RendezVous ()

//-----------------------------------------------------------------------------*

task T0 @stacksize 512 @autostart {
  var top $uint32 = 0

  event @onSetup first {
    digital.set (!mode:.OUTPUT !toPort:.D13)
    digital.set (!mode:.OUTPUT !toPort:LED_L0)
    digital.set (!mode:.OUTPUT !toPort:LED_L1)
    digital.set (!mode:.OUTPUT !toPort:LED_L2)
    digital.set (!mode:.OUTPUT !toPort:LED_L3)
    digital.set (!mode:.OUTPUT !toPort:LED_L4)
  }


  event time.wait (!until:self.top) {
    rdvs.output ()
    self.top += 250
    digital.write (!yes !toPort:LED_L0)
    time.wait (!until:self.top)
    rdvs.output ()
    self.top += 250
    digital.write (!no !toPort:LED_L0)
  }
}

//-----------------------------------------------------------------------------*

task T1 @stacksize 512 @autostart {

  event rdvs.input () {
    digital.write (!yes !toPort:LED_L1)
    rdvs.input ()
    digital.write (!no !toPort:LED_L1)
  }
}

//-----------------------------------------------------------------------------*
