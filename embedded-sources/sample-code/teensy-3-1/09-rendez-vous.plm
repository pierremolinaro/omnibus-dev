target "teensy-3-1"

//——————————————————————————————————————————————————————————————————————————————
//   ACTIVITY LED                                                               
//——————————————————————————————————————————————————————————————————————————————

func safe activityLedOn @global () {
  digitalWrite (!port:.D13 !true)
}

func safe activityLedOff @global () {
  digitalWrite (!port:.D13 !false)
}

//-----------------------------------------------------------------------------*

init 100 {
  pinMode (!port:.D13 !mode:.OUTPUT)
}

//-----------------------------------------------------------------------------*

let LED_L0 = $digitalPort.D3
let LED_L1 = $digitalPort.D4
let LED_L2 = $digitalPort.D5
let LED_L3 = $digitalPort.D6
let LED_L4 = $digitalPort.D7

//-----------------------------------------------------------------------------*

init 1000 {
  pinMode (!port:LED_L0 !mode:.OUTPUT)
  pinMode (!port:LED_L1 !mode:.OUTPUT)
  pinMode (!port:LED_L2 !mode:.OUTPUT)
  pinMode (!port:LED_L3 !mode:.OUTPUT)
  pinMode (!port:LED_L4 !mode:.OUTPUT)
}

//-----------------------------------------------------------------------------*

struct $rendezVous {
  var inputWaitList = $taskList ()
  var outputWaitList = $taskList ()
  var inputGuardList = $guardList ()
  var outputGuardList = $guardList ()

  public primitive input () {
    makeTaskReady (!?fromList:self.outputWaitList ?found:let found)
    if not found {
      notifyChange (!?forGuard:self.outputGuardList)
      block (!?inList:self.inputWaitList)
    }
  }

  public primitive output () {
    makeTaskReady (!?fromList:self.inputWaitList ?found:let found)
    if not found {
      notifyChange (!?forGuard:self.inputGuardList)
      block (!?inList:self.outputWaitList)
    }
  }

  public primitive inputUntil @noUnusedWarning (?deadline:inDeadline $uint32) -> $bool {
    makeTaskReady (!?fromList:self.outputWaitList ?found:result)
    if (not result) and (inDeadline > time.millis ()) { 
      block (!?inList:self.inputWaitList !onDeadline:inDeadline)
    }
  }

  public primitive outputUntil @noUnusedWarning (?deadline:inDeadline $uint32) -> $bool {
    makeTaskReady (!?fromList:self.inputWaitList ?found:result)
    if (not result) and (inDeadline > time.millis ()) { 
      block (!?inList:self.outputWaitList !onDeadline:inDeadline)
    }
  }

  public guard input @noUnusedWarning () {
    makeTaskReady (!?fromList:self.outputWaitList ?found:accept)
    if not accept {
      handle (!?guard:self.inputGuardList)
    }
  }

  guard output @noUnusedWarning () {
    makeTaskReady (!?fromList:self.inputWaitList ?found:accept)
    if not accept {
      handle (!?guard:self.outputGuardList)
    }
  }

}

//-----------------------------------------------------------------------------*

var rdvs = $rendezVous ()

//-----------------------------------------------------------------------------*

task T0 priority 0 stackSize 512 {
  var top $uint32 = 0

  while time.waitUntilMS (!deadline:self.top) {
    rdvs.output ()
    self.top += 250
    digitalWrite (!port:LED_L0 !true)
    time.waitUntilMS (!deadline:self.top)
    rdvs.output ()
    self.top += 250
    digitalWrite (!port:LED_L0 !false)
  }
}

//-----------------------------------------------------------------------------*

task T1 priority 1 stackSize 512 {

  while rdvs.input () {
    digitalWrite (!port:LED_L1 !true)
    rdvs.input ()
    digitalWrite (!port:LED_L1 !false)
  }
}

//-----------------------------------------------------------------------------*
