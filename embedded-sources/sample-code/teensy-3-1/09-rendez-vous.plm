target "teensy-3-1/unprivileged"

driver lcd (!DB4:.D16 !DB5:.D15 !DB6:.D14 !DB7:.D19 !RS:.D18 !ENABLE:.D17)

//——————————————————————————————————————————————————————————————————————————————
//   ACTIVITY LED                                                               
//——————————————————————————————————————————————————————————————————————————————

func safe activityLedOn @exported () {
  digital.write (!yes !toPort:.D13)
}

func safe activityLedOff @exported () {
  digital.write (!no !toPort:.D13)
}

//-----------------------------------------------------------------------------*

let LED_L0 = $digitalPort.D3
let LED_L1 = $digitalPort.D4
let LED_L2 = $digitalPort.D5
let LED_L3 = $digitalPort.D6
let LED_L4 = $digitalPort.D7

//-----------------------------------------------------------------------------*

struct $rendezVous {
  var inputWaitList = $taskList ()
  var outputWaitList = $taskList ()
  var inputGuardList = $guardList ()
  var outputGuardList = $guardList ()

  public system primitive input @mutating () {
    makeTaskReady (!?fromList:self.outputWaitList ?found:let found)
    if not found {
      notifyChange (!?forGuard:self.outputGuardList)
      block (!?inList:self.inputWaitList)
    }
  }

  public system primitive output @mutating () {
    makeTaskReady (!?fromList:self.inputWaitList ?found:let found)
    if not found {
      notifyChange (!?forGuard:self.inputGuardList)
      block (!?inList:self.outputWaitList)
    }
  }

  public system primitive inputUntil @noUnusedWarning @mutating (?deadline:inDeadline $uint32
                                                                 !result: result $bool) {
    makeTaskReady (!?fromList:self.outputWaitList ?found:result)
    if (not result) and (inDeadline > time.now ()) { 
      block (!?inList:self.inputWaitList !onDeadline:inDeadline !?result:result)
    }
  }

  public system primitive outputUntil @noUnusedWarning @mutating (?deadline:inDeadline $uint32
                                                                  !result: result $bool) {
    makeTaskReady (!?fromList:self.inputWaitList ?found:result)
    if (not result) and (inDeadline > time.now ()) { 
      block (!?inList:self.outputWaitList !onDeadline:inDeadline !?result:result)
    }
  }

  public guard input @noUnusedWarning () {
    makeTaskReady (!?fromList:self.outputWaitList ?found:accept)
    if not accept {
      handle (!?guard:self.inputGuardList)
    }
  }

  guard output @noUnusedWarning () {
    makeTaskReady (!?fromList:self.inputWaitList ?found:accept)
    if not accept {
      handle (!?guard:self.outputGuardList)
    }
  }

}

//-----------------------------------------------------------------------------*

task T0 priority 0 stackSize 512 {
  var top $uint32 = 0

  setup 100 {
    digital.set (!mode:.OUTPUT !toPort:.D13)
    digital.set (!mode:.OUTPUT !toPort:LED_L0)
    digital.set (!mode:.OUTPUT !toPort:LED_L1)
    digital.set (!mode:.OUTPUT !toPort:LED_L2)
    digital.set (!mode:.OUTPUT !toPort:LED_L3)
    digital.set (!mode:.OUTPUT !toPort:LED_L4)
  }


  on time.wait (!untilDeadline:self.top) {
    T1.enter ()
    self.top += 250
    digital.write (!yes !toPort:LED_L0)
    time.wait (!untilDeadline:self.top)
    T1.enter ()
    self.top += 250
    digital.write (!no !toPort:LED_L0)
  }
}

//-----------------------------------------------------------------------------*

task T1 priority 1 stackSize 512 {
  var rdvs = $rendezVous ()

  entry enter : self.rdvs.output ()
 
  on self.rdvs.input () {
    digital.write (!yes !toPort:LED_L1)
    self.rdvs.input ()
    digital.write (!no !toPort:LED_L1)
  }
}

//-----------------------------------------------------------------------------*
