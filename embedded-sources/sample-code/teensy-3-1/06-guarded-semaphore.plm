target "teensy-3-1"

//-----------------------------------------------------------------------------*

init 1000 {
  digitalMode (!port:.D3 !mode:.OUTPUT)
  digitalMode (!port:.D4 !mode:.OUTPUT)
  digitalMode (!port:.D5 !mode:.OUTPUT)
  digitalMode (!port:.D6 !mode:.OUTPUT)
  digitalMode (!port:.D7 !mode:.OUTPUT)
}

//-----------------------------------------------------------------------------*

struct $monSemaphore {
  var value $uint32
  var list = $taskList ()
  var guardList = $guardList ()

  public primitive P @noUnusedWarning () {
    if self.value > 0 {
      self.value -= 1
    }else{
      block (!?inList:self.list)
    }
  }

  public service V () {
    makeTaskReady (!?fromList:self.list ?found:let found)
    if not found {
      self.value += 1
      notifyChange (!?forGuard:self.guardList)
    }
  }

  public guard P () {
    accept = self.value > 0
    if accept {
      self.value -= 1
    }else{
      handle (!?guard:self.guardList)
    }
  }
  
  public guard PP () : self.P () {
  }
}

//-----------------------------------------------------------------------------*

var s0 = $monSemaphore (!value:0)

//-----------------------------------------------------------------------------*

var s1 = $monSemaphore (!value:0)

//-----------------------------------------------------------------------------*

task T0 priority 0 stackSize 512 {
  var top $uint32 = 0

  while time.waitUntilMS (!deadline:self.top) {
    s0.V ()
    self.top += 250
    write (!port:.D3 !true)
    time.waitUntilMS (!deadline:self.top)
    s0.V ()
    self.top += 250
    write (!port:.D3 !false)
  }
}

//-----------------------------------------------------------------------------*

task T1 priority 1 stackSize 512 {
  var top $uint32 = 0

  while time.waitUntilMS (!deadline:self.top) {
    s1.V ()
    self.top += 249
    write (!port:.D7 !true)
    time.waitUntilMS (!deadline:self.top)
    s1.V ()
    self.top += 249
    write (!port:.D7 !false)
  }
}

//-----------------------------------------------------------------------------*

task T2 priority 2 stackSize 512 {
  var deadline $uint32 = 0
  
  while s0.PP () {
    toggle (!port:.D4)
  }
  
  while s1.P () {
    toggle (!port:.D6)
  }
  
  while time.waitUntilMS (!deadline:self.deadline) {
    self.deadline += 200
    toggle (!port:.D5)
  }
}

//-----------------------------------------------------------------------------*
