target "teensy-3-1/privileged"

module lcd (!DB4:.D16 !DB5:.D15 !DB6:.D14 !DB7:.D19 !RS:.D18 !ENABLE:.D17)

//——————————————————————————————————————————————————————————————————————————————
//   ACTIVITY LED                                                               
//——————————————————————————————————————————————————————————————————————————————

func safe activityLedOn @exported () {
  digitalWrite (!port:.D13 !yes)
}

func safe activityLedOff @exported () {
  digitalWrite (!port:.D13 !no)
}

//-----------------------------------------------------------------------------*

init 100 {
  pinMode (!port:.D13 !mode:.OUTPUT)
}

//-----------------------------------------------------------------------------*

let LED_L1 = $digitalPort.D4

//-----------------------------------------------------------------------------*

init 1000 {
  pinMode (!port:LED_L1 !mode:.OUTPUT)
  SIM_SCGC6 |= SIM_SCGC6_PIT
}

//------------------------------------------------*

system section setupPIT () {
  PIT_MCR = 0
  PIT_LDVAL [0] = 200000
  PIT_TCTRL [0] = {PIT_TCTRL !TIE:1 !TEN:1} // Interrupt, enabled
  enable (!interrupt: .PITChannel0)
}

//------------------------------------------------*

module pit {
  var gPITValue $uint32 = 0

  system section getPITValue (!outValue $uint32) {
    outValue = self.gPITValue
  }

  isr safe PITChannel0 {
  //--- Acquitter l'interruption
    PIT_TFLG [0] = {PIT_TFLG !TIF:1}
  //--- Incrémenter le compteur
    self.gPITValue += 1
  }
}

module pit ()

//------------------------------------------------*

task T priority 12 stackSize 512 {
  var deadline $uint32 = 0

  setup 0 {
    setupPIT ()
  }
  
  on time.wait (!untilDeadline:self.deadline) {
    self.deadline +%= 250
    digitalWrite (!port:LED_L1 !yes) // Allumer la led
    time.wait (!untilDeadline:self.deadline)
    self.deadline +%= 250
    digitalWrite (!port:LED_L1 !no)  // Éteindre la led
    lcd.goto (!line:1 !column:0)
    lcd.print (!spaces:10)
    lcd.goto (!line:1 !column:0)
    var value $uint32
    pit.getPITValue (?value)
    lcd.print (!unsigned:value)
  }
}

//------------------------------------------------*
