target "teensy-3-1/unprivileged"

import "carte-tp-teensy-3-1.omnibus-import"

//-----------------------------------------------------------------------------*

section setupPIT () {
  ©PIT.MCR = 0
  ©PIT.LDVAL [0] = 200000
  ©PIT.TCTRL [0] = {©PIT.TCTRL !TIE:1 !TEN:1} // $interrupt, enabled
  enable (!interrupt: .PITChannel0)
}

//------------------------------------------------*

driver pit > digital {
  var gPITValue $uint32 = 0

  startup {
    digital.set (!mode:.output !toPort:.D13)
    digital.set (!mode:.output !toPort:LED_L1)
    ©SIM.SCGC6 |= SIM_SCGC6_PIT
  }

  section getPITValue (!outValue $uint32) {
    outValue = self.gPITValue
  }

  interrupt section PITChannel0 {
  //--- Acquitter l'interruption
    ©PIT.TFLG [0] = {©PIT.TFLG !TIF:1}
  //--- Incrémenter le compteur
    self.gPITValue += 1
  }
}

driver pit ()

//------------------------------------------------*

task T @stacksize 512 @autostart {
  var deadline $uint32 = 0

  event @onSetup first {
    setupPIT ()
  }

  event time.wait (!until:self.deadline) {
    self.deadline +%= 250
    digital.write (!yes !toPort:LED_L1) // Allumer la led
    time.wait (!until:self.deadline)
    self.deadline +%= 250
    digital.write (!no !toPort:LED_L1)  // Éteindre la led
    lcd.goto (!line:1 !column:0)
    lcd.print (!spaces:10)
    lcd.goto (!line:1 !column:0)
    var value $uint32
    pit.getPITValue (?value)
    lcd.print (!u32:value)
  }
}

//------------------------------------------------*
