target "teensy-3-1"

//——————————————————————————————————————————————————————————————————————————————
//   ACTIVITY LED                                                               
//——————————————————————————————————————————————————————————————————————————————

func safe activityLedOn @exported () {
  digitalWrite (!port:.D13 !true)
}

func safe activityLedOff @exported () {
  digitalWrite (!port:.D13 !false)
}

//-----------------------------------------------------------------------------*

init 100 {
  pinMode (!port:.D13 !mode:.OUTPUT)
}

let LED_L0 = $digitalPort.D3
let LED_L1 = $digitalPort.D4
let LED_L2 = $digitalPort.D5
let LED_L3 = $digitalPort.D6
let LED_L4 = $digitalPort.D7

//-----------------------------------------------------------------------------*

init 1000 {
  pinMode (!port:LED_L0 !mode:.OUTPUT)
  pinMode (!port:LED_L1 !mode:.OUTPUT)
  pinMode (!port:LED_L2 !mode:.OUTPUT)
  pinMode (!port:LED_L3 !mode:.OUTPUT)
  pinMode (!port:LED_L4 !mode:.OUTPUT)
}

//-----------------------------------------------------------------------------*

struct $monSemaphore {
  var value $uint32
  var list = $taskList ()
  var guardList = $guardList ()

  public system primitive P @noUnusedWarning @mutating () {
    if self.value > 0 {
      self.value -= 1
    }else{
      block (!?inList:self.list)
    }
  }

  public system service V @mutating () {
    makeTaskReady (!?fromList:self.list ?found:let found)
    if not found {
      self.value += 1
      notifyChange (!?forGuard:self.guardList)
    }
  }

  public guard P () {
    accept = self.value > 0
    if accept {
      self.value -= 1
    }else{
      handle (!?guard:self.guardList)
    }
  }
  
  public guard PP () : self.P () {
  }
}

//-----------------------------------------------------------------------------*

var s0 = $monSemaphore (!value:0)

//-----------------------------------------------------------------------------*

var s1 = $monSemaphore (!value:0)

//-----------------------------------------------------------------------------*

task T0 priority 0 stackSize 512 {
  var top $uint32 = 0

  while time.waitUntilMS (!deadline:self.top) {
    s0.V ()
    self.top += 250
    digitalWrite (!port:LED_L0 !true)
    time.waitUntilMS (!deadline:self.top)
    s0.V ()
    self.top += 250
    digitalWrite (!port:LED_L0 !false)
  }
}

//-----------------------------------------------------------------------------*

task T1 priority 1 stackSize 512 {
  var top $uint32 = 0

  while time.waitUntilMS (!deadline:self.top) {
    s1.V ()
    self.top += 249
    digitalWrite (!port:LED_L4 !true)
    time.waitUntilMS (!deadline:self.top)
    s1.V ()
    self.top += 249
    digitalWrite (!port:LED_L4 !false)
  }
}

//-----------------------------------------------------------------------------*

task T2 priority 2 stackSize 512 {
  var deadline $uint32 = 0
  
  while s0.PP () {
    toggle (!port:LED_L1)
  }
  
  while s1.P () {
    toggle (!port:LED_L3)
  }
  
  while time.waitUntilMS (!deadline:self.deadline) {
    self.deadline += 200
    toggle (!port:LED_L2)
  }
}

//-----------------------------------------------------------------------------*
