target "teensy-3-1/unprivileged"

driver lcd (!DB4:.D16 !DB5:.D15 !DB6:.D14 !DB7:.D19 !RS:.D18 !ENABLE:.D17)

//——————————————————————————————————————————————————————————————————————————————
//   ACTIVITY LED                                                               
//——————————————————————————————————————————————————————————————————————————————

func section activityLedOn @safe @exported () {
  digital.write (!yes !toPort:.D13)
}

func section activityLedOff @safe @exported () {
  digital.write (!no !toPort:.D13)
}

//-----------------------------------------------------------------------------*

let LED_L0 = $digitalPort.D3
let LED_L1 = $digitalPort.D4
let LED_L2 = $digitalPort.D5
let LED_L4 = $digitalPort.D7

//-----------------------------------------------------------------------------*

sync $rendezVousData {
  var readBarrier  = $semaphore (!value:0)
  var writeBarrier = $semaphore (!value:1)
  var data $uint32 = 0

  public func user output @mutating @noUnusedWarning (?data:inData $uint32) {
    self.writeBarrier.wait ()
    self.data = inData
    self.readBarrier.signal ()
  }

  public guard output @noUnusedWarning (?data:inData $uint32) : self.writeBarrier.wait () {
    self.data = inData
    self.readBarrier.signal ()
  }

  public func user input @mutating @noUnusedWarning (!data:outData $uint32) {
    self.readBarrier.wait ()
    outData = self.data
    self.writeBarrier.signal ()
  }

  public guard input @noUnusedWarning (!data:outData $uint32) : self.readBarrier.wait () {
    outData = self.data
    self.writeBarrier.signal ()
  }

}

//-----------------------------------------------------------------------------*

sync s = $semaphore (!value:1)
sync rdvs = $rendezVousData ()
sync rdvs2 = $rendezVousData ()

//-----------------------------------------------------------------------------*

task T0 priority 0 stackSize 512 {
  var top $uint32 = 0

  setup 100 {
    digital.set (!mode:.OUTPUT !toPort:.D13)
    digital.set (!mode:.OUTPUT !toPort:LED_L0)
    digital.set (!mode:.OUTPUT !toPort:LED_L1)
    digital.set (!mode:.OUTPUT !toPort:LED_L2)
    digital.set (!mode:.OUTPUT !toPort:LED_L4)
  }

  on time.wait (!untilDeadline:self.top) {
    rdvs2.input (?data:let n)
    rdvs.output (!data:n)
    digital.toggle (!port:LED_L0)
    self.top += 500
  }
}

//-----------------------------------------------------------------------------*

task T1 priority 1 stackSize 1024 {
  on rdvs.input (?data:let x) {
    digital.toggle (!port:LED_L1)
    s.wait ()
      lcd.goto (!line:0 !column:0)
      lcd.print (!u32:x)
    s.signal ()
  }
}

//-----------------------------------------------------------------------------*

task T2 priority 2 stackSize 1024 {
  var deadline $uint32 = 0
  var n $uint32 = 0

  on rdvs2.output (!data:self.n) {
    digital.toggle (!port:LED_L2)
    s.wait ()
      lcd.goto (!line:1 !column:0)
      lcd.print (!u32:self.n)
    s.signal ()
    self.n += 1
  }
  
  on time.wait (!untilDeadline:self.deadline) {
    self.deadline += 200
    digital.toggle (!port:LED_L4)
  }
}

//-----------------------------------------------------------------------------*
