target "teensy-3-1/unprivileged"

module lcd (!DB4:.D16 !DB5:.D15 !DB6:.D14 !DB7:.D19 !RS:.D18 !ENABLE:.D17)

//——————————————————————————————————————————————————————————————————————————————
//   ACTIVITY LED                                                               
//——————————————————————————————————————————————————————————————————————————————

func safe activityLedOn @exported () {
  digitalWrite (!port:.D13 !yes)
}

func safe activityLedOff @exported () {
  digitalWrite (!port:.D13 !no)
}

//-----------------------------------------------------------------------------*

init 100 {
  pinMode (!port:.D13 !mode:.OUTPUT)
}

//-----------------------------------------------------------------------------*

let LED_L0 = $digitalPort.D3
let LED_L1 = $digitalPort.D4
let LED_L2 = $digitalPort.D5
let LED_L3 = $digitalPort.D6
let LED_L4 = $digitalPort.D7

//-----------------------------------------------------------------------------*

init 1000 {
  pinMode (!port:LED_L0 !mode:.OUTPUT)
  pinMode (!port:LED_L1 !mode:.OUTPUT)
  pinMode (!port:LED_L2 !mode:.OUTPUT)
  pinMode (!port:LED_L3 !mode:.OUTPUT)
  pinMode (!port:LED_L4 !mode:.OUTPUT)
}

//-----------------------------------------------------------------------------*

var s0 = $semaphore (!value:0)

//-----------------------------------------------------------------------------*

var s1 = $semaphore (!value:0)

guard PP () : s1.wait () {
}

guard convenienceWait (?untilDeadline:inDeadline $uint32)
: time.wait (!untilDeadline:inDeadline) {
}

//-----------------------------------------------------------------------------*

task T0 priority 0 stackSize 512 {
  var top $uint32 = 0

  on time.wait (!untilDeadline:self.top) {
    s0.signal ()
    self.top += 250
    digitalWrite (!port:LED_L0 !yes)
    time.wait (!untilDeadline:self.top)
    s0.signal ()
    self.top += 250
    digitalWrite (!port:LED_L0 !no)
  }
}

//-----------------------------------------------------------------------------*

task T1 priority 1 stackSize 512 {
  var top $uint32 = 0

  on time.wait (!untilDeadline:self.top) {
    s1.signal ()
    self.top += 249
    digitalWrite (!port:LED_L4 !yes)
    time.wait (!untilDeadline:self.top)
    s1.signal ()
    self.top += 249
    digitalWrite (!port:LED_L4 !no)
  }
}

//-----------------------------------------------------------------------------*

task T2 priority 2 stackSize 512 {
  var deadline $uint32 = 0
  var toggleD3 = no
  
  on s0.wait () {
    self.toggleD3 = yes // Just for having a boolean guard
  }
  
  on (self.toggleD3) {
    toggle (!port:LED_L1)
    self.toggleD3 = no
  }
  
  on PP () {
    toggle (!port:LED_L3)
  }
  
  on convenienceWait (!untilDeadline:self.deadline) {
    self.deadline += 200
    toggle (!port:LED_L2)
  }
}

//-----------------------------------------------------------------------------*
