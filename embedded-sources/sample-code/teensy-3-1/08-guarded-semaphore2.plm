target "teensy-3-1/unprivileged"

driver lcd (!DB4:.D16 !DB5:.D15 !DB6:.D14 !DB7:.D19 !RS:.D18 !ENABLE:.D17)

//——————————————————————————————————————————————————————————————————————————————
//   ACTIVITY LED                                                               
//——————————————————————————————————————————————————————————————————————————————

func safe activityLedOn @exported () {
  digital.write (!yes !toPort:.D13)
}

func safe activityLedOff @exported () {
  digital.write (!no !toPort:.D13)
}

//-----------------------------------------------------------------------------*

let LED_L0 = $digitalPort.D3
let LED_L1 = $digitalPort.D4
let LED_L2 = $digitalPort.D5
let LED_L3 = $digitalPort.D6
let LED_L4 = $digitalPort.D7

//-----------------------------------------------------------------------------*

sync s0 = $semaphore (!value:0)
sync s1 = $semaphore (!value:0)

//-----------------------------------------------------------------------------*

task T0 priority 0 stackSize 512 {
  var top $uint32 = 0

  setup 1000 {
    digital.set (!mode:.OUTPUT !toPort:.D13)
    digital.set (!mode:.OUTPUT !toPort:LED_L0)
    digital.set (!mode:.OUTPUT !toPort:LED_L1)
    digital.set (!mode:.OUTPUT !toPort:LED_L2)
    digital.set (!mode:.OUTPUT !toPort:LED_L3)
    digital.set (!mode:.OUTPUT !toPort:LED_L4)
  }

  on time.wait (!untilDeadline:self.top) {
    s0.signal ()
    self.top += 250
    digital.write (!yes !toPort:LED_L0)
    time.wait (!untilDeadline:self.top)
    s0.signal ()
    self.top += 250
    digital.write (!no !toPort:LED_L0)
  }
}

//-----------------------------------------------------------------------------*

task T1 priority 1 stackSize 512 {
  var top $uint32 = 0

  on time.wait (!untilDeadline:self.top) {
    s1.signal ()
    self.top += 249
    digital.write (!yes !toPort:LED_L4)
    time.wait (!untilDeadline:self.top)
    s1.signal ()
    self.top += 249
    digital.write (!no !toPort:LED_L4)
  }
}

//-----------------------------------------------------------------------------*

task T2 priority 2 stackSize 512 {
  var deadline $uint32 = 0
  var toggleD3 = no
 
  on s0.wait () {
    self.toggleD3 = yes // Just for having a boolean guard
  }
  
  on (self.toggleD3) {
    digital.toggle (!port:LED_L1)
    self.toggleD3 = no
  }
  
  on s1.wait () {
    digital.toggle (!port:LED_L3)
  }
  
  on time.wait (!untilDeadline:self.deadline) {
    self.deadline += 200
    digital.toggle (!port:LED_L2)
  }
}

//-----------------------------------------------------------------------------*
