target "teensy-3-1"

//——————————————————————————————————————————————————————————————————————————————
//   ACTIVITY LED                                                               
//——————————————————————————————————————————————————————————————————————————————

func safe activityLedOn @global () {
  digitalWrite (!port:.D13 !true)
}

func safe activityLedOff @global () {
  digitalWrite (!port:.D13 !false)
}

//-----------------------------------------------------------------------------*

init 100 {
  digitalMode (!port:.D13 !mode:.OUTPUT)
}

//-----------------------------------------------------------------------------*

let LED_L0 = $digitalPort.D3
let LED_L1 = $digitalPort.D4
let LED_L2 = $digitalPort.D5
let LED_L3 = $digitalPort.D6
let LED_L4 = $digitalPort.D7

//-----------------------------------------------------------------------------*

init 1000 {
  digitalMode (!port:LED_L0 !mode:.OUTPUT)
  digitalMode (!port:LED_L1 !mode:.OUTPUT)
  digitalMode (!port:LED_L2 !mode:.OUTPUT)
  digitalMode (!port:LED_L3 !mode:.OUTPUT)
  digitalMode (!port:LED_L4 !mode:.OUTPUT)
}

//-----------------------------------------------------------------------------*

var s0 = $semaphore (!value:0)

//-----------------------------------------------------------------------------*

var s1 = $semaphore (!value:0)

guard PP () : s1.wait () {
}

guard convenienceWaitUntilMS (?deadline:inDeadline $uint32)
: time.waitUntilMS (!deadline:inDeadline) {
}

//-----------------------------------------------------------------------------*

task T0 priority 0 stackSize 512 {
  var top $uint32 = 0

  while time.waitUntilMS (!deadline:self.top) {
    s0.signal ()
    self.top += 250
    userDigitalWrite (!port:LED_L0 !true)
    time.waitUntilMS (!deadline:self.top)
    s0.signal ()
    self.top += 250
    userDigitalWrite (!port:LED_L0 !false)
  }
}

//-----------------------------------------------------------------------------*

task T1 priority 1 stackSize 512 {
  var top $uint32 = 0

  while time.waitUntilMS (!deadline:self.top) {
    s1.signal ()
    self.top += 249
    userDigitalWrite (!port:LED_L4 !true)
    time.waitUntilMS (!deadline:self.top)
    s1.signal ()
    self.top += 249
    userDigitalWrite (!port:LED_L4 !false)
  }
}

//-----------------------------------------------------------------------------*

task T2 priority 2 stackSize 512 {
  var deadline $uint32 = 0
  
  while s0.wait () {
    userToggle (!port:LED_L1)
  }
  
  while PP () {
    userToggle (!port:LED_L3)
  }
  
  while convenienceWaitUntilMS (!deadline:self.deadline) {
    self.deadline += 200
    userToggle (!port:LED_L2)
  }
}

//-----------------------------------------------------------------------------*
