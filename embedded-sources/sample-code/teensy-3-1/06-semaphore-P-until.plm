target "teensy-3-1/unprivileged"

driver lcd (!DB4:.D16 !DB5:.D15 !DB6:.D14 !DB7:.D19 !RS:.D18 !ENABLE:.D17)

//——————————————————————————————————————————————————————————————————————————————
//   ACTIVITY LED                                                               
//——————————————————————————————————————————————————————————————————————————————

func safe activityLedOn @exported () {
  digital.write (!yes !toPort:.D13)
}

func safe activityLedOff @exported () {
  digital.write (!no !toPort:.D13)
}

//-----------------------------------------------------------------------------*

let LED_L0 = $digitalPort.D3
let LED_L1 = $digitalPort.D4

//-----------------------------------------------------------------------------*

task T1 priority 1 stackSize 512 {
  var s = $semaphore (!value:0)
  var top $uint32 = 0
 
  setup 1000 {
    digital.set (!mode:.OUTPUT !toPort:.D13)
    digital.set (!mode:.OUTPUT !toPort:LED_L0)
    digital.set (!mode:.OUTPUT !toPort:LED_L1)
  }
 
  entry wait : self.s.wait (?untilDeadline:inDeadline $uint32 !result:result $bool)

  on time.wait (!untilDeadline:self.top) {
    self.s.signal ()
    self.top += 300
    digital.write (!yes !toPort:LED_L0)
    time.wait (!untilDeadline:self.top)
    self.s.signal ()
    self.top += 700
    digital.write (!no !toPort:LED_L0)
  }
}

//-----------------------------------------------------------------------------*

task T2 priority 2 stackSize 512 {
  var top $uint32 = 0
  var deadlineReachedCount $uint32 = 0
  var semaphoreMatchCount $uint32 = 0

  on time.wait (!untilDeadline:0) { // no wait...
    T1.wait (!untilDeadline:self.top ?result:let r)
    if r {
      digital.toggle (!port:LED_L1)
      self.deadlineReachedCount += 1
      lcd.goto (!line:0 !column:0)
      lcd.print (!unsigned:self.deadlineReachedCount)
    }else{
      self.top += 400
      digital.toggle (!port:LED_L1)
      self.semaphoreMatchCount += 1
      lcd.goto (!line:1 !column:0)
      lcd.print (!unsigned:self.semaphoreMatchCount)
    }
  }
}

//-----------------------------------------------------------------------------*
