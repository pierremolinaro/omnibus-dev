target "teensy-3-1-tp"

//------------------------------------------------*

init 100_000 {
  SIM_SCGC6 |= SIM_SCGC6_PIT
  NVIC_ISER2 = 1 << ((84 - 16) & 31)
  AICS0_PARCG = 0
}

//------------------------------------------------*

section setupPIT () {
  PIT_MCR = 0
  PIT_LDVAL0 = 200000
  PIT_TCTRL0 = 3 // Interrupt, enabled
}

//------------------------------------------------*

section getPITValue (!outValue $uint32) {
  outValue = gPITValue
}

//------------------------------------------------*

var gPITValue $uint32 = 0 {
  @rw isr PITChannel0
  section getPITValue
}

//------------------------------------------------*

isr PITChannel0 {
//--- Acquitter l'interruption
  PIT_TFLG0 = 1
//--- Incrémenter le compteur
  gPITValue += 1
}

//------------------------------------------------*

task T priority 12 stackSize 512 {
  var deadline $uint32 = 0

  init 0 {
    setupPIT ()
  }
  
  while time.waitUntilMS (!deadline:self.deadline) {
    self.deadline +%= 250
    leds.on (!LED_L1) // Allumer la led
    time.waitUntilMS (!deadline:self.deadline)
    self.deadline +%= 250
    leds.off (!LED_L1)  // Éteindre la led
    lcd.goto (!line:1 !column:0)
    lcd.printSpaces (!10)
    lcd.goto (!line:1 !column:0)
    var value $uint32
    getPITValue (?value)
    lcd.printUnsigned (!value)
  }
}

//------------------------------------------------*
