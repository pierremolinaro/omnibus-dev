target "teensy-3-1-tp"

//------------------------------------------------*

struct $monSemaphore {
  var value $uint32
  var list = $taskList ()

  public primitive P () {
    if self.value > 0 then
      self.value -= 1
    else
      blockInList (!?list:self.list)
    end
  }

  public service V () {
    let found = makeTaskReady (!?list:self.list)
    if not found then
      self.value += 1
    end
  }
}

//------------------------------------------------*

var s = $monSemaphore (!value:0) {
  func $T1.loop
  func $T2.loop
}

//------------------------------------------------*

task T1 priority 1 stackSize 512 {
  var top $uint32 = 0

  init 123 {
    self.top = 1
  }
  
  init 256 {
    self.top += 2
  }
  
  func loop () {
    time.waitUntilMS (!deadline:self.top)
    s.V ()
    self.top += 250
    leds.on (!LED_L1)
    time.waitUntilMS (!deadline:self.top)
    s.V ()
    self.top += 250
    leds.off (!LED_L1)
  }
}

//------------------------------------------------*

task T2 priority 2 stackSize 512 {

  func loop () {
    s.P ()
    leds.on (!LED_L2)
    s.P ()
    leds.off (!LED_L2)
  }
}

//------------------------------------------------*
