target "teensy-3-1-tp"

//-----------------------------------------------------------------------------*

struct $semaphore {
  var value $uint32
  var list = $taskList ()

  service V `kernel () {
    makeTaskReady (!?list:self.list ?found:let found)
    if not found then
      self.value += 1
    end
  }

  service P_until `kernel (?deadline:inDeadline $uint32) -> $bool {
    result = self.value > 0
    if result then
      self.value -= 1
    elsif inDeadline > millis () then 
      blockInListAndOnDeadline (!?list:self.list !deadline:inDeadline)
    end
  }

}

//-----------------------------------------------------------------------------*

var s = $semaphore (!value:0) {
  func $T1.loop
  func $T2.loop
}

//-----------------------------------------------------------------------------*

task T1 priority 1 stackSize 512 {
  var top $uint32 = 0

  init 123 {
    self.top = 1
  }
  
  init 256 {
    self.top += 2
  }
  
  func loop () {
    waitUntilMS (!deadline:self.top)
    s.V ()
    self.top += 300
    ledOn (!LED_L0)
    waitUntilMS (!deadline:self.top)
    s.V ()
    self.top += 700
    ledOff (!LED_L0)
  }
}

//-----------------------------------------------------------------------------*

task T2 priority 2 stackSize 512 {
  var top $uint32 = 0
  var flagSem = true
  var flagTop = true

  func loop () {
    let r = s.P_until (!deadline:self.top)
    if r then
      if self.flagSem then
        ledOn (!LED_L1)
      else
        ledOff (!LED_L1)
      end
      self.flagSem = self.flagSem xor true
    else
      self.top += 400
      if self.flagTop then
        ledOn (!LED_L4)
      else
        ledOff (!LED_L4)
      end
      self.flagTop = self.flagTop xor true
    end
  }
}

//-----------------------------------------------------------------------------*
