target "teensy-3-1-tp"

//-----------------------------------------------------------------------------*

struct $monSemaphore {
  var value $uint32
  var list = $taskList ()

  service V () {
    let found = makeTaskReady (!?list:self.list)
    if not found then
      self.value += 1
    end
  }

  primitive P_until (?deadline:inDeadline $uint32) -> $bool {
    result = self.value > 0
    if result then
      self.value -= 1
    elsif inDeadline > time.millis () then 
      blockInListAndOnDeadline (!?list:self.list !deadline:inDeadline)
    end
  }

}

//-----------------------------------------------------------------------------*

var s = $monSemaphore (!value:0) {
  func $T1.loop
  func $T2.loop
}

//-----------------------------------------------------------------------------*

task T1 priority 1 stackSize 512 {
  var top $uint32 = 0

  init 123 {
    self.top = 1
  }
  
  init 256 {
    self.top += 2
  }
  
  func loop () {
    time.waitUntilMS (!deadline:self.top)
    s.V ()
    self.top += 300
    leds.on (!LED_L0)
    time.waitUntilMS (!deadline:self.top)
    s.V ()
    self.top += 700
    leds.off (!LED_L0)
  }
}

//-----------------------------------------------------------------------------*

task T2 priority 2 stackSize 512 {
  var top $uint32 = 0

  func loop () {
    let r = s.P_until (!deadline:self.top)
    if r then
      leds.toggle (!LED_L1)
    else
      self.top += 400
      leds.toggle (!LED_L4)
    end
  }
}

//-----------------------------------------------------------------------------*
