target "teensy-3-1-tp"

//------------------------------------------------*

struct $semaphore {
  var value $uint32
  var list = $taskList ()
  var guardList = $guardList ()

  service P `kernel () {
    if self.value > 0 then
      self.value -= 1
    else
      blockInList (!?list:self.list)
    end
  }

  service V `kernel () {
    makeTaskReady (!?list:self.list ?found:let found)
    if not found then
      self.value += 1
      guardDidChange (!?guard:self.guardList)
    end
  }

  guard P () {
    accepted = self.value > 0
    if accepted then
      self.value -= 1
    end
    handleGuardedCommand (!?guard:self.guardList !accepted:accepted)
  }
}

//------------------------------------------------*

var s0 = $semaphore (!value:0) {
  proc $T0.loop
}

//------------------------------------------------*

var s1 = $semaphore (!value:0) {
  proc $T1.loop
}

//------------------------------------------------*

task T0 priority 0 stackSize 512 {
  var top $uint32 = 0

  proc loop () {
    waitUntilMS (!deadline:self.top)
    s0.V ()
    self.top += 250
    ledOn (!LED_L0)
    waitUntilMS (!deadline:self.top)
    s0.V ()
    self.top += 250
    ledOff (!LED_L0)
  }
}

//------------------------------------------------*

task T1 priority 1 stackSize 512 {
  var top $uint32 = 0

  proc loop () {
    waitUntilMS (!deadline:self.top)
    s1.V ()
    self.top += 249
    ledOn (!LED_L4)
    waitUntilMS (!deadline:self.top)
    s1.V ()
    self.top += 249
    ledOff (!LED_L4)
  }
}

//------------------------------------------------*

task T2 priority 2 stackSize 512 {
  var flag0 = true
  var flag1 = true

  proc loop () {
    on s0.P () then
      if self.flag0 then
        ledOn  (!LED_L1)
      else
        ledOff (!LED_L1)
      end
      self.flag0 = self.flag0 xor true
    elson s1.P () then
      if self.flag1 then
        ledOn  (!LED_L3)
      else
        ledOff (!LED_L3)
      end
      self.flag1 = self.flag1 xor true
    end
  }
}

//------------------------------------------------*
