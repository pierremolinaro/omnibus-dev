target "teensy-3-1-tp"

//-----------------------------------------------------------------------------*

struct $rendezVous {
  var inputWaitList = $taskList ()
  var outputWaitList = $taskList ()
  var inputGuardList = $guardList ()
  var outputGuardList = $guardList ()

  public primitive input () {
    let found = makeTaskReady (!?list:self.outputWaitList)
    if not found then
      guardDidChange (!?guard:self.outputGuardList)
      blockInList (!?list:self.inputWaitList)
    end
  }

  public primitive output () {
    let found = makeTaskReady (!?list:self.inputWaitList)
    if not found then
      guardDidChange (!?guard:self.inputGuardList)
      blockInList (!?list:self.outputWaitList)
    end
  }

  public primitive inputUntil @noWarningIfUnused (?deadline:inDeadline $uint32) -> $bool {
    result = makeTaskReady (!?list:self.outputWaitList)
    if (not result) and (inDeadline > time.millis ()) then 
      blockInListAndOnDeadline (!?list:self.inputWaitList !deadline:inDeadline)
    end
  }

  public primitive outputUntil @noWarningIfUnused (?deadline:inDeadline $uint32) -> $bool {
    result = makeTaskReady (!?list:self.inputWaitList)
    if (not result) and (inDeadline > time.millis ()) then 
      blockInListAndOnDeadline (!?list:self.outputWaitList !deadline:inDeadline)
    end
  }

  guard input @noWarningIfUnused () {
    accept = makeTaskReady (!?list:self.outputWaitList)
    if not accept then
      handleGuardedCommand (!?guard:self.inputGuardList)
    end
  }

  guard output @noWarningIfUnused () {
    accept = makeTaskReady (!?list:self.inputWaitList)
    if not accept then
      handleGuardedCommand (!?guard:self.outputGuardList)
    end
  }

}

//-----------------------------------------------------------------------------*

var rdvs = $rendezVous () {
  func $T0.loop
  func $T1.loop
}

//-----------------------------------------------------------------------------*

task T0 priority 0 stackSize 512 {
  var top $uint32 = 0

  func loop () {
    time.waitUntilMS (!deadline:self.top)
    rdvs.output ()
    self.top += 250
    leds.on (!LED_L0)
    time.waitUntilMS (!deadline:self.top)
    rdvs.output ()
    self.top += 250
    leds.off (!LED_L0)
  }
}

//-----------------------------------------------------------------------------*

task T1 priority 1 stackSize 512 {

  func loop () {
    rdvs.input ()
    leds.on (!LED_L1)
    rdvs.input ()
    leds.off (!LED_L1)
  }
}

//-----------------------------------------------------------------------------*
