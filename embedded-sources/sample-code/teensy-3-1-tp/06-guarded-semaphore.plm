target "teensy-3-1-tp"

//-----------------------------------------------------------------------------*

struct $monSemaphore {
  var value $uint32
  var list = $taskList ()
  var guardList = $guardList ()

  public primitive P @noWarningIfUnused () {
    if self.value > 0 :
      self.value -= 1
    else
      blockInList (!?list:self.list)
    end
  }

  public service V () {
    let found = makeTaskReady (!?list:self.list)
    if not found :
      self.value += 1
      guardDidChange (!?guard:self.guardList)
    end
  }

  public guard P () {
    accept = self.value > 0
    if accept :
      self.value -= 1
    else
      handleGuardedCommand (!?guard:self.guardList)
    end
  }
  
  public guard PP () : self.P () {
  }
}

//-----------------------------------------------------------------------------*

var s0 = $monSemaphore (!value:0) {
  func $T0.loop
}

//-----------------------------------------------------------------------------*

var s1 = $monSemaphore (!value:0) {
  func $T1.loop
}

//-----------------------------------------------------------------------------*

task T0 priority 0 stackSize 512 {
  var top $uint32 = 0

  func loop () {
    time.waitUntilMS (!deadline:self.top)
    s0.V ()
    self.top += 250
    leds.on (!LED_L0)
    time.waitUntilMS (!deadline:self.top)
    s0.V ()
    self.top += 250
    leds.off (!LED_L0)
  }
}

//-----------------------------------------------------------------------------*

task T1 priority 1 stackSize 512 {
  var top $uint32 = 0

  func loop () {
    time.waitUntilMS (!deadline:self.top)
    s1.V ()
    self.top += 249
    leds.on (!LED_L4)
    time.waitUntilMS (!deadline:self.top)
    s1.V ()
    self.top += 249
    leds.off (!LED_L4)
  }
}

//-----------------------------------------------------------------------------*

task T2 priority 2 stackSize 512 {
  var deadline $uint32 = 0
  
  func loop () {
    sync
    while s0.PP () :
      leds.toggle  (!LED_L1)
    while s1.P () :
      leds.toggle  (!LED_L3)
    while time.waitUntilMS (!deadline:self.deadline) :
      self.deadline += 200
      leds.toggle  (!LED_L2)
    end
  }
}

//-----------------------------------------------------------------------------*
