target "teensy-3-1-tp"

//-----------------------------------------------------------------------------*

struct $monSemaphore {
  var value $uint32
  var list = $taskList ()
  var guardList = $guardList ()

  public primitive P @noWarningIfUnused () {
    if self.value > 0 {
      self.value -= 1
    }else{
      block (!?inList:self.list)
    }
  }

  public service V () {
    makeTaskReady (!?fromList:self.list ?found:let found)
    if not found {
      self.value += 1
      notifyChange (!?forGuard:self.guardList)
    }
  }

  public guard P () {
    accept = self.value > 0
    if accept {
      self.value -= 1
    }else{
      handle (!?guard:self.guardList)
    }
  }
  
  public guard PP () : self.P () {
  }
}

//-----------------------------------------------------------------------------*

var s0 = $monSemaphore (!value:0)

//-----------------------------------------------------------------------------*

var s1 = $monSemaphore (!value:0)

//-----------------------------------------------------------------------------*

task T0 priority 0 stackSize 512 {
  var top $uint32 = 0

  while time.waitUntilMS (!deadline:self.top) {
    s0.V ()
    self.top += 250
    leds.write (!on:LED_L0)
    time.waitUntilMS (!deadline:self.top)
    s0.V ()
    self.top += 250
    leds.write (!off:LED_L0)
  }
}

//-----------------------------------------------------------------------------*

task T1 priority 1 stackSize 512 {
  var top $uint32 = 0

  while time.waitUntilMS (!deadline:self.top) {
    s1.V ()
    self.top += 249
    leds.write (!on:LED_L4)
    time.waitUntilMS (!deadline:self.top)
    s1.V ()
    self.top += 249
    leds.write (!off:LED_L4)
  }
}

//-----------------------------------------------------------------------------*

task T2 priority 2 stackSize 512 {
  var deadline $uint32 = 0
  
  while s0.PP () {
   leds.write (!toggle:LED_L1)
  }
  
  while s1.P () {
    leds.write (!toggle:LED_L3)
  }
  
  while time.waitUntilMS (!deadline:self.deadline) {
    self.deadline += 200
    leds.write (!toggle:LED_L2)
  }
}

//-----------------------------------------------------------------------------*
