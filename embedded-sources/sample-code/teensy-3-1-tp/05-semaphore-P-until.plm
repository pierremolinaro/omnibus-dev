target "teensy-3-1-tp"

//-----------------------------------------------------------------------------*

struct $monSemaphore {
  var value $uint32
  var list = $taskList ()

  public service V () {
    makeTaskReady (!?list:self.list ?found:let found)
    if not found {
      self.value += 1
    }
  }

  public primitive P_until (?deadline:inDeadline $uint32) -> $bool {
    result = self.value > 0
    if result {
      self.value -= 1
    }else if inDeadline > time.millis () {
      blockInListAndOnDeadline (!?list:self.list !deadline:inDeadline)
    }
  }

}

//-----------------------------------------------------------------------------*

var s = $monSemaphore (!value:0)

//-----------------------------------------------------------------------------*

task T1 priority 1 stackSize 512 {
  var top $uint32 = 0

  init 123 {
    self.top = 1
  }
  
  init 256 {
    self.top += 2
  }
  
  while time.waitUntilMS (!deadline:self.top) {
    s.V ()
    self.top += 300
    leds.write (!on:LED_L0)
    time.waitUntilMS (!deadline:self.top)
    s.V ()
    self.top += 700
    leds.write (!off:LED_L0)
  }
}

//-----------------------------------------------------------------------------*

task T2 priority 2 stackSize 512 {
  var top $uint32 = 0

  while time.waitUntilMS (!deadline:0) { // no wait...
    let r = s.P_until (!deadline:self.top)
    if r {
      leds.write (!toggle:LED_L1)
    }else{
      self.top += 400
      leds.write (!toggle:LED_L4)
    }
  }
}

//-----------------------------------------------------------------------------*
