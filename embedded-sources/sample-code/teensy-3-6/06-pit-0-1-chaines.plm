target "teensy-3-6/privileged"
let F_CPU_MHZ = 192

module lcd (!DB4:.D16 !DB5:.D15 !DB6:.D14 !DB7:.D19 !RS:.D18 !ENABLE:.D17)
module timer ()

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
//   ACTIVITY LED                                                                                                       
//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

func safe activityLedOn @exported () {
  digitalWrite (!port:.D13 !yes)
}

func safe activityLedOff @exported () {
  digitalWrite (!port:.D13 !no)
}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

let LED_L0 = $digitalPort.D3

init 1000 {
  pinMode (!port:.D13 !mode:.OUTPUT) // Led Teensy
  pinMode (!port:LED_L0 !mode:.OUTPUT)
}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

module timer {
 
  init 1 {
  //--- Power on DMA_MUX, PIT, DMA
    SIM_SCGC6 |= {SIM_SCGC6 !PIT:1}
   //--- Enable PIT module
    PIT_MCR = 0
  //--- Disable PIT0 and PIT1
    PIT_TCTRL [0] = 0
    PIT_TCTRL [1] = 0
  //--- PIT0 clock frequency is F_BUS (in Hertz)
    PIT_LDVAL [0] = $uint32.max
    PIT_LDVAL [1] = $uint32.max
  //--- Enable PIT0 and PIT1 : start counting, no interrupt
    PIT_TCTRL [1] = {PIT_TCTRL !CHN:1 !TEN:1}
    PIT_TCTRL [0] = {PIT_TCTRL !TEN:1}
  }
  
  public system safe now @noUnusedWarning () -> $timerTick {
    var r $uint64 = extend (PIT_LTMR64H)
    r <<= 32
    r |= extend (PIT_LTMR64L)
    r = ~ r
    r +%= 1
    result = $timerTick (!rawValue: r)
  } 
}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

struct $timerTick @copyable {
  let rawValue $uint64
  
  public func raw @noUnusedWarning () -> $uint64 {
    result = self.rawValue
  }

  public func µs @noUnusedWarning () -> $uint32 {
    let r = (self.rawValue / F_BUS_MHZ) % 1_000
    result = truncate (r)
  }

  public func ms @noUnusedWarning () -> $uint32 {
    let r = (self.rawValue / (F_BUS_MHZ * 1_000)) % 1_000
    result = truncate (r)
  }

  public func s @noUnusedWarning () -> $uint32 {
    let r = self.rawValue / (F_BUS_MHZ * 1_000_000)
    result = truncate (r)
  }
}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

task Tâche priority 1 stackSize 512 {
  var compteur $uint32 = 0
  
  on time.waitUntilMS (!deadline:self.compteur) continue {
    let tick = timer.now ()
    toggle (!port:LED_L0)
    lcd.goto (!line:0 !column:0)
    lcd.print (!unsigned:self.compteur)
    lcd.goto (!line:1 !column:0)
    lcd.print (!u64:tick.raw () / F_BUS_MHZ)
    lcd.goto (!line:2 !column:0)
    lcd.print (!unsigned:tick.s ())
    lcd.print (!string:" ")
    lcd.print (!unsigned:tick.ms ())
    lcd.print (!string:" ")
    lcd.print (!unsigned:tick.µs ())
    self.compteur +%= 1_001
  }
}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
