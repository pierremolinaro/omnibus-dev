target "teensy-3-6/privileged"
let F_CPU_MHZ = 180

driver lcd (!DB4:.D16 !DB5:.D15 !DB6:.D14 !DB7:.D19 !RS:.D18 !ENABLE:.D17)

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
//   ACTIVITY LED                                                                                                       
//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

func activityLedOn @safe @exported () {
  digital.write (!yes !toPort:.D13)
}

func activityLedOff @safe @exported () {
  digital.write (!no !toPort:.D13)
}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

let LED_L0 = $digitalPort.D3
let LED_L1 = $digitalPort.D4
let LED_L2 = $digitalPort.D5

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

let SIZE = 10

sync $synchronizationBuffer {
  var data = $uint32 [SIZE] (!repeated:$uint32 ())
  var readIndex $uint32 = 0
  var writeIndex $uint32 = 0
  var readSemaphore = $semaphore (!value:0)
  var writeSemaphore = $semaphore (!value:SIZE)

  //············································································
  
  public func user write @mutating @noUnusedWarning (? value $uint32) {
    self.writeSemaphore.wait ()
    self.data [self.writeIndex] = value
    self.writeIndex += 1
    if self.writeIndex == SIZE {
      self.writeIndex = 0
    }
    self.readSemaphore.signal ()
  }
  
  //············································································
  
  public func user read @mutating @noUnusedWarning (! value $uint32) {
    self.readSemaphore.wait ()
    value = self.data [self.readIndex]
    self.readIndex += 1
    if self.readIndex == SIZE {
      self.readIndex = 0
    }
    self.writeSemaphore.signal ()
  }

  //············································································
  
  public guard read @noUnusedWarning (! value $uint32) : self.readSemaphore.wait () {
    value = self.data [self.readIndex]
    self.readIndex += 1
    if self.readIndex == SIZE {
      self.readIndex = 0
    }
    self.writeSemaphore.signal ()
  }

  //············································································

}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

sync buffer = $synchronizationBuffer ()

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

task Tâche1 priority 1 stackSize 512 {
  var compteur $uint32 = 0
  var index $uint32 = 0

  setup 1000 {
    digital.set (!mode:.OUTPUT !toPort:.D13) // Led Teensy
    digital.set (!mode:.OUTPUT !toPort:LED_L0)
    digital.set (!mode:.OUTPUT !toPort:LED_L1)
    digital.set (!mode:.OUTPUT !toPort:LED_L2)
  }
   
  on time.wait (!untilDeadline:self.compteur) {
    digital.toggle (!port:LED_L0)
    self.compteur +%= 500
    self.index += 1
    buffer.write (!self.index)
  }
}

task Tâche2 priority 2 stackSize 512 {
   
  on buffer.read (?var value $uint32) {
    lcd.goto (!line:0 !column:0)
    lcd.print (!unsigned:value)
  }
}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
