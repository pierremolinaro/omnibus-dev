target "teensy-3-6/unprivileged"
let F_CPU_MHZ = 180

driver lcd (!DB4:.D16 !DB5:.D15 !DB6:.D14 !DB7:.D19 !RS:.D18 !ENABLE:.D17)

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
//   ACTIVITY LED                                                                                                       
//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

func activityLedOn @safe @exported () {
  digital.write (!yes !toPort:.D13)
}

func activityLedOff @safe @exported () {
  digital.write (!no !toPort:.D13)
}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

let LED_L0 = $digitalPort.D3
let LED_L1 = $digitalPort.D4
let LED_L2 = $digitalPort.D5

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

sync $synchronizationGate {
  var isOpen $bool
  var taskList = $taskList ()
  var guardList = $guardList ()

  //············································································
  
  public system service close @noUnusedWarning @mutating () {
    self.isOpen = no
  }

  //············································································
  
  public system service open @noUnusedWarning @mutating () {
    if not self.isOpen {
      var continue = yes
      while continue {
        makeTaskReady (!?fromList:self.taskList ?found:continue)
      }
      notifyChange (!?forGuard:self.guardList)
      self.isOpen = yes
    }
  }

  //············································································
  
  public system primitive wait @noUnusedWarning @mutating () {
    if not self.isOpen {
      block (!?inList:self.taskList)
    }
  }

   //············································································

  public guard wait @noUnusedWarning () {
    accept = self.isOpen
    if not accept {
      handle (!?guard:self.guardList)
    }
  }
}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

sync s1 = $semaphore (!value:0)
sync s2 = $semaphore (!value:0)
sync gate = $synchronizationGate (!isOpen:no)

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

task Tâche1 priority 1 stackSize 512 {
  var compteur $uint32 = 0

  setup 1000 {
    digital.set (!mode:.OUTPUT !toPort:.D13) // Led Teensy
    digital.set (!mode:.OUTPUT !toPort:LED_L0)
    digital.set (!mode:.OUTPUT !toPort:LED_L1)
    digital.set (!mode:.OUTPUT !toPort:LED_L2)
  }
   
  on time.wait (!untilDeadline:self.compteur) {
    digital.toggle (!port:LED_L0)
    self.compteur +%= 500
    gate.open ()
    s1.wait ()
    s1.wait ()
    gate.open ()
    s2.signal ()
    s2.signal ()
  }
}

task Tâche2 priority 2 stackSize 512 {
 
  on gate.wait () {
    digital.toggle (!port:LED_L1)
    s1.signal ()
    s2.wait ()
  }
}

task Tâche3 priority 3 stackSize 512 {
   
  on gate.wait () {
    digital.toggle (!port:LED_L2)
    s1.signal ()
    s2.wait ()
  }
}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
