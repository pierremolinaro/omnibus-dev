target "teensy-3-6/xtr32"
let F_CPU_MHZ = 180

import "carte-tp-teensy-3-6.omnibus-import"

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

let SIZE = 10

sync $SynchronizationBuffer {
  var data = [SIZE : $u32] (!repeated:$u32 ())
  var readIndex $u32 = 0
  var writeIndex $u32 = 0
  var readSemaphore = $Semaphore (!value:0)
  var writeSemaphore = $Semaphore (!value:SIZE)

  //············································································

  public func write user @mutating @noUnusedWarning (? value $u32) {
    self.writeSemaphore.wait ()
    self.data [self.writeIndex] = value
    self.writeIndex += 1
    if self.writeIndex == SIZE {
      self.writeIndex = 0
    }
    self.readSemaphore.signal ()
  }

  //············································································

  public func read user @mutating @noUnusedWarning (! value $u32) {
    self.readSemaphore.wait ()
    value = self.data [self.readIndex]
    self.readIndex += 1
    if self.readIndex == SIZE {
      self.readIndex = 0
    }
    self.writeSemaphore.signal ()
  }

  //············································································

  public guard read @noUnusedWarning (! value $u32) : self.readSemaphore.wait () {
    value = self.data [self.readIndex]
    self.readIndex += 1
    if self.readIndex == SIZE {
      self.readIndex = 0
    }
    self.writeSemaphore.signal ()
  }

  //············································································

}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

sync buffer = $SynchronizationBuffer ()

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

task Tâche1 @stacksize 512 {
  var compteur $u32 = 0
  var index $u32 = 0

  event @onSetup first {
    digital.set (!mode:.output !toPort:LED_L0)
    digital.set (!mode:.output !toPort:LED_L1)
    digital.set (!mode:.output !toPort:LED_L2)
  }

  event time.wait (!until:self.compteur) {
    digital.toggle (!port:LED_L0)
    self.compteur +%= 500
    self.index += 1
    buffer.write (!self.index)
  }
}

task Tâche2 @stacksize 512 @autostart {

  event buffer.read (?var value $u32) {
    lcd.goto (!line:0 !column:0)
    lcd.print (!u32:value)
  }
}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
