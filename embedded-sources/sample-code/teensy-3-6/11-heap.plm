target "teensy-3-6/xtr32"
let F_CPU_MHZ = 240

import "carte-tp-teensy-3-6.plm-import"

option "task-strict-priority-order"

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

driver project > digital {
  startup {
    digital.set (!mode:.OUTPUT !toPort:.D13) // Led Teensy
    digital.set (!mode:.OUTPUT !toPort:LED_L4)
    digital.set (!mode:.OUTPUT !toPort:LED_L3)
    digital.set (!mode:.OUTPUT !toPort:LED_L0)
    digital.set (!mode:.OUTPUT !toPort:LED_L0)
    digital.set (!mode:.INPUT_PULLUP !toPort:BUTTON_P0)
  }
}

driver project ()

typealias Array8 = [UInt8]

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

struct StructWithARC {
  public var unEntierQuelconque : UInt32 = 0
  public var donnée = new Array8 ()
}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

struct AutreStructWithARC {
  public var autreEntierQuelconque : UInt32 = 0
  public var structure = new StructWithARC ()
  public var autreDonnée = new Array8 ()
}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

task T1 > T2 @stacksize 512 @activate {
  var échéance : UInt32 = 0
  var freeRam : UInt32 = 0
  var start : UInt32 = 0
  var maxAllocationParSeconde : UInt32 = 0
  var alloc : UInt32 = 0
  var current : UInt32 = 0

  on @setup first {
    lcd.print (!string:"Free:")
    lcd.goto (!line:1 !column:0)
    lcd.print (!string:"Start:")
    lcd.goto (!line:2 !column:0)
    lcd.print (!string:"Max:")
  }

  on time.wait (!until:self.échéance) {
    self.échéance +%= 1_000
    digital.toggle (!port:LED_L3)
    let freeRam = freeByteCount ()
    if self.freeRam ≠ freeRam {
      self.freeRam = freeRam
      lcd.goto (!line:0 !column:6)
      lcd.print (!u32:freeRam !width:6)
    }
    let start = heapStartAddress ()
    if self.start ≠ start {
      self.start = start
      lcd.goto (!line:1 !column:6)
      lcd.print (!hex8:start)
    }
    let alloc = totalAllocationObjectCount ()
    let allocationParSecondes = alloc - self.alloc
    if allocationParSecondes > self.maxAllocationParSeconde {
      self.maxAllocationParSeconde = allocationParSecondes
      lcd.goto (!line:2 !column:6)
      lcd.print (!u32:allocationParSecondes !width:7)
    }
    if self.alloc ≠ alloc {
      self.alloc = alloc
      lcd.goto (!line:3 !column:0)
      lcd.print (!u32:alloc !width:10)
    }
    let current = currentlyAllocatedObjectCount ()
    if self.current ≠ current {
      self.current = current
      lcd.goto (!line:3 !column:17)
      lcd.print (!u32:current !width:3)
    }
  }
}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

task T2 > backgroundTask @stacksize 512 @activate {
  var échéance1 : UInt32 = 2001
  var échéance2 : UInt32 = 2001
  var échéance3 : UInt32 = 2001
  var data1 = new Array8 ()
  var data2 = new Array8 ()
  var structure = new AutreStructWithARC ()
  var b : UInt8 = 0

  on time.wait (!until:self.échéance1) {
    self.échéance1 +%= 71
    digital.toggle (!port:LED_L4)
    if digital.read (!port:BUTTON_P0) {
      if self.data1.length () < 500 {
        self.data1.append (!self.b)
        self.b +%= 1
        // assert (self.b < 25) // For checking assert instruction
      }else{
        self.data1.removeAll ()
      }
    }else{
      self.data1.removeAll ()
    }
  }

  on time.wait (!until:self.échéance2) {
    self.échéance2 +%= 97
    self.data2 = self.data1
  }

  on time.wait (!until:self.échéance3) {
    self.échéance3 +%= 511
    self.structure.structure.donnée = self.data2
    self.testReleaseLocalVar (!in:self.structure.structure.donnée)
  }

  func testReleaseLocalVar (?in: inData Array8) {
    var d = new StructWithARC ()
    d.donnée = inData
  }
}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

task backgroundTask @stacksize 512 @activate {
  var data = new Array8 ()

  on time.wait (!until:0) {
    digital.toggle (!port:LED_L0)
    if digital.read (!port:BUTTON_P0) {
      if self.data.length () == 0 {
        self.data.append (!0)
        self.testReleaseLocalVar (!in:self.data)
      }else{
        self.data.removeAll ()
      }
    }else{
      self.data.removeAll ()
    }
  }

  func testReleaseLocalVar (?in: inData Array8) {
    externalSection (!in:inData)
    var d : UInt32 = 143
    var x : UInt3 = d [3 ... 5]
  }

}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

section externalSection (?in: inData Array8) {
  var d = inData
  d.append (!0)
  var xxxx = new [Bool] ()
  xxxx.append (!yes)
  var lg = xxxx.length ()
}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
