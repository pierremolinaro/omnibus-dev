
check target "teensy-3-6/xtr32", "teensy-3-6/xtr64"

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

driver time ()

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
//   TIME (for Cortex M4)
//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

driver time > root {
  var mUptime $uint32 = 0

  //····················································································································

  startup { // Configure Systick interrupt every ms
    ©(SYST:RVR) = F_CPU_MHZ * 1000 - 1 // $interrupt every ms
    ©(SYST:CVR) = 0
    ©(SYST:CSR) = ©{SYST:CSR !CLKSOURCE:1 !ENABLE:1 !TICKINT:1}
  }

  //····················································································································
  //   Functions callable in startup mode
  //····················································································································

  public func oneMillisecondBusyWait startup @noUnusedWarning () {
    while ©(SYST:CSR:COUNTFLAG) == 0 {}
  }

  //····················································································································

  public func busyWaitingDuringMS startup @noUnusedWarning (?inDelay $uint32) {
    for _ $uint32 in 0 ..< inDelay {
      self.oneMillisecondBusyWait ()
    }
  }

  //····················································································································
  //   Functions callable in panic mode
  //····················································································································

  public func panicOneMillisecondBusyWait panic @noUnusedWarning () {
    while ©(SYST:CSR:COUNTFLAG) == 0 {}
  }

  //····················································································································

  public func panicBusyWaitingDuringMS panic @noUnusedWarning (?inDelay $uint32) {
    for _ $uint32 in 0 ..< inDelay {
      self.panicOneMillisecondBusyWait ()
    }
  }

  //····················································································································
  //   systick interrupt service routine
  //····················································································································

  interrupt service systick {
 //   noteCurrentTaskFreeStackSize ()
    let now = self.mUptime +% 1
    self.mUptime = now
    makeTasksReady (!fromCurrentDate:now)
    notifyChangeForGuardedWaitUntil (!withCurrentDate:now)
  }

  //····················································································································
  //   Primitives callable in user mode
  //····················································································································

  public primitive wait @noUnusedWarning (?until: inDate $uint32) {
    if inDate > self.mUptime {
      block (!onDeadline:inDate)
    }
  }

  //····················································································································

  public primitive wait @noUnusedWarning (?during: inDelay $uint32) {
    if inDelay > 0 {
      block (!onDeadline:self.mUptime +% inDelay)
    }
  }

  //····················································································································

  public guard wait @noUnusedWarning (?until:inDeadline $uint32) {
//    noteCurrentTaskFreeStackSize ()
    accept = inDeadline ≤ self.mUptime
    if not accept {
      handle (!guardedDeadline:inDeadline)
    }
  }

  //····················································································································
  //   Primitives callable in user mode
  //····················································································································

  public section now safe @noUnusedWarning () -> $uint32 {
    result = self.mUptime
  }

  //····················································································································

}

//——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
