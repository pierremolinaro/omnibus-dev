
check target "teensy-3-1"

//——————————————————————————————————————————————————————————————————————————————
//   SYNCHRONIZATION TOOLS ROUTINES                                             
//——————————————————————————————————————————————————————————————————————————————

type $taskList : ((32)) @instantiable

//--- Block running task
extern func primitive
block (?!inList:ioWaitingList $taskList) : "blockInList"

extern func primitive
block (?onDeadline:inDeadline $uint32) : "blockOnDeadline"

extern func primitive
block (?!inList:ioWaitingList $taskList
       ?onDeadline:inDeadline $uint32) : "blockInListAndOnDeadline"

//--- Make task Ready
extern func service
makeTaskReady (?!fromList:ioWaitingList $taskList
               !found: outFound $bool) : "makeTaskReady"

extern func service
makeTasksReady (?fromCurrentDate:inCurrentDate $uint32) : "makeTasksReadyFromCurrentDate"

//——————————————————————————————————————————————————————————————————————————————
//   GUARD                                                                      
//——————————————————————————————————————————————————————————————————————————————

type $guardList : ((32)) @instantiable

extern func guard
handle (?!guard:ioGuard $guardList) : "handleGuardedCommand"

extern func guard
handle (?guardedDeadline:inDeadlineMS $uint32) : "handleGuardedWaitUntil"

extern func service
notifyChange (?!forGuard:ioGuard $guardList) : "guardDidChange"

extern func service
notifyChangeForGuardedWaitUntil (?withCurrentDate:inCurrentDate $uint32)
: "tickHandlerForGuardedWaitUntil"

//——————————————————————————————————————————————————————————————————————————————
//   ACTIVITY LED                                                               
//——————————————————————————————————————————————————————————————————————————————

required func safe activityLedOn @exported ()

required func safe activityLedOff @exported ()

//——————————————————————————————————————————————————————————————————————————————
//   INIT                                                                       
//——————————————————————————————————————————————————————————————————————————————

init 0 { // Configure Systick interrupt every ms
  SYST_RVR = 96000 - 1 // Interrupt every 96000 core clocks, i.e. every ms
  SYST_CVR = 0
  SYST_CSR = $SYST_CSR {CLKSOURCE, ENABLE, TICKINT}
}

//——————————————————————————————————————————————————————————————————————————————
//   TIME                                                                       
//——————————————————————————————————————————————————————————————————————————————

module time {
  var mUptimeMS $uint32 = 0 

  //············································································
  
  public func init oneMillisecondBusyWait @noUnusedWarning () {
    loop while not SYST_CSR.COUNTFLAG.bool {}
  }

  //············································································
  
  public func panic panicOneMillisecondBusyWait @noUnusedWarning () {
    loop while not SYST_CSR.COUNTFLAG.bool {}
  }

  //············································································
  
  public system safe millis @noUnusedWarning () -> $uint32 {
    result = self.mUptimeMS
  }
  
  //············································································
  
  public func init busyWaitingDuringMS @noUnusedWarning (?inDelay $uint32) {
    for _ $uint32 in 0 ..< inDelay {
      self.oneMillisecondBusyWait ()
    }
  }
  
  //············································································
  
  public func panic panicBusyWaitingDuringMS @noUnusedWarning (?inDelay $uint32) {
    for _ $uint32 in 0 ..< inDelay {
      self.panicOneMillisecondBusyWait ()
    }
  }
  
  //············································································
  
  isr service systick {
    let now = self.mUptimeMS +% 1
    self.mUptimeMS = now
    makeTasksReady (!fromCurrentDate:now)
    notifyChangeForGuardedWaitUntil (!withCurrentDate:now)
//    userTickHandler ()
  }
  
  //············································································
  
  public system primitive waitUntilMS @noUnusedWarning (?deadline: inDate $uint32) {
    if inDate > self.mUptimeMS {
      block (!onDeadline:inDate)
    }
  }
  
  //············································································
  
  public system primitive waitDuringMS @noUnusedWarning (?delay: inDelay $uint32) {
    if inDelay > 0 {
      block (!onDeadline:self.mUptimeMS +% inDelay)
    }
  }
  
  //············································································
  
  public guard waitUntilMS @noUnusedWarning (?deadline:inDeadline $uint32) {
    accept = (inDeadline) <= self.mUptimeMS
    if not accept {
      handle (!guardedDeadline:inDeadline)
    }
  }
  
  //············································································

}
 
//——————————————————————————————————————————————————————————————————————————————

