/*----------------------------------------------------------------------------*/
/*                                                                            */
/*                                   Memory                                   */
/*                                                                            */
/*----------------------------------------------------------------------------*/

MEMORY {
  flash (rx) : ORIGIN = 0, LENGTH = 256k 
  sram (rwx) : ORIGIN = 0x20000000, LENGTH = 32k 
}

/*----------------------------------------------------------------------------*/
/*                                                                            */
/*                                Discard                                     */
/*                                                                            */
/*----------------------------------------------------------------------------*/

SECTIONS {
  /DISCARD/ : {
    *(.gnu.linkonce.t.*) ;
    *(.glue_7t);
    *(.glue_7);
    *(.ARM.*);
    *(.comment);
    *(.debug_frame);
    *(.v4_bx);
    *(.iplt);
    *(.rel.*);
    *(.igot.plt);
    *(.note.GNU-stack);
  }
}

/*----------------------------------------------------------------------------*/
/*                                                                            */
/*                                ISR Vectors                                 */
/*                                                                            */
/*----------------------------------------------------------------------------*/

SECTIONS {
  .vectors : {
    __vectors_start = . ;
    KEEP (*(.isr_vector)) ;
    __vectors_end = . ;
  } > flash
}

/*----------------------------------------------------------------------------*/
/*                                                                            */
/*                                    Code                                    */
/*                                                                            */
/*----------------------------------------------------------------------------*/

SECTIONS {
  .text : {
    FILL(0xff)
    __code_start = . ;
  /*--- Code */
    *(.text*) ;
    *(text*) ;
    *(.vfp11_veneer);
  /*---- ROM data ----*/
    . = ALIGN(4);
    *(.rodata*);
    . = ALIGN(4);
    *(.shstrtab);
    . = ALIGN(4);
    __code_end = . ;
  } > flash
}

/*----------------------------------------------------------------------------*/
/*                                                                            */
/*                          BSS (uninitialized data)                          */
/*                                                                            */
/*----------------------------------------------------------------------------*/

SECTIONS {
  .bss (NOLOAD) : {
    . = ALIGN(4);
    __bss_start = . ;
    * (.bss*) ;
    * (COMMON) ;
    . = ALIGN(4);
    __bss_end = . ;
  } > sram
}

/*----------------------------------------------------------------------------*/
/*                                                                            */
/*                          Data (initialized data)                           */
/*                                                                            */
/*----------------------------------------------------------------------------*/

SECTIONS {
  .data : {
    FILL (0xFF)
    . = ALIGN (4) ;
    __data_start = . ;
    * (.data*) ;
    . = ALIGN (4) ;
    __data_end = . ;
  } > sram AT > flash
}


/*----------------------------------------------------------------------------*/

__data_load_start = LOADADDR (.data) ;
__data_load_end   = LOADADDR (.data) + SIZEOF (.data) ;

/*----------------------------------------------------------------------------*/
/*                                                                            */
/*                                System stack                                */
/*                                                                            */
/*----------------------------------------------------------------------------*/

SECTIONS {
  .system_stack :{
    . = ALIGN (4) ;
    __system_stack_start = . ;
    . += !SYSTEMSTACKSIZE! ;
    . = ALIGN (4) ;
    __system_stack_end = . ;
  } > sram
}

/*----------------------------------------------------------------------------*/
/*                                                                            */
/*                                    Heap                                    */
/*                                                                            */
/*----------------------------------------------------------------------------*/

SECTIONS {
  .heap : {
    . = ALIGN (4) ;
    __heap_start = . ;
  } > sram
}

/*----------------------------------------------------------------------------*/

__heap_end = ORIGIN(sram) + LENGTH(sram) ;

/*----------------------------------------------------------------------------*/
