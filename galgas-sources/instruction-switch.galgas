#—————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#—————————————————————————————————————————————————————————————————————————————————————————————————

list @switchCaseListAST {
  public var @lstringlist mCaseIdentifiers
  public var @instructionListAST mCaseInstructionList
}

#·······················································································································

class @switchInstructionAST : @instructionAST {
  public var @expressionAST mSwitchExpression
  public var @location mEndOf_test_expression
  public var @switchCaseListAST mSwitchCaseList
  public var @location mEndOf_switch_instruction
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#—————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <instruction> ?!@ast ioAST ?!@instructionListAST ioInstructionList  {
    $switch$
    let instructionLocation = @location.here
    <expression> !?ioAST ?let @expressionAST switchExpression
    let switchExpressionEndLocation = @location.here
    ${$
    var @switchCaseListAST switchCaseList = §[]
    repeat
      $case$
      var @lstringlist caseIdentifiers = §[]
      repeat
        $identifier$ ?let caseIdf
        caseIdentifiers += !caseIdf
      while
        $,$
      end
      $:$
      <instructionList> !?ioAST ?let instructionList
      switchCaseList += !caseIdentifiers !instructionList
    while
    end
    let endOfInstruction = @location.here
    $}$
    ioInstructionList += !@switchInstructionAST.init {
      !instructionLocation
      !switchExpression
      !switchExpressionEndLocation
      !switchCaseList
      !endOfInstruction
    }
  }
  
  #·····················································································································

}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @switchInstructionAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  [self.mSwitchExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
  for (* instructionList) in self.mSwitchCaseList do
    [instructionList noteInstructionListTypesInPrecedenceGraph !?ioGraph]
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @switchInstructionAST instructionSemanticAnalysis
  ?self:let @omnibusType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
#--- Analyze test expression
  var @instructionListIR switchExpressionGenerationList = §[]
  [self.mSwitchExpression analyzeExpression
    !self:inSelfType
    !routineAttributes: ()
    !optionalTargetType:voidType ()
    !context:inContext
    !mode:inMode
    !?temporary:ioTemporaries
    !?staticEntityMap:ioStaticEntityMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:switchExpressionGenerationList
    ?result:@objectIR switchValueIR
  ]
  [!?ioInstructionGenerationList appendLoadWhenReference
    !?ioTemporaries
    !?switchValueIR
  ]  
#--- Check inferred type
  let switchTypeName = @lstring.init {![switchValueIR type].omnibusTypeDescriptionName !self.mEndOf_test_expression}
  [inContext.mTypeConstantMap searchKey !switchTypeName ?let enumConstantMap]
#  let enumConstantMap = [switchValueIR type].classConstantMap
  if not [[switchValueIR type].kind isEnumeration] then
    error self.mEndOf_test_expression
      : "test expression type is '" + [switchValueIR omnibusTypeDescriptionName] + "', it should be boolean"
  end
  if [switchValueIR isStatic] then
    error self.mEndOf_test_expression : "test expression type should not be static"
  end
#--- Analyse case list
  [!?ioUniversalMap openOverrideForSelectBlock]
  var @stringset usedEnumerationValues = §[]
  var @switchCaseListIR switchCaseListIR = §[]
  for (caseIdentifiers instructionList) in self.mSwitchCaseList do
    var @uintlist caseIdentifierIndexList = §[]
    for (caseIdf) in caseIdentifiers do
      [enumConstantMap searchKey !caseIdf ?let @bigint constantIdx ?1*]
      caseIdentifierIndexList += !constantIdx
      if [usedEnumerationValues hasKey !caseIdf] then
        error caseIdf : "duplicated enumeration constant" fixit { remove }
      end
      usedEnumerationValues += !caseIdf
    end
    var @instructionListIR instructionGenerationList = §[]
    [instructionList analyzeBranchInstructionList
      !self:inSelfType
      !routineAttributes:inRoutineAttributes
      !endOfInstructionList:self.mEndOf_switch_instruction
      !context:inContext
      !mode:inMode
      !?temporary:ioTemporaries
      !?staticEntityMap:ioStaticEntityMap
      !?variableMap:ioUniversalMap
      !?alloca:ioAllocaList
      !?instructionListIR:instructionGenerationList
    ]
    switchCaseListIR += !caseIdentifierIndexList !instructionGenerationList
  end  
  [!?ioUniversalMap closeOverride !self.mEndOf_switch_instruction]
#--- Check all enumeration values are named in switch instructions
  for (lkey 2*) in enumConstantMap do
    if not [usedEnumerationValues hasKey !lkey] then
      error self.mEndOf_test_expression : "missing '" + lkey + "' enumeration constant"
    end  
  end 
#--- Code generation
  ioInstructionGenerationList += !@switchInstructionIR.init {
    ![self.mEndOf_test_expression endLocationIndex]
    !switchExpressionGenerationList
    !switchValueIR
    !switchCaseListIR
  }
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  CODE GENERATION                                                                                                      
#—————————————————————————————————————————————————————————————————————————————————————————————————

list @switchCaseListIR {
  public var @uintlist mCaseIdentifierIndexes
  public var @instructionListIR mCaseInstructionList
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

class @switchInstructionIR : @abstractInstructionIR {
  public var @uint mLabelIndex
  public var @instructionListIR mSwitchExpressionGenerationList
  public var @objectIR mSwitchExpression
  public var @switchCaseListIR mCaseGenerationList
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @switchInstructionIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext inGenerationContext
  ?!@generationAdds ioGenerationAdds
{
#--- Generate instructions that compute switch expression
  [self.mSwitchExpressionGenerationList instructionListLLVMCode !?ioLLVMcode !inGenerationContext !?ioGenerationAdds]
#--- Generate switch instruction
  let labelOtherwise = "switch." + self.mLabelIndex + ".otherwise"
  ioLLVMcode += "  switch " + [self.mSwitchExpression llvmTypeName] + " " + [self.mSwitchExpression llvmName]
  ioLLVMcode += ", label %" + labelOtherwise + " [\n"
  for (caseIdentifierIndexes *) in self.mCaseGenerationList do (branchIdx)
    for (caseIdfIndex) in caseIdentifierIndexes do
      ioLLVMcode += "    " + [self.mSwitchExpression llvmTypeName] + " " + caseIdfIndex
      ioLLVMcode += ", label %switch." + self.mLabelIndex + ".branch." + branchIdx + "\n"
    end
  end
  ioLLVMcode += "  ]\n\n"
#--- Generate branches
  for (* instructionList) in self.mCaseGenerationList do (branchIdx)
    ioLLVMcode += "switch." + self.mLabelIndex + ".branch." + branchIdx + ":\n"
    [instructionList instructionListLLVMCode !?ioLLVMcode !inGenerationContext !?ioGenerationAdds]
    ioLLVMcode += "  br label %" + labelOtherwise + "\n\n"
  end
#--- Otherwise label
  ioLLVMcode += labelOtherwise + ":\n"
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @switchInstructionIR enterAccessibleEntities
  ?!@stringset ioInvokedRoutineSet
  ?!@uint ioMaxBranchOfOnInstructions
{
  [self.mSwitchExpressionGenerationList enterAccessibleEntities !?ioInvokedRoutineSet !?ioMaxBranchOfOnInstructions]
  for (* instructionList) in self.mCaseGenerationList do
    [instructionList enterAccessibleEntities !?ioInvokedRoutineSet !?ioMaxBranchOfOnInstructions]
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
