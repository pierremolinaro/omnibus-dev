#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @typeDynamicArrayDeclarationAST : @abstractDeclarationAST {
  @lstring mAnonymousDynamicArrayTypeName
  @lstring mElementTypeName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension plm_syntax {

  #·····················································································································

  rule <type_definition> ?!@ast ioAST !@lstring outTypeName {
    $[$
    $\$type$ ?let elementTypeName
    $]$
    outTypeName = ["anonymous.dynamic.array." + ioAST.mTypeDeclarationIndex here]
    ioAST.mTypeDeclarationIndex ++
    ioAST.mDeclarationListAST += !@typeDynamicArrayDeclarationAST.new {!outTypeName !elementTypeName}
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  ADD EXTENSION                                                                                                        
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @typeDynamicArrayDeclarationAST addExtension
  ?!@extensionDeclarationListAST unused ioExtensionDeclarationListAST
  ?!@extendStaticArrayDeclarationAST unused ioExtendStaticArrayDeclarationAST
  !@abstractDeclarationAST outNewDeclaration
{
  outNewDeclaration = self
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @typeDynamicArrayDeclarationAST enterInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  let newTypeName = llvmTypeNameFromPLMname (!mAnonymousDynamicArrayTypeName)
  let elementTypeName = llvmTypeNameFromPLMname (!mElementTypeName)
  [!?ioGraph addNode !newTypeName !self]
  [!?ioGraph addEdge !newTypeName !elementTypeName]
  let rootNode = @lstring.new {!configurationNodeNameForPrecedenceGraph () !mAnonymousDynamicArrayTypeName.location}
  [!?ioGraph addEdge !newTypeName !rootNode]
}

#·······················································································································

override getter @typeDynamicArrayDeclarationAST keyRepresentationForErrorSignaling -> @string outRepresentation {
  outRepresentation = "anonymous $" + mAnonymousDynamicArrayTypeName 
}

#·······················································································································

override getter @typeDynamicArrayDeclarationAST location -> @location outLocation {
  outLocation = mAnonymousDynamicArrayTypeName.location
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  ENTER IN CONTEXT                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @typeDynamicArrayDeclarationAST enterInContext
  ?procedureDeclarationList:let @functionDeclarationListAST unused inProcedureListAST
  ?!context:@semanticContext ioContext
  ?!@declarationDecorationList ioDecoratedDeclarationList
  ?!@subprogramInvocationGraph ioSubprogramInvocationGraph
  ?!staticlistValues:@staticListInitializationMap unused ioStaticListValueMap
  ?!staticStringMap:@staticStringMap unused ioGlobalLiteralStringMap
{
#-------------------------------------------- Get element type
  [ioContext.mTypeMap searchKey !lkey:mElementTypeName ?type:let elementType ?1*]
  if not [elementType copyable] then
    error mElementTypeName : "an array element should be copyable"
  end
  if not [elementType instanciable] then
    error mElementTypeName : "an array element should be instanciable"
  end
#-------------------------------------------- Property map
  @propertyMap propertyMap = {}
#--- Method 'append'
  [!?propertyMap addFunctionWithInputArgument
    !?context:ioContext
    !?ioSubprogramInvocationGraph
    !receiverTypeName:mAnonymousDynamicArrayTypeName
    !methodName:"append"
    !safe:false
    !inputArgumentSelector:""
    !inputArgumentTypeName:mElementTypeName
    !inputArgumentName:"inValue"
    !resultTypeProxy:.null
    !canMutateProperties:true
  ]
#-------------------------------------------- Constructor map
  @constructorMap constructorMap = {}
  [!?constructorMap insertKey !["()" nowhere] !{} !.null]
#-------------------------------------------- Type attributes
  let @PLMTypeAttributes typeAttributes = .instanciable | .generateAssignmentRoutine | .copyable | .arc
#-------------------------------------------- Enter in type map
  let arrayType = @PLMType.new {
    !propertyMap:propertyMap
    !classConstantMap:{}
    !constructorMap:constructorMap
    !guardMap:{}
    !typeAttributes:typeAttributes
    !plmTypeName:mAnonymousDynamicArrayTypeName
    !kind:.dynamicArrayType {!elementType:elementType !ptrSize:ioContext.mTargetParameters.mPointerSize}
  }
  [!?ioContext.mTypeMap insertType
    !lkey:mAnonymousDynamicArrayTypeName
    !type:arrayType
    !index:[ioContext.mTypeMap count]
  ]
#---------------- Add decorated declaration
  ioDecoratedDeclarationList += !@decoratedTypeDynamicArrayDeclaration.new {
    !mAnonymousDynamicArrayTypeName
    !ioContext.mTargetParameters.mPointerSize
    !elementType
  }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTIC ANALYSIS                                                                                                    
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @decoratedTypeDynamicArrayDeclaration : @abstractDecoratedDeclaration {
  @string mAnonymousDynamicArrayTypeName
  @bigint mPtrSize
  @PLMType mElementType
}

#·······················································································································

override method @decoratedTypeDynamicArrayDeclaration semanticAnalysis
  ?context:let @semanticContext unused inContext
  ?!temporary:@semanticTemporariesStruct unused ioTemporaries
  ?!intermediateCode:@intermediateCodeStruct ioIntermediateCodeStruct
{
#----------- Generate enumeration specific code
  ioIntermediateCodeStruct.mGenerationListIR += !@dynamicArrayIR.new {
    !mAnonymousDynamicArrayTypeName
    !mPtrSize
    !mElementType
  }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  CODE GENERATION                                                                                                    
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @dynamicArrayIR : @abstractGenerationIR {
  @string mAnonymousDynamicArrayTypeName
  @uint mPtrSize
  @PLMType mElementType
}

#·······················································································································

override method @dynamicArrayIR generateLLVMcode
  ?!@string ioLLVMcode
  ?!@generationAdds ioGenerationAdds
{
#--- Add extern functions
  let insertFunction = "i32 @arc.insert.at.index "
                     + "(i32 %inPointer, i32 %inIndex, i32 %inElementSize, i32* %outElementPtr)"
  ioGenerationAdds.mExternFunctionDeclarationSet += !insertFunction
  let bufferLengthFunctionName = llvmNameForFunction (!"arc.buffer.length")
#  ioGenerationAdds.mExternFunctionDeclarationSet += !"i32 @" + bufferLengthFunctionName + " (i32 %inPointer)"

  let elementLLVMTypeName = [mElementType llvmTypeName]
#--- Method 'append'
  let argumentList = @routineFormalArgumentListAST {
    !.input
    !["" nowhere]
    ![mElementType.plmTypeName nowhere]
    !["inValue" nowhere]
  }
  let routineMangledName = routineMangledNameFromAST (
    !llvmTypeStringFromPLMname (!mAnonymousDynamicArrayTypeName)
    !["append" nowhere]
    !argumentList
  )
  let routineLLVMName = llvmNameForFunction (!routineMangledName.string)
  ioLLVMcode += llvmTitleComment (!routineMangledName)
  ioLLVMcode += "define internal void @" + routineLLVMName + " ("
  ioLLVMcode += "i" + mPtrSize + "* %ioArray"
  ioLLVMcode += ", " + elementLLVMTypeName + " %inValue"
  ioLLVMcode += ")" + llvmAttributeFunction () + "{\n"
  ioLLVMcode += ";--- Inserted element pointer\n"
  ioLLVMcode += " %insert.address = alloca i" + mPtrSize + "\n"
  ioLLVMcode += ";--- value pointer\n"
  ioLLVMcode += "  %value.pointer = alloca " + elementLLVMTypeName + "\n"
  ioLLVMcode += "  store " + elementLLVMTypeName + " %inValue, " + elementLLVMTypeName + "* %value.pointer\n"
  ioLLVMcode += ";--- Size of element\n"
  ioLLVMcode += "  %sizeAsPtr = getelementptr inbounds " + elementLLVMTypeName + ", " + elementLLVMTypeName + "* null, i32 1\n"
  ioLLVMcode += "  %element.byte.size = ptrtoint " + elementLLVMTypeName + "* %sizeAsPtr to i32\n"
  ioLLVMcode += ";--- Get buffer length\n"
  ioLLVMcode += "  %current.buffer.pointer = load i" + mPtrSize + ", i" + mPtrSize + "* %ioArray\n"
  ioLLVMcode += "  %current.buffer.length = call i" + mPtrSize + " @" + bufferLengthFunctionName
             +  " (i" + mPtrSize + " %current.buffer.pointer)\n"
  ioLLVMcode += ";--- Get inserted element pointer\n"
  ioLLVMcode += "  %new.buffer.pointer = call i" + mPtrSize + " @arc.insert.at.index "
             + "(i" + mPtrSize + " %current.buffer.pointer, i32 %current.buffer.length, i32 %element.byte.size, "
             + "i" + mPtrSize + "* %insert.address)\n"
  ioLLVMcode += ";--- Perform copy\n"
  ioLLVMcode += "  %typed.insert.address = bitcast i" + mPtrSize + "* %insert.address to " + elementLLVMTypeName + "*\n"
  ioLLVMcode += "  call void @plm.assign." + [mElementType.plmTypeName identifierRepresentation]
  ioLLVMcode += " (" + elementLLVMTypeName + "* %typed.insert.address, " + elementLLVMTypeName + "* %value.pointer)\n"
  ioLLVMcode += ";--- Store new buffer pointer\n"
  ioLLVMcode += "  store i" + mPtrSize + " %new.buffer.pointer, i" + mPtrSize + "* %ioArray\n"
  ioLLVMcode += ";--- Return\n"
  ioLLVMcode += "  ret void\n"
  ioLLVMcode += "}\n\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
