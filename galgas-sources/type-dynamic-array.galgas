#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @typeDynamicArrayDeclarationAST : @abstractDeclarationAST {
  @lstring mAnonymousDynamicArrayTypeName
  @lstring mElementTypeName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension plm_syntax {

  #·····················································································································

  rule <type_definition> ?!@ast ioAST !@lstring outTypeName {
    $[$
    $\$type$ ?let elementTypeName
    $]$
    outTypeName = ["dynarray." + ioAST.mTypeDeclarationIndex here]
    ioAST.mTypeDeclarationIndex ++
    ioAST.mDeclarationListAST += !@typeDynamicArrayDeclarationAST.new {!outTypeName !elementTypeName}
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  ADD EXTENSION                                                                                                        
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @typeDynamicArrayDeclarationAST addExtension
  ?!@extensionDeclarationListAST unused ioExtensionDeclarationListAST
  ?!@extendStaticArrayDeclarationAST unused ioExtendStaticArrayDeclarationAST
  !@abstractDeclarationAST outNewDeclaration
{
  outNewDeclaration = self
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @typeDynamicArrayDeclarationAST enterInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  let newTypeName = llvmTypeNameFromPLMname (!mAnonymousDynamicArrayTypeName)
  let elementTypeName = llvmTypeNameFromPLMname (!mElementTypeName)
  [!?ioGraph addNode !newTypeName !self]
  [!?ioGraph addEdge !newTypeName !elementTypeName]
  let rootNode = @lstring.new {!configurationNodeNameForPrecedenceGraph () !mAnonymousDynamicArrayTypeName.location}
  [!?ioGraph addEdge !newTypeName !rootNode]
}

#·······················································································································

override getter @typeDynamicArrayDeclarationAST keyRepresentationForErrorSignaling -> @string outRepresentation {
  outRepresentation = "anonymous $" + mAnonymousDynamicArrayTypeName 
}

#·······················································································································

override getter @typeDynamicArrayDeclarationAST location -> @location outLocation {
  outLocation = mAnonymousDynamicArrayTypeName.location
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  ENTER IN CONTEXT                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @typeDynamicArrayDeclarationAST enterInContext
  ?procedureDeclarationList:let @functionDeclarationListAST unused inProcedureListAST
  ?!context:@semanticContext ioContext
  ?!@declarationDecorationList ioDecoratedDeclarationList
  ?!@subprogramInvocationGraph ioSubprogramInvocationGraph
  ?!staticlistValues:@staticListInitializationMap unused ioStaticListValueMap
  ?!staticStringMap:@staticStringMap unused ioGlobalLiteralStringMap
{
  if not ioContext.mTargetParameters.mHandleDynamicArray then
    error mElementTypeName : "this target does not handle dynamic array"
  end
#-------------------------------------------- Get element type
  [ioContext.mTypeMap searchKey !lkey:mElementTypeName ?type:let elementType ?1*]
  if not [elementType copyable] then
    error mElementTypeName : "an array element should be copyable"
  end
  if not [elementType instanciable] then
    error mElementTypeName : "an array element should be instanciable"
  end
#-------------------------------------------- Property map
  @propertyMap propertyMap = {}
#--- Method 'removeAll()'
  [!?propertyMap addFunctionWithoutArgument
    !?ioSubprogramInvocationGraph
    !receiverTypeName:mAnonymousDynamicArrayTypeName
    !methodName:"removeAll"
    !safe:false
    !resultTypeProxy: .null
    !canMutateProperties:true
  ]
#--- Method 'length()'
  [@unifiedTypeMap-proxy makeProxy !?ioContext.mTypeMap !["uint32" nowhere] ?let uint32TypeProxy]
  [!?propertyMap addFunctionWithoutArgument
    !?ioSubprogramInvocationGraph
    !receiverTypeName:mAnonymousDynamicArrayTypeName
    !methodName:"length"
    !safe:false
    !resultTypeProxy: uint32TypeProxy
    !canMutateProperties:false
  ]
#--- Method 'append(?:)'
  [!?propertyMap addFunctionWithInputArgument
    !?context:ioContext
    !?ioSubprogramInvocationGraph
    !receiverTypeName:mAnonymousDynamicArrayTypeName
    !methodName:"append"
    !safe:false
    !inputArgumentSelector:""
    !inputArgumentTypeName:mElementTypeName
    !inputArgumentName:"inValue"
    !resultTypeProxy:.null
    !canMutateProperties:true
  ]
#--- Method 'insert(?:?atIndex:)'
  [!?propertyMap addFunctionWithTwoInputArguments
    !?context:ioContext
    !?ioSubprogramInvocationGraph
    !receiverTypeName:mAnonymousDynamicArrayTypeName
    !methodName:"append"
    !safe:false
    !inputArgumentSelector1:""
    !inputArgumentTypeName1:mElementTypeName
    !inputArgumentName1:"inValue"
    !inputArgumentSelector2:"atIndex"
    !inputArgumentTypeName2:"uint32"
    !inputArgumentName2:"inIndex"
    !resultTypeProxy:.null
    !canMutateProperties:true
  ]
#-------------------------------------------- Constructor map
  @constructorMap constructorMap = {}
  [!?constructorMap insertKey !["()" nowhere] !{} !.simple {!value:0G}]
#-------------------------------------------- Type attributes
  let @PLMTypeAttributes typeAttributes = .instanciable | .generateAssignmentRoutine | .copyable | .arc
#-------------------------------------------- Enter in type map
  let arrayType = @PLMType.new {
    !propertyMap:propertyMap
    !classConstantMap:{}
    !constructorMap:constructorMap
    !guardMap:{}
    !typeAttributes:typeAttributes
    !plmTypeName:mAnonymousDynamicArrayTypeName
    !kind:.dynamicArrayType {!elementType:elementType !ptrSize:ioContext.mTargetParameters.mPointerSize}
  }
  [!?ioContext.mTypeMap insertType
    !lkey:mAnonymousDynamicArrayTypeName
    !type:arrayType
    !index:[ioContext.mTypeMap count]
  ]
#---------------- Add decorated declaration
  ioDecoratedDeclarationList += !@decoratedTypeDynamicArrayDeclaration.new {
    !mAnonymousDynamicArrayTypeName
    !ioContext.mTargetParameters.mPointerSize
    !elementType
  }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTIC ANALYSIS                                                                                                    
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @decoratedTypeDynamicArrayDeclaration : @abstractDecoratedDeclaration {
  @string mAnonymousDynamicArrayTypeName
  @bigint mPtrSize
  @PLMType mElementType
}

#·······················································································································

override method @decoratedTypeDynamicArrayDeclaration semanticAnalysis
  ?context:let @semanticContext unused inContext
  ?!temporary:@semanticTemporariesStruct unused ioTemporaries
  ?!intermediateCode:@intermediateCodeStruct ioIntermediateCodeStruct
{
#----------- Generate enumeration specific code
  ioIntermediateCodeStruct.mGenerationListIR += !@dynamicArrayIR.new {
    !mAnonymousDynamicArrayTypeName
    !mPtrSize
    !mElementType
  }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  CODE GENERATION                                                                                                    
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @dynamicArrayIR : @abstractGenerationIR {
  @string mAnonymousDynamicArrayTypeName
  @uint mPtrSize
  @PLMType mElementType
}

#·······················································································································

override method @dynamicArrayIR generateLLVMcode
  ?!@string ioLLVMcode
  ?!@generationAdds unused ioGenerationAdds
{
  let pointer = "i" + mPtrSize
  let elementLLVMTypeName = [mElementType llvmTypeName]
  let bufferLengthFunctionName = llvmNameForFunction (!"arc.buffer.length")
#---------- Method 'removeAll()'
  let removeAllRoutineMangledName = routineMangledNameFromAST (
    !llvmTypeStringFromPLMname (!mAnonymousDynamicArrayTypeName)
    !["removeAll" nowhere]
    !{}
  )
  let removeAllRoutineLLVMName = llvmNameForFunction (!removeAllRoutineMangledName.string)
  ioLLVMcode += llvmTitleComment (!removeAllRoutineMangledName)
  ioLLVMcode += "define internal void @" + removeAllRoutineLLVMName + " (" + pointer + "* %ioArray)" + llvmAttributeFunction () + "{\n"
  ioLLVMcode += "  %buffer.pointer = load " + pointer + ", " + pointer + "* %ioArray\n"
  ioLLVMcode += "  call void @arc.release (" + pointer + " %buffer.pointer)\n"
  ioLLVMcode += "  store " + pointer + " 0, " + pointer + "* %ioArray\n"
  ioLLVMcode += "  ret void\n"
  ioLLVMcode += "}\n\n"
#---------- Method 'length()'
  let lengthRoutineMangledName = routineMangledNameFromAST (
    !llvmTypeStringFromPLMname (!mAnonymousDynamicArrayTypeName)
    !["length" nowhere]
    !{}
  )
  let lengthRoutineLLVMName = llvmNameForFunction (!lengthRoutineMangledName.string)
  ioLLVMcode += llvmTitleComment (!lengthRoutineMangledName)
  ioLLVMcode += "define internal i32 @" + lengthRoutineLLVMName + " (" + pointer + "* %ioArray)" + llvmAttributeFunction () + "{\n"
  ioLLVMcode += "  %buffer.pointer = load " + pointer + ", " + pointer + "* %ioArray\n"
  ioLLVMcode += "  %length = call i32 @" + bufferLengthFunctionName + "(" + pointer + " %buffer.pointer)\n"
  ioLLVMcode += "  ret i32 %length\n"
  ioLLVMcode += "}\n\n"
#---------- Method 'insert(?:?atIndex:)'
  @routineFormalArgumentListAST argumentList = {
    !.input
    !["" nowhere]
    ![mElementType.plmTypeName nowhere]
    !["inValue" nowhere]
  ,
    !.input
    !["atIndex" nowhere]
    !["uint32" nowhere]
    !["inIndex" nowhere]
  }
  let insertRoutineMangledName = routineMangledNameFromAST (
    !llvmTypeStringFromPLMname (!mAnonymousDynamicArrayTypeName)
    !["insert" nowhere]
    !argumentList
  )
  let insertRoutineLLVMName = llvmNameForFunction (!insertRoutineMangledName.string)
  ioLLVMcode += llvmTitleComment (!insertRoutineMangledName)
  ioLLVMcode += "define internal void @" + insertRoutineLLVMName + " ("
  ioLLVMcode += pointer + "* %ioArray"
  ioLLVMcode += ", " + elementLLVMTypeName + " %inValue"
  ioLLVMcode += ", i32 %inIndex"
  ioLLVMcode += ")" + llvmAttributeFunction () + "{\n"
  ioLLVMcode += ";--- Inserted element pointer\n"
  ioLLVMcode += "  %insert.address = alloca " + pointer + "\n"
  ioLLVMcode += ";--- value pointer\n"
  ioLLVMcode += "  %value.pointer = alloca " + elementLLVMTypeName + "\n"
  ioLLVMcode += "  store " + elementLLVMTypeName + " %inValue, " + elementLLVMTypeName + "* %value.pointer\n"
  ioLLVMcode += ";--- Size of element\n"
  ioLLVMcode += "  %sizeAsPtr = getelementptr inbounds " + elementLLVMTypeName + ", " + elementLLVMTypeName + "* null, i32 1\n"
  ioLLVMcode += "  %element.byte.size = ptrtoint " + elementLLVMTypeName + "* %sizeAsPtr to i32\n"
  ioLLVMcode += ";--- Get inserted element pointer\n"
  ioLLVMcode += "  %current.buffer.pointer = load " + pointer + ", " + pointer + "* %ioArray\n"
  ioLLVMcode += "  %new.buffer.pointer = call " + pointer + " @arc.insert.at.index "
             + "(" + pointer + " %current.buffer.pointer, i32 %inIndex, i32 %element.byte.size, "
             + pointer + "* %insert.address)\n"
  ioLLVMcode += ";--- Perform copy\n"
  ioLLVMcode += "  %typed.insert.address = bitcast " + pointer + "* %insert.address to " + elementLLVMTypeName + "*\n"
  ioLLVMcode += "  call void @plm.assign." + [mElementType.plmTypeName identifierRepresentation]
  ioLLVMcode += " (" + elementLLVMTypeName + "* %typed.insert.address, " + elementLLVMTypeName + "* %value.pointer)\n"
  ioLLVMcode += ";--- Store new buffer pointer\n"
  ioLLVMcode += "  store " + pointer + " %new.buffer.pointer, " + pointer + "* %ioArray\n"
  ioLLVMcode += ";--- Return\n"
  ioLLVMcode += "  ret void\n"
  ioLLVMcode += "}\n\n"
#---------- Method 'append(?:)'
  argumentList = {
    !.input
    !["" nowhere]
    ![mElementType.plmTypeName nowhere]
    !["inValue" nowhere]
  }
  let routineMangledName = routineMangledNameFromAST (
    !llvmTypeStringFromPLMname (!mAnonymousDynamicArrayTypeName)
    !["append" nowhere]
    !argumentList
  )
  var routineLLVMName = llvmNameForFunction (!routineMangledName.string)
  ioLLVMcode += llvmTitleComment (!routineMangledName)
  ioLLVMcode += "define internal void @" + routineLLVMName + " ("
  ioLLVMcode += pointer + "* %ioArray"
  ioLLVMcode += ", " + elementLLVMTypeName + " %inValue"
  ioLLVMcode += ")" + llvmAttributeFunction () + "{\n"
  ioLLVMcode += ";--- Get buffer length\n"
  ioLLVMcode += "  %current.buffer.pointer = load " + pointer + ", " + pointer + "* %ioArray\n"
  ioLLVMcode += "  %current.buffer.length = call " + pointer + " @" + bufferLengthFunctionName
             +  " (" + pointer + " %current.buffer.pointer)\n"
  ioLLVMcode += ";--- Insert\n"
  ioLLVMcode += "  call void @" + insertRoutineLLVMName
             + "(" + pointer + "* %ioArray, " + elementLLVMTypeName + " %inValue, i32 %current.buffer.length)\n"
  ioLLVMcode += ";--- Return\n"
  ioLLVMcode += "  ret void\n"
  ioLLVMcode += "}\n\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
