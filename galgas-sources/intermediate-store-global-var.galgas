
setter @instructionListIR appendStoreGlobalVariable
  ?let @string inGlobalVarName
  ?let @unifiedTypeMap-proxy inTargetVarType
  ?let @operandIR inSourceValue
  ?volatile:let @bool inIsVolatile
{
  self += !@storeGlobalVariableIR.new {!inGlobalVarName !inTargetVarType !inSourceValue !inIsVolatile}
}

#----------------------------------------------------------------------------------------------------------------------*

class @storeGlobalVariableIR : @abstractInstructionIR {
  @string mGlobalVarName
  @unifiedTypeMap-proxy mTargetVarType
  @operandIR mSourceValue
  @bool mIsVolatile
}

#----------------------------------------------------------------------------------------------------------------------*

override method @storeGlobalVariableIR llvmInstructionCode
  ?!@string ioCode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  let llvmType = [mTargetVarType llvmTypeName]
  ioCode += "  store "
  if mIsVolatile then
    ioCode += "volatile "
  end
  ioCode += llvmType + " " + mSourceValue.mValue + ", " + llvmType + "* @" + mangledNameForGlobalVariable (!mGlobalVarName) + "\n"
}

#----------------------------------------------------------------------------------------------------------------------*

override method @storeGlobalVariableIR enterAccessibleEntities ?!@accessibleEntities ioAccessibleEntities {
  ioAccessibleEntities.mGlobalVariableSet += !mGlobalVarName
}

#----------------------------------------------------------------------------------------------------------------------*
