#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! AST                                                                                                                  
#—————————————————————————————————————————————————————————————————————————————————————————————————

enum @effectiveArgumentPassingModeAST {
  case input (@lstring name)
  case inputWithType (@bool constant @lstring typeName @lstring name)
  case output (@expressionAST exp @location endOfExp)
  case outputInput (@lstring name)
  case outputInputSelfVariable (@lstring name)
}

#·······················································································································

getter @effectiveArgumentPassingModeAST matchingFormalArgument -> @string {
  switch self 
  case input (*) : result = "!"
  case inputWithType (* * *) : result = "!"
  case output (* *) : result = "?"
  case outputInput (*) : result = "?!"
  case outputInputSelfVariable (*) : result = "?!"
  end
}

#·······················································································································

list @effectiveArgumentListAST {
  public var @effectiveArgumentPassingModeAST mEffectiveParameterKind
  public var @lstring mSelector
}

#·······················································································································

getter @effectiveArgumentListAST mangledName
    ?receiverTypeName: let @string inReceiverTypeName
    ?functionName: let @lstring inFunctionName
-> @lstring {
  var s = ""
  if inReceiverTypeName != "" then
    s += inReceiverTypeName + "."
  end
  s += inFunctionName.string + "("
  for (effectiveArgumentPassingMode selector) in self do
    s += [effectiveArgumentPassingMode matchingFormalArgument] + selector + ":"
  end
  s += ")"
  result = .init {!s !inFunctionName.location}
}

#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SYNTAX                                                                                                               
#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <effective_parameters> ?!@ast ioAST !@effectiveArgumentListAST outEffectiveParameterList !@location outEndOfArgs {
    outEffectiveParameterList = §[]
    $($
    repeat
    while
      $!selector:$ ?let selector
      <expression> !?ioAST ?let expression
      outEffectiveParameterList += !.output{!exp:expression !endOfExp:.here} !selector
    while
      $!?selector:$ ?let selector
      $identifier$ ?let effectiveParameterName
      outEffectiveParameterList += !.outputInput{!name:effectiveParameterName} !selector
    while
      $!?selector:$ ?let selector
      $self$
      $.$
      $identifier$ ?let effectiveParameterName
      outEffectiveParameterList += !.outputInputSelfVariable{!name:effectiveParameterName} !selector
    while
      $?selector:$ ?let selector
      $identifier$ ?let effectiveParameterName
      outEffectiveParameterList += !.input{!name:effectiveParameterName} !selector
    while
      $?selector:$ ?let selector
      let @bool constant
      select
        $var$
        constant = false
      or
        $let$
        constant = true
      end
      $identifier$ ?let effectiveParameterName
      let @lstring typeName
      select
        typeName = ["" nowhere]
      or
        <type_definition> !?ioAST ?typeName
      end
      outEffectiveParameterList += 
        !.inputWithType{!constant:constant !typeName:typeName !name:effectiveParameterName}
        !selector
    end
    outEndOfArgs = .here
    $)$
  }

  #·····················································································································

}

#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SEMANTICS                                                                                                            
#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc analyzeEffectiveParameters
  ?self:let @omnibusType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?formal:let @routineTypedSignature inFormalSignature
  ?effective:let @effectiveArgumentListAST inEffectiveParameterList
  ?errorLocation:let @location inErrorLocation
  ?context:let @semanticContext inContext
  ?requiredMode:let @mode inRequiredMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  ?!effectiveIR:@procCallEffectiveParameterListIR ioEffectiveParameterListIR
{
#--- Analyze effective parameters
  var @procEffectiveParameterList parameterList = §[]
  for (* * formal_mTypeProxy *) in inFormalSignature, (effective_mEffectiveParameterKind effective_mSelector) in inEffectiveParameterList do
    switch effective_mEffectiveParameterKind 
    case inputWithType (@bool constant @lstring typeName @lstring name) :
      let type = if typeName.string == "" then
        [formal_mTypeProxy type]
      else
        [[inContext.mTypeMap searchKey !typeName] type]
      end
      if constant then
        [!?ioUniversalMap insertLocalConstant !name !false !type !name]
      else
        [!?ioUniversalMap insertLocalVariable !name !type !name !.hasUnreadValue !false]
      end
      ioAllocaList += !llvmNameForLocalVariable (!name) !type !true
      parameterList += !effective_mEffectiveParameterKind !effective_mSelector !type
      ioEffectiveParameterListIR +=
        !.input
        !.reference {!type:type !llvmName:llvmNameForLocalVariable (!name)}
    case input (@lstring name) :
      [!?ioUniversalMap searchValuedObjectForWriteAccess !name ?let objectIR]
#      if not accessIsAllowed then
#        error name : "routine has no access right on the this variable"
#      end
#      if not canBeUsedAsInputParameter then
#        error name : "this variable cannot be used as input parameter"
#      end
#      if not isCopyable then
#        error name : "an $" + [type key] + " instance is not copyable"
#      end
      parameterList += !effective_mEffectiveParameterKind !effective_mSelector ![objectIR type]
      ioEffectiveParameterListIR +=
        !.input
        !.reference {
          !type:[objectIR type]
          !llvmName:[[objectIR llvmName] nowhere]
        }
    case output (@expressionAST expression @location endOfExp) :
      [expression analyzeExpression
        !self:inSelfType
        !routineAttributes:inRoutineAttributes
        !optionalTargetType:[formal_mTypeProxy type]
        !context:inContext
        !mode:inRequiredMode
        !?temporary:ioTemporaries
        !?staticEntityMap:ioStaticEntityMap
        !?variableMap:ioUniversalMap
        !?alloca:ioAllocaList
        !?instructionListIR:ioInstructionGenerationList
        ?result:@objectIR expressionResult
      ]
      [!?ioInstructionGenerationList appendLoadWhenReference
        !?ioTemporaries
        !?expressionResult
      ]  
      let result = checkAssignmentCompatibility (
        !inContext.mAssignmentOperatorMap
        !source:expressionResult
        !targetAnnotationType:[formal_mTypeProxy type]
        !errorLocation:endOfExp
        !staticTypeAllowed:false
      )
      parameterList += !effective_mEffectiveParameterKind !effective_mSelector ![formal_mTypeProxy type]
      ioEffectiveParameterListIR += !.output !result
    case outputInput (@lstring name) :
      [!?ioUniversalMap searchValuedObjectForReadWriteAccess !name ?let objectIR]
      parameterList += !effective_mEffectiveParameterKind !effective_mSelector ![objectIR type]
      let argumentIR = @objectIR.reference {
        !type:[objectIR type]
        !llvmName:[objectIR llvmName]
      }
      ioEffectiveParameterListIR += !.outputInput !argumentIR
    case outputInputSelfVariable (@lstring name) :
#      let typeOmnibusName = @lstring.new {!inSelfType.omnibusTypeDescriptionName !name.location}
      let propertyGetterMap = [inContext propertyGetterMap !inSelfType]
#      [inContext.mTypePropertyGetterMap searchKey !typeOmnibusName ?let propertyGetterMap]
      [propertyGetterMap searchKey !name ?* ?let @propertyGetterKind propertyAccess]
      switch propertyAccess
      case constantProperty (*) :
        error inErrorLocation : "a constant property cannot be used as output / input parameter"
      case storedProperty (@omnibusType propertyType @uint propertyIndex) :
        [!?ioTemporaries newTempLLVMVar ?let property_llvmName]
        [!?ioInstructionGenerationList appendGetUniversalPropertyReference
          !inSelfType
          !llvmNameForSelf ()
          !property_llvmName
          !propertyIndex
          !name
        ]
        parameterList += !effective_mEffectiveParameterKind !effective_mSelector !propertyType
        ioEffectiveParameterListIR += !.outputInput !.reference {!type:propertyType !llvmName:property_llvmName}
      case computedProperty (2*) :
        error inErrorLocation : "a computed property cannot be used as output / input parameter"
      end    
    end
  end
#--- Check parameters
  if [inFormalSignature count] != [inEffectiveParameterList count] then
    error inErrorLocation: "this routine requires " + [inFormalSignature count] + " argument(s), this call names "
      + [inEffectiveParameterList count] + " argument(s)"
  else
    for (formal_mFormalArgumentPassingMode formal_mSelector formal_mTypeProxy *) in inFormalSignature, (actual_mEffectiveParameterPassingMode actual_mSelector actual_mParameterType) in parameterList do
      if [formal_mTypeProxy type].omnibusTypeDescriptionName != actual_mParameterType.omnibusTypeDescriptionName then
        error actual_mSelector : "the actual parameter type is $" + actual_mParameterType.omnibusTypeDescriptionName +
           ", and is incompatible with the formal type $" + [formal_mTypeProxy key]
      end
      let requiredPassingMode = [formal_mFormalArgumentPassingMode requiredActualPassingModeForSelector !formal_mSelector]
      let testedPassingMode = [actual_mEffectiveParameterPassingMode passingModeForActualSelector !actual_mSelector]
      if requiredPassingMode != testedPassingMode then
        error actual_mSelector : "the required selector is '" + requiredPassingMode + "'"
      end
    end  
  end
}

#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
