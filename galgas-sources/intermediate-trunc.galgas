#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

setter @instructionListIR appendTrunc
  ?let @omnibusType inResultType
  ?!@objectIR ioObject
  ?!@semanticTemporariesStruct ioTemporaries
{
  if [ioObject isLiteralInteger] then
    [ioObject literalInteger ?type:* ?value:let @bigint value]
    ioObject = .literalInteger {!type:inResultType !value:value}
  else
    getNewTempValue (!inResultType !?ioTemporaries ?let result)
    self += !@truncIR.new {!result !ioObject}
    ioObject = result
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

shared class @truncIR : @abstractInstructionIR {
  @objectIR mResult
  @objectIR mSource
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @truncIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  ioLLVMcode += "  " + [self.mResult llvmName] + " = trunc "
  ioLLVMcode += [self.mSource  llvmTypeName] + " " + [self.mSource llvmName] + " to " + [self.mResult llvmTypeName] + "\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
