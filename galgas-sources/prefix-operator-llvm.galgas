#—————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#—————————————————————————————————————————————————————————————————————————————————————————————————

enum @llvmPrefixOperatorEnumeration {
 case notOp   # not
 case bitWiseComplement   # ~
 case minusOp # -
 case minusOpNoOvf # -%
}

#·······················································································································

getter @llvmPrefixOperatorEnumeration string -> @string {
  switch self
  case notOp   : result = "not"
  case minusOp : result = "-"
  case minusOpNoOvf : result = "-%"
  case bitWiseComplement : result = "~"
  end
}

#·······················································································································

getter @llvmPrefixOperatorEnumeration prefixOperator -> @prefixOperator {
  switch self
  case notOp   : result = .notOp
  case minusOp : result = .minusOp
  case minusOpNoOvf : result = .minusNoOvf
  case bitWiseComplement : result = .bitWiseComplement
  end
}

#·······················································································································

refclass @llvmPrefixOperatorAST : @abstractDeclarationAST {
  public var @llvmPrefixOperatorEnumeration mPrefixOperator
  public var @location mPrefixOperatorLocation
  public var @lstring mReceiverName
  public var @lstring mReceiverTypeName
  public var @llvmGenerationInstructionList mInstructionList
  public var @lstring mResultTypeName
}

#·······················································································································

override getter @llvmPrefixOperatorAST locationForErrorSignaling -> @location outLocation {
  outLocation = self.mPrefixOperatorLocation
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

proc appendPrefixOperatorDeclaration_minus
     ?let @string inOmnibusReceiverTypeName
     ?!@semanticTypePrecedenceGraph ioPrecedenceGraph
{
#--- Operator type
  var @llvmGenerationInstructionList llvmInstructionList = §[]
  var @llvmGenerationInstructionElementList instructionElements = §[]
  instructionElements += !.symbol {!name: ["result" nowhere]}
  instructionElements += !.string {!string: " = sub "}
  instructionElements += !.type {!name: ["SELF" nowhere]}
  instructionElements += !.string {!string: " 0, "}
  instructionElements += !.symbol {!name: ["SELF" nowhere]}
  llvmInstructionList += !@llvmGenerationInstruction (instructionElements)
  var @abstractDeclarationAST declaration = @llvmPrefixOperatorAST.init {
    !.minusOp
    !.nowhere
    !["SELF" nowhere]
    ![inOmnibusReceiverTypeName nowhere]
    !llvmInstructionList
    ![inOmnibusReceiverTypeName nowhere]
  }
  [declaration enterInPrecedenceGraph !?ioPrecedenceGraph]
  declaration = @llvmPrefixOperatorAST.init {
    !.minusOpNoOvf
    !.nowhere
    !["SELF" nowhere]
    ![inOmnibusReceiverTypeName nowhere]
    !llvmInstructionList
    ![inOmnibusReceiverTypeName nowhere]
  }
  [declaration enterInPrecedenceGraph !?ioPrecedenceGraph]
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

proc appendPrefixOperatorDeclaration_bitWiseComplement
     ?let @string inOmnibusReceiverTypeName
     ?!@semanticTypePrecedenceGraph ioPrecedenceGraph
{
#--- Operator type
  var @llvmGenerationInstructionList llvmInstructionList = §[]
  var @llvmGenerationInstructionElementList instructionElements = §[]
  instructionElements += !.symbol {!name: ["result" nowhere]}
  instructionElements += !.string {!string: " = xor "}
  instructionElements += !.type {!name: ["SELF" nowhere]}
  instructionElements += !.string {!string: " "}
  instructionElements += !.symbol {!name: ["SELF" nowhere]}
  instructionElements += !.string {!string: ", -1"}
  llvmInstructionList += !@llvmGenerationInstruction (instructionElements)
  let @abstractDeclarationAST declaration = @llvmPrefixOperatorAST.init {
    !.bitWiseComplement
    !.nowhere
    !["SELF" nowhere]
    ![inOmnibusReceiverTypeName nowhere]
    !llvmInstructionList
    ![inOmnibusReceiverTypeName nowhere]
  }
  [declaration enterInPrecedenceGraph !?ioPrecedenceGraph]
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#!  SYNTAX                                                                                                               
#—————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <llvmPrefixOperator> !@llvmPrefixOperatorEnumeration outOperator {
    select
      $not$
      outOperator = .notOp
    or
      $-$
      outOperator = .minusOp
    or
      $-%$
      outOperator = .minusOpNoOvf
    or
      $~$
      outOperator = .bitWiseComplement
    end
  }
  
  #·····················································································································

  rule <declaration> ?!@ast ioAST {
    $llvm$
    $func$
    let operationLocation = @location.here
    <llvmPrefixOperator> ?let prefixOperator
    $($
    $identifier$ ?let receiverName
    $\$type$ ?let receiverType
    $)$
    $->$
    $\$type$ ?let resultType
    ${$
    <llvm_instruction_list> ?let instructionList
    $}$
    ioAST.mDeclarationListAST += !@llvmPrefixOperatorAST.init {
      !prefixOperator
      !operationLocation
      !receiverName
      !receiverType
      !instructionList
      !resultType
    }
  }

  #·····················································································································

}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH                                                                                                     
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @llvmPrefixOperatorAST enterInPrecedenceGraph
  ?!@semanticTypePrecedenceGraph ioGraph
{
  let nodeName = @lstring.init {![self.mPrefixOperator string] + "." + self.mReceiverTypeName !self.mPrefixOperatorLocation}
  [!?ioGraph addNode !nodeName !self]
  [!?ioGraph addEdge !nodeName !self.mReceiverTypeName]
  [!?ioGraph addEdge !nodeName !self.mResultTypeName]
}

#·······················································································································

override getter @llvmPrefixOperatorAST keyRepresentationForErrorSignaling -> @string outRepresentation {
  outRepresentation = [self.mPrefixOperator string] + " " + self.mReceiverTypeName
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#!  ENTER IN CONTEXT                                                                                                     
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @llvmPrefixOperatorAST enterInContext
  ?!context:@semanticContext ioContext
  ?!@declarationDecorationList unused ioDecoratedDeclarationList
  ?!@decoratedRegularRoutineList unused ioDecoratedRoutineList
  ?!@routineListIR ioRoutineListIR
  ?!staticlistValues:@staticListInitializationMap unused ioStaticListValueMap
  ?!staticEntityMap:@staticEntityMap unused ioStaticEntityMap
  ?!registerUserAccess:@controlRegisterUserAccesMapAST unused ioControlRegisterUserAccesMapAST
  ?!userDefinedTypes:@userLLVMTypeDefinitionListIR unused ioUserLLVMTypeDefinitionListIR
{
#--- Check types
  [!?ioContext.mTypeMap makeEntry !self.mReceiverTypeName ?let receiverTypeProxy]
  let receiverType = [receiverTypeProxy type]
  [receiverType checkIsNotCompileTimeType !self.mReceiverTypeName.location]
  [!?ioContext.mTypeMap makeEntry !self.mResultTypeName ?let resultTypeProxy]
  let resultType = [resultTypeProxy type]
  [resultType checkIsNotCompileTimeType !self.mResultTypeName.location]
#--- Enter in infix operator map
  let prefixMangledFunctionName = prefixOperatorFunctionName (!receiverType !self.mPrefixOperator)
  let key = prefixOperatorMapKey (!receiverType !self.mPrefixOperator !self.mPrefixOperatorLocation)
  let definition = @llvmPrefixOperatorUsage.init {
    !prefixMangledFunctionName
    !self.mReceiverName
    !self.mInstructionList
  }
  [!?ioContext.mPrefixOperatorMap insertKey !key !receiverType !resultType !definition]
#--- Generation routine
  var @assignmentGenerationVarMap varMap = §[]
  [!?varMap insertKey !self.mReceiverName !"%" + self.mReceiverName]
  var @assignmentGenerationVarMap typeMap = §[]
  [!?typeMap insertKey !self.mReceiverName ![receiverType llvmTypeName]]
  [!?typeMap insertKey !["result" nowhere] ![resultType llvmTypeName]]
  var @llvmGenerationInstructionList instructionList = §[]
  instructionList += !@llvmVarInstruction.init {!["result" nowhere]}
  instructionList += self.mInstructionList
  [!?instructionList appendReturn !["result" nowhere] !["result" nowhere]]
  var @stringlist generatedInstructions = §[]
  var @allocaList allocaList = §[]
  var @uint temporaryIndex = 0
  [instructionList generateIRCode !varMap !typeMap !?generatedInstructions !?temporaryIndex !?allocaList]
  let routine = @prefixOperatorRoutineIR.init {
    !.init {!prefixMangledFunctionName !self.mPrefixOperatorLocation}
    !isRequired: true
    !warnsIfUnused: false
    !receiverType
    !self.mReceiverName.string
    !resultType
    !generatedInstructions
    !allocaList
  }
  ioRoutineListIR += !routine
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#   INFIX OPERATOR MAP
#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @llvmPrefixOperatorUsage : @prefixOperatorUsage {
  public var @string mPrefixMangledFunctionName
  public var @lstring mReceiverOperandName
  public var @llvmGenerationInstructionList mInstructionList
}

#·······················································································································

override method @llvmPrefixOperatorUsage generateCode
  ?let @objectIR inReceiverOperand
  ?let @location inOperatorLocation
  ?let @omnibusType inResultType
  ?noPanicCode: let @bool unused inDoNotGeneratePanicCode
  ?safeMode: let @bool unused inSafeMode
  ?!@semanticTemporariesStruct ioTemporaries
  ?!@instructionListIR ioInstructionGenerationList
  !@objectIR outResultValue
{
#--- If reference, load it
  var receiverOperand = inReceiverOperand
  [!?ioInstructionGenerationList appendLoadWhenReference
    !?ioTemporaries
    !?receiverOperand
  ]  
#--- New LLVM variable for result
  getNewTempValue (!inResultType !?ioTemporaries ?outResultValue)
  var @procCallEffectiveParameterListIR argumentList = §[]
  argumentList += !.output !receiverOperand
  ioInstructionGenerationList += !@standaloneRoutineCallIR.init {
    !outResultValue
    !.init {!self.mPrefixMangledFunctionName !inOperatorLocation}
    !self.mPrefixMangledFunctionName
    !argumentList
  }
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#   CODE GENERATION
#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @prefixOperatorRoutineIR : @abstractRoutineIR {
  public var @omnibusType mReceiverType
  public var @string mReceiverVarName
  public var @omnibusType mResultType
  public var @stringlist mGeneratedInstructions
  public var @allocaList mAllocaList
}

#·······················································································································

override method @prefixOperatorRoutineIR llvmCodeGeneration 
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  ioLLVMcode += "define internal " + [self.mResultType llvmTypeName] + " @" + [self.mRoutineMangledName assemblerRepresentation]
  ioLLVMcode += " (" + [self.mReceiverType llvmTypeName] + " %" + self.mReceiverVarName + ") nounwind {\n"
  [self.mAllocaList generateAllocaList !?ioLLVMcode]
  for (instruction) in self.mGeneratedInstructions do
    ioLLVMcode += "  " + instruction + "\n"
  end
  ioLLVMcode += "}\n\n"
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

