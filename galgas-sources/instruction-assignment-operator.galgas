#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <assignment_combined_with_operator> !@omnibusInfixOperator outInfixOperator !@location outOperatorLocation {
    select
      $|=$
      outInfixOperator = .bitWiseOrOp
    or
      $&=$
      outInfixOperator = .bitWiseAndOp
    or
      $^=$
      outInfixOperator = .bitWiseXorOp
    or
      $+=$
      outInfixOperator = .addOp
    or
      $+%=$
      outInfixOperator = .addOpNoOvf
    or
      $-=$
      outInfixOperator = .subOp
    or
      $-%=$
      outInfixOperator = .subOpNoOvf
    or
      $*=$
      outInfixOperator = .mulOp
    or
      $*%=$
      outInfixOperator = .mulOpNoOvf
    or
      $<<=$
      outInfixOperator = .leftShiftOp
    or
      $>>=$
      outInfixOperator = .rightShiftOp
    end
    outOperatorLocation = .here
  }

  #·····················································································································

  rule <instruction> ?!@ast ioAST ?!@instructionListAST ioInstructionList {
  #--- LValue
    <lvalue> !?ioAST ?let lvalue
    let instructionLocation = @location.here
  #--- Assignment operator
    <assignment_combined_with_operator> ?let infixOperator ?let operatorLocation
  #--- Right expression
    <expression> !?ioAST ?let @expressionAST rightExpression
  #--- Build source expression
    var @primaryInExpressionAccessListAST expressionAccessList = {}
    [lvalue.mOperand buildExpressionAccessList !?expressionAccessList]
#    for (access) in lvalue.mAccessList do
#      switch access
#      case property (@lstring name) :
#        expressionAccessList += !.property {!name:name}
#      case arrayAccess (@expressionAST index @location endOfIndex checkIndexExpression) :
#        expressionAccessList += !.arrayAccess {!index:index !endOfIndex:endOfIndex !checkIndexExpression:checkIndexExpression}
#      end
#    end
    let leftExpression = @primaryInExpressionAST.new {!lvalue.mIdentifier !expressionAccessList}
    let expression = @omnibusInfixOperatorExpressionAST.new {
      !leftExpression
      !operatorLocation
      !infixOperator
      !rightExpression
    }
  #--- Build assignment instruction
    ioInstructionList += !@assignmentInstructionAST.new {
      !instructionLocation
      !lvalue
      !expression
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @LValueOperandAST buildExpressionAccessList ?!@primaryInExpressionAccessListAST ioExpressionAccessList {
  switch self
  case noOperand :
  case property (@lstring name @LValueOperandAST next) :
    ioExpressionAccessList += !.property {!name:name}
    [next buildExpressionAccessList !?ioExpressionAccessList]
  case arrayAccess (@expressionAST index @location endOfIndex @bool checkIndexExpression @LValueOperandAST next) :
    ioExpressionAccessList += !.arrayAccess {!index:index !endOfIndex:endOfIndex !checkIndexExpression:checkIndexExpression}
    [next buildExpressionAccessList !?ioExpressionAccessList]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

