
setter @instructionListIR appendGetElementPtr
  ?let @objectInMemoryIR inTarget
  ?let @objectInMemoryIR inSource
  ?let @elementPtrList inElementList
{
  if [inElementList length] == 0 then
    error .nowhere : "<<INTERNAL ERROR:inElementList is empty>>"
  end
  self += !@getElementPtrIR.new {!inTarget !inSource !inElementList}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @getElementPtrIR : @abstractInstructionIR {
  @objectInMemoryIR mTarget
  @objectInMemoryIR mSource
  @elementPtrList mElementList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @getElementPtrIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  let llvmType = [[mSource type] llvmTypeName]
  ioLLVMcode += "  " + [mTarget llvmName] + " = getelementptr inbounds "
  ioLLVMcode += llvmType + ", " + llvmType + "* " + [mSource llvmName] + ", i32 0"
  for (idx *) in mElementList do
    switch idx
    case absolute (index) : ioLLVMcode += ", i32 " + index
    case indirect (index) : ioLLVMcode += ", " + [index.mType llvmTypeName] + " " + [index.mValue llvmName]
    end
  end
  ioLLVMcode += " ;"
  for (* fieldName) in mElementList do
    ioLLVMcode += " " + fieldName
  end
  ioLLVMcode += "\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @getElementPtrIR enterAccessibleEntities 
  ?!@accessibleEntities unused ioAccessibleEntities
  ?!@uint unused ioMaxBranchOfOnInstructions
{
}


#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
