#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @varInstructionWithAssignmentAST : @instructionAST {
  public var @lstring mVarName
  public var @lstring mOptionalTypeName
  public var @expressionAST mSourceExpression
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @varDeclarationInstructionAST : @instructionAST {
  public var @lstring mVarName
  public var @lstring mTypeName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <instruction> ?!@ast ioAST ?!@instructionListAST ioInstructionList {
    $var$
    $identifier$ ?let varName
    $=$
    <expression> !?ioAST ?let @expressionAST expression
    ioInstructionList += !@varInstructionWithAssignmentAST.new {
      !varName.location
      !varName
      !["" here]
      !expression
    }
  }

  #·····················································································································

#  rule <instruction> ?!@ast ioAST ?!@instructionListAST ioInstructionList {
#    $var$
#    $identifier$ ?let varName
#    <type_definition> !?ioAST ?let typeName
#    select
#      ioInstructionList += !@varDeclarationInstructionAST.new {
#        !varName.location
#        !varName
#        !typeName
#      }
#    or
#      $=$
#      <expression> !?ioAST ?let @expressionAST expression
#      ioInstructionList += !@varInstructionWithAssignmentAST.new {
#        !varName.location
#        !varName
#        !typeName
#        !expression
#      }
#   end
#  }

  #·····················································································································

  rule <instruction> ?!@ast ioAST ?!@instructionListAST ioInstructionList {
    $var$
    $identifier$ ?let varName
    <type_definition> !?ioAST ?let typeName
    ioInstructionList += !@varDeclarationInstructionAST.new {
      !varName.location
      !varName
      !typeName
    }
    select
    or
      $=$
      <expression> !?ioAST ?let @expressionAST sourceExpression
      let assignmentTargetAST = @LValueAST.new {!varName !.noOperand}
      ioInstructionList += !@assignmentInstructionAST.new {
       !assignmentTargetAST.mIdentifier.location
       !assignmentTargetAST
       !sourceExpression
     }
    end
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @varInstructionWithAssignmentAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  if self.mOptionalTypeName.string != "" then
    [!?ioGraph noteNode !self.mOptionalTypeName]
  end
  [self.mSourceExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @varDeclarationInstructionAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  [!?ioGraph noteNode !self.mTypeName]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @varInstructionWithAssignmentAST instructionSemanticAnalysis
  ?self:let @omnibusType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
  let @omnibusType targetType = if self.mOptionalTypeName.string == ""
    then voidType ()
    else [@unifiedTypeMap-entry.searchKey {!inContext.mTypeMap !self.mOptionalTypeName} type]
  end
#--- Analyze source expression
  [self.mSourceExpression analyzeExpression
    !self:inSelfType
    !routineAttributes:inRoutineAttributes
    !optionalTargetType:targetType
    !context:inContext
    !mode:inMode
    !?temporary:ioTemporaries
    !?staticEntityMap:ioStaticEntityMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:ioInstructionGenerationList
    ?result:let @objectIR sourcePossibleReference
  ]
  let result = checkAssignmentCompatibility (
    !inContext.mAssignmentOperatorMap
    !source:sourcePossibleReference
    !targetAnnotationType:targetType
    !errorLocation:self.mVarName.location
    !staticTypeAllowed:false
  )
#--- Declare local variable
  let omnibusLocalVariableName = @lstring.new {!self.mVarName.string + "." + ioTemporaries.mTemporaryIndex !self.mVarName}
  ioTemporaries.mTemporaryIndex ++
  [!?ioUniversalMap insertLocalVariable !self.mVarName ![result type] !omnibusLocalVariableName !.hasUnreadValue !false]
#--- Code generation
  ioAllocaList += !llvmNameForLocalVariable (!omnibusLocalVariableName) ![result type] !false
#--- Assignment generation
  [!?ioInstructionGenerationList appendAssignmentIR
    !?ioTemporaries
    !inContext.mAssignmentOperatorMap
    !?ioAllocaList
    ![result type]
    !llvmNameForLocalVariable (!omnibusLocalVariableName)
    !sourcePossibleReference
    !self.mVarName
    !initializedTarget: false
  ]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @varDeclarationInstructionAST instructionSemanticAnalysis
  ?self:let @omnibusType unused inSelfType
  ?routineAttributes:let @routineAttributes unused inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode unused inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap unused ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR unused ioInstructionGenerationList
{
  let targetType = [@unifiedTypeMap-entry.searchKey {!inContext.mTypeMap !self.mTypeName} type]
#---
  if [targetType isCompileTimeType] then
    error self.mVarName : "$" + targetType.omnibusTypeDescriptionName + " type is a compile time type, and is not instanciable"
  elsif not [targetType instanciable] then
    error self.mVarName : "$" + targetType.omnibusTypeDescriptionName + " type is not instanciable"
  end
#--- Declare local variable
  let omnibusName = @lstring.new {!self.mVarName.string + "." + ioTemporaries.mTemporaryIndex !self.mVarName}
  ioTemporaries.mTemporaryIndex ++
  [!?ioUniversalMap insertLocalVariable !self.mVarName !targetType !omnibusName !.noValue !true]
#--- Code generation
  ioAllocaList += !llvmNameForLocalVariable (!omnibusName) !targetType !false
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
