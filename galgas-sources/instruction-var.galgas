#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @varInstructionWithAssignmentAST : @instructionAST {
  @lstring mVarName
  @lstring mOptionalTypeName
  @expressionAST mSourceExpression
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @varInstructionAST : @instructionAST {
  @lstring mVarName
  @lstring mTypeName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension common_syntax {

  #·····················································································································

  rule <instruction> ?!@instructionListAST ioInstructionList ?!@labelMap unused ioLabelMap {
    $var$
    $identifier$ ?let varName
    @lstring optionalTypeName
    select
      optionalTypeName = ["" nowhere]
    or
      $typeName$ ?optionalTypeName
    end
    $=$
    <expression> ?let @expressionAST expression
    ioInstructionList += !varName.location !@varInstructionWithAssignmentAST.new {
      !varName
      !optionalTypeName
      !expression
    }
  }

  #·····················································································································

  rule <instruction> ?!@instructionListAST ioInstructionList ?!@labelMap unused ioLabelMap {
    $var$
    $identifier$ ?let varName
    $typeName$ ?let typeName
    ioInstructionList += !varName.location !@varInstructionAST.new {
      !varName
      !typeName
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @varInstructionWithAssignmentAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  if mOptionalTypeName.string != "" then
    let typeName = @lstring.new {!"$" + mOptionalTypeName !mOptionalTypeName.location}
    [!?ioGraph noteNode !typeName]
  end
  [mSourceExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @varInstructionAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  let typeName = @lstring.new {!"$" + mTypeName !mTypeName.location}
  [!?ioGraph noteNode !typeName]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @varInstructionWithAssignmentAST analyze
  ?self:let @unifiedTypeMap-proxy inSelfType
  ?propertiesAreMutable:let @bool unused inRoutineCanMutateProperties
  ?directAccessToPropertiesAllowed:let @bool inDirectAccessToPropertiesAllowed
  ?routineNameForInvocationGraph:let @lstring inCallerNameForInvocationGraph
  ?context:let @semanticContext inContext
  ?modes:let @stringset inModes
  ?allowPanic:let @bool inAllowPanic
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@staticStringMap ioGlobalLiteralStringMap
  ?!variableMap:@variableMap ioVariableMap
  ?!namedObjectMap:@namedObjectMap ioNamedObjectMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
  @unifiedTypeMap-proxy targetType = if mOptionalTypeName.string == ""
    then .null
    else .searchKey {!inContext.mTypeMap !mOptionalTypeName}
  end
#--- Analyze source expression
  [mSourceExpression analyzeExpression
    !self:inSelfType
    !directAccessToPropertiesAllowed:inDirectAccessToPropertiesAllowed
    !inGuard:false
    !routineNameForInvocationGraph:inCallerNameForInvocationGraph
    !optionalTargetType:targetType
    !context:inContext
    !modes:inModes
    !allowPanic:inAllowPanic
    !?temporary:ioTemporaries
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioVariableMap
    !?namedObjectMap: ioNamedObjectMap
    !?alloca:ioAllocaList
    !?instructionListIR:ioInstructionGenerationList
    ?result:@objectIR expressionResult
  ]
#--- Check type compatibility
  if targetType == .null then
    targetType = [expressionResult type]
  end
  let result = checkAssignmentCompatibility (
    !source:expressionResult
    !targetAnnotationType:targetType
    !errorLocation:mVarName.location
    !staticTypeAllowed:false
  )
  drop expressionResult, targetType
#--- Declare local variable
  let varName = mVarName.string + "." + ioTemporaries.mTemporaryIndex
  ioTemporaries.mTemporaryIndex ++
  [!?ioVariableMap insertDefinedVariable
    !lkey:mVarName
    !type:[result type]
    !readAccessAllowed:true
    !objectIR:.localVariableReference {!type:[result type] !plmName:varName}
    !copyable:[[result type] copyable]
    !canBeUsedAsInputParameter:true
  ]
  let targetObject = @objectIR.localVariableReference {!type:[result type] !plmName:varName}
  [!?ioNamedObjectMap insertKey
    !mVarName
    !targetObject
  ]
#--- Code generation
  ioAllocaList += !varName ![result type]
  [!?ioInstructionGenerationList appendStoreLocalVariable
    !varName
    ![result type]
    !result
  ]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @varInstructionAST analyze
  ?self:let @unifiedTypeMap-proxy unused inSelfType
  ?propertiesAreMutable:let @bool unused inRoutineCanMutateProperties
  ?directAccessToPropertiesAllowed:let @bool unused inDirectAccessToPropertiesAllowed
  ?routineNameForInvocationGraph:let @lstring unused inCallerNameForInvocationGraph
  ?context:let @semanticContext inContext
  ?modes:let @stringset unused inModes
  ?allowPanic:let @bool unused inAllowPanic
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@staticStringMap unused ioGlobalLiteralStringMap
  ?!variableMap:@variableMap ioVariableMap
  ?!namedObjectMap:@namedObjectMap unused ioNamedObjectMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR unused ioInstructionGenerationList
{
  let @unifiedTypeMap-proxy targetType = .searchKey {!inContext.mTypeMap !mTypeName}
#---
  if not [targetType instantiable] then
    error mVarName : "$" + [targetType key] + " type is not instanciable"
  end
#--- Declare local variable
  let varLLVMName = mVarName.string + "." + ioTemporaries.mTemporaryIndex
  ioTemporaries.mTemporaryIndex ++
  [!?ioVariableMap insertUndefinedVariable
    !lkey:mVarName
    !type:targetType
    !readAccessAllowed:true
    !objectIR:.localVariableReference {!type:targetType !plmName:varLLVMName}
    !copyable:[targetType copyable]
    !canBeUsedAsInputParameter:true
  ]
#--- Code generation
  ioAllocaList += !varLLVMName !targetType
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
