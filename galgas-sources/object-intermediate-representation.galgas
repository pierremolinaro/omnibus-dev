#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! @objectIR
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @objectIR {
#--- No object (for example, no return value for a procedure)
  case null
#--- Reference
  case reference (@PLMType type @string llvmName)
#--- Values
  case llvmValue (@PLMType type @string llvmName)
  case literalInteger  (@PLMType type @bigint value)
  case llvmStructureConstant (@PLMType type @sortedOperandIRList values)
  case llvmArrayConstant (@PLMType type @operandIRList values)
  case literalString  (@uint utf8Size @uint index)
  case zero (@PLMType type)
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

getter @objectIR llvmName -> @string {
  switch self
  case null : error .nowhere : "<<@objectIR llvmName null>>" : result
  case zero (*) : result = "zeroinitializer"
  case literalInteger (* @bigint value) : result = [value string]
  case llvmValue (* llvmName) : result = llvmName
  case reference (* llvmName) : result = llvmName
  case llvmStructureConstant (* @sortedOperandIRList inValues) :
    result = "{"
    for () in inValues
      do result += [mOperand llvmTypeName] + " " + [mOperand llvmName]
      between result += ", "
    end
    result += "}"
  case llvmArrayConstant (* @operandIRList inValues) :
    result = "["
    for () in inValues
      do result += [mOperand llvmTypeName] + " " + [mOperand llvmName]
      between result += ", "
    end
    result += "]"
  case literalString  (@uint size @uint index) :
    let sizeStr = [size string]
    result = "getelementptr inbounds ([" + sizeStr + " x i8], [" + sizeStr + " x i8]* "
    result += literalCharacterArrayName (!index) + ", i32 0, i32 0)"
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

getter @objectIR type -> @PLMType {
  switch self
  case null :
    error .nowhere : "<<@objectIR type null>>" : result
  case reference (type *) : result = type
  case zero (type) : result = type
  case literalInteger (type *) : result = type
  case llvmValue (type *) : result = type
  case llvmStructureConstant (type *) : result = type
  case llvmArrayConstant (type *) : result = type
  case literalString  (* *) : result = literalStringType ()
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

getter @objectIR llvmTypeName -> @string {
  result = [[self type] llvmTypeName]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

getter @objectIR key -> @string {
  result = [[self type] key]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

getter @objectIR withType ?let @PLMType inType -> @objectIR {
  switch self
  case null : error .nowhere : "<<@objectIR withType null>>" : result
  case reference (* llvmName) :
    result = .reference {!type:inType !llvmName:llvmName}
  case literalInteger (* @bigint value) :
    result = .literalInteger {!type: inType !value: value}
  case llvmValue (* llvmName) :
    result = .llvmValue {!type: inType !llvmName:llvmName}
  case zero (*) :
    result = .zero {!type: inType}
  case llvmStructureConstant (* @sortedOperandIRList values) :
    result = .llvmStructureConstant {!type: inType !values: values}
  case llvmArrayConstant (* @operandIRList values) :
    result = .llvmArrayConstant {!type: inType !values: values}
  case literalString  (@uint size @uint index) :
    result = .literalString {!utf8Size: size !index: index}
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

getter @objectIR name -> @string {
  switch self
  case null : error .nowhere : "<<@objectIR name null>>" : result
  case reference (* llvmName) : result = llvmName
  case zero (*) : result = "zeroinitializer"
  case literalInteger (* @bigint value) : result = [value string]
  case llvmValue (* name) : result = name
  case llvmStructureConstant (* *) : result = "{...}"
  case llvmArrayConstant (* *) : result = "[...]"
  case literalString  (@uint size @uint index) :
    let sizeStr = [size string]
    result = "getelementptr inbounds ([" + sizeStr + " x i8], [" + sizeStr + " x i8]* "
    result += literalCharacterArrayName (!index) + ", i32 0, i32 0)"
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

getter @objectIR isStatic -> @bool {
  switch self
  case null : error .nowhere : "<<@objectIR isStatic null>>" : result
  case reference (**) : result = false
  case zero (*) : result = true
  case literalInteger (* *) : result = true
  case llvmValue (* *) : result = false
  case llvmStructureConstant (* *) : result = true
  case llvmArrayConstant (* *) : result = true
  case literalString  (* *) : result = true
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
