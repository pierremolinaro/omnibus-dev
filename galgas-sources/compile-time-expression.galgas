#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

abstract refclass @ctExpressionAST {
}

#·······················································································································

refclass @ctInfixExpressionAST : @ctExpressionAST {
  public var @ctExpressionAST mLeftExpression
  public var @location mOperatorLocation
  public var @compileTimeInfixOperatorEnumeration mInfixOperator
  public var @ctExpressionAST mRightExpression
}

#·······················································································································

refclass @ctPrefixExpressionAST : @ctExpressionAST {
  public var @location mOperatorLocation
  public var @compileTimePrefixOperatorEnumeration mPrefixOperator
  public var @ctExpressionAST mExpression
}

#·······················································································································

refclass @ctTrueExpressionAST : @ctExpressionAST {
}

#·······················································································································

refclass @ctFalseExpressionAST : @ctExpressionAST {
}

#·······················································································································

refclass @ctIntExpressionAST : @ctExpressionAST {
  public var @lbigint mValue
}

#·······················································································································

refclass @ctIdentifierExpressionAST : @ctExpressionAST {
  public var @lstring mIdentifier
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <compile_time_expression> !@ctExpressionAST outExpression {
    <compile_time_expression_bitwise_xor> ?outExpression
    repeat
    while
      $|$
      let operatorLocation = @location.here
      <compile_time_expression_bitwise_xor> ?let rightExpression
      outExpression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.bitWiseOrOp
        !rightExpression
      }
    end
  }

  #·····················································································································

  rule <compile_time_expression_bitwise_xor> !@ctExpressionAST outExpression {
    <compile_time_expression_bitwise_and> ?outExpression
    repeat
    while
      $^$
      let operatorLocation = @location.here
      <compile_time_expression_bitwise_and> ?let rightExpression
      outExpression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.bitWiseXorOp
        !rightExpression
      }
    end
   }

  #·····················································································································

  rule <compile_time_expression_bitwise_and> !@ctExpressionAST outExpression {
    <compile_time_expression_equality> ?outExpression
    repeat
    while
      $&$
      let operatorLocation = @location.here
      <compile_time_expression_equality> ?let rightExpression
      outExpression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.bitWiseAndOp
        !rightExpression
      }
    end
  }

  #·····················································································································

  rule <compile_time_expression_equality> !@ctExpressionAST outExpression {
    <compile_time_expression_comparison> ?outExpression
    select
    or
      $==$
      let operatorLocation = @location.here
      <compile_time_expression_comparison> ?let rightExpression
      outExpression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.equal
        !rightExpression
      }
    or
      $≠$ # (a ≠ b) equivalent to not (a == b)
      let operatorLocation = @location.here
      <compile_time_expression_comparison> ?let rightExpression
      let expression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.equal
        !rightExpression
      }
      outExpression = @ctPrefixExpressionAST.new {
        !operatorLocation
        !.notOp
        !expression
      }
    end
  }

  #·····················································································································

  rule <compile_time_expression_comparison> !@ctExpressionAST outExpression {
    <compile_time_expression_shift> ?outExpression
    select
    or
      $≤$ # (a ≤ b) equivalent to (a == b) | (a < b)
      let operatorLocation = @location.here
      <compile_time_expression_shift> ?let rightExpression
      let lessThanExpression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.lessThan
        !rightExpression
      }
      let equalExpression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.equal
        !rightExpression
      }
      outExpression = @ctInfixExpressionAST.new {
        !lessThanExpression
        !operatorLocation
        !.bitWiseOrOp
        !equalExpression
      }
    or
      $≥$ # (a ≥ b) equivalent to not (a < b)
      let operatorLocation = @location.here
      <compile_time_expression_shift> ?let rightExpression
      let expression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.lessThan
        !rightExpression
      }
      outExpression = @ctPrefixExpressionAST.new {
        !operatorLocation
        !.notOp
        !expression
      }
    or
      $<$
      let operatorLocation = @location.here
      <compile_time_expression_shift> ?let rightExpression
      outExpression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.lessThan
        !rightExpression
      }
    or
      $>$ # (a > b) equivalent to not ((a == b) | (a < b))
      let operatorLocation = @location.here
      <compile_time_expression_shift> ?let rightExpression
     let lessThanExpression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.lessThan
        !rightExpression
      }
      let equalExpression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.equal
        !rightExpression
      }
      let lessThanOrEqualExpression = @ctInfixExpressionAST.new {
        !lessThanExpression
        !operatorLocation
        !.bitWiseOrOp
        !equalExpression
      }
      outExpression = @ctPrefixExpressionAST.new {
        !operatorLocation
        !.notOp
        !lessThanOrEqualExpression
      }
    end
  }

  #·····················································································································

  rule <compile_time_expression_shift> !@ctExpressionAST outExpression {
    <compile_time_expression_addition> ?outExpression
    repeat
    while
      $<<$
      let operatorLocation = @location.here
      <compile_time_expression_addition> ?let rightExpression
      outExpression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.leftShift
        !rightExpression
      }
    while
      $>>$
      let operatorLocation = @location.here
      <compile_time_expression_addition> ?let rightExpression
      outExpression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.rightShift
        !rightExpression
      }
    end
  }

  #·····················································································································

  rule <compile_time_expression_addition> !@ctExpressionAST outExpression {
    <compile_time_expression_product> ?outExpression
    repeat
    while
      $+$
      let operatorLocation = @location.here
      <compile_time_expression_product> ?let rightExpression
      outExpression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.addOp
        !rightExpression
      }
    while
      $-$
      let operatorLocation = @location.here
      <compile_time_expression_product> ?let rightExpression
      outExpression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.addOp
        !@ctPrefixExpressionAST.new {!operatorLocation !.minusOp !rightExpression}
      }
    end
  }

  #·····················································································································

  rule <compile_time_expression_product> !@ctExpressionAST outExpression {
    <compile_time_primary> ?outExpression
    repeat
    while
      $*$
      let operatorLocation = @location.here
      <compile_time_primary> ?let rightExpression
      outExpression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.mulOp
        !rightExpression
      }
   while
      $%$
      let operatorLocation = @location.here
      <compile_time_primary> ?let rightExpression
      outExpression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.modOp
        !rightExpression
      }
    while
      $!%$
      let operatorLocation = @location.here
      <compile_time_primary> ?let rightExpression
      outExpression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.moduloNoOvf
        !rightExpression
      }
    while
      $/$
      let operatorLocation = @location.here
      <compile_time_primary> ?let rightExpression
      outExpression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.divOp
        !rightExpression
      }
    while
      $!/$
      let operatorLocation = @location.here
      <compile_time_primary> ?let rightExpression
      outExpression = @ctInfixExpressionAST.new {
        !outExpression
        !operatorLocation
        !.divNoOvf
        !rightExpression
      }
    end
  }

  #·····················································································································

  rule <compile_time_primary> !@ctExpressionAST outExpression {
    $not$
    let operatorLocation = @location.here
    <compile_time_primary> ?let expression
    outExpression = @ctPrefixExpressionAST.new {
      !operatorLocation
      !.notOp
      !expression
    }
  }

  #·····················································································································

  rule <compile_time_primary> !@ctExpressionAST outExpression {
    $-$
    let operatorLocation = @location.here
    <compile_time_primary> ?let expression
    outExpression = @ctPrefixExpressionAST.new {
      !operatorLocation
      !.minusOp
      !expression
    }
  }

  #·····················································································································

  rule <compile_time_primary> !@ctExpressionAST outExpression {
    $($
    <compile_time_expression> ?outExpression
    $)$
  }

  #·····················································································································

  rule <compile_time_primary> !@ctExpressionAST outExpression {
    $yes$
    outExpression = @ctTrueExpressionAST.new
  }

  #·····················································································································

  rule <compile_time_primary> !@ctExpressionAST outExpression {
    $no$
    outExpression = @ctFalseExpressionAST.new
  }

  #·····················································································································

  rule <compile_time_primary> !@ctExpressionAST outExpression {
    $integer$ ?let value
    outExpression = @ctIntExpressionAST.new {!value}
  }

  #·····················································································································

  rule <compile_time_primary> !@ctExpressionAST outExpression {
    $identifier$ ?let name
    outExpression = @ctIdentifierExpressionAST.new {!name}
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  CHECK COMPILE TIME EXPRESSION                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

map @ctCheckMap {
  insert insertKey error message "the '%K' key is already declared in %L"
  search searchKey error message "there is no '%K' key"
}

#·······················································································································

abstract method @ctExpressionAST checkCompileTimeExpression ?let @ctCheckMap inMap

#·······················································································································

override method @ctInfixExpressionAST checkCompileTimeExpression ?let @ctCheckMap inMap {
  [self.mLeftExpression checkCompileTimeExpression  !inMap]
  [self.mRightExpression checkCompileTimeExpression !inMap]
}

#·······················································································································

override method @ctPrefixExpressionAST checkCompileTimeExpression ?let @ctCheckMap inMap {
  [self.mExpression checkCompileTimeExpression !inMap]
}

#·······················································································································

override method @ctTrueExpressionAST checkCompileTimeExpression ?let @ctCheckMap unused inMap {
}

#·······················································································································

override method @ctFalseExpressionAST checkCompileTimeExpression ?let @ctCheckMap unused inMap {
}

#·······················································································································

override method @ctIntExpressionAST checkCompileTimeExpression ?let @ctCheckMap unused inMap {
}

#·······················································································································

override method @ctIdentifierExpressionAST checkCompileTimeExpression ?let @ctCheckMap inMap {
  [inMap searchKey !self.mIdentifier]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  COMPUTE COMPILE TIME EXPRESSION                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

map @ctMap {
  public var @bigint mValue
  insert insertKey error message "the '%K' key is already declared in %L"
  search searchKey error message "there is no '%K' key"
}

#·······················································································································

abstract method @ctExpressionAST computeCompileTimeExpression
  ?let @ctMap inMap
  !result: @bigint outValue

#·······················································································································

override method @ctInfixExpressionAST computeCompileTimeExpression
  ?let @ctMap inMap
  !result: @bigint outValue
{
  [self.mLeftExpression computeCompileTimeExpression  !inMap ?result: let left]
  [self.mRightExpression computeCompileTimeExpression !inMap ?result: let right]
  switch self.mInfixOperator
   case equal     : outValue = if left == right then 1 else 0 end
   case lessThan  : outValue = if left <  right then 1 else 0 end
   case bitWiseAndOp: outValue = left & right
   case bitWiseOrOp : outValue = left | right
   case bitWiseXorOp : outValue = left ^ right
   case addOp     : outValue = left + right
   case subOp     : outValue = left - right
   case mulOp     : outValue = left * right
   case divOp     : outValue = left / right
   case divNoOvf  : outValue = left / right
   case modOp     : outValue = left mod right
   case moduloNoOvf: outValue = left mod right
   case leftShift : outValue = left << right
   case rightShift: outValue = left >> right
  end
}

#·······················································································································

override method @ctPrefixExpressionAST computeCompileTimeExpression
  ?let @ctMap inMap
  !result: @bigint outValue
{
  [self.mExpression computeCompileTimeExpression !inMap ?result: let exp]
  switch self.mPrefixOperator
   case minusOp  : outValue = - exp
   case notOp  : outValue = if exp == 0 then 1 else 0 end
  end
}

#·······················································································································

override method @ctTrueExpressionAST computeCompileTimeExpression
  ?let @ctMap unused inMap
  !result: @bigint outValue
{
  outValue = 1
}

#·······················································································································

override method @ctFalseExpressionAST computeCompileTimeExpression
  ?let @ctMap unused inMap
  !result: @bigint outValue
{
  outValue = 0
}

#·······················································································································

override method @ctIntExpressionAST computeCompileTimeExpression
  ?let @ctMap unused inMap
  !result: @bigint outValue
{
  outValue = self.mValue.bigint
}

#·······················································································································

override method @ctIdentifierExpressionAST computeCompileTimeExpression
  ?let @ctMap inMap
  !result: @bigint outValue
{
  [inMap searchKey !self.mIdentifier ?outValue]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
