#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  AST                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @typeAliasDeclarationAST : @abstractDeclarationAST {
  @lstring mAliasTypeName
  @lstring mOriginalTypeName
}

#·······················································································································

override getter @typeAliasDeclarationAST location -> @location outLocation {
  outLocation = mAliasTypeName.location
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension plm_syntax {

  #·····················································································································

  rule <declaration> ?!@ast ioAST {
    $typealias$
    $\$type$ ?let newTypeName
    $=$
    <type_definition> !?ioAST ?let typeName
    ioAST.mDeclarationListAST += !@typeAliasDeclarationAST.new {!newTypeName !typeName}
  }

  #·····················································································································

  rule <type_definition> ?!@ast unused ioAST !@lstring outTypeName {
    $\$type$ ?outTypeName # Simple redéfinition de type
  }
  
  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  PRECEDENCE GRAPH                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @typeAliasDeclarationAST enterInPrecedenceGraph
  ?!@semanticTypePrecedenceGraph ioGraph
{
  [!?ioGraph addNode !mAliasTypeName !self]
  [!?ioGraph addEdge !mAliasTypeName !mOriginalTypeName]
}

#·······················································································································

override getter @typeAliasDeclarationAST keyRepresentationForErrorSignaling -> @string outRepresentation {
  outRepresentation = "typealias $" + mAliasTypeName 
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  TYPE INVENTORY                                                                                                   
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

#override method @typeAliasDeclarationAST typeInventory
#  ?!context:@semanticContext unused ioContext
#  ?!@typeInventoryMap ioTypeInventoryMap
#{
#  [ioTypeInventoryMap searchKey
#    !lkey:mOriginalTypeName
#    ?plmDescriptionName:let plmOriginalTypeDescriptionName
#    ?llvmBaseTypeName:let llvmOriginalBaseTypeName
#  ]
#
#  [!?ioTypeInventoryMap insertKey
#    !lkey:mAliasTypeName
#    !plmDescriptionName:plmOriginalTypeDescriptionName
#    !llvmBaseTypeName:llvmOriginalBaseTypeName
#  ]
#}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  ENTER IN CONTEXT                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @typeAliasDeclarationAST enterInContext
  ?procedureDeclarationList:let @functionDeclarationListAST unused inProcedureListAST
  ?!context:@semanticContext ioContext
  ?!@declarationDecorationList unused ioDecoratedDeclarationList
  ?!@subprogramInvocationGraph unused ioSubprogramInvocationGraph
  ?!staticlistValues:@staticListInitializationMap unused ioStaticListValueMap
  ?!staticEntityMap:@staticEntityMap unused ioStaticEntityMap
  ?!globalVariables:@globalVariableIRList unused ioGlobalVariableIRList
  ?!registerUserAccess:@controlRegisterUserAccesMapAST unused ioControlRegisterUserAccesMapAST
{
#---------- Enter in type map
  [ioContext.mTypeMap searchKey !lkey:mOriginalTypeName ?type:var type ?1*]
#---------- Enter in type map
  [!?ioContext.mTypeMap insertType !lkey:mAliasTypeName !type:type !index:[ioContext.mTypeMap count]]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
