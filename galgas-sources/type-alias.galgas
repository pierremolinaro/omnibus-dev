#—————————————————————————————————————————————————————————————————————————————————————————————————
#!  AST                                                                                                               
#—————————————————————————————————————————————————————————————————————————————————————————————————

class @typeAliasDeclarationAST : @abstractDeclarationAST {
  public var @lstring mAliasTypeName
  public var @lstring mOriginalTypeName
}

#·······················································································································

override getter @typeAliasDeclarationAST locationForErrorSignaling -> @location {
  result = self.mAliasTypeName.location
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#!  SYNTAX                                                                                                               
#—————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <declaration> ?!@ast ioAST {
    $typealias$
    $\$type$ ?let newTypeName
    $=$
    <type_definition> !?ioAST ?let typeName
    ioAST.mDeclarationListAST += !@typeAliasDeclarationAST {!newTypeName !typeName}
  }

  #·····················································································································

  rule <type_definition> ?!@ast unused ioAST !@lstring outTypeName {
    $\$type$ ?outTypeName # Simple redéfinition de type
  }
  
  #·····················································································································

}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#!  PRECEDENCE GRAPH                                                                                                     
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @typeAliasDeclarationAST enterInPrecedenceGraph
  ?!@semanticTypePrecedenceGraph ioGraph
{
  [!?ioGraph addNode !self.mAliasTypeName !self]
  [!?ioGraph addEdge !from: self.mAliasTypeName !to: self.mOriginalTypeName]
}

#·······················································································································

override getter @typeAliasDeclarationAST keyRepresentationForErrorSignaling -> @string {
  result = "typealias " + self.mAliasTypeName 
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#!  ENTER IN CONTEXT                                                                                                     
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @typeAliasDeclarationAST enterInContext
  ?!context:@semanticContext ioContext
  ?!@declarationDecorationList unused ioDecoratedDeclarationList
  ?!@decoratedRegularRoutineList unused ioDecoratedRoutineList
  ?!@routineListIR unused ioRoutineListIR
  ?!staticlistValues:@staticListInitializationMap unused ioStaticListValueMap
  ?!staticEntityMap:@staticEntityMap unused ioStaticEntityMap
  ?!registerUserAccess:@controlRegisterUserAccesMapAST unused ioControlRegisterUserAccesMapAST
  ?!userDefinedTypes:@userLLVMTypeDefinitionListIR unused ioUserLLVMTypeDefinitionListIR
{
#---------- Enter in type map
  [ioContext.mTypeMap searchType !lkey:self.mOriginalTypeName ?type: let type]
#---------- Enter in type map
  [!?ioContext.mTypeMap insertType
    !self.mAliasTypeName
    !type:type
  ]
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
