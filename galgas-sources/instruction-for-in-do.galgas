#—————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#—————————————————————————————————————————————————————————————————————————————————————————————————

class @forInstructionAST : @instructionAST {
  public var @lstring mVarName
  public var @lstring mIteratedObject
  public var @expressionAST mWhileExpression
  public var @location mEndOf_whileExpression
  public var @bool mStaticWhileExpression
  public var @instructionListAST mDoInstructionList
  public var @location mEndOf_for_instruction
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#—————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <instruction> ?!@ast ioAST ?!@instructionListAST ioInstructionList {
    $for$
    $identifier$ ?let varName
    $in$
    $identifier$ ?let iteratedObject
    var staticWhileExpression = false
    let @expressionAST whileExpression
    select
      whileExpression = @literalBooleanInExpressionAST.init {!true}
      staticWhileExpression = true
    or
      $while$
      select
        $@attribute$ ? let attributeValue
        if attributeValue.string == staticAttribute () then
          staticWhileExpression = true
        else
          error attributeValue : "the attribute should be \"@" + staticAttribute () + "\"" 
          fixit { replace "@" + staticAttribute () }
        end
      or
      end
      <expression> !?ioAST ?whileExpression
    end
    let endOf_whileExpression_instruction = @location.here
    ${$
    <instructionList> !?ioAST ?let instructionList
    let endOfInstruction = @location.here
    $}$
    ioInstructionList += !@forInstructionAST.init {
      !varName.location
      !varName
      !iteratedObject
      !whileExpression
      !endOf_whileExpression_instruction
      !staticWhileExpression
      !instructionList
      !endOfInstruction
    }
  }
  
  #·····················································································································

}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @forInstructionAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  [self.mDoInstructionList noteInstructionListTypesInPrecedenceGraph !?ioGraph]
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @forInstructionAST instructionSemanticAnalysis
  ?self:let @omnibusType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
#--- Analyze iterated expression
  [!?ioUniversalMap searchValuedObjectForReadAccess !self.mIteratedObject ?let @objectIR iteratedObjectPointer]
#--- Type kind
  let iteratedType = [iteratedObjectPointer type]
#--- Get iterated element type
  let @omnibusType iteratedElementType
  switch iteratedType.subscript
  case noSubscript :
    error self.mIteratedObject : "this object is not enumerable" : iteratedElementType
  case literalString :
    iteratedElementType = [[inContext.mTypeMap searchKey !["u8" nowhere]] type]
  case staticSubscript (elementType *) :
    iteratedElementType = elementType
  end
#---
  let omnibusName = @lstring.init {!self.mVarName.string + "." + ioTemporaries.mTemporaryIndex !self.mVarName}
  ioTemporaries.mTemporaryIndex += 1
  ioAllocaList += !llvmNameForLocalVariable (!omnibusName) !iteratedElementType !false
#--- Analyze instruction list
  [!?ioUniversalMap openOverrideForRepeatBlock]
  [!?ioUniversalMap insertLocalConstant !self.mVarName !false !iteratedElementType !omnibusName]
#------------------------------------ Analyze while expression
  var @instructionListIR whileExpression_GenerationList = §[]
  [self.mWhileExpression analyzeExpression
    !self:inSelfType
    !routineAttributes:inRoutineAttributes
    !optionalTargetType:voidType ()
    !context:inContext
    !mode:inMode
    !?temporary:ioTemporaries
    !?staticEntityMap:ioStaticEntityMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:whileExpression_GenerationList
    ?result:let @objectIR whileExpressionResult
  ]
#--- Check "while" expression type is boolean
  [inContext.mImplicitConversionToBooleanMap generateConvertToBooleanCode
    !whileExpressionResult
    !self.mEndOf_whileExpression
    !?ioTemporaries
    !?whileExpression_GenerationList
    !?alloca: ioAllocaList
    ?let whileExpressionBooleanResult
  ]
  switch whileExpressionBooleanResult 
  case compileTime (boolValue) :
    if not boolValue then
      error self.mEndOf_whileExpression : "test expression is always false"
    end
  case llvmVariable (*) :
  end


#  if not [[whileExpressionResult type].kind isBoolean] then
#    error mEndOf_whileExpression : "'while' expression should be boolean"
#  elsif mStaticWhileExpression && not [whileExpressionResult isStatic] then
#    error mEndOf_whileExpression : "'while' expression is not static"
#  elsif [whileExpressionResult isStatic] && not mStaticWhileExpression then
#    warning mEndOf_whileExpression : "'while' expression is static (insert @" + staticAttribute () + " attribute)"
#  end
#------------------------------------ "DO" instruction list
  var @instructionListIR doInstructionList_GenerationList = §[]
  [self.mDoInstructionList analyzeBranchInstructionList
    !self:inSelfType
    !routineAttributes:inRoutineAttributes
    !endOfInstructionList:self.mEndOf_for_instruction
    !context:inContext
    !mode:inMode
    !?temporary:ioTemporaries
    !?staticEntityMap:ioStaticEntityMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:doInstructionList_GenerationList
  ]
  [!?ioUniversalMap closeOverride !self.mEndOf_for_instruction]
#------------------------------------ Code generation
  switch iteratedType.subscript
  case noSubscript :
  case literalString :
    ioInstructionGenerationList += !@forInstructionOnLiteralStringIR.init {
      !omnibusName
      !self.mIteratedObject.location
      !iteratedObjectPointer
      !iteratedType
      !whileExpression_GenerationList
      !whileExpressionResult
      !doInstructionList_GenerationList
    }
  case staticSubscript (elementType size) :
  #--- Static array map ? If yes, enter invoked function set
    var @stringset invokedFunctionSet = §[]
    if [ioTemporaries.mStaticArrayMapForTemporaries hasKey !self.mIteratedObject] then
      [ioTemporaries.mStaticArrayMapForTemporaries searchKey !self.mIteratedObject ?invokedFunctionSet]
    end
    ioInstructionGenerationList += !@forInstructionOnArrayIR.init {
      !omnibusName
      !self.mIteratedObject
      !iteratedObjectPointer
      !whileExpression_GenerationList
      ![whileExpressionBooleanResult llvmName]
      !doInstructionList_GenerationList
      !elementType
      !size
      !invokedFunctionSet
    }
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  CODE GENERATION (ON ARRAY)
#—————————————————————————————————————————————————————————————————————————————————————————————————

class @forInstructionOnArrayIR : @abstractInstructionIR {
  public var @string mEnumeratedValueName
  public var @lstring mIteratedObjectName
  public var @objectIR mIteratedObject
  public var @instructionListIR mWhileInstructionList
  public var @string mWhileExpressionBooleanResult_llvmName
  public var @instructionListIR mDoInstructionList
  public var @omnibusType mElementType
  public var @uint mArraySize
  public var @stringset mInvokedFunctionSet
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#----------------- La boucle exprimée en C est :
#  TYPE_ELEMENT * ptr = VALEUR_LISTE ; 
#  unsigned idx = TAILLE_LISTE ;
#  while (idx > 0) {
#    ... instructions de la boucle ...
#    ptr ++ ;
#    idx -- ;
#  }
#
#-------------------- La boucle est traduite en LLVM :
#  br label %startLabel
#
#startLabel:
#  store [TAILLE_LISTE x %TYPE_ELEMENT] %VALEUR_LISTE, [TAILLE_LISTE x %TYPE_ELEMENT] * %LISTE_EN_MEMOIRE
#  %ptrStart = getelementptr inbounds [TAILLE_LISTE x %TYPE_ELEMENT], [TAILLE_LISTE x %TYPE_ELEMENT]* %LISTE_EN_MEMOIRE, i32 0, i32 0
#  br label %testLabel
#
#testLabel:
#  %ptrVar = phi %TYPE_ELEMENT* [%ptrStart, %startLabel], [%indexVarNext, %loopLabel]
#  %indexVar = phi i32 [TAILLE_LISTE, %startLabel], [%ptrVarNext, %loopLabel]
#  %testExit = icmp eq i32 %indexVar, 0
#  br i1 %testExit, label %endLabel, label %loopLabel
#
#loopLabel:                                      ; preds = %1
#  %...accès.à.une.propriété... = getelementptr inbounds %TYPE_ELEMENT, %TYPE_ELEMENT* %ptrVar, i32 0, i32 0
#  %ptrVarNext = getelementptr inbounds %TYPE_ELEMENT, %TYPE_ELEMENT* %ptrVar, i32 1
#  %indexVarNext = add i32 %indexVar, -1
#  br label %1
#
#endLabel:

override method @forInstructionOnArrayIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext inGenerationContext
  ?!@generationAdds ioGenerationAdds
{
  let elementTypeLLVMName = [self.mElementType llvmTypeName]
  let listTypeLLVMName = "[" + self.mArraySize + " x " + elementTypeLLVMName + "]"
  let locationIndex = [[self.mIteratedObjectName.location endLocationIndex] string]
  
  let startLabel = "for.label.start." + locationIndex
  let testLabel = "for.label.test." + locationIndex
  let whileLabel = "for.label.while." + locationIndex
  let nextLabel = "for.label.next." + locationIndex
  let loopVar = "for.loop." + locationIndex
  let loopLabel = "for.label.loop." + locationIndex
  let endLabel  = "for.label.end." + locationIndex
 
  let indexVar  = "for.index." + locationIndex
  let ptrVar  = "for.ptr." + locationIndex
  let currentValue = "for.currentValue." + locationIndex
 
#--- Start of 'for' instruction
  ioLLVMcode += "  br label %" + startLabel + "\n\n"

#--------- Loop entry
  ioLLVMcode += startLabel + ":\n"
#--- Init pointer
  ioLLVMcode += "  %" + ptrVar + ".start = getelementptr inbounds " + listTypeLLVMName
  ioLLVMcode += ", " + listTypeLLVMName + "* " + [self.mIteratedObject llvmName]
  ioLLVMcode += ", i32 0, i32 0\n"
#--- Init index
  ioLLVMcode += "  br label %" + testLabel + "\n\n"
  
#------------ Test
  ioLLVMcode += testLabel + ":\n"
#--- Ptr var
  ioLLVMcode += "  %" + ptrVar + " = phi " + elementTypeLLVMName + "* [%" + ptrVar + ".start, %" + startLabel
              + "], [%" + ptrVar + ".next, %" + nextLabel + "]\n"
#--- Index var
  ioLLVMcode += "  %" + indexVar + " = phi i32 [" + self.mArraySize + ", %" + startLabel
              + "], [%" + indexVar + ".next, %" + nextLabel + "]\n"
  ioLLVMcode += "  %" + loopVar + " = icmp ugt i32 %" + indexVar + ", 0\n"
#--- Ptr var
  ioLLVMcode += "  br i1 %" + loopVar + ", label %" + whileLabel + ", label %" + endLabel + "\n\n"

#------------ While
  ioLLVMcode += whileLabel + ":\n"
#--- While instructions
  [self.mWhileInstructionList instructionListLLVMCode !?ioLLVMcode !inGenerationContext !?ioGenerationAdds]
  ioLLVMcode += "  br i1 " + self.mWhileExpressionBooleanResult_llvmName + ", label %" + loopLabel + ", label %" + endLabel + "\n\n"

#------------ Loop  
  ioLLVMcode += loopLabel + ":\n"
#--- Load current value
  ioLLVMcode += "  %" + currentValue + " = load " + elementTypeLLVMName + ", "
  ioLLVMcode += elementTypeLLVMName + "* %" + ptrVar + "\n"
  ioLLVMcode += "  store " + elementTypeLLVMName + " %" + currentValue + ", " + elementTypeLLVMName
             + "* " + llvmNameForLocalVariable (!self.mEnumeratedValueName) + "\n"
#--- Loop instructions
  [self.mDoInstructionList instructionListLLVMCode !?ioLLVMcode !inGenerationContext !?ioGenerationAdds]
  ioLLVMcode += "  br label %" + nextLabel + "\n\n"

  ioLLVMcode += nextLabel + ":\n"
#--- Increment pointer
  ioLLVMcode += "  %" + ptrVar + ".next = getelementptr inbounds " + elementTypeLLVMName + ", "
  ioLLVMcode += elementTypeLLVMName + " * %" + ptrVar + ", i32 1\n"
#--- Decrement index
  ioLLVMcode += "  %" + indexVar + ".next = add i32 %" + indexVar + ", -1\n"
  ioLLVMcode += "  br label %" + testLabel + "\n\n"

#--- End
  ioLLVMcode += endLabel + ":\n"
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @forInstructionOnArrayIR enterAccessibleEntities
  ?!@stringset ioInvokedRoutineSet
  ?!@uint ioMaxBranchOfOnInstructions
{
  [self.mWhileInstructionList enterAccessibleEntities !?ioInvokedRoutineSet !?ioMaxBranchOfOnInstructions]
  [self.mDoInstructionList enterAccessibleEntities !?ioInvokedRoutineSet !?ioMaxBranchOfOnInstructions]
  ioInvokedRoutineSet += self.mInvokedFunctionSet
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  CODE GENERATION (ON STRING)
#—————————————————————————————————————————————————————————————————————————————————————————————————

class @forInstructionOnLiteralStringIR : @abstractInstructionIR {
  public var @string mVarName
  public var @location mLocation
  public var @objectIR mStringElementIteratedObject
  public var @omnibusType mLiteralStringType
  public var @instructionListIR mWhileInstructionList
  public var @objectIR mWhileExpressionResult
  public var @instructionListIR mDoInstructionList
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @forInstructionOnLiteralStringIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext inGenerationContext
  ?!@generationAdds ioGenerationAdds
{
  let startLabel = "for.label.start." + [self.mLocation endLocationIndex]
  let testLabel = "for.label.test." + [self.mLocation endLocationIndex]
  let loopLabel = "for.label.loop." + [self.mLocation endLocationIndex]
  let whileLabel = "for.label.while." + [self.mLocation endLocationIndex]
  let nextLabel = "for.label.next." + [self.mLocation endLocationIndex]
  let endLabel  = "for.label.end." + [self.mLocation endLocationIndex]
  let ptrVar    = "for.ptr." + [self.mLocation endLocationIndex]
  let currentVar = "for.current." + [self.mLocation endLocationIndex]
  let stringLLVMTypeName = [self.mLiteralStringType llvmTypeName]
#--- Start of 'for' instruction
  ioLLVMcode += "  br label %" + startLabel + "\n\n"

  ioLLVMcode += startLabel + ":\n"
  ioLLVMcode += "  %" + ptrVar + ".start = load " + stringLLVMTypeName + ", " + stringLLVMTypeName + "* " + [self.mStringElementIteratedObject llvmName] + "\n"
  ioLLVMcode += "  br label %" + testLabel + "\n\n"
  
#------------ Test
  ioLLVMcode += testLabel + ":\n"
  ioLLVMcode += "  %" + ptrVar + " = phi " + stringLLVMTypeName + " [%" + ptrVar + ".start, %"
  ioLLVMcode += startLabel + "], [%" + ptrVar + ".next, %" + nextLabel + "]\n"
  ioLLVMcode += "  %" + currentVar + " = load i8, " + stringLLVMTypeName + " %" + ptrVar + "\n"
  ioLLVMcode += "  %" + currentVar + ".nul = icmp eq i8 %" + currentVar + ", 0\n"
  ioLLVMcode += "  br i1 %" + currentVar + ".nul, label %" + endLabel + ", label %" + whileLabel + "\n\n"

#------------ While
  ioLLVMcode += whileLabel + ":\n"
#--- While instructions
  [self.mWhileInstructionList instructionListLLVMCode !?ioLLVMcode !inGenerationContext !?ioGenerationAdds]
  ioLLVMcode += "  br i1 " + [self.mWhileExpressionResult llvmName] + ", label %" + loopLabel + ", label %" + endLabel + "\n\n"

#--------------------- Loop  
  ioLLVMcode += loopLabel + ":\n"
#--- Get current value
  ioLLVMcode += "  store i8 %" + currentVar + ", "
  ioLLVMcode += "i8 * " + llvmNameForLocalVariable (!self.mVarName) + "\n"
#--- Loop body
  [self.mDoInstructionList instructionListLLVMCode !?ioLLVMcode !inGenerationContext !?ioGenerationAdds]
  ioLLVMcode += "  br label %" + nextLabel + "\n\n"
#--- Next value
  ioLLVMcode += nextLabel + ":\n"
  ioLLVMcode += "  %" + ptrVar + ".next = getelementptr inbounds i8, "
  ioLLVMcode += "i8 * %" + ptrVar + ", i32 1\n"
  ioLLVMcode += "  br label %" + testLabel + "\n\n"

#--- End
  ioLLVMcode += endLabel + ":\n"
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @forInstructionOnLiteralStringIR enterAccessibleEntities
  ?!@stringset ioInvokedRoutineSet
  ?!@uint ioMaxBranchOfOnInstructions
{
  [self.mWhileInstructionList enterAccessibleEntities !?ioInvokedRoutineSet !?ioMaxBranchOfOnInstructions]
  [self.mDoInstructionList enterAccessibleEntities !?ioInvokedRoutineSet !?ioMaxBranchOfOnInstructions]
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
