#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension common_syntax {

  #·····················································································································

  rule <declaration> ?!@ast ioAST {
    $module$
    $identifier$ ?let moduleName
    ${$
    @procedureDeclarationListAST procedureDeclarationListAST = {}
    @structureFieldListAST fieldList = {}
    @serviceDeclarationListAST serviceListAST = {}
    @primitiveDeclarationListAST primitiveListAST = {}
    @guardDeclarationListAST guardListAST = {}
    @initList initList = {}
    repeat
    while
      <declaration_init> !?initList
    while
      <isr> !?ioAST.mISRDeclarationListAST
    while
      $var$
      $identifier$ ?let fieldName
      @lstring typeName
      select
        $typeName$ ?typeName
      or
        typeName = ["" nowhere]
      end
      $=$
      <expression> ?let initExpression
      fieldList += !fieldName !typeName !.expression{!initExpression:initExpression}
    while
      <procedure> !?procedureDeclarationListAST
    while
      <service> !?serviceListAST
    while
      <guard> !?guardListAST
    while
      <primitive> !?primitiveListAST
    while
      $;$
    end
    $}$
  #--- Declare init list
    @2lstringlist requiredByList = {}
    for function in procedureDeclarationListAST do
      requiredByList += !moduleName !function.mProcedureName
    end
    for service in serviceListAST do
      requiredByList += !moduleName !service.mServiceName
    end
    for guard in guardListAST do
      requiredByList += !moduleName !guard.mGuardName
    end
    for primitive in primitiveListAST do
      requiredByList += !moduleName !primitive.mPrimitiveName
    end
    for init in initList do
      ioAST.mInitList +=
       !init.mInitLocation
       !requiredByList
       !init.mInstructionList
       !init.mEndOfInitLocation
       !init.mPriority
    end
  #--- Declare structure
    ioAST.mDeclarationList += !@structureDeclaration.new {
      !moduleName
      !{}
      !fieldList
      !procedureDeclarationListAST
      !serviceListAST
      !primitiveListAST
      !guardListAST
    }
  #--- Declare global variable
    ioAST.mGlobalVarDeclarationList +=
      !moduleName
      !moduleName
      !@constructorCall.new {!moduleName !{} !.here}
      !{} # allowedRoutineList
      !{} # allowedInitList
      !{} # allowedPanicList
      !{} # allowedGuardList
      !{} # allowedISRList
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

