
setter @instructionListIR appendLoadGlobalVariable
  ?let @operandIR inTargetValue
  ?let @string inRegisterName
  ?let @unifiedTypeMap-proxy inTargetVarType
  ?volatile:@bool inIsVolatile
{
  self += !@loadGlobalVariableIR.new {!inTargetValue !inRegisterName !inTargetVarType !inIsVolatile}
}

#----------------------------------------------------------------------------------------------------------------------*

class @loadGlobalVariableIR : @abstractInstructionIR {
  @operandIR mTargetValue
  @string mVariableName
  @unifiedTypeMap-proxy mVariableType
  @bool mIsVolatile
}

#----------------------------------------------------------------------------------------------------------------------*

override method @loadGlobalVariableIR llvmInstructionCode
  ?!@string ioCode
  ?let @generationContext unused inGenerationContext
  ?!@stringset unused ioIntrinsicsDeclarationSet
{
  let llvmType = [mVariableType llvmTypeName]
  ioCode += "  " + mTargetValue + " = load "
  if mIsVolatile then
    ioCode += "volatile "
  end
  ioCode += llvmType + ", " + llvmType + "* @" + mangledNameForGlobalVariable (!mVariableName) + "\n"
}

#----------------------------------------------------------------------------------------------------------------------*

override method @loadGlobalVariableIR enterAccessibleEntities ?!@accessibleEntities ioAccessibleEntities {
  ioAccessibleEntities.mGlobalVariableSet += !mVariableName
}


#----------------------------------------------------------------------------------------------------------------------*
