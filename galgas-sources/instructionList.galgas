#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

abstract class @instructionAST {
  @location mInstructionLocation
}

#·······················································································································

list @instructionListAST {
  @instructionAST mInstruction
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <instructionList> ?!@ast ioAST !@instructionListAST outInstructionList {
    outInstructionList = {}
    repeat
    while
      <instruction> !?ioAST !?outInstructionList
#      $;$
#     select
#        $;$
#      or
#        let s = @string.separatorString 
#        if not [s containsCharacter !'\n'] then
#          error .here : "instruction should be terminated by an end of line or a ';'"
#        end
#      end
    end
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

abstract method @instructionAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph

#·······················································································································

method @instructionListAST noteInstructionListTypesInPrecedenceGraph
  ?!@semanticTypePrecedenceGraph ioGraph
{
  for () in self do
    [mInstruction noteInstructionTypesInPrecedenceGraph !?ioGraph]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

boolset @routineAttributes {
  mutating,
  guard
}

#·······················································································································

abstract method @instructionAST instructionSemanticAnalysis
  ?self:let @omnibusType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList

#·······················································································································

method @instructionListAST analyzeBranchInstructionList
  ?self:let @omnibusType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?endOfInstructionList:@location inEndOfInstructionList
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
#--- Begin branch
  [!?ioUniversalMap openBranch]
  for () in self do
    [!?ioInstructionGenerationList appendSourceLineComment ![mInstruction mInstructionLocation]]
    [mInstruction instructionSemanticAnalysis 
      !self:inSelfType
      !routineAttributes:inRoutineAttributes
      !context:inContext
      !mode:inMode
      !?temporary:ioTemporaries
      !?staticEntityMap:ioStaticEntityMap
      !?variableMap:ioUniversalMap
      !?alloca:ioAllocaList
      !?instructionListIR:ioInstructionGenerationList
    ]
  end
#--- End branch
  [!?ioUniversalMap closeBranch !inEndOfInstructionList]
}

#·······················································································································

method @instructionListAST analyzeRoutineInstructionList
  ?self:let @omnibusType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!generatedCode:@instructionListIR ioInstructionGenerationList
{
  for () in self do
    [!?ioInstructionGenerationList appendSourceLineComment ![mInstruction mInstructionLocation]]
    [mInstruction instructionSemanticAnalysis 
      !self:inSelfType
      !routineAttributes:inRoutineAttributes
      !context:inContext
      !mode:inMode
      !?temporary:ioTemporaries
      !?staticEntityMap:ioStaticEntityMap
      !?variableMap:ioUniversalMap
      !?alloca:ioAllocaList
      !?instructionListIR:ioInstructionGenerationList
    ]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  CODE GENERATION                                                                                                      
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @instructionListIR instructionListLLVMCode
  ?!@string ioLLVMcode
  ?let @generationContext inGenerationContext
  ?!@generationAdds ioGenerationAdds
{
  for () in self do
    [mInstructionGeneration llvmInstructionCode
      !?ioLLVMcode
      !inGenerationContext
      !?ioGenerationAdds
    ]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
