#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @accessList {
  @lstring mName
  @arrayElementAccess mArrayAccess
}

#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension common_syntax {

  #·····················································································································

  rule <access> !@location outInstructionLocation !@accessList outAccessList {
    outAccessList = {}
    outInstructionLocation = .here
    repeat
      $identifier$ ?let fieldName
      select
        outAccessList += !fieldName !.noAccess
      or
        $[$
        <expression> ?let indexExpression
        let @location endOfExp = .here
        $]$
        outAccessList += !fieldName !.access {!indexExpression:indexExpression !endOfExp:endOfExp}
      end
    while
      $.$
    end
  }

  #·····················································································································

}

#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH FROM EXPRESSION                                                                                     
#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @accessList addDependenceEdgeForStaticExpression
  ?let @lstring inConstantName
  ?!@semanticTypePrecedenceGraph ioGraph
{
  for (fieldName arrayAccess) in self do
    switch arrayAccess
    case noAccess :
    case access (@expressionAST indexExpression *) :
      [indexExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @accessList noteExpressionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  for (fieldName arrayAccess) in self do
    switch arrayAccess
    case noAccess :
    case access (@expressionAST indexExpression *) :
      [indexExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
    end
  end
}

#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc analyzeVariableAccess
  ?fieldList:let @accessList inAccessList
  ?self:let @unifiedTypeMap-proxy inSelfType
  ?directAccessToPropertiesAllowed:let @bool inDirectAccessToPropertiesAllowed
  ?inGuard:let @bool inGuard
  ?routineNameForInvocationGraph:let @lstring inCallerNameForInvocationGraph
  ?context:let @semanticContext inContext
  ?modes:let @stringset inModeSet
  ?allowPanic:let @bool inAllowPanic
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@staticStringMap ioGlobalLiteralStringMap
  ?!variableMap:@variableMap ioVariableMap
  ?!namedObjectMap:@namedObjectMap ioNamedObjectMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !resultPtr:@objectInMemoryIR outResultPtr
{
#--- Extract base variable and access
  var accessList = inAccessList
  [!?accessList popFirst ?let baseName ?let baseElementArrayAccess]
#--- Search base name in variable map — can be a local variable, a global variable, a control register, a constant
  [!?ioVariableMap neutralAccess
    !lkey:baseName
    ?type:@unifiedTypeMap-proxy type
    ?1*
    ?variableKind:let @objectInMemoryIR sourcePtr
    ?objectIR:let unused objectIR
    ?2*
  ]
#--- Handle array access
  @elementPtrList elementPtrList = {}
  analyzeArrayElementAccess (
    !baseElementArrayAccess
    !?type
    !?elementPtrList
    !baseName.location
    !self:inSelfType
    !directAccessToPropertiesAllowed:inDirectAccessToPropertiesAllowed
    !inGuard:inGuard
    !routineNameForInvocationGraph:inCallerNameForInvocationGraph
    !context:inContext
    !modes:inModeSet
    !allowPanic:inAllowPanic
    !?temporary:ioTemporaries
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioVariableMap
    !?namedObjectMap: ioNamedObjectMap
    !?alloca:ioAllocaList
    !?instructionListIR:ioInstructionGenerationList
  )
#--- Handle properties
  var publicIsRequired = false
  for (fieldName arrayElementAccess) in accessList do
    if [[type kind] isStructure] then
      [[type kind] structure ?1* ?propertyMap:let propertyMap ?propertyList:*]
      [propertyMap searchKey !fieldName ?let public ?type ?let index]
      if publicIsRequired && not public then
        error fieldName : "this field is not public"
      end
      elementPtrList += {!.absolute {!index:index} !"." + fieldName}
      publicIsRequired = true
      analyzeArrayElementAccess (
        !arrayElementAccess
        !?type
        !?elementPtrList
        !fieldName.location
        !self:inSelfType
        !directAccessToPropertiesAllowed:inDirectAccessToPropertiesAllowed
        !inGuard:inGuard
        !routineNameForInvocationGraph:inCallerNameForInvocationGraph
        !context:inContext
        !modes:inModeSet
        !allowPanic:inAllowPanic
        !?temporary:ioTemporaries
        !?staticStringMap:ioGlobalLiteralStringMap
        !?variableMap:ioVariableMap
        !?namedObjectMap: ioNamedObjectMap
        !?alloca:ioAllocaList
        !?instructionListIR:ioInstructionGenerationList
      )
    else
      error fieldName : "the field type is not a structure" : type
    end
  end
  if [elementPtrList length] == 0 then
    outResultPtr = sourcePtr
  else
    getLocalNameOfNewTempObjectInMemory (!type !?ioTemporaries ?outResultPtr)
    [!?ioInstructionGenerationList appendGetElementPtr
      !outResultPtr
      !sourcePtr
      !elementPtrList
    ]        
  end
}

#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc analyzeVariableWriteAccess
  ?fieldList:let @accessList inAccessList
  ?self:let @unifiedTypeMap-proxy inSelfType
  ?directAccessToPropertiesAllowed:let @bool inDirectAccessToPropertiesAllowed
  ?inGuard:let @bool inGuard
  ?routineNameForInvocationGraph:let @lstring inCallerNameForInvocationGraph
  ?context:let @semanticContext inContext
  ?modes:let @stringset inModeSet
  ?allowPanic:let @bool inAllowPanic
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@staticStringMap ioGlobalLiteralStringMap
  ?!variableMap:@variableMap ioVariableMap
  ?!namedObjectMap:@namedObjectMap ioNamedObjectMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !resultPtr:@objectInMemoryIR outResultPtr
{
#--- Extract base variable and access
  [inAccessList first ?let baseName ?let baseElementArrayAccess]
#--- Check target access
  @bool accessIsAllowed
  @bool isCopyable
  @objectInMemoryIR variableKind
  if ([inAccessList length] == 1) && (baseElementArrayAccess == .noAccess) then
    [!?ioVariableMap searchForWriteAccess
      !lkey:baseName
      ?1*
      ?readAccessAllowed:accessIsAllowed
      ?variableKind:variableKind
      ?objectIR:let unused objectIR
      ?copyable:isCopyable
      ?1*
    ]
  else
    [!?ioVariableMap searchForReadWriteAccess
      !lkey:baseName
      ?1*
      ?readAccessAllowed:accessIsAllowed
      ?variableKind:variableKind
      ?objectIR:let unused objectIR
      ?copyable:isCopyable
      ?1*
    ]
  end
  if not accessIsAllowed then
    error baseName : "routine has no access right on the this variable"
  end
#--- Register or variable ?
  if [variableKind isRegister] then
    analyzeRegisterWriteAccess (
      !fieldList:inAccessList
      !self:inSelfType
      !directAccessToPropertiesAllowed:inDirectAccessToPropertiesAllowed
      !inGuard:inGuard
      !routineNameForInvocationGraph:inCallerNameForInvocationGraph
      !context:inContext
      !modes:inModeSet
      !allowPanic:inAllowPanic
      !?temporary:ioTemporaries
      !?staticStringMap:ioGlobalLiteralStringMap
      !?variableMap:ioVariableMap
      !?namedObjectMap: ioNamedObjectMap
      !?alloca:ioAllocaList
      !?instructionListIR:ioInstructionGenerationList
      ?resultPtr:outResultPtr
    )
  else
  #--- Analyze variable
    analyzeVariableAccess (
      !fieldList:inAccessList
      !self:inSelfType
      !directAccessToPropertiesAllowed:inDirectAccessToPropertiesAllowed
      !inGuard:inGuard
      !routineNameForInvocationGraph:inCallerNameForInvocationGraph
      !context:inContext
      !modes:inModeSet
      !allowPanic:inAllowPanic
      !?temporary:ioTemporaries
      !?staticStringMap:ioGlobalLiteralStringMap
      !?variableMap:ioVariableMap
      !?namedObjectMap: ioNamedObjectMap
      !?alloca:ioAllocaList
      !?instructionListIR:ioInstructionGenerationList
      ?resultPtr:outResultPtr
    )
  end
#--- Source instance is copyable ?
  if not isCopyable then
    error baseName : "an $" + [[outResultPtr type] key] + " instance is not copyable"
  end
}

#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc analyzeRegisterWriteAccess
  ?fieldList:let @accessList inAccessList
  ?self:let @unifiedTypeMap-proxy inSelfType
  ?directAccessToPropertiesAllowed:let @bool inDirectAccessToPropertiesAllowed
  ?inGuard:let @bool inGuard
  ?routineNameForInvocationGraph:let @lstring inCallerNameForInvocationGraph
  ?context:let @semanticContext inContext
  ?modes:let @stringset inModeSet
  ?allowPanic:let @bool inAllowPanic
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@staticStringMap ioGlobalLiteralStringMap
  ?!variableMap:@variableMap ioVariableMap
  ?!namedObjectMap:@namedObjectMap ioNamedObjectMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !resultPtr:@objectInMemoryIR outResultPtr
{
  [inAccessList first ?let baseName ?let baseElementArrayAccess]
  if [inAccessList length] != 1 then
    error baseName : "invalid register access" : outResultPtr
  else
    [!?ioVariableMap neutralAccess
      !lkey:baseName
      ?type:let @unifiedTypeMap-proxy unused type
      ?readAccessAllowed:let unused accessIsAllowed
      ?variableKind:outResultPtr
      ?objectIR:let unused objectIR
      ?copyable:let unused isCopyable
      ?1*
    ]
    [outResultPtr register
      ?1*
      ?plmName:let @lstring registerName
      ?readable:let readable
      ?writable:let writable
      ?address:let @bigint registerAddress
      ?1*
      ?arraySize:let @uint arraySize
      ?elementSize:let @uint elementArraySize
    ]
    switch baseElementArrayAccess
    case noAccess :
      if arraySize > 0 then
        error baseName : "control register array access requires subscripting" : outResultPtr
      end
    case access (indexExpression endOfExp) :
      if arraySize == 0 then
        error baseName : "control register do not handle subscripting" : outResultPtr
      end
      [indexExpression analyzeExpression
        !self:inSelfType
        !directAccessToPropertiesAllowed:inDirectAccessToPropertiesAllowed
        !inGuard:inGuard
        !routineNameForInvocationGraph:inCallerNameForInvocationGraph
        !optionalTargetType:.null
        !context:inContext
        !modes:inModeSet
        !allowPanic:inAllowPanic
        !?temporary:ioTemporaries
        !?staticStringMap:ioGlobalLiteralStringMap
        !?variableMap:ioVariableMap
        !?namedObjectMap: ioNamedObjectMap
        !?alloca:ioAllocaList
        !?instructionListIR:ioInstructionGenerationList
        ?result:let @objectIR indexResult
      ]
      if [indexResult isLiteralInteger] then # Static expression
        [indexResult literalInteger ?1* ?value:let @bigint indexValue]
        if (indexValue < 0G) || (indexValue >= arraySize) then
          error endOfExp : "static index should be positive and < " + arraySize : outResultPtr
        else
          let address = registerAddress + indexValue * elementArraySize
          outResultPtr = .register {
            !type:type
            !plmName: registerName
            !readable:readable
            !writable:writable
            !address:address
            !sliceMap: {}
            !arraySize: 0
            !elementSize: 0
          }
        end
      elsif [[indexResult kind] isInteger] then
        if not [option plm_options.noPanicGeneration value] then
          [!?ioInstructionGenerationList testArrayIndex !indexResult !endOfExp !arraySize ?let generatePanicInstruction]
          if generatePanicInstruction && not inAllowPanic then
            error endOfExp : "index computation can generated panic"
          end
        end
        outResultPtr = .registerAddress {!type:type !llvmName: "%register.address." + ioTemporaries.mTemporaryIndex}
        ioTemporaries.mTemporaryIndex ++
        [!?ioInstructionGenerationList appendLoadVolatileRegisterAddress
          !outResultPtr
          !indexResult
          !registerAddress
          !elementArraySize
        ]
      else
        error endOfExp : "index expression is not an integer" : outResultPtr
      end
    end  
  end
}

#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc analyzeVariableReadWriteAccess
  ?fieldList:let @accessList inAccessList
  ?requiresCopy:let @bool inRequiresCopy
  ?self:let @unifiedTypeMap-proxy inSelfType
  ?directAccessToPropertiesAllowed:let @bool inDirectAccessToPropertiesAllowed
  ?inGuard:let @bool inGuard
  ?routineNameForInvocationGraph:let @lstring inCallerNameForInvocationGraph
  ?context:let @semanticContext inContext
  ?modes:let @stringset inModeSet
  ?allowPanic:let @bool inAllowPanic
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@staticStringMap ioGlobalLiteralStringMap
  ?!variableMap:@variableMap ioVariableMap
  ?!namedObjectMap:@namedObjectMap ioNamedObjectMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !resultPtr:@objectInMemoryIR outResultPtr
  !baseGlobalVariableName:@string outBaseGlobalVariableName # "" if base variable is not a global variable
{
  analyzeVariableAccess (
    !fieldList:inAccessList
    !self:inSelfType
    !directAccessToPropertiesAllowed:inDirectAccessToPropertiesAllowed
    !inGuard:inGuard
    !routineNameForInvocationGraph:inCallerNameForInvocationGraph
    !context:inContext
    !modes:inModeSet
    !allowPanic:inAllowPanic
    !?temporary:ioTemporaries
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioVariableMap
    !?namedObjectMap: ioNamedObjectMap
    !?alloca:ioAllocaList
    !?instructionListIR:ioInstructionGenerationList
    ?resultPtr:outResultPtr
  )
#--- Extract base variable and access
  [inAccessList first ?let baseName ?*]
#--- Check target access
  [!?ioVariableMap neutralAccess # searchForReadWriteAccess
    !lkey:baseName
    ?1*
    ?readAccessAllowed:let accessIsAllowed
    ?variableKind:let @objectInMemoryIR variableKind
    ?objectIR:let unused objectIR
    ?copyable:let isCopyable
    ?1*
  ]
  outBaseGlobalVariableName = if [variableKind isGlobalVariable] then baseName.string else "" end
  if not accessIsAllowed then
    error baseName : "routine has no access right on the this variable"
  end
  if inRequiresCopy && not isCopyable then
    error baseName : "an $" + [[outResultPtr type] key] + " instance is not copyable"
  end
}

#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
