#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @compileTimePrefixOperatorEnumeration {
 case notOp   # not
 case minusOp # -
}

#·······················································································································

getter @compileTimePrefixOperatorEnumeration string -> @string {
  switch self
  case notOp   : result = "not"
  case minusOp : result = "-"
  end
}

#·······················································································································

getter @compileTimePrefixOperatorEnumeration prefixOperator -> @prefixOperator {
  switch self
  case notOp   : result = .notOp
  case minusOp : result = .minusOp
  end
}

#·······················································································································

refclass @compiletimePrefixOperatorAST : @abstractDeclarationAST {
  public var @compileTimePrefixOperatorEnumeration mPrefixOperator
  public var @location mPrefixOperatorLocation
  public var @lstring mReceiverName
  public var @lstring mReceiverTypeName
  public var @ctExpressionAST mExpression
  public var @lstring mResultTypeName
}

#·······················································································································

override getter @compiletimePrefixOperatorAST locationForErrorSignaling -> @location outLocation {
  outLocation = self.mPrefixOperatorLocation
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <compileTimePrefixOperator> !@compileTimePrefixOperatorEnumeration outOperator {
    select
      $not$
      outOperator = .notOp
    or
      $-%$
      outOperator = .minusOp
    end
  }
  
  #·····················································································································

  rule <declaration> ?!@ast ioAST {
    $compiletime$
    $func$
    let operationLocation = @location.here
    <compileTimePrefixOperator> ?let prefixOperator
    $($
    $identifier$ ?let receiverName
    $\$type$ ?let receiverType
    $)$
    $->$
    $\$type$ ?let resultType
    ${$
    <compile_time_expression> ?let @ctExpressionAST expression
    $}$
    ioAST.mDeclarationListAST += !@compiletimePrefixOperatorAST.new {
      !prefixOperator
      !operationLocation
      !receiverName
      !receiverType
      !expression
      !resultType
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @compiletimePrefixOperatorAST enterInPrecedenceGraph
  ?!@semanticTypePrecedenceGraph ioGraph
{
  let nodeName = @lstring.new {![self.mPrefixOperator string] + "." + self.mReceiverTypeName !self.mPrefixOperatorLocation}
  [!?ioGraph addNode !nodeName !self]
  [!?ioGraph addEdge !nodeName !self.mReceiverTypeName]
  [!?ioGraph addEdge !nodeName !self.mResultTypeName]
  [!?ioGraph addEdge !compileTimeDeclarationBarrier () !nodeName ]
}

#·······················································································································

override getter @compiletimePrefixOperatorAST keyRepresentationForErrorSignaling -> @string outRepresentation {
  outRepresentation = [self.mPrefixOperator string] + " " + self.mReceiverTypeName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  ENTER IN CONTEXT                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @compiletimePrefixOperatorAST enterInContext
  ?!context: @semanticContext ioContext
  ?!@declarationDecorationList unused ioDecoratedDeclarationList
  ?!@decoratedRegularRoutineList unused ioDecoratedRoutineList
  ?!@routineListIR unused ioRoutineListIR
  ?!staticlistValues: @staticListInitializationMap unused ioStaticListValueMap
  ?!staticEntityMap: @staticEntityMap unused ioStaticEntityMap
  ?!registerUserAccess: @controlRegisterUserAccesMapAST unused ioControlRegisterUserAccesMapAST
  ?!userDefinedTypes: @userLLVMTypeDefinitionListIR unused ioUserLLVMTypeDefinitionListIR
{
#--- Check types
  [@unifiedTypeMap-entry makeEntry !?ioContext.mTypeMap !self.mReceiverTypeName ?let receiverTypeProxy]
  let receiverType = [receiverTypeProxy type]
  [receiverType checkIsCompileTimeType !self.mReceiverTypeName.location]
  [@unifiedTypeMap-entry makeEntry !?ioContext.mTypeMap !self.mResultTypeName ?let resultTypeProxy]
  let resultType = [resultTypeProxy type]
  [resultType checkIsCompileTimeType !self.mResultTypeName.location]
#--- Check native operator expression
  var @ctCheckMap ctCheckMap = {}
  [!?ctCheckMap insertKey !self.mReceiverName]
  [self.mExpression checkCompileTimeExpression !ctCheckMap]
#--- Enter in infix operator map  
  let key = prefixOperatorMapKey (!receiverType !self.mPrefixOperator !self.mPrefixOperatorLocation)
  let definition = @compileTimePrefixOperatorUsage.new {
    !self.mPrefixOperatorLocation
    !self.mReceiverName
    !self.mExpression
  }
  [!?ioContext.mPrefixOperatorMap insertKey !key !receiverType !resultType !definition]
#--- Enter derived operators ?
  if self.mPrefixOperator == .minusOp then
    let minusNoOvf_key = prefixOperatorMapKey (!receiverType !.minusNoOvf !self.mPrefixOperatorLocation)
    [!?ioContext.mPrefixOperatorMap insertKey !minusNoOvf_key !receiverType !resultType !definition]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   PREFIX OPERATOR USAGE
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @compileTimePrefixOperatorUsage : @prefixOperatorUsage {
  public var @location mPrefixOperatorLocation
  public var @lstring mReceiverOperandName
  public var @ctExpressionAST mExpression
}

#·······················································································································

override method @compileTimePrefixOperatorUsage generateCode
  ?let @objectIR inReceiverOperand
  ?let @location unused inOperatorLocation
  ?let @omnibusType inResultType
  ?noPanicCode: let @bool unused inDoNotGeneratePanicCode
  ?safeMode: let @bool unused inSafeMode
  ?!@semanticTemporariesStruct unused ioTemporaries
  ?!@instructionListIR unused ioInstructionGenerationList
  !@objectIR outResultValue
{
  [inReceiverOperand literalInteger ?1* ?value: let receiverValue]
  var @ctMap varMap = {}
  [!?varMap insertKey !self.mReceiverOperandName !receiverValue]
  [self.mExpression computeCompileTimeExpression !varMap ?result: let @bigint result]
  outResultValue = .literalInteger {!type: inResultType !value: result}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

