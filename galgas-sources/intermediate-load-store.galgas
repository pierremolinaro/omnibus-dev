setter @instructionListIR appendLoadFromMemory
  ?!@semanticTemporariesStruct ioTemporaries
  ?let @objectInMemoryIR inSource
  !@operandIR outResultingValue
{
  switch inSource
  case register (@unifiedTypeMap-proxy type @lstring name @bigint address *) :
    getNewTempVariable (!type !?ioTemporaries ?outResultingValue)
    [!?self appendLoadVolatileRegister
      !outResultingValue
      !name.string
      !address
    ]
  case globalVariable (@unifiedTypeMap-proxy type @string name @bool isVolatile) :
    getNewTempVariable (!type !?ioTemporaries ?outResultingValue)
    [!?self appendLoadGlobalVariable
      !outResultingValue
      !name
      !volatile:isVolatile
    ]
  case localValue (@unifiedTypeMap-proxy type @string name) :
    getNewTempVariable (!type !?ioTemporaries ?outResultingValue)
    [!?self appendLoadLocalVariable
      !outResultingValue
      !name
    ]
  case globalConstant (@operandIR value @string unused name) :
    outResultingValue = value
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

setter @instructionListIR appendStoreInMemory
  ?let @objectInMemoryIR inTargetPtr
  ?let @operandIR inValue
{
  switch inTargetPtr
  case register (@unifiedTypeMap-proxy type @lstring registerName @bigint address *) :
    [!?self appendStoreVolatileRegister !registerName.string !type !address !inValue]
  case globalVariable (@unifiedTypeMap-proxy type @string name @bool isVolatile) :
    [!?self appendStoreGlobalVariable !name !type !inValue !volatile:isVolatile]
  case localValue (@unifiedTypeMap-proxy type @string name) :
    [!?self appendStoreLocalVariable !name !type !inValue]
  case globalConstant (* *) :
     error .nowhere : "<<@objectInMemoryIR storeInMemory globalConstant>>"
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
