#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @objectInMemoryIR loadFromMemory
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !resultingValue:@operandIR outResultingValue
{
  switch self
  case register (@unifiedTypeMap-proxy type @lstring name @bigint address) :
    getNewTempVariable (!type !?ioTemporaries ?outResultingValue)
    [!?ioInstructionGenerationList appendComment ![outResultingValue.mValue string] + " = " + name]
    [!?ioInstructionGenerationList appendLoadVolatileRegister
      !outResultingValue
      !name.string
      !address
    ]
  case globalVariable (@unifiedTypeMap-proxy type @string name @bool isVolatile) :
    getNewTempVariable (!type !?ioTemporaries ?outResultingValue)
    [!?ioInstructionGenerationList appendComment ![outResultingValue.mValue string] + " = " + name]
    [!?ioInstructionGenerationList appendLoadGlobalVariable
      !outResultingValue
      !name
      !volatile:isVolatile
    ]
  case localValue (@unifiedTypeMap-proxy type @string name) :
    getNewTempVariable (!type !?ioTemporaries ?outResultingValue)
    [!?ioInstructionGenerationList appendComment ![outResultingValue.mValue string] + " = " + name]
    [!?ioInstructionGenerationList appendLoadLocalVariable
      !outResultingValue
      !name
    ]
  case globalConstant (@unifiedTypeMap-proxy type @string name) :
    getNewTempVariable (!type !?ioTemporaries ?outResultingValue)
    [!?ioInstructionGenerationList appendComment ![outResultingValue.mValue string] + " = " + name]
    [!?ioInstructionGenerationList appendLoadGlobalVariable
      !outResultingValue
      !name
      !volatile:false
    ]
  case fieldAccess (@unifiedTypeMap-proxy unused objectType
                    @string unused objectName
                    @unifiedTypeMap-proxy unused fieldType
                    @uint unused fieldIndex) :
    error .nowhere : "@objectInMemoryIR loadFromMemory:fieldAccess" : outResultingValue
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @objectInMemoryIR storeInMemory
  ?value:@operandIR inValue
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
  switch self
  case register (@unifiedTypeMap-proxy type @lstring registerName @bigint address) :
    [!?ioInstructionGenerationList appendStoreVolatileRegister !registerName.string !type !address !inValue]
  case globalVariable (@unifiedTypeMap-proxy type @string name @bool isVolatile) :
    [!?ioInstructionGenerationList appendStoreGlobalVariable !name !type !inValue !volatile:isVolatile]
  case localValue (@unifiedTypeMap-proxy type @string name) :
    [!?ioInstructionGenerationList appendStoreLocalVariable !name !type !inValue]
  case globalConstant (* *) :
     error .nowhere : "<<@objectInMemoryIR storeInMemory globalConstant>>"
  case fieldAccess (@unifiedTypeMap-proxy unused objectType
                    @string unused objectName
                    @unifiedTypeMap-proxy unused fielfType
                    @uint unused fieldIndex) :
#     error .nowhere : "<<@objectInMemoryIR storeInMemory localAccess>>"
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
