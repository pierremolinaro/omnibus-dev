#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

setter @instructionListIR appendStoreFromReference
  ?let @objectIR inTargetReference
  ?let @objectIR inValue
{
  switch inTargetReference
#--- Temporary ?
  case selfObject (*) :
    error [inTargetReference location] : "<<@instructionListIR appendStoreFromReference selfObject>>"
#--- References
  case globalVariableReference (@PLMType type @lstring plmName) :
    [!?self appendNoteGlobalVariableAccess !plmName]
    [!?self appendStoreGlobalVariable !plmName !type !inValue]
  case localVariableReference (@PLMType type plmName) :
    [!?self appendStoreLocalVariable !plmName !type !inValue]
  case llvmReference (@PLMType type llvmName) :
    [!?self appendStoreTemporaryReference !type !llvmName !inValue]
#--- Structure property
  case propertyRef (* * *) :
    error [inTargetReference location] : "<<@instructionListIR llvmGlobalObjectValue property>>"
#--- Values
  case llvmTemporaryValue (* llvmName) :
    error llvmName : "<<@instructionListIR llvmGlobalObjectValue llvmTemporaryValue>>"
  case literalInteger  (* *) :
    error [inTargetReference location] : "<<@instructionListIR llvmGlobalObjectValue literalInteger>>"
  case llvmStructureConstant (* *) :
    error [inTargetReference location] : "<<@instructionListIR llvmGlobalObjectValue llvmStructureConstant>>"
  case literalString  (* *) :
    error [inTargetReference location] : "<<@instructionListIR llvmGlobalObjectValue literalString>>"
  case zero (*) :
    error [inTargetReference location] : "<<@instructionListIR llvmGlobalObjectValue zero>>"
  case null :
    error [inTargetReference location] : "<<@instructionListIR llvmGlobalObjectValue null>>"
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

setter @instructionListIR appendLoadFromReference
  ?!@semanticTemporariesStruct ioTemporaries
  ?let @objectIR inSource
  !@objectIR outResultingValue
{
  switch inSource
  case null :
    error .nowhere : "<<@instructionListIR appendLoadFromReference null>>" : outResultingValue
  case selfObject (*) :
    outResultingValue = inSource
#--- References
  case globalVariableReference (@PLMType type @lstring name) :
    getNewTempVariable (!type !name !?ioTemporaries ?outResultingValue)
    [!?self appendNoteGlobalVariableAccess !name]
    [!?self appendLoadGlobalVariable !outResultingValue !name]
  case localVariableReference (@PLMType type name) :
    getNewTempVariable (!type !name !?ioTemporaries ?outResultingValue)
    [!?self appendLoadLocalVariable
      !outResultingValue
      !name
      !false
    ]
  case llvmReference (@PLMType type llvmName) :
    getNewTempVariable (!type !.nowhere !?ioTemporaries ?outResultingValue)
    [!?self appendLoadFromTemporaryReference
      !outResultingValue
      !llvmName
    ]
#--- Structure property
  case propertyRef (type plmName *) :
    getNewTempVariable (!type !plmName !?ioTemporaries ?outResultingValue)
    [!?self appendGetPropertyReference
      !outResultingValue
      !inSource
      !plmName
    ]
#--- Values
  case literalInteger (**) :
    outResultingValue = inSource
  case llvmTemporaryValue (**) :
    outResultingValue = inSource
  case llvmStructureConstant (* *) :
    outResultingValue = inSource
  case literalString (**) :
    outResultingValue = inSource
  case zero (*) :
    outResultingValue = inSource
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
