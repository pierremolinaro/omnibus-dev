#----------------------------------------------------------------------------------------------------------------------*

method @objectInMemoryIR loadFromMemory
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !resultingValue:@operandIR outResultingValue
{
  switch self
  case register (@unifiedTypeMap-proxy type @lstring name @bigint address) :
    getNewTempVariable (!?ioTemporaries ?outResultingValue)
    [!?ioInstructionGenerationList appendComment ![outResultingValue string] + " = " + name]
    [!?ioInstructionGenerationList appendLoadVolatileRegister
      !outResultingValue
      !name.string
      !type
      !address
    ]
  case globalVariable (@unifiedTypeMap-proxy type @string name @bool isVolatile) :
    getNewTempVariable (!?ioTemporaries ?outResultingValue)
    [!?ioInstructionGenerationList appendComment ![outResultingValue string] + " = " + name]
    [!?ioInstructionGenerationList appendLoadGlobalVariable
      !outResultingValue
      !name
      !type
      !volatile:isVolatile
    ]
  case localValue (@unifiedTypeMap-proxy type @lstring name) :
    getNewTempVariable (!?ioTemporaries ?outResultingValue)
    [!?ioInstructionGenerationList appendComment ![outResultingValue string] + " = " + name]
    [!?ioInstructionGenerationList appendLoadLocalVariable
      !outResultingValue
      !name.string
      !type
    ]
  case globalConstant (@unifiedTypeMap-proxy type @string name) :
    getNewTempVariable (!?ioTemporaries ?outResultingValue)
    [!?ioInstructionGenerationList appendComment ![outResultingValue string] + " = " + name]
    [!?ioInstructionGenerationList appendLoadGlobalVariable
      !outResultingValue
      !name
      !type
      !volatile:false
    ]
  case localAccess (@lstring access) :
    error access : "@objectInMemoryIR loadFromMemory:access" : outResultingValue
  case literalString (**) :
    error .nowhere : "@objectInMemoryIR loadFromMemory:literalString" : outResultingValue
  case literalStructure (*) :
    error .nowhere : "@objectInMemoryIR loadFromMemory:literalStructure" : outResultingValue
  end
}

#----------------------------------------------------------------------------------------------------------------------*

method @objectInMemoryIR storeInMemory
  ?value:@operandIR inValue
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
  switch self
  case register (@unifiedTypeMap-proxy type @lstring registerName @bigint address) :
    [!?ioInstructionGenerationList appendStoreVolatileRegister !registerName.string !type !address !inValue]
  case globalVariable (@unifiedTypeMap-proxy type @string name @bool isVolatile) :
    [!?ioInstructionGenerationList appendStoreGlobalVariable !name !type !inValue !volatile:isVolatile]
  case localValue (@unifiedTypeMap-proxy type @lstring unused name) :
    [!?ioInstructionGenerationList appendStoreLocalVariable !name.string !type !inValue]
  case globalConstant (* *) :
     error .nowhere : "<<@objectInMemoryIR storeInMemory globalConstant>>"
  case localAccess (@lstring unused access) :
     error .nowhere : "<<@objectInMemoryIR storeInMemory localAccess>>"
  case literalString (@string unused value @string unused name) :
     error .nowhere : "<<@objectInMemoryIR storeInMemory literalString>>"
  case literalStructure (@literalStructureList unused value) :
     error .nowhere : "<<@objectInMemoryIR storeInMemory literalStructure>>"
  end
}

#----------------------------------------------------------------------------------------------------------------------*
