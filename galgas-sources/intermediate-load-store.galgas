#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @objectInMemoryIR loadFromMemory
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !resultingValue:@operandIR outResultingValue
{
  switch self
  case register (@unifiedTypeMap-proxy type @lstring name @bigint address) :
    getNewTempVariable (!type !?ioTemporaries ?outResultingValue)
    [!?ioInstructionGenerationList appendLoadVolatileRegister
      !outResultingValue
      !name.string
      !address
    ]
  case globalVariable (@unifiedTypeMap-proxy type @string name @bool isVolatile) :
    getNewTempVariable (!type !?ioTemporaries ?outResultingValue)
    [!?ioInstructionGenerationList appendLoadGlobalVariable
      !outResultingValue
      !name
      !volatile:isVolatile
    ]
  case localValue (@unifiedTypeMap-proxy type @string name) :
    getNewTempVariable (!type !?ioTemporaries ?outResultingValue)
    [!?ioInstructionGenerationList appendLoadLocalVariable
      !outResultingValue
      !name
    ]
  case globalConstant (@operandIR value @string unused name) :
    outResultingValue = value
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @objectInMemoryIR storeInMemory
  ?value:@operandIR inValue
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
  switch self
  case register (@unifiedTypeMap-proxy type @lstring registerName @bigint address) :
    [!?ioInstructionGenerationList appendStoreVolatileRegister !registerName.string !type !address !inValue]
  case globalVariable (@unifiedTypeMap-proxy type @string name @bool isVolatile) :
    [!?ioInstructionGenerationList appendStoreGlobalVariable !name !type !inValue !volatile:isVolatile]
  case localValue (@unifiedTypeMap-proxy type @string name) :
    [!?ioInstructionGenerationList appendStoreLocalVariable !name !type !inValue]
  case globalConstant (* *) :
     error .nowhere : "<<@objectInMemoryIR storeInMemory globalConstant>>"
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
