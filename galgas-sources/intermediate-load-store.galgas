setter @instructionListIR appendLoadFromMemory
  ?!@semanticTemporariesStruct ioTemporaries
  ?let @objectInMemoryIR inSource
  !@operandIR outResultingValue
{
  switch inSource
  case registerAddress (* *) :
    error .nowhere : "<<@instructionListIR appendLoadFromMemory>>" : outResultingValue
  case register (@unifiedTypeMap-proxy type * @bigint address * * *) :
    getNewTempVariable (!type !?ioTemporaries ?outResultingValue)
    [!?self appendLoadVolatileRegister
      !outResultingValue
      !address
    ]
  case globalVariable (@unifiedTypeMap-proxy type @string name @bool isVolatile) :
    getNewTempVariable (!type !?ioTemporaries ?outResultingValue)
    [!?self appendLoadGlobalVariable
      !outResultingValue
      !name
      !volatile:isVolatile
    ]
  case localValue (@unifiedTypeMap-proxy type @string name) :
    getNewTempVariable (!type !?ioTemporaries ?outResultingValue)
    [!?self appendLoadLocalVariable
      !outResultingValue
      !name
    ]
  case staticConstant (@operandIR value @string unused name) :
    outResultingValue = value
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

setter @instructionListIR appendStoreInMemory
  ?let @objectInMemoryIR inTargetPtr
  ?let @operandIR inValue
{
  switch inTargetPtr
  case registerAddress (@unifiedTypeMap-proxy type @string llvmName) :
    [!?self appendStoreVolatileRegisterIndirect !type !llvmName !inValue]
  case register (@unifiedTypeMap-proxy type * @bigint address * * *) :
    [!?self appendStoreVolatileRegister !type !address !inValue]
  case globalVariable (@unifiedTypeMap-proxy type @string name @bool isVolatile) :
    [!?self appendStoreGlobalVariable !name !type !inValue !volatile:isVolatile]
  case localValue (@unifiedTypeMap-proxy type @string name) :
    [!?self appendStoreLocalVariable !name !type !inValue]
  case staticConstant (* *) :
     error .nowhere : "<<@objectInMemoryIR storeInMemory globalConstant>>"
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
