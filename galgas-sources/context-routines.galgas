#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

struct @routineDescriptor {
  @bool mIsPublic
  @bool mGlobal
  @routineKind mRoutineKind
  @mode mMode
  @procedureSignature mSignature
  @unifiedTypeMap-proxy mReturnType # .null if no returned value
  @bool mCanAccessProperties
  @bool mCanMutateProperties
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

#map @newRoutineMapForContext { # Key is a routine name
#  @newRoutineSignatureMapForContext mSignatureMap
#
#  insert insertKey error message "the '%K' routine is already declared in %L"
#  search searchKey error message "there is no '%K' routine"
#}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

#map @newRoutineSignatureMapForContext { # Key is the signature name
#  @routineDescriptor mDescriptor
#
#  insert insertKey error message "the '%K' routine is already declared in %L"
#  search searchKey error message "there is no '%K' routine"
#}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

func routineMangledNameFromAST ?let @string inReceiverTypeName
                               ?let @lstring inRoutineName
                               ?let @procFormalArgumentList inFormalArgumentList-> @lstring
{
  result = if inReceiverTypeName == "" then
    inRoutineName
  else
    @lstring.new {!inReceiverTypeName + "." + inRoutineName !inRoutineName.location}
  end
  result.string += "("
  for (formalArgumentPassingMode selector formalArgumentTypeName *) in inFormalArgumentList do
    result.string += [formalArgumentPassingMode formalPassingModeString] + selector + ":"
  end
  result.string += ")"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

func routineMangledNameFromCall ?let @string inReceiverTypeName
                                ?let @lstring inRoutineName
                                ?let @effectiveParameterListAST inEffectiveParameterList-> @lstring
{
  result = if inReceiverTypeName == "" then
    inRoutineName
  else
    @lstring.new {!inReceiverTypeName + "." + inRoutineName !inRoutineName.location}
  end
  result.string += "("
  for (passingMode selector) in inEffectiveParameterList do
    result.string += [passingMode matchingFormalArgument] + selector + ":"
  end
  result.string += ")"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc routineSignature ?!@unifiedTypeMap ioTypeMap
                      ?let @procFormalArgumentList inFormalArgumentList
                      !@procedureSignature outResult
{
  outResult = {}
  for (formalArgumentPassingMode selector formalArgumentTypeName *) in inFormalArgumentList do
    [@unifiedTypeMap-proxy makeProxy !?ioTypeMap !formalArgumentTypeName ?let typeProxy]
    outResult += !formalArgumentPassingMode !selector !typeProxy
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

func argumentSignatureFromFormalArgumentList ?let @location inRoutineNameLocation
                                             ?let @procFormalArgumentList inFormalArgumentList-> @lstring
{
  var key = ""
  key += "("
  for (formalArgumentPassingMode selector * *) in inFormalArgumentList do
    key += [formalArgumentPassingMode formalPassingModeString] + selector + ":"
  end
  key += ")"
  result = .new {!key !inRoutineNameLocation}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

func argumentSignatureFromEffectiveArgumentList ?let @location inRoutineNameLocation
                                                ?let @effectiveParameterListAST inEffectiveParameterList -> @lstring
{
  var key = ""
  key += "("
  for (passingMode selector) in inEffectiveParameterList do
    key += [passingMode matchingFormalArgument] + selector + ":"
  end
  key += ")"
  result = .new {!key !inRoutineNameLocation}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

#map @routineMapForContext { # Key is a mangled name
#  @lstring inRoutineName
#  @lstring mArgumentSignature
#  @lstring mRoutineLLVMName
#  @routineDescriptor mDescriptor
#
#  insert insertKey error message "the '%K' routine is already declared in %L"
#  search searchKey error message "there is no '%K' routine"
#}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

map @internalRoutineArgumentSignatureMapForContext { # argument signature is the key
  @lstring mRoutineLLVMName
  @routineDescriptor mDescriptor

  insert insertKey error message "the '%K' routine signature is already declared in %L"
  search searchKey %location error message "there is no '%K' routine signature"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

map @internalRoutineMapForContext { # Key is a mangled name
  @internalRoutineArgumentSignatureMapForContext mInternalRoutineArgumentSignatureMapForContext

  insert insertKey error message "the '%K' routine is already declared in %L"
  search searchKey error message "there is no '%K' routine"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

struct @routineMapForContext {
  @internalRoutineMapForContext mInternalRoutineMapForContext
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

setter @routineMapForContext insertKey
  ?let @lstring inRoutineName
  ?let @lstring inArgumentSignature
  ?let @lstring inRoutineLLVMName
  ?let @routineDescriptor inDescriptor
{
  with inRoutineName.string in !?mInternalRoutineMapForContext do
    [!?mInternalRoutineArgumentSignatureMapForContext insertKey
      !inArgumentSignature
      !inRoutineLLVMName
      !inDescriptor
    ]
  else
    @internalRoutineArgumentSignatureMapForContext internalRoutineArgumentSignatureMapForContext = {}
    [!?internalRoutineArgumentSignatureMapForContext insertKey
      !inArgumentSignature
      !inRoutineLLVMName
      !inDescriptor
    ]
    [!?mInternalRoutineMapForContext insertKey !inRoutineName !internalRoutineArgumentSignatureMapForContext]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @routineMapForContext searchKey
  ?let @lstring inRoutineName
  ?let @lstring inArgumentSignature
  !@lstring outRoutineLLVMName
  !@routineDescriptor outDescriptor
  !@location outKeyLocation
{
  [mInternalRoutineMapForContext searchKey !inRoutineName ?let routineArgumentSignatureMapForContext]
  [routineArgumentSignatureMapForContext searchKey !inArgumentSignature ?outRoutineLLVMName ?outDescriptor ?outKeyLocation]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

getter @routineMapForContext hasKey ?let @lstring inRoutineName ?let @lstring inArgumentSignature -> @bool {
  result = [mInternalRoutineMapForContext hasKey !inRoutineName]
  if result then
    [mInternalRoutineMapForContext searchKey !inRoutineName ?let routineArgumentSignatureMapForContext]
    result = [routineArgumentSignatureMapForContext hasKey !inArgumentSignature]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

# Key is a mangled name: "type.routineName"; for standalone routine, ".routineName"
map @guardMapForContext {
  @bool mIsPublic
  @procedureSignature mSignature

  insert insertKey error message "the '%K' guard is already declared in %L"
  search searchKey error message "there is no '%K' guard"
  remove removeKey error message "there is no '%K' guard"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  ENTER IN CONTEXT                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @procedureSignature {
  @procFormalArgumentPassingMode mFormalArgumentPassingMode
  @lstring mSelector
  @unifiedTypeMap-proxy mType
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
