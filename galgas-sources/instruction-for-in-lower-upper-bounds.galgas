#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @forLowerUpperBoundInstructionAST : @instructionAST {
  public var @lstring mVarName
  public var @lstring mTypeName
  public var @expressionAST mLowerBoundExpression
  public var @location mEndOf_lowerBoundExpression_instruction
  public var @expressionAST mUpperBoundExpression
  public var @location mEndOf_upperBoundExpression_instruction
  public var @instructionListAST mDoInstructionList
  public var @location mEndOf_do_instruction
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <instruction> ?!@ast ioAST ?!@instructionListAST ioInstructionList {
    $for$
    let @lstring varName
    select
      $_$
      varName = ["" here]
    or
      $identifier$ ?varName
    end
    <type_definition> !?ioAST ?let typeName
    $in$
    <expression> !?ioAST  ?let @expressionAST lowerBoundExpression
    let endOf_lowerBoundExpression_instruction = @location.here
    $..<$
    <expression> !?ioAST  ?let @expressionAST upperBoundExpression
    let endOf_upperBoundExpression_instruction = @location.here
    ${$
    <instructionList> !?ioAST ?let instructionList
    let endOfInstruction = @location.here
    $}$
    ioInstructionList += !@forLowerUpperBoundInstructionAST.new {
      !varName.location
      !varName
      !typeName
      !lowerBoundExpression
      !endOf_lowerBoundExpression_instruction
      !upperBoundExpression
      !endOf_upperBoundExpression_instruction
      !instructionList
      !endOfInstruction
    }
  }
  
  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @forLowerUpperBoundInstructionAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  if self.mTypeName.string != "" then
    [!?ioGraph noteNode !self.mTypeName]
  end
  [self.mDoInstructionList noteInstructionListTypesInPrecedenceGraph !?ioGraph]
  [self.mLowerBoundExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
  [self.mUpperBoundExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @forLowerUpperBoundInstructionAST instructionSemanticAnalysis
  ?self:let @omnibusType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
#--- Get iterated element type
  let type = [[inContext.mTypeMap searchKey !self.mTypeName] type]
  switch type.kind
  case boolean, void :
    error self.mTypeName : "an integer type is required here"
  case staticArrayType (* *) :
    error self.mTypeName : "an integer type is required here"
  case dynamicArrayType (*) :
    error self.mTypeName : "an integer type is required here"
  case enumeration (*) :
  case literalString :
    error self.mTypeName : "an integer type is required here"
  case opaque (*) :
    error self.mTypeName : "an integer type is required here"
  case compileTimeBool :
    error self.mTypeName : "an integer type is required here"
  case llvmType (*) :
    error self.mTypeName : "an integer type is required here"
  case generic (3*) :
    error self.mTypeName : "an integer type is required here"
  case compileTimeInteger :
    error self.mTypeName : "an integer type is required here"
  case structure (*) :
    error self.mTypeName : "an integer type is required here"
  case syncTool :
    error self.mTypeName : "an integer type is required here"
  case function (3*) :
    error self.mTypeName : "an integer type is required here"
  case integer (@bigint unused min @bigint unused max @bool unsigned @uint unused bitCount) :
  #--- Analyze expressions
    [self.mLowerBoundExpression analyzeExpression
      !self:inSelfType
      !routineAttributes:inRoutineAttributes
      !optionalTargetType:type
      !context:inContext
      !mode:inMode
      !?temporary:ioTemporaries
      !?staticEntityMap:ioStaticEntityMap
      !?variableMap:ioUniversalMap
      !?alloca:ioAllocaList
      !?instructionListIR:ioInstructionGenerationList
      ?result:@objectIR lowerBoundExpressionResult
    ]
    [!?ioInstructionGenerationList appendLoadWhenReference
      !?ioTemporaries
      !?lowerBoundExpressionResult
    ]  
    [self.mUpperBoundExpression analyzeExpression
      !self:inSelfType
      !routineAttributes:inRoutineAttributes
      !optionalTargetType:type
      !context:inContext
      !mode:inMode
      !?temporary:ioTemporaries
      !?staticEntityMap:ioStaticEntityMap
      !?variableMap:ioUniversalMap
      !?alloca:ioAllocaList
      !?instructionListIR:ioInstructionGenerationList
      ?result:@objectIR upperBoundExpressionResult
    ]
    [!?ioInstructionGenerationList appendLoadWhenReference
      !?ioTemporaries
      !?upperBoundExpressionResult
    ]  
  #--- Check expression types
    let lowerBound = checkAssignmentCompatibility (
      !inContext.mAssignmentOperatorMap
      !source:lowerBoundExpressionResult
      !targetAnnotationType:type
      !errorLocation:self.mEndOf_lowerBoundExpression_instruction
      !staticTypeAllowed:false
    )
    let upperBound = checkAssignmentCompatibility (
      !inContext.mAssignmentOperatorMap
      !source:upperBoundExpressionResult
      !targetAnnotationType:type
      !errorLocation:self.mEndOf_upperBoundExpression_instruction
      !staticTypeAllowed:false
    )
  #---
    [!?ioUniversalMap openOverrideForRepeatBlock]
  #--- Enter iteration variable name
    let enumeratedVarName = @lstring.new {!self.mVarName.string + "." + ioTemporaries.mTemporaryIndex !self.mVarName}
    ioTemporaries.mTemporaryIndex += 1
    ioAllocaList += !llvmNameForLocalVariable (!enumeratedVarName) !type !false
    if self.mVarName.string != "" then
     [!?ioUniversalMap insertLocalConstant !self.mVarName !false !type !enumeratedVarName]
    end
  #--- Analyze instruction list
    var @instructionListIR instructionGenerationList = {}
    [self.mDoInstructionList analyzeBranchInstructionList
      !self:inSelfType
      !routineAttributes:inRoutineAttributes
      !endOfInstructionList:self.mEndOf_do_instruction
      !context:inContext
      !mode:inMode
      !?temporary:ioTemporaries
      !?staticEntityMap:ioStaticEntityMap
      !?variableMap:ioUniversalMap
      !?alloca:ioAllocaList
      !?instructionListIR:instructionGenerationList
    ]
    [!?ioUniversalMap closeOverride !self.mEndOf_do_instruction]
  #--- Code generation
    ioInstructionGenerationList += !@forLowerUpperBoundInstructionIR.new {
      !enumeratedVarName
      !type
      !unsigned
      !self.mEndOf_do_instruction
      !lowerBound
      !upperBound
      !instructionGenerationList
    }
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  CODE GENERATION                                                                                                      
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @forLowerUpperBoundInstructionIR : @abstractInstructionIR {
  public var @string mVarName
  public var @omnibusType mType
  public var @bool mUnsigned
  public var @location mLocation
  public var @objectIR mLowerExpressionResult
  public var @objectIR mUpperExpressionResult
  public var @instructionListIR mInstructionList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#         for x in LOW ..< UP do INST end 
#  is implemented as:
#         x := LOW ; while x < UP do INST ; x := x + 1 end

override method @forLowerUpperBoundInstructionIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext inGenerationContext
  ?!@generationAdds ioGenerationAdds
{
  let llvmType = [self.mType llvmTypeName]
  let llvmVarName = llvmNameForLocalVariable (!self.mVarName)
  let testLabel = "for." + [self.mLocation locationIndex] + ".test"
  let loopLabel = "for." + [self.mLocation locationIndex] + ".loop"
  let endLabel  = "for." + [self.mLocation locationIndex] + ".end"
  let testResult = "%for." + [self.mLocation locationIndex] + ".test.result"
  let loadedVarName = "" + llvmNameForLocalVariable (!self.mVarName + ".loaded")
  let currentVarName = "" + llvmNameForLocalVariable (!self.mVarName + ".current")
  let nextVarName = "" + llvmNameForLocalVariable (!self.mVarName + ".next")
#--- Store LOW in x
  ioLLVMcode += "  store " + llvmType + " " + [self.mLowerExpressionResult llvmName] + ", " + llvmType + " * " + llvmVarName + "\n"
  ioLLVMcode += "  br label %" + testLabel + "\n\n"
#--- Test
  ioLLVMcode += testLabel + ":\n"
  ioLLVMcode += "  " + loadedVarName + " = load " + llvmType + ", " + llvmType + " * " + llvmVarName + "\n"
  ioLLVMcode += "  " + testResult + " = icmp "
  ioLLVMcode += if self.mUnsigned then "ult" else "slt" end
  ioLLVMcode += " " + llvmType + " " + loadedVarName + ", " + [self.mUpperExpressionResult llvmName] + "\n"
  ioLLVMcode += "  br i1 " + testResult + ", label %" + loopLabel + ", label %" + endLabel + "\n\n"
#--- Loop  
  ioLLVMcode += loopLabel + ":\n"
  [self.mInstructionList instructionListLLVMCode !?ioLLVMcode !inGenerationContext !?ioGenerationAdds]
  ioLLVMcode += "  " + currentVarName + " = load " + llvmType + ", " + llvmType + " * " + llvmVarName + "\n"
  ioLLVMcode += "  " + nextVarName + " = add " + llvmType + " " + currentVarName + ", 1\n"
  ioLLVMcode += "  store " + llvmType + " " + nextVarName + ", " + llvmType + " * " + llvmVarName + "\n"
  ioLLVMcode += "  br label %" + testLabel + "\n\n"
#--- End
  ioLLVMcode += endLabel + ":\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @forLowerUpperBoundInstructionIR enterAccessibleEntities
  ?!@stringset ioInvokedRoutineSet
  ?!@uint ioMaxBranchOfOnInstructions
{
  [self.mInstructionList enterAccessibleEntities !?ioInvokedRoutineSet !?ioMaxBranchOfOnInstructions]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
