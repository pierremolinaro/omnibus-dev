#—————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @letInstructionWithAssignmentAST : @instructionAST {
  public var @lstring mConstantName
  public var @lstring mOptionalTypeName
  public var @expressionAST mSourceExpression
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#—————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <instruction> ?!@ast ioAST ?!@instructionListAST ioInstructionList {
    $let$
    $identifier$ ?let varName
    let @lstring optionalTypeName
    select
      optionalTypeName = ["" nowhere]
    or
      <type_definition> !?ioAST ?optionalTypeName
    end
    $=$
    <expression> !?ioAST ?let @expressionAST expression
    ioInstructionList += !@letInstructionWithAssignmentAST.init {
      !varName.location
      !varName
      !optionalTypeName
      !expression
    }
  }

  #·····················································································································

}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @letInstructionWithAssignmentAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  if self.mOptionalTypeName.string != "" then
    [!?ioGraph noteNode !self.mOptionalTypeName]
  end
  [self.mSourceExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @letInstructionWithAssignmentAST instructionSemanticAnalysis
  ?self:let @omnibusType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
  let @omnibusType targetType = if self.mOptionalTypeName.string == ""
    then voidType ()
    else [[inContext.mTypeMap searchKey !self.mOptionalTypeName] type]
  end
#--- Analyze source expression
  [self.mSourceExpression analyzeExpression
    !self:inSelfType
    !routineAttributes:inRoutineAttributes
    !optionalTargetType:targetType
    !context:inContext
    !mode:inMode
    !?temporary:ioTemporaries
    !?staticEntityMap:ioStaticEntityMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:ioInstructionGenerationList
    ?result:@objectIR expressionResult
  ]
  [!?ioInstructionGenerationList appendLoadWhenReference
    !?ioTemporaries
    !?expressionResult
  ]  
#--- Declare local constant
  var result = checkAssignmentCompatibility (
    !inContext.mAssignmentOperatorMap
    !source:expressionResult
    !targetAnnotationType:targetType
    !errorLocation:self.mConstantName.location
    !staticTypeAllowed:true
  )
#---
  if not [[result type] instanciable] then
    error self.mConstantName : [result omnibusTypeDescriptionName] + " type is not instanciable" : result
  end
#--- Declare local constant
  let omnibusName = @lstring.init {!self.mConstantName.string + "." + ioTemporaries.mTemporaryIndex !self.mConstantName}
  ioTemporaries.mTemporaryIndex += 1
  if [result omnibusTypeDescriptionName] != ctIntegerTypeName () then
    ioAllocaList += !llvmNameForLocalVariable (!omnibusName) ![result type] !false
    [!?ioInstructionGenerationList appendStoreTemporaryReference
      ![result type]
      !llvmNameForLocalVariable (!omnibusName)
      !result
    ]
  end
  [!?ioUniversalMap insertLocalConstant !self.mConstantName !false ![result type] !omnibusName]
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
