#—————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#—————————————————————————————————————————————————————————————————————————————————————————————————

class @systemRoutineDeclarationAST : @abstractDeclarationAST {
  public var @lstring mReceiverTypeName
  public var @lstring mSystemRoutineName
  public var @mode mMode
  public var @bool mPublic
  public var @lstringlist mAttributeList
  public var @routineFormalArgumentListAST mFormalArgumentList
  public var @lstring mReturnTypeName
  public var @instructionListAST mInstructionList
  public var @location mEndOfInstructionListLocation
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#—————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <system_routine_declaration> ?!@ast ioAST  ?receiverTypeName: let @lstring inReceiverTypeName {
    let @bool publicAccess
    select
      publicAccess = false
    or
      $public$
      publicAccess = true
    end
    $section$
    $identifier$ ?let name
    let @mode mode
    select
      mode = .sectionMode
    or
      $safe$
      mode = .safeSectionMode
    end
    var @lstringlist attributeList = @[]
    repeat
    while
      $@attribute$ ?let attribute
      attributeList += !attribute
    end
    <procedure_formal_arguments> !?ioAST ?let formalArgumentList
    let @lstring returnTypeName
    select
      returnTypeName = ["" here]
    or
      $->$
      <type_definition> !?ioAST ?returnTypeName
    end
    ${$
    <instructionList> !?ioAST ?let instructionList
    ioAST.mDeclarationListAST += !@systemRoutineDeclarationAST {
      !inReceiverTypeName
      !name
      !mode
      !publicAccess
      !attributeList
      !formalArgumentList
      !returnTypeName
      !instructionList
      !.here
    }
    $}$
  }

  #·····················································································································

  # A primitive or a service accepts only input arguments and does not return any value

  rule <system_routine_declaration> ?!@ast ioAST  ?receiverTypeName: let @lstring inReceiverTypeName {
    let @bool publicAccess
    select
      publicAccess = false
    or
      $public$
      publicAccess = true
    end
    $primitive$
    $identifier$ ?let name
    let @mode mode
    select
      mode = .primitiveMode
    or
      $safe$
      mode = .safePrimitiveMode
    end
    var @lstringlist attributeList = @[]
    repeat
    while
      $@attribute$ ?let attribute
      attributeList += !attribute
    end
    <procedure_input_formal_arguments> !?ioAST ?let formalArgumentList
    ${$
    <instructionList> !?ioAST ?let instructionList
    $}$
    ioAST.mDeclarationListAST += !@systemRoutineDeclarationAST {
      !inReceiverTypeName
      !name
      !mode
      !publicAccess
      !attributeList
      !formalArgumentList
      !["" here]
      !instructionList
      !.here
    }
  }

  #·····················································································································

  rule <system_routine_declaration> ?!@ast ioAST  ?receiverTypeName: let @lstring inReceiverTypeName {
    let @bool publicAccess
    select
      publicAccess = false
    or
      $public$
      publicAccess = true
    end
    $service$
    $identifier$ ?let name
    let @mode mode
    select
      mode = .serviceMode
    or
      $safe$
      mode = .safeServiceMode
    end
    var @lstringlist attributeList = @[]
    repeat
    while
      $@attribute$ ?let attribute
      attributeList += !attribute
    end
    <procedure_input_formal_arguments> !?ioAST ?let formalArgumentList
    ${$
    <instructionList> !?ioAST ?let instructionList
    $}$
    ioAST.mDeclarationListAST += !@systemRoutineDeclarationAST {
      !inReceiverTypeName
      !name
      !mode
      !publicAccess
      !attributeList
      !formalArgumentList
      !["" here]
      !instructionList
      !.here
    }
  }

  #·····················································································································

}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES IN PRECEDENCE GRAPH                                                                                       
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @systemRoutineDeclarationAST enterInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  var s = ""
  if self.mReceiverTypeName.string != "" then
    s += self.mReceiverTypeName.string + "."
  end
  s += self.mSystemRoutineName.string + "("
  for (formalArgumentPassingMode selector * *) in self.mFormalArgumentList do
    s += [formalArgumentPassingMode formalPassingModeString] + selector + ":"
  end
  s += ")"
  let nodeKey = @lstring {!s !self.mSystemRoutineName.location}
  [!?ioGraph addNode !nodeKey !self]

  for (* * formalArgumentTypeName *) in self.mFormalArgumentList do
    [!?ioGraph noteNode !formalArgumentTypeName]
  end
  [self.mInstructionList noteInstructionListTypesInPrecedenceGraph !?ioGraph]
  if self.mReceiverTypeName.string != "" then
    [!?ioGraph noteNode !self.mReceiverTypeName]
    [!?ioGraph addEdge !nodeKey !self.mReceiverTypeName]
  end
  if self.mReturnTypeName.string != "" then
    [!?ioGraph noteNode !self.mReturnTypeName]
    [!?ioGraph addEdge !nodeKey !self.mReturnTypeName]
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override getter @systemRoutineDeclarationAST keyRepresentationForErrorSignaling -> @string {
  result = "system " + self.mReceiverTypeName + "." + self.mSystemRoutineName
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override getter @systemRoutineDeclarationAST locationForErrorSignaling -> @location {
  result = self.mSystemRoutineName.location
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#!   UTILITIES                                                                                                            
#—————————————————————————————————————————————————————————————————————————————————————————————————

private func systemUserMangledNameFromAST ?let @string inReceiverLLVMBaseTypeName
                                          ?let @routineKind inKind
                                          ?let @lstring inGuardName
                                          ?let @routineFormalArgumentListAST inFormalArgumentList
-> @lstring {
  var @string s
  switch inKind 
  case section :
    s = "section"
  case service :
    s = "service"
  case primitive :
    s = "primitive"
  case function :
    s = "???"
  end
  s += ".user."
  if inReceiverLLVMBaseTypeName != "" then
    s += inReceiverLLVMBaseTypeName + "."
  end
  s += inGuardName.string
  s += "("
  for (formalArgumentPassingMode selector * *) in inFormalArgumentList do
    s += [formalArgumentPassingMode formalPassingModeString] + selector + ":"
  end
  s += ")"
  result = @{!s !inGuardName.location}
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

func systemImplementationMangledNameFromAST ?let @string inReceiverLLVMBaseTypeName
                                            ?let @routineKind inKind
                                            ?let @lstring inGuardName
                                            ?let @routineFormalArgumentListAST inFormalArgumentList -> @lstring
{
  var @string s
  switch inKind 
  case section :
    s = "section"
  case service :
    s = "service"
  case primitive :
    s = "primitive"
  case function :
    s = "???"
  end
  s += ".implementation."
  if inReceiverLLVMBaseTypeName != "" then
    s += inReceiverLLVMBaseTypeName + "."
  end
  s += inGuardName.string
  s += "("
  for (formalArgumentPassingMode selector * *) in inFormalArgumentList do
    s += [formalArgumentPassingMode formalPassingModeString] + selector + ":"
  end
  s += ")"
  result = @{!s !inGuardName.location}
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  ENTER IN CONTEXT                                                                                                     
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @systemRoutineDeclarationAST enterInContext
  ?!context:@semanticContext ioContext
  ?!@declarationDecorationList unused ioDecoratedDeclarationList
  ?!@decoratedRegularRoutineList ioDecoratedRoutineList
  ?!@routineListIR ioRoutineListIR
  ?!staticlistValues:@staticListInitializationMap unused ioStaticListValueMap
  ?!staticEntityMap:@staticEntityMap unused ioStaticEntityMap
  ?!registerUserAccess:@controlRegisterUserAccesMapAST unused ioControlRegisterUserAccesMapAST
  ?!userDefinedTypes:@userLLVMTypeDefinitionListIR unused ioUserLLVMTypeDefinitionListIR
{
#---------------------------------------- Self type proxy
  [!?ioContext.mTypeMap makeOptionalEntry !self.mReceiverTypeName ?let selfTypeProxy]
#  log mReceiverTypeName
  let receiverLLVMBaseTypeName = if [selfTypeProxy isNull] then "" else [selfTypeProxy type].llvmBaseTypeName end
#--- Attributes
  var @stringset attributeSet = @[]
  for attribute in self.mAttributeList do
    attributeSet += !attribute.mValue.string
  end
#---
  let @routineKind routineKind
  switch self.mMode
  case sectionMode, safeSectionMode :
    routineKind = .section
  case serviceMode, safeServiceMode :
    routineKind = .service
  case primitiveMode, safePrimitiveMode :
    routineKind = .primitive
  case userMode, safeUserMode, anyMode, anySafeMode, bootMode, guardMode, panicMode, safeGuardMode, startupMode :
    error self.mSystemRoutineName : "internal error" : routineKind
  end
#--- Routine mangled names
  let userRoutineMangledName = systemUserMangledNameFromAST (
    !receiverLLVMBaseTypeName
    !routineKind
    !self.mSystemRoutineName
    !self.mFormalArgumentList
  )
  let implementationRoutineMangledName = systemImplementationMangledNameFromAST (
    !receiverLLVMBaseTypeName
    !routineKind
    !self.mSystemRoutineName
    !self.mFormalArgumentList
  )
#--- Signature
  routineTypedSignature (!?ioContext.mTypeMap !self.mFormalArgumentList ?let signature)
#--- Return type
  [!?ioContext.mTypeMap makeOptionalEntry  !self.mReturnTypeName ?let returnTypeProxy]
#--- Attributes
  var mutatingRoutine = false
  var noUnusedWarning = false
  for (attribute) in self.mAttributeList do
    if attribute.string == noUnusedWarningAttribute () then
      if noUnusedWarning then
        error attribute : "duplicated @" + noUnusedWarningAttribute () + " attribute" fixit { remove }
      end
      noUnusedWarning = true    
    elsif attribute.string == mutatingAttribute () then
      if mutatingRoutine then
        error attribute : "duplicated @" + mutatingAttribute () + " attribute" fixit { remove }
      end
      mutatingRoutine = true    
    else
      error attribute 
      : "unknown attribute; available attributes are @" + mutatingAttribute () + ", @" + noUnusedWarningAttribute ()
      fixit { remove }
    end
  end
  let @routineAttributes attributes = .mutatingRoutine
#--- Enter in routine map
  let @routineLLVMNameDict routineLLVMNameDict = [self.mMode routineLLVMDictionaryForSystemRoutine
    !userRoutineMangledName
    !implementationRoutineMangledName
  ]
  let routineMangledName = [signature mangledName !receiverTypeName: self.mReceiverTypeName !functionName: self.mSystemRoutineName]
  [!?ioContext.mRoutineMap insertKey
    !routineMangledName
    !self.mPublic
    !signature
    !returnTypeProxy
    !routineLLVMNameDict
    !true
    !self.mMode
  ]
#--- Prepare semantic analysis
  ioDecoratedRoutineList +=
    !receiverTypeName: self.mReceiverTypeName
    !mode: self.mMode
    !isRequired:false
    !routineKind: routineKind
    !warnIfUnused: not noUnusedWarning
    !exportedRoutine: true
    !routineAttributes: attributes
    !routineMangledLLVMName: implementationRoutineMangledName
    !formalArgumentList: self.mFormalArgumentList
    !warningOnUnusedArgs: noUnusedWarning
    !self.mInstructionList
    !self.mEndOfInstructionListLocation
    !returnTypeName: self.mReturnTypeName  # Empty string if no returned value
#--- 
  ioRoutineListIR += !@systemUserRoutineIR {
    !userRoutineMangledName
    !isRequired:false
    !warnsIfUnused:false
    !implementationRoutineMangledName
    !signature
    !selfTypeProxy
    !routineKind == .section # If false, service or primitive
    !returnTypeProxy
  }

}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#!  CODE GENERATION                                                                                                      
#—————————————————————————————————————————————————————————————————————————————————————————————————

list @sectionIRlist {
  public var @string mSectionCallName
  public var @string mSectionImplementationName
  public var @bool invocationFromAnyMode %initArgLabel
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

list @primitiveAndServiceIRlist {
  public var @string mCallName
  public var @string mImplementationName
  public var @bool mHasReturnValue
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

func llvmFunctionPrototype ?let @string inReturnTypeLLVMName
                           ?let @string inFunctionName
                           ?let @omnibusType inReceiverType
                           ?let @routineFormalArgumentListIR inFormalArgumentListForGeneration
                           -> @string {
  result = inReturnTypeLLVMName
  result += " @" + inFunctionName + " ("
  var first = true
  if inReceiverType.kind != .void then
    result += [inReceiverType llvmTypeName] + " * %receiver"
    first = false
  end
  for (mFormalArgumentKind mFormalArgumentType mFormalArgumentName) in inFormalArgumentListForGeneration do
    if first then
      first = false
    else
      result += ", "
    end
    switch mFormalArgumentKind
    case input :
      result += [mFormalArgumentType llvmTypeName] + " %in." + mFormalArgumentName
    case output :
      result += [mFormalArgumentType llvmTypeName] + " * " + llvmNameForLocalVariable (!mFormalArgumentName)
   case inputOutput :
      result += [mFormalArgumentType llvmTypeName] + " * " + llvmNameForLocalVariable (!mFormalArgumentName)
    end
  end
  result += ")" + llvmAttributeFunction ()
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

proc generateSectionDispatcher
  ?let @string inCurrentDirectory
  ?let @string inTargetName
  ?targetParameters:let @targetParameters inTargetParameters
  ?!@string ioAssemblerCode
  ?let @sectionIRlist inSectionIRlist
{
  for section in inSectionIRlist do (idx)
    if section.invocationFromAnyMode then
      var s = getTargetTextFile (
        !currentDir:inCurrentDirectory
        !from:inTargetName + "/" + inTargetParameters.mSectionDispatcherInvocationFromAnyMode
      )
      s = [s stringByReplacingStringByString !"!USER_ROUTINE!" !section.mSectionCallName]
      s = [s stringByReplacingStringByString !"!IMPLEMENTATION_ROUTINE!" !section.mSectionImplementationName]
      s = [s stringByReplacingStringByString !"!IDX!" ![idx string]]
      ioAssemblerCode += s
    else
      var s = getTargetTextFile (
        !currentDir:inCurrentDirectory
        !from:inTargetName + "/" + inTargetParameters.mSectionDispatcherInvocationFromUserMode
      )
      s = [s stringByReplacingStringByString !"!USER_ROUTINE!" !section.mSectionCallName]
      s = [s stringByReplacingStringByString !"!IMPLEMENTATION_ROUTINE!" !section.mSectionImplementationName]
      s = [s stringByReplacingStringByString !"!IDX!" ![idx string]]
      ioAssemblerCode += s
    end
  end
  ioAssemblerCode += getTargetTextFile (
    !currentDir:inCurrentDirectory
    !from:inTargetName + "/" + inTargetParameters.mSectionDispatcherHeader
  )
  for (callName implementationName *) in inSectionIRlist do (idx)
    var s = getTargetTextFile (
      !currentDir:inCurrentDirectory
      !from:inTargetName + "/" + inTargetParameters.mSectionDispatcherEntry
    )
    s = [s stringByReplacingStringByString !"!USER_ROUTINE!" !callName]
    s = [s stringByReplacingStringByString !"!IMPLEMENTATION_ROUTINE!" !implementationName]
    s = [s stringByReplacingStringByString !"!IDX!" ![idx string]]
    ioAssemblerCode += s
  end
  ioAssemblerCode += "\n"
  ioAssemblerCode += getTargetTextFile (
    !currentDir:inCurrentDirectory
    !from:inTargetName + "/" + inTargetParameters.mSectionHandler
  )
  ioAssemblerCode += "@-------------------------------------------------------------"
  ioAssemblerCode += "---------------------------------------------------------*\n"
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

proc generatePrimitiveAndServiceDispatcher
  ?let @string inCurrentDirectory
  ?let @string inTargetName
  ?!@string ioAssemblerCode
  ?let @primitiveAndServiceIRlist inServiceList
  ?targetParameters:let @targetParameters inTargetParameters
{
#--- Generate service handler
  ioAssemblerCode += getTargetTextFile (
    !currentDir:inCurrentDirectory
    !from:inTargetName + "/" + inTargetParameters.mServiceHandler
  )
#---
  for service in inServiceList do (idx)
    if service.mHasReturnValue then
      var s = getTargetTextFile (
        !currentDir:inCurrentDirectory
        !from:inTargetName + "/" + inTargetParameters.mServiceEntryWithReturnValue
      )
      s = [s stringByReplacingStringByString !"!ENTRY!" !service.mCallName]
      s = [s stringByReplacingStringByString !"!IDX!" ![idx string]]
      ioAssemblerCode += s
    else
      var s = getTargetTextFile (
        !currentDir:inCurrentDirectory
        !from:inTargetName + "/" + inTargetParameters.mServiceEntryNoReturnedValue
      )
      s = [s stringByReplacingStringByString !"!ENTRY!" !service.mCallName]
      s = [s stringByReplacingStringByString !"!IDX!" ![idx string]]
      ioAssemblerCode += s
    end
  end
  ioAssemblerCode += getTargetTextFile (
    !currentDir:inCurrentDirectory
    !from:inTargetName + "/" + inTargetParameters.mServiceDispatcherHeader
  )
  for service in inServiceList do (idx)
    var s = getTargetTextFile (
      !currentDir:inCurrentDirectory
      !from:inTargetName + "/" + inTargetParameters.mServiceDispatcherEntry
    )
    s = [s stringByReplacingStringByString !"!ENTRY!" !service.mImplementationName]
    s = [s stringByReplacingStringByString !"!IDX!" ![idx string]]
    ioAssemblerCode += s
  end
  ioAssemblerCode += "\n"
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#! SYSTEM USER ROUTINE IR
#—————————————————————————————————————————————————————————————————————————————————————————————————

class @systemUserRoutineIR : @abstractRoutineIR {
  public var @string mMangledImplementationName
  public var @routineTypedSignature mFormalArgumentList
  public var @unifiedTypeMapEntry mReceiverTypeProxy
  public var @bool mIsSection # If false, service or primitive
  public var @unifiedTypeMapEntry mReturnTypeProxy
}

#·······················································································································

override method @systemUserRoutineIR enterAccessibleEntities
   ?!@stringset ioInvokedRoutineSet
   ?!@uint unused ioMaxBranchOfOnInstructions
{
  ioInvokedRoutineSet += !self.mMangledImplementationName
}

#·······················································································································

override method @systemUserRoutineIR svcDeclarationGeneration
  ?!@primitiveAndServiceIRlist ioPrimitiveAndServiceList
  ?!@sectionIRlist ioSectionList
  ?!@generationAdds unused ioGenerationAdds
{
  let userMangledName = [self.mRoutineMangledName assemblerRepresentation]
  let implementationMangledName = [self.mMangledImplementationName assemblerRepresentation]
  if self.mIsSection then
    ioSectionList += !userMangledName !implementationMangledName !invocationFromAnyMode: false
  else
    ioPrimitiveAndServiceList += !userMangledName !implementationMangledName !false
  end
}

#·······················································································································

override method @systemUserRoutineIR llvmCodeGeneration 
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  ioLLVMcode += "declare "
  if [self.mReturnTypeProxy isNull] then
    ioLLVMcode += "void"
  else
    ioLLVMcode += [[self.mReturnTypeProxy type] llvmTypeName]
  end
  ioLLVMcode += " @" + [self.mRoutineMangledName assemblerRepresentation] + " ("
  var first = true
  if not [self.mReceiverTypeProxy isNull] then
    ioLLVMcode += [[self.mReceiverTypeProxy type] llvmTypeName] + "* %receiver"
    first = false
  end
  for (mFormalArgumentPassingMode * mTypeProxy *) in self.mFormalArgumentList do
    if first then
      first = false
    else
      ioLLVMcode += ", "
    end
    switch mFormalArgumentPassingMode
    case input :
      ioLLVMcode += [[mTypeProxy type] llvmTypeName]
    case output :
      ioLLVMcode += [[mTypeProxy type] llvmTypeName] + "*"
   case inputOutput :
      ioLLVMcode += [[mTypeProxy type] llvmTypeName] + "*"
    end
  end
  ioLLVMcode += ")" + llvmAttributeFunction () + "\n\n"
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
