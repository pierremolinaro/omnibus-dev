#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @standaloneRoutineCallIR : @abstractInstructionIR {
  @objectIR mResult
  @lstring mFunctionMangledName
  @string mFunctionNameForGeneration
  @procCallEffectiveParameterListIR mArgumentList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @standaloneRoutineCallIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  ioLLVMcode += "  "
  if mResult == .void then
    ioLLVMcode += "call void"
  else
    ioLLVMcode += [mResult llvmName] + " = call " + [mResult llvmTypeName]
  end
  ioLLVMcode += " @" + [mFunctionNameForGeneration assemblerRepresentation] + " ("
  for () in mArgumentList do
    switch mEffectiveParameterPassingMode
    case input:
      ioLLVMcode += [mParameter llvmTypeName] + "* " + [mParameter llvmName]
    case output:
      ioLLVMcode += [mParameter llvmTypeName] + " " + [mParameter llvmName]
    case outputInput:
      ioLLVMcode += [mParameter llvmTypeName] + "* " + [mParameter llvmName]
    end
  between
    ioLLVMcode += ", "
  end
  ioLLVMcode += ")\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @standaloneRoutineCallIR enterAccessibleEntities
  ?!@stringset ioInvokedRoutineSet
  ?!@uint unused ioMaxBranchOfOnInstructions
 {
  ioInvokedRoutineSet += !mFunctionNameForGeneration
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @standaloneRoutineCallIR_EX : @abstractInstructionIR {
  @objectIR mResult
  @lstring mFunctionMangledName
  @string mFunctionNameForGeneration
  @calleeKindIR mKind
  @procCallEffectiveParameterListIR mArgumentList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @standaloneRoutineCallIR_EX llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  ioLLVMcode += "  "
  if mResult == .void then
    ioLLVMcode += "call void"
  else
    ioLLVMcode += [mResult llvmName] + " = call " + [mResult llvmTypeName]
  end
  ioLLVMcode += " @"
  switch mKind 
  case function :
    ioLLVMcode += [mFunctionNameForGeneration assemblerRepresentation]
  case serviceFromProcessorUserMode :
    ioLLVMcode += llvmNameForServiceCall (!mFunctionNameForGeneration)
  case serviceFromProcessorPrivilegedMode :
    ioLLVMcode += llvmNameForServiceImplementation (!mFunctionNameForGeneration)
  case sectionFromProcessorUserMode :
    ioLLVMcode += llvmNameForSectionCall (!mFunctionNameForGeneration)
  case sectionFromProcessorPrivilegedMode :
    ioLLVMcode += llvmNameForSectionImplementation (!mFunctionNameForGeneration)
  case primitiveFromProcessorUserMode :
    ioLLVMcode += llvmNameForPrimitiveCall (!mFunctionNameForGeneration)
  case primitiveFromProcessorPrivilegedMode :
    ioLLVMcode += llvmNameForPrimitiveImplementation (!mFunctionNameForGeneration)
  end
  ioLLVMcode += " ("
  for () in mArgumentList do
    switch mEffectiveParameterPassingMode
    case input:
      ioLLVMcode += [mParameter llvmTypeName] + "* " + [mParameter llvmName]
    case output:
      ioLLVMcode += [mParameter llvmTypeName] + " " + [mParameter llvmName]
    case outputInput:
      ioLLVMcode += [mParameter llvmTypeName] + "* " + [mParameter llvmName]
    end
  between
    ioLLVMcode += ", "
  end
  ioLLVMcode += ")\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @standaloneRoutineCallIR_EX enterAccessibleEntities
  ?!@stringset ioInvokedRoutineSet
  ?!@uint unused ioMaxBranchOfOnInstructions
 {
  switch mKind 
  case function :
    ioInvokedRoutineSet += !mFunctionNameForGeneration
  case serviceFromProcessorUserMode :
    ioInvokedRoutineSet += !"service.user." + mFunctionNameForGeneration
  case serviceFromProcessorPrivilegedMode :
    ioInvokedRoutineSet += !"service.implementation." + mFunctionNameForGeneration
  case sectionFromProcessorUserMode :
    ioInvokedRoutineSet += !"section.user." + mFunctionNameForGeneration
  case sectionFromProcessorPrivilegedMode :
    ioInvokedRoutineSet += !"section.implementation." + mFunctionNameForGeneration
  case primitiveFromProcessorUserMode :
    ioInvokedRoutineSet += !"primitive.user." + mFunctionNameForGeneration
  case primitiveFromProcessorPrivilegedMode :
    ioInvokedRoutineSet += !"primitive.implementation." + mFunctionNameForGeneration
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
