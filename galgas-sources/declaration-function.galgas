#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @functionDeclarationAST : @abstractDeclarationAST {
  public var @mode mMode
  public var @bool mPublicFunction
  public var @lstring mReceiverTypeName # Empty string if standalone
  public var @lstring mFunctionName
  public var @lstringlist mFunctionAttributeList
  public var @routineFormalArgumentListAST mFunctionFormalArgumentList
  public var @lstring mFunctionReturnTypeName # Empty string if no return value
  public var @instructionListAST mFunctionInstructionList
  public var @location mEndOfFunctionDeclaration
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <declaration> ?!@ast ioAST {
    <function_declaration> !?ioAST !receiverTypeName: ["" nowhere]
  }

  #·····················································································································

  rule <function_declaration> ?!@ast ioAST ?receiverTypeName: let @lstring inReceiverTypeName {
  #--- Public ?
    let @bool publicAccess
    select
      publicAccess = false
    or
      $public$
      publicAccess = true
    end
    <function_header>
      !?ioAST
      ?mode:let @mode mode
      ?name:let procName
      ?attributes:let attributeList
      ?formalArguments:let procFormalArgumentList
    let @lstring returnTypeName
    select
      returnTypeName = ["" here]
    or
      $->$
      <type_definition>!?ioAST  ?returnTypeName
    end
    ${$
    <instructionList> !?ioAST ?let instructionList
    $}$
    ioAST.mDeclarationListAST += !@functionDeclarationAST.new {
      !mode
      !publicAccess
      !inReceiverTypeName
      !procName
      !attributeList
      !procFormalArgumentList
      !returnTypeName
      !instructionList
      !.here
    }
  }

  #·····················································································································

  rule <mode> !@mode outMode {
    select
      outMode = .anyMode
    or
      $user$
      outMode = .userMode
    or
      $panic$
      outMode = .panicMode
    or
      $boot$
      outMode = .bootMode
    or
      $startup$
      outMode = .startupMode
    or
      $section$
      outMode = .sectionMode
    or
      $service$
      outMode = .serviceMode
    or
      $primitive$
      outMode = .primitiveMode
    or
      $guard$
      outMode = .guardMode
    or
      $safe$
      select
        outMode = .anySafeMode
      or
        $user$
        outMode = .safeUserMode
      or
        $section$
        outMode = .safeSectionMode
      or
        $service$
        outMode = .safeServiceMode
      or
        $primitive$
        outMode = .safePrimitiveMode
      or
        $guard$
        outMode = .safeGuardMode
      end
    end
  }

  #·····················································································································

  rule <function_header>
    ?!@ast ioAST
    !mode:@mode outMode
    !name:@lstring outProcName
    !attributes:@lstringlist outAttributeList
    !formalArguments:@routineFormalArgumentListAST outProcFormalArgumentList
  {
    $func$
    $identifier$ ?outProcName
    <mode> ?outMode
    outAttributeList = {}
    repeat
    while
      $@attribute$ ?let attribute
      outAttributeList += !attribute
    end
    <procedure_formal_arguments> !?ioAST ?outProcFormalArgumentList
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES IN PRECEDENCE GRAPH                                                                                       
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @functionDeclarationAST enterInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  var s = ""
  if self.mReceiverTypeName.string != "" then
    s += self.mReceiverTypeName.string + "."
  end
  s += self.mFunctionName.string + "("
  for (formalArgumentPassingMode selector * *) in self.mFunctionFormalArgumentList do
    s += [formalArgumentPassingMode formalPassingModeString] + selector + ":"
  end
  s += ")"
  let nodeName = @lstring.new {!s !self.mFunctionName.location}
  [!?ioGraph addNode !nodeName !self]
  [!?ioGraph addEdge !nodeName !compileTimeDeclarationBarrier ()]
#---
  for (* * formalArgumentTypeName *) in self.mFunctionFormalArgumentList do
    [!?ioGraph noteNode !formalArgumentTypeName]
  end
  [self.mFunctionInstructionList noteInstructionListTypesInPrecedenceGraph !?ioGraph]
  if self.mReceiverTypeName.string != "" then
    [!?ioGraph noteNode !self.mReceiverTypeName]
  end
  if self.mFunctionReturnTypeName.string != "" then
    [!?ioGraph noteNode !self.mFunctionReturnTypeName]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @functionDeclarationAST keyRepresentationForErrorSignaling -> @string {
  result = "function " + self.mReceiverTypeName + "." + self.mFunctionName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @functionDeclarationAST locationForErrorSignaling -> @location {
  result = self.mFunctionName.location
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  ENTER IN CONTEXT                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @functionDeclarationAST enterInContext
  ?!context:@semanticContext ioContext
  ?!@declarationDecorationList unused ioDecoratedDeclarationList
  ?!@decoratedRegularRoutineList ioDecoratedRoutineList
  ?!@routineListIR unused ioRoutineListIR
  ?!staticlistValues:@staticListInitializationMap unused ioStaticListValueMap
  ?!staticEntityMap:@staticEntityMap unused ioStaticEntityMap
  ?!registerUserAccess:@controlRegisterUserAccesMapAST unused ioControlRegisterUserAccesMapAST
  ?!userDefinedTypes:@userLLVMTypeDefinitionListIR unused ioUserLLVMTypeDefinitionListIR
{
#--- Signature
  routineTypedSignature (!?ioContext.mTypeMap !self.mFunctionFormalArgumentList ?let signature)
#--- Return type
  [!?ioContext.mTypeMap makeOptionalEntry !self.mFunctionReturnTypeName ?let returnTypeProxy]
#--- Attributes
  var warnIfUnused = true
  var exportedFunction = false
  var canMutateProperties = false
  for (attribute) in self.mFunctionAttributeList do
    if attribute.string == noUnusedWarningAttribute () then
      if not warnIfUnused then
        error attribute : "duplicated @" + noUnusedWarningAttribute () + " attribute"
      end
      warnIfUnused = false    
    elsif attribute.string == exportedAttribute () then
      if exportedFunction then
        error attribute : "duplicated @" + exportedAttribute () + " attribute"
      end
      exportedFunction = true
    elsif attribute.string == mutatingAttribute () then
      if canMutateProperties then
        error attribute : "duplicated @" + mutatingAttribute () + " attribute"
      end
      canMutateProperties = true
    else
      error attribute : "unknown attribute; available attributes are @"
        + noUnusedWarningAttribute () + ", @" + noUnusedWarningAttribute () + " and @" + exportedAttribute ()
    end
  end
#--- Routine mangled name
  let functionLLVMName = routineMangledNameFromAST (
    !self.mReceiverTypeName.string
    !self.mFunctionName
    !self.mFunctionFormalArgumentList
  )
#---------------- Enter in routine map (for function availability)
  let routineLLVMNameDict = [self.mMode routineLLVMDictionaryForFunction !functionLLVMName]
  let routineMangledName = [signature mangledName !receiverTypeName: self.mReceiverTypeName.string !functionName: self.mFunctionName]
  [!?ioContext.mRoutineMap insertKey
    !routineMangledName
    !self.mPublicFunction
    !signature
    !returnTypeProxy
    !routineLLVMNameDict
    !exportedFunction
    !self.mMode
  ]
#---------------- Add decorated declaration (for function semantic analysis)
  ioDecoratedRoutineList +=
    !receiverTypeName: self.mReceiverTypeName
    !mode:self.mMode
    !isRequired:[ioContext.mRequiredRoutineSet hasKey !functionLLVMName.string]
    !routineKind: .function
    !warnIfUnused: warnIfUnused
    !exportedRoutine: exportedFunction
    !routineAttributes: .none
    !routineMangledLLVMName: functionLLVMName
    !formalArgumentList: self.mFunctionFormalArgumentList
    !warningOnUnusedArgs: true
    !self.mFunctionInstructionList
    !self.mEndOfFunctionDeclaration
    !returnTypeName: self.mFunctionReturnTypeName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! ROUTINE MAP
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

map @routineMapForContext {
  public var @bool mIsPublic
  public var @routineTypedSignature mSignature
  public var @unifiedTypeMapEntry mReturnTypeProxy # null if no returned value
  public var @routineLLVMNameDict mModeDictionary
  public var @bool mIsExported
  public var @mode mFunctionMode

  %insertSetter insertKey %errorMessage "the '%K' function is already declared in %L"
  %searchMethod searchKey %errorMessage "there is no '%K' function"
}

#·······················································································································

#typealias @routineLLVMNameDict = [@mode : @string]
dict @routineLLVMNameDict : @mode {
  let @string llvmName
}

#·······················································································································

setter @routineMapForContext addFunctionWithoutArgument
  ?omnibusTypeDescriptionName: let @string inOmnibusTypeDescriptionName
  ?llvmBaseTypeName:let @string inLLVMBaseTypeName
  ?methodName:let @lstring inMethodName
  ?mode:let @mode inMode
  ?resultTypeProxy:let @unifiedTypeMapEntry inResultType
  !routineLLVMName:@lstring outRoutineLLVMName
{
  let @routineFormalArgumentListAST formalArguments = {}
  let routineMangledName = @lstring.new {
    !inOmnibusTypeDescriptionName + "." + inMethodName.string + [formalArguments routineSignature !inMethodName]
    !inMethodName
  }
  outRoutineLLVMName = routineMangledNameFromAST (!inLLVMBaseTypeName !inMethodName !formalArguments)
  let modeDictionary = [inMode routineLLVMDictionaryForFunction !outRoutineLLVMName]
  [!?self insertKey
    !routineMangledName
    !true
    !{}
    !inResultType
    !modeDictionary
    !false
    !inMode
  ]
}

#·······················································································································

setter @routineMapForContext addFunctionWithInputArgument
  ?omnibusTypeDescriptionName: let @string inOmnibusTypeDescriptionName
  ?!context:@semanticContext ioContext
  ?llvmBaseTypeName:let @string inLLVMBaseTypeName
  ?methodName:let @lstring inMethodName
  ?mode:let @mode inMode
  ?inputArgumentSelector:let @string inInputSelector
  ?inputArgumentTypeProxy:let @unifiedTypeMapEntry inInputArgumentTypeProxy
  ?inputArgumentName: let @string inInputArgumentName
  ?resultTypeProxy:let @unifiedTypeMapEntry inResultType
  !routineLLVMName:@lstring outRoutineLLVMName
{
  let argumentList = @routineFormalArgumentListAST {
    !.input
    ![inInputSelector nowhere]
    ![[inInputArgumentTypeProxy key] nowhere]
    ![inInputArgumentName nowhere]
  }
  routineTypedSignature (!?ioContext.mTypeMap !argumentList ?let signature)
  let routineMangledName = @lstring.new {
    !inOmnibusTypeDescriptionName + "." + inMethodName.string + [argumentList routineSignature !inMethodName]
    !inMethodName
  }
  outRoutineLLVMName = routineMangledNameFromAST (!inLLVMBaseTypeName !inMethodName !argumentList)
  let modeDictionary = [inMode routineLLVMDictionaryForFunction !outRoutineLLVMName]
  [!?self insertKey
    !routineMangledName
    !true
    !signature
    !inResultType
    !modeDictionary
    !false
    !inMode
  ]
}

#·······················································································································

setter @routineMapForContext addFunctionWithTwoInputArguments
  ?omnibusTypeDescriptionName: let @string inOmnibusTypeDescriptionName
  ?!context:@semanticContext ioContext
  ?llvmBaseTypeName:let @string inLLVMBaseTypeName
  ?methodName:let @lstring inMethodName
  ?mode:let @mode inMode
  ?inputArgumentSelector1:let @string inInputSelector1
  ?inputArgumentTypeProxy1:let @unifiedTypeMapEntry inInputArgumentTypeProxy1
  ?inputArgumentName1: let @string inInputArgumentName1
  ?inputArgumentSelector2:let @string inInputSelector2
  ?inputArgumentTypeProxy2:let @unifiedTypeMapEntry inInputArgumentTypeProxy2
  ?inputArgumentName2: let @string inInputArgumentName2
  ?resultTypeProxy:let @unifiedTypeMapEntry inResultType
  !routineLLVMName:@lstring outRoutineLLVMName
{
  let argumentList = @routineFormalArgumentListAST {
    !.input
    ![inInputSelector1 nowhere]
    ![[inInputArgumentTypeProxy1 key] nowhere]
    ![inInputArgumentName1 nowhere]
  ,
    !.input
    ![inInputSelector2 nowhere]
    ![[inInputArgumentTypeProxy2 key] nowhere]
    ![inInputArgumentName2 nowhere]
  }
  outRoutineLLVMName = routineMangledNameFromAST (
    !inLLVMBaseTypeName
    !.new {!inMethodName !.nowhere}
    !argumentList
  )
  routineTypedSignature (!?ioContext.mTypeMap !argumentList ?let signature)
  let routineMangledName = @lstring.new {
    !inOmnibusTypeDescriptionName + "." + inMethodName.string + [argumentList routineSignature !inMethodName]
    !inMethodName
  }
  let modeDictionary = [inMode routineLLVMDictionaryForFunction !outRoutineLLVMName]
  [!?self insertKey
    !routineMangledName
    !true
    !signature
    !inResultType
    !modeDictionary
    !false
    !inMode
  ]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
