#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @allowedRoutineList {
  @bool mHasWriteAccess
  @lstring mReceiverTypeName
  @lstring mRoutineName
  @routineKind mRoutineKind
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @allowedInitList {
  @bool mHasWriteAccess
  @lbigint mInitPriority
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @allowedPanicList {
  @bool mHasWriteAccess
  @lstring mPanicName
  @lbigint mPanicPriority
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @allowedGuardList {
  @lstring mOptionalTypeName
  @lstring mGuardName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @allowedISRList {
  @bool mHasWriteAccess
  @lstring mISRName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @globalVarDeclarationList {
  @lstring mTypeName # Empty string if no type annotation
  @lstring mVarName
  @expressionAST mSourceExpression
  @allowedRoutineList mAllowedProcList
  @allowedInitList mAllowedInitList
  @allowedPanicList mAllowedPanicList
  @allowedGuardList mAllowedGuardList
  @allowedISRList mAllowedISRList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension common_syntax {

  #·····················································································································

  rule <declaration> ?!@ast ioAST {
    $var$
    $identifier$ ?let varName
    @lstring typeName
    select
      typeName = ["" nowhere]
    or
      $typeName$ ?typeName
    end
    $=$
    <expression> ?let @expressionAST expression
    @allowedRoutineList allowedRoutineList = {}
    @allowedInitList allowedInitList = {}
    @allowedPanicList allowedPanicList = {}
    @allowedGuardList allowedGuardList = {}
    @allowedISRList allowedISRList = {}
    ${$
    repeat
      @bool hasWriteAccess
      select
        hasWriteAccess = false
      or
        $attribute$ ?let attribute
        if attribute.string != "rw" then
          error attribute: "attribute should be @rw"
        end
        hasWriteAccess = true
      end
      select
        $guard$
        @lstring typeName
        select
          typeName = ["" nowhere]
        or
          $typeName$ ?typeName
          $.$
        end
        $identifier$ ?let sectionName
        allowedGuardList += !typeName !sectionName
      or
        $section$
        @lstring typeName
        select
          typeName = ["" nowhere]
        or
          $typeName$ ?typeName
          $.$
        end
        $identifier$ ?let sectionName
        allowedRoutineList += !hasWriteAccess !typeName !sectionName !.section
      or
        $func$
        $identifier$ ?let procName
        allowedRoutineList += !hasWriteAccess !["" nowhere] !procName !.function
      or
        $func$
        $typeName$ ?let typeName
        $.$
        $identifier$ ?let procName
        allowedRoutineList += !hasWriteAccess !typeName !procName !.function
      or
        $init$
        $integer$ ?let @lbigint priority
        allowedInitList += !hasWriteAccess !priority
      or
        $isr$
        $identifier$ ?let isrName
        allowedISRList += !hasWriteAccess !isrName
      or
        $panic$
        $func$
        $identifier$ ?let panicName
        $integer$ ?let @lbigint priority
        allowedPanicList += !hasWriteAccess !panicName !priority
      end
    while
    end
    $}$
    ioAST.mGlobalVarDeclarationList +=
      !typeName
      !varName
      !expression
      !allowedRoutineList
      !allowedInitList
      !allowedPanicList
      !allowedGuardList
      !allowedISRList
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES IN PRECEDENCE GRAPH                                                                                       
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @globalVarDeclarationList noteTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  for (globalVariableTypeName 7*) in self do
    if globalVariableTypeName.string != "" then
      let typeName = @lstring.new {!"$" + globalVariableTypeName !globalVariableTypeName.location}
      [!?ioGraph noteNode !typeName]
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  ENTER IN CONTEXT                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

map @allowedRoutineMap {
  @bool mHasWriteAccess
  insert insertKey error message "the '%K' routine is already declared in %L"
  search searchKey error message "there is no '%K' routine"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @globalVarDeclarationList-element enterInContext
  ?!context:@semanticContext ioContext
  ?!staticStringMap:@staticStringMap ioGlobalLiteralStringMap
{
#--- Get destination type
  @unifiedTypeMap-proxy variableAnnotationType
  if mTypeName.string == "" then
    variableAnnotationType = .null
  else
    variableAnnotationType = @unifiedTypeMap-proxy.searchKey {!ioContext.mTypeMap !mTypeName}
  end
#--- Analyze source expression
  @semanticTemporariesStruct temporaries = .default
  @instructionListIR instructionGenerationList = {}
  @allocaList allocaList = {}
  @variableMap variableMap = variableMapWithConstants (!context:ioContext)
  [mSourceExpression analyzeExpression
     !self:.null
     !routineNameForInvocationGraph:["compiler" nowhere]
     !optionalTargetType:variableAnnotationType
     !context:ioContext
     !modes:{}
     !allowPanic:true
     !?temporary:temporaries
     !?staticStringMap:ioGlobalLiteralStringMap
     !?variableMap:variableMap
     !?alloca:allocaList
     !?instructionListIR:instructionGenerationList
     ?result:let @operandIR expressionResult
  ]
  if ([instructionGenerationList length] > 0) || ([allocaList length] > 0) || not [expressionResult.mValue isStatic] then
    error mVarName : "source expression cannot be statically computed"
  end
#--- Check variable type and expression type
  if variableAnnotationType == .null && [[expressionResult.mType kind] isStaticInteger] then
    error mVarName : "cannot infer type"
  end
  let result = checkAssignmentCompatibility (
    !source:expressionResult
    !targetAnnotationType:variableAnnotationType
    !errorLocation:mVarName.location
    !staticTypeAllowed:false
  )
  @stringset executionModes = {}
#--- Check allowed init routines are defined
  @allowedRoutineMap allowedRoutineMap = {}
  for () in mAllowedInitList do
    [ioContext.mInitRoutineMap searchKey !.new {![mInitPriority.bigint string] !mInitPriority.location}]
    [!?allowedRoutineMap insertKey !initNameForInvocationGraph (!.null !mInitPriority) !mHasWriteAccess]
  end
#--- Check allowed panic routines are defined
  for () in mAllowedPanicList do
    if mPanicName.string == "setup" then
      [ioContext.mPanicSetupRoutinePriorityMap searchSetupKey
        !.new{![mPanicPriority.bigint string]
        !mPanicPriority.location}
      ]
    elsif mPanicName.string == "loop" then
      [ioContext.mPanicLoopRoutinePriorityMap searchLoopKey
        !.new{![mPanicPriority.bigint string]
        !mPanicPriority.location}
      ]
    else
      error mPanicName : "panic routine name should be \"setup\" or \"loop\""
    end
    [!?allowedRoutineMap insertKey
      !panicNameForInvocationGraph (!mPanicName.string !mPanicPriority)
      !mHasWriteAccess
    ]
  end
#--- Check allowed routines (procedures, sections, services) are defined
  for procedure in mAllowedProcList do
    @lstring routineMangledName
    @unifiedTypeMap-proxy receiverType
    if procedure.mReceiverTypeName.string == "" then
      routineMangledName = .new {!"." + procedure.mRoutineName !procedure.mRoutineName.location}
      receiverType = .null
    else
      routineMangledName = .new {!"$" + procedure.mReceiverTypeName + "." + procedure.mRoutineName !procedure.mRoutineName.location}
      receiverType = @unifiedTypeMap-proxy.searchKey {!ioContext.mTypeMap !procedure.mReceiverTypeName}
    end
    [ioContext.mRoutineMapForContext searchKey
      !routineMangledName
      ?*
      ?let procedureModeMap
      ?4*
    ]
    for () in procedureModeMap do
      executionModes += !lkey.string
    end
    @lstring routineNameForInvocationGraph
    switch procedure.mRoutineKind
    case function : routineNameForInvocationGraph = procNameForInvocationGraph (!receiverType !procedure.mRoutineName)
    case section : routineNameForInvocationGraph = sectionNameForInvocationGraph (!receiverType !procedure.mRoutineName)
    case service : routineNameForInvocationGraph = serviceNameForInvocationGraph (!receiverType !procedure.mRoutineName)
    case primitive : routineNameForInvocationGraph = primitiveNameForInvocationGraph (!receiverType !procedure.mRoutineName)
    end
    [!?allowedRoutineMap insertKey !routineNameForInvocationGraph !procedure.mHasWriteAccess]
  end
#--- Check allowed guards are defined
  @allowedGuardMap allowedGuardMap = {}
  for guard in mAllowedGuardList do
    @lstring guardMangledName
    @unifiedTypeMap-proxy receiverType
    if guard.mOptionalTypeName.string == "" then
      guardMangledName = .new {!"." + guard.mGuardName !guard.mGuardName.location}
      receiverType = .null
    else
      guardMangledName = .new {!"$" + guard.mOptionalTypeName + "." + guard.mGuardName !guard.mGuardName.location}
      receiverType = @unifiedTypeMap-proxy.searchKey {!ioContext.mTypeMap !guard.mOptionalTypeName}
    end
    [ioContext.mGuardMapForContext searchKey
      !guardMangledName
      ?*
    ]
    executionModes += !serviceModeName ()
    let routineNameForInvocationGraph = guardNameForInvocationGraph (!receiverType !guard.mGuardName)
    [!?allowedGuardMap insertKey !routineNameForInvocationGraph]
  end
#--- Check allowed isr are defined
  for isr in mAllowedISRList do
    if not [ioContext.mDefinedInterruptSet hasKey !isr.mISRName.string] then
      error isr.mISRName : "this isr is not defined"
    end
  end
#--- Enter variable in globale variable map
  [!?ioContext.mGlobalVariableMap insertKey
    !lkey:mVarName
    !type:result.mType
    !executionModeSet:executionModes
    !allowedRoutineMap:allowedRoutineMap
    !allowedGuardMap:allowedGuardMap
    !initialValue:result.mValue
    !isConstant:false # Is not constant
  ]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTIC ANALYSIS                                                                                                    
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @globalVarDeclarationList-element semanticAnalysis
  ?context:let @semanticContext inContext
  ?!intermediateCode:@intermediateCodeStruct ioIntermediateCodeStruct
{
#--- Code generation
  [inContext.mGlobalVariableMap searchKey
    !lkey:mVarName
    ?type:let inferredType
    ?executionModeSet:let executionModeSet
    ?allowedRoutineMap:let unused allowedProcedureMap
    ?allowedGuardMap:let unused allowedGuardMap
    ?initialValue:let expressionGeneratedCode
    ?isConstant:let unused isConstant
  ]
  [!?ioIntermediateCodeStruct.mGlobalVariableMap insertKey
    !mVarName
    !inferredType
    ![executionModeSet count] > 1
    !expressionGeneratedCode
  ]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  CODE GENERATION                                                                                                      
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @globalVariableMapIR-element generateLLVM ?!@string ioLLVMcode {
  ioLLVMcode += "@" + llvmNameForGlobalVariable (!lkey.string) + " = internal global "
  ioLLVMcode += [mType llvmTypeName] + " "
  let initialString = [mInitialValue llvmName]
  if initialString == "" then
    ioLLVMcode += "zeroinitializer"
  else
    ioLLVMcode += initialString
  end
  ioLLVMcode += "\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
