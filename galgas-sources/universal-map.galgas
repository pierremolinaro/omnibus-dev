#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! UNIVERSAL VARIABLE MAP
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

struct @universalValuedObjectMap {
  @flatValuedObjectMap mInternalPropertyAndRoutineMap
  @scopeStack mScopeStack
  @lstringlist mLocalObjectList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! INSERT SETTERS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

setter @universalValuedObjectMap insertTask
  ?let @lstring inTaskName
  ?let @omnibusType inTaskType
{
  [!?mInternalPropertyAndRoutineMap insertKey
    !inTaskName
    !.noValue
    !false
    !.task {!type:inTaskType}
  ]
}

#·······················································································································

setter @universalValuedObjectMap insertDriver
  ?let @lstring inDriverName
  ?let @bool inIsInstancied
  ?let @omnibusType inDriverType
{
  [!?mInternalPropertyAndRoutineMap insertKey
    !inDriverName
    !.noValue
    !false
    !.driver {!type:inDriverType !instancied:inIsInstancied}
  ]
}

#·······················································································································

setter @universalValuedObjectMap insertGlobalConstant
  ?let @lstring inConstantName
  ?let @objectIR inConstantObjectIR
{
  [!?mInternalPropertyAndRoutineMap insertKey
    !inConstantName
    !.noValue
    !false
    !.globalConstant {!objectIR:inConstantObjectIR}
  ]
}

#·······················································································································

setter @universalValuedObjectMap insertGlobalSyncInstance
  ?let @lstring inSyncInstanceName
  ?let @omnibusType inType
{
  [!?mInternalPropertyAndRoutineMap insertKey
    !inSyncInstanceName
    !.noValue
    !false
    !.globalSyncInstance {!type:inType !omnibusName:inSyncInstanceName}
  ]
}

#·······················································································································

setter @universalValuedObjectMap insertLocalConstant
  ?let @lstring inLocalConstantName
  ?let @bool inIsFormalInputArgument
  ?let @omnibusType inType
  ?let @lstring inOmnibusConstantName
{
  [!?mInternalPropertyAndRoutineMap insertKey
    !inLocalConstantName
    !.hasUnreadValue
    !false
    !.localConstant {!type:inType !omnibusName:inOmnibusConstantName !isFormalInputArgument:inIsFormalInputArgument}
  ]
  mLocalObjectList += !inLocalConstantName
}

#·······················································································································

setter @universalValuedObjectMap insertUsedLocalConstant
  ?let @lstring inLocalConstantName
  ?let @bool inIsFormalInputArgument
  ?let @omnibusType inType
  ?let @lstring inOmnibusConstantName
{
  [!?mInternalPropertyAndRoutineMap insertKey
    !inLocalConstantName
    !.hasReadValue
    !false
    !.localConstant {!type:inType !omnibusName:inOmnibusConstantName !isFormalInputArgument:inIsFormalInputArgument}
  ]
  mLocalObjectList += !inLocalConstantName
}

#·······················································································································

setter @universalValuedObjectMap insertLocalVariable
  ?let @lstring inLocalVariableName
  ?let @omnibusType inType
  ?let @lstring inOmnibusName
  ?let @valuedObjectState inVariableInitialState
  ?let @bool inObjectShouldBeValuedAtEndOfScope
{
  [!?mInternalPropertyAndRoutineMap insertKey
    !inLocalVariableName
    !inVariableInitialState
    !inObjectShouldBeValuedAtEndOfScope
    !.localVariable {!type:inType !omnibusName:inOmnibusName}
  ]
  mLocalObjectList += !inLocalVariableName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SEARCH METHODS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @universalValuedObjectMap searchEntity
  ?let @lstring inValuedObjectName
  !@valuedObject outEntity
{
  [mInternalPropertyAndRoutineMap searchKey !inValuedObjectName ?2* ?outEntity]
}

#·······················································································································

method @universalValuedObjectMap searchValuedObject
  ?let @lstring inValuedObjectName
  ?let @mode inMode
  ?let @stringset inInitializedDriverSet
  !@objectIR outObjectIR
{
  [mInternalPropertyAndRoutineMap searchKey !inValuedObjectName ?2* ?let entity]
  switch entity
  case task (@omnibusType type) :
    if inMode == .bootMode then
      error inValuedObjectName : "cannot call a task entry in a boot routine" : outObjectIR
    else
      outObjectIR = .reference {!type:type !llvmName:llvmNameForGlobalVariable (!inValuedObjectName)}
    end
  case driver (@omnibusType type instancied) :
    if inMode == .bootMode then
      error inValuedObjectName : "cannot call a driver in a boot routine" : outObjectIR
    elsif not instancied then
      error inValuedObjectName : "the driver should be instancied" : outObjectIR
    elsif not [inInitializedDriverSet hasKey !inValuedObjectName] then
      error inValuedObjectName : "the driver should be named in driver dependence list" : outObjectIR
    else
      outObjectIR = .reference {!type:type !llvmName:llvmNameForGlobalVariable (!inValuedObjectName)}
    end
  case globalConstant (@objectIR objectIR) :
    outObjectIR = objectIR
  case localConstant (@omnibusType type @lstring omnibusName *) :
    outObjectIR = .reference {!type:type !llvmName:llvmNameForLocalVariable (!omnibusName)}
  case localVariable (@omnibusType type @lstring omnibusName) :
    outObjectIR = .reference {!type:type !llvmName:llvmNameForLocalVariable (!omnibusName)}
  case globalSyncInstance (@omnibusType type @lstring omnibusName) :
    outObjectIR = .reference {!type:type !llvmName:llvmNameForGlobalSyncInstance (!omnibusName)}
  end
}

#·······················································································································

setter @universalValuedObjectMap readAccess
  ?let @lstring inValuedObjectName
{
  [mInternalPropertyAndRoutineMap searchKey !inValuedObjectName ?let valueState ?* ?let property]
  switch property
  case task (*) :
  case driver (* *) :
  case globalConstant (*) :
  case globalSyncInstance (2*) :
  case localConstant (3*) :
    if valueState == .hasUnreadValue then
      [!?mInternalPropertyAndRoutineMap setMObjectStateForKey !.hasReadValue !inValuedObjectName]
    end
  case localVariable (2*) :
    switch valueState 
    case noValue :
      error inValuedObjectName : "'" + inValuedObjectName + "' has no value"
    case hasUnreadValue :
      [!?mInternalPropertyAndRoutineMap setMObjectStateForKey !.hasReadValue !inValuedObjectName]
    case hasReadValue :
    end
  end
}

#·······················································································································

setter @universalValuedObjectMap searchValuedObjectForReadAccess
  ?let @lstring inValuedObjectName
  !@objectIR outObjectIR
{
  [mInternalPropertyAndRoutineMap searchKey !inValuedObjectName ?let valueState ?* ?let property]
  switch property
  case task (@omnibusType type) :
    outObjectIR = .reference {!type:type !llvmName:llvmNameForGlobalVariable (!inValuedObjectName)}
  case driver (@omnibusType type instancied) :
    if instancied then
      outObjectIR = .reference {!type:type !llvmName:llvmNameForGlobalVariable (!inValuedObjectName)}
    else
      error inValuedObjectName : "the driver should be instancied" : outObjectIR
    end
  case globalConstant (@objectIR objectIR) :
    outObjectIR = objectIR
  case localConstant (@omnibusType type @lstring omnibusName *) :
    outObjectIR = @objectIR.reference {!type:type !llvmName:llvmNameForLocalVariable (!omnibusName)}
    if valueState == .hasUnreadValue then
      [!?mInternalPropertyAndRoutineMap setMObjectStateForKey !.hasReadValue !inValuedObjectName]
    end
  case globalSyncInstance (@omnibusType type @lstring omnibusName) :
    outObjectIR = .reference {!type:type !llvmName:llvmNameForGlobalSyncInstance (!omnibusName)}
  case localVariable (@omnibusType type @lstring omnibusName) :
    switch valueState 
    case noValue :
      error inValuedObjectName : "'" + inValuedObjectName + "' has no value" : outObjectIR
    case hasUnreadValue :
      [!?mInternalPropertyAndRoutineMap setMObjectStateForKey !.hasReadValue !inValuedObjectName]
      outObjectIR = .reference {!type:type !llvmName:llvmNameForLocalVariable (!omnibusName)}
    case hasReadValue :
      outObjectIR = .reference {!type:type !llvmName:llvmNameForLocalVariable (!omnibusName)}
    end
  end
}

#·······················································································································

setter @universalValuedObjectMap searchValuedObjectForWriteAccess
  ?let @lstring inValuedObjectName
  !@objectIR outObjectIR
{
  [mInternalPropertyAndRoutineMap searchKey !inValuedObjectName ?let valueState ?* ?let property]
  switch property
  case task (*) :
    error inValuedObjectName : "a task cannot be written" : outObjectIR
  case driver (* *) :
    error inValuedObjectName : "a driver cannot be written" : outObjectIR
  case globalConstant (*) :
    error inValuedObjectName : "a constant cannot be written" : outObjectIR
  case globalSyncInstance (2*) :
    error inValuedObjectName : "a global sync instance cannot be written" : outObjectIR
  case localConstant (3*) :
    error inValuedObjectName : "a constant cannot be written" : outObjectIR
  case localVariable (@omnibusType type @lstring omnibusName) :
    outObjectIR = @objectIR.reference {!type:type !llvmName:llvmNameForLocalVariable (!omnibusName)}
    switch valueState 
    case noValue, hasReadValue :
      [!?mInternalPropertyAndRoutineMap setMObjectStateForKey !.hasUnreadValue !inValuedObjectName]
    case hasUnreadValue :
      warning inValuedObjectName : "previous '" + inValuedObjectName + "' value has not been read"
    end
  end
}

#·······················································································································

setter @universalValuedObjectMap searchValuedObjectForReadWriteAccess
  ?let @lstring inValuedObjectName
  !@objectIR outObjectIR
{
  [mInternalPropertyAndRoutineMap searchKey !inValuedObjectName ?let valueState ?* ?let property]
  switch property
  case task (*) :
    error inValuedObjectName : "a task has no value" : outObjectIR
  case driver (* *) :
    error inValuedObjectName : "a driver has no value" : outObjectIR
  case globalConstant (*) :
    error inValuedObjectName : "a constant cannot be written" : outObjectIR
  case localConstant (3*) :
    error inValuedObjectName : "a constant cannot be written" : outObjectIR
  case globalSyncInstance (2*) :
    error inValuedObjectName : "a global sync instance cannot be written" : outObjectIR
  case localVariable (@omnibusType type @lstring omnibusName) :
    switch valueState 
    case noValue :
      error inValuedObjectName : "object has no value" : outObjectIR
    case hasUnreadValue :
      outObjectIR = @objectIR.reference {!type:type !llvmName:llvmNameForLocalVariable (!omnibusName)}
    case hasReadValue :
      [!?mInternalPropertyAndRoutineMap setMObjectStateForKey !.hasUnreadValue !inValuedObjectName]
      outObjectIR = @objectIR.reference {!type:type !llvmName:llvmNameForLocalVariable (!omnibusName)}
    end
  end
}

#·······················································································································

method @universalValuedObjectMap searchValuedObjectType
  ?let @typePropertyGetterMap inTypePropertyGetterMap
  ?selfType: let @omnibusType inSelfType
  ?let @LValueAST inLValue
  !@omnibusType outType
{
  if inLValue.mIdentifier.string == "" then # Receiver is self
    outType = inSelfType
  else
    [mInternalPropertyAndRoutineMap searchKey !inLValue.mIdentifier ?2* ?let property]
    switch property
    case task (*) :
      error inLValue.mIdentifier : "undefined in this context" : outType
    case driver (* *) :
      error inLValue.mIdentifier : "undefined in this context" : outType
    case globalConstant (*) :
      error inLValue.mIdentifier : "undefined in this context" : outType
    case localConstant (3*) :
      error inLValue.mIdentifier : "undefined in this context" : outType
    case localVariable (@omnibusType type *) :
      outType = type
    case globalSyncInstance (@omnibusType type *) :
      outType = type
    end
  end
  [inLValue.mOperand getValuedObjectType !inTypePropertyGetterMap !?outType]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @LValueOperandAST getValuedObjectType ?let @typePropertyGetterMap inTypePropertyGetterMap ?!@omnibusType ioType {
  switch self
  case noOperand :
  case property (@lstring propertyName @LValueOperandAST next) :
    let @propertyGetterMap propertyGetterMap = ioType.propertyGetterMapp
    [propertyGetterMap searchKey !propertyName ?* ?let accessKind]
    switch accessKind
    case constantProperty (value) :
      ioType = [value type]
    case storedProperty (type *) :
      ioType = type
    case computedProperty (propertyTypeProxy *) :
      ioType = [propertyTypeProxy type]
    end
    [next getValuedObjectType !inTypePropertyGetterMap!?ioType]
  case arrayAccess (3* @LValueOperandAST next) :
    switch ioType.subscript
    case noSubscript :
    case literalString :
    case staticSubscript (@omnibusType elementType *) :
      ioType = elementType
    end
    [next getValuedObjectType !inTypePropertyGetterMap !?ioType]
  end
}


#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! STATE HANDLING
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @universalValuedObjectMap checkLocalVariableFinalState ?!@instructionListIR ioInstructionGenerationList {
  for entry in mInternalPropertyAndRoutineMap do
    switch entry.mValuedObject
    case task (*) :
    case driver (* *) :
    case globalSyncInstance (2*) :
    case globalConstant (*) :
    case localConstant (@omnibusType type @lstring omnibusName @bool isFormalInputArg) :
#      if entry.mObjectState == .hasUnreadValue then
#        warning entry.lkey : "'" + entry.lkey + "' is never read"
#      end
      if not isFormalInputArg then
        [!?ioInstructionGenerationList generateRelease !type !omnibusName]
      end
    case localVariable (@omnibusType type @lstring omnibusName) :
      if (entry.mObjectState == .noValue) && entry.mObjectShouldBeValuedAtEndOfScope then
        error entry.lkey : "'" + entry.lkey + "' should be valued at the end of routine"
      end
      [!?ioInstructionGenerationList generateRelease !type !omnibusName]
    end
  end
}

#·······················································································································

setter @universalValuedObjectMap openOverrideForSelectBlock {
  @referenceStateMap initialStateMap = {}
  for entry in mInternalPropertyAndRoutineMap do
    switch entry.mValuedObject
    case task (*) :
    case driver (* *) :
    case globalSyncInstance (2*) :
    case globalConstant (*) :
    case localConstant (3*) :
      [!?initialStateMap insertKey !entry.lkey !entry.mObjectState]
    case localVariable (**) :
      [!?initialStateMap insertKey !entry.lkey !entry.mObjectState]
    end
  end
  mScopeStack += !.selectScope !true !initialStateMap !{} !mLocalObjectList
  mLocalObjectList = {}
}

#·······················································································································

setter @universalValuedObjectMap openOverrideForRepeatBlock {
  @referenceStateMap initialStateMap = {}
  for entry in mInternalPropertyAndRoutineMap do
    switch entry.mValuedObject
    case task (*) :
    case driver (* *) :
    case globalSyncInstance (2*) :
    case globalConstant (*) :
    case localConstant (3*) :
      [!?initialStateMap insertKey !entry.lkey !entry.mObjectState]
    case localVariable (**) :
      [!?initialStateMap insertKey !entry.lkey !entry.mObjectState]
    end
  end
  mScopeStack += !.repeatScope !true !initialStateMap ! {} !mLocalObjectList
  mLocalObjectList = {}
}

#·······················································································································

setter @universalValuedObjectMap openBranch {
  [mScopeStack last ?* ?* ?let initialStateMap ?2*]
  for (key objectState) in initialStateMap do
    [!?mInternalPropertyAndRoutineMap setMObjectStateForKey !objectState !key]
  end
}

#·······················································································································

setter @universalValuedObjectMap closeBranch ?let @location inErrorLocation {
  [!?mScopeStack popLast ?let scopeKind ?let firstBranch ?let initialStateMap ?var referenceStateMap ?let localObjectList]
  if firstBranch then
    @referenceStateMap newReferenceStateMap = {}
    for entry in mInternalPropertyAndRoutineMap do
      switch entry.mValuedObject
      case task (*) :
      case driver (* *) :
      case globalConstant (*) :
      case globalSyncInstance (2*) :
      case localConstant (3*) :
        [!?newReferenceStateMap insertKey !entry.lkey !entry.mObjectState]
      case localVariable (**) :
        [!?newReferenceStateMap insertKey !entry.lkey !entry.mObjectState]
      end
    end
    mScopeStack += !scopeKind !false !initialStateMap !newReferenceStateMap !localObjectList
  else
    switch scopeKind
    case selectScope :
      for (lkey referenceState) in referenceStateMap do
        [mInternalPropertyAndRoutineMap searchKey !lkey ?let currentObjectState ?2*]
        if referenceState == currentObjectState then # Ok
        elsif (currentObjectState == .hasUnreadValue) & (referenceState == .hasReadValue) then # Ok
        elsif (currentObjectState == .hasReadValue) & (referenceState == .hasUnreadValue) then # Ok, modify reference state
          [!?referenceStateMap setMStateForKey !.hasReadValue !lkey]
        else
          error inErrorLocation
            : "invalid state for object '" + lkey + "': found " + currentObjectState + ", required " + referenceState
        end
      end
      mScopeStack += !scopeKind !false !initialStateMap !referenceStateMap !localObjectList
    case repeatScope :
     for (lkey objectState) in referenceStateMap do
        [mInternalPropertyAndRoutineMap searchKey !lkey ?let currentObjectState ?2*]
        if objectState != currentObjectState then
          error inErrorLocation
            : "invalid state for object '" + lkey + "': found " + currentObjectState + ", required " + objectState
        end
      end
      mScopeStack += !scopeKind !false !initialStateMap !referenceStateMap !localObjectList
    end
  end
}

#·······················································································································

setter @universalValuedObjectMap closeOverride ?let @location inErrorLocation {
  [!?mScopeStack popLast ?* ?* ?* ?let referenceStateMap ?let localObjectList]
#--- Restore value object state
  for (key objectState) in referenceStateMap do
    [!?mInternalPropertyAndRoutineMap setMObjectStateForKey !objectState !key]
  end
#--- For current scope objects, check their final state
  for (lkey) in mLocalObjectList do
    [mInternalPropertyAndRoutineMap searchKey
      !lkey
      ?let currentObjectState
      ?let objectShouldBeValuedAtEndOfScope
      ?let possibleValuedObject
    ]
    switch possibleValuedObject
    case task (*) :
    case driver (* *) :
    case globalSyncInstance (2*) :
    case globalConstant (*) :
    case localConstant (3*) :
      if currentObjectState == .hasUnreadValue then
        warning inErrorLocation : "'" + lkey + "' is never read"
      end
    case localVariable (**) :
      if (currentObjectState == .noValue) && objectShouldBeValuedAtEndOfScope then
        error inErrorLocation : "'" + lkey + "' should be valued at the end of routine"
      end
    end
  end
#--- Remove current scope objets
  for (lkey) in mLocalObjectList do
    [!?mInternalPropertyAndRoutineMap removeKey !lkey ?3*]
  end
#--- Restore current scope objects
  mLocalObjectList = localObjectList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! INTERNAL MAPS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

map @flatValuedObjectMap { # Key is a valued object
  @valuedObjectState mObjectState
  @bool mObjectShouldBeValuedAtEndOfScope
  @valuedObject mValuedObject

  insert insertKey error message "the '%K' entity is already declared in %L"
  search searchKey error message "there is no '%K' entity"
  remove removeKey error message "there is no '%K' entity"
}

#·······················································································································

enum @scopeKind {
  case selectScope
  case repeatScope
}

#·······················································································································

list @scopeStack {
  @scopeKind mScopeKind
  @bool mFirstBranch
  @referenceStateMap mInitialStateMap
  @referenceStateMap mReferenceStateMap
  @lstringlist mObjectList
}

#·······················································································································

map @referenceStateMap {
  @valuedObjectState mState
  insert insertKey error message "INTERNAL ERROR"
  search searchKey error message "INTERNAL ERROR"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! VALUED OBJECT
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @valuedObject {
  case driver (@omnibusType type @bool instancied)
  case task (@omnibusType type)
  case globalConstant (@objectIR objectIR)
  case localConstant (@omnibusType type @lstring omnibusName @bool isFormalInputArgument)
  case localVariable (@omnibusType type @lstring omnibusName)
  case globalSyncInstance (@omnibusType type @lstring omnibusName)
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! VALUED OBJECT STATE
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @valuedObjectState {
  case noValue
  case hasUnreadValue
  case hasReadValue
}

#·······················································································································

getter @valuedObjectState string -> @string {
  switch self
  case noValue : result = "no value"
  case hasUnreadValue : result = "unread value"
  case hasReadValue : result = "read value"
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
