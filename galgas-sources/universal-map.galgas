#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! UNIVERSAL PROPERTY - ROUTINE MAP
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
# Cette table contient routines et variables. La clé est le nom de la variable ou le nom de la routine. Il est valide
# d'avoir plusieurs routines de même nom, du moment qu'elle se distinguent par leur signature. Il est valide d'avoir une
# variable de même nom qu'une routine : la syntaxe permet de savoir si une routine ou une variable est attendue.

#·······················································································································

struct @universalPropertyAndRoutineMapForContext {
  @internalUniversalPropertyAndRoutineMapForContext mInternalPropertyAndRoutineMapForContext
  @scopeStack mScopeStack
  @lstringlist mLocalObjectList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! INSERT SETTERS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

setter @universalPropertyAndRoutineMapForContext insertRoutine
  ?let @lstring inRoutineName
  ?let @lstring inArgumentSignature
  ?let @string inLLVMInvocationRoutineName
  ?let @routineDescriptor inDescriptor
{
  with inRoutineName.string in !?mInternalPropertyAndRoutineMapForContext do
    [!?mRoutineSignatureMapForContext insertKey
      !inArgumentSignature
      !inLLVMInvocationRoutineName
      !inDescriptor
    ]
  else
    @routineSignatureMapForContext routineSignatureMapForContext = {}
    [!?routineSignatureMapForContext insertKey
      !inArgumentSignature
      !inLLVMInvocationRoutineName
      !inDescriptor
    ]
    [!?mInternalPropertyAndRoutineMapForContext insertKey
      !inRoutineName
      !.noValue
      !false
      !.undefined
      !routineSignatureMapForContext
    ]
  end
}

#·······················································································································

setter @universalPropertyAndRoutineMapForContext insertProperty
  ?let @lstring inPropertyName
  ?let @bool inIsPublic
  ?let @objectIR inObjectIR
{
  let newValue = @possibleValuedObject.property {!isPublic:inIsPublic !objectIR:inObjectIR}
  with inPropertyName.string in !?mInternalPropertyAndRoutineMapForContext do
    if [mPossibleValuedObject isUndefined] then
      mPossibleValuedObject = newValue
    else
      error inPropertyName : "object already defined in this context"
    end
  else
    [!?mInternalPropertyAndRoutineMapForContext insertKey
      !inPropertyName
      !.noValue
      !false
      !newValue
      !{} # empty routine map
    ]
  end
}

#·······················································································································

setter @universalPropertyAndRoutineMapForContext insertModule
  ?let @lstring inModuleName
  ?let @unifiedTypeMap-proxy inModuleType
{
  let newValue = @possibleValuedObject.module {!type:inModuleType}
  with inModuleName.string in !?mInternalPropertyAndRoutineMapForContext do
    if [mPossibleValuedObject isUndefined] then
      mPossibleValuedObject = newValue
    else
      error inModuleName : "object already defined in this context"
    end
  else
    [!?mInternalPropertyAndRoutineMapForContext insertKey
      !inModuleName
      !.noValue
      !false
      !newValue
      !{} # empty routine map
    ]
  end
}

#·······················································································································

setter @universalPropertyAndRoutineMapForContext insertRegister
  ?let @lstring inRegisterName
  ?readable:let @bool inReadable
  ?writable:let @bool inWritable
  ?let @objectIR inRegisterObjectIR
{
  let newValue = @possibleValuedObject.register {
    !readable:inReadable
    !writable:inWritable
    !objectIR:inRegisterObjectIR
  }
  with inRegisterName.string in !?mInternalPropertyAndRoutineMapForContext do
    if [mPossibleValuedObject isUndefined] then
      mPossibleValuedObject = newValue
    else
      error inRegisterName : "object already defined in this context"
    end
  else
    [!?mInternalPropertyAndRoutineMapForContext insertKey
      !inRegisterName
      !.noValue
      !false
      !newValue
      !{} # empty routine map
    ]
  end
}

#·······················································································································

setter @universalPropertyAndRoutineMapForContext insertGlobalConstant
  ?let @lstring inConstantName
  ?let @objectIR inConstantObjectIR
{
  let newValue = @possibleValuedObject.globalConstant {!objectIR:inConstantObjectIR}
  with inConstantName.string in !?mInternalPropertyAndRoutineMapForContext do
    if [mPossibleValuedObject isUndefined] then
      mPossibleValuedObject = newValue
    else
      error inConstantName : "object already defined in this context"
    end
  else
    [!?mInternalPropertyAndRoutineMapForContext insertKey
      !inConstantName
      !.noValue
      !false
      !newValue
      !{} # empty routine map
    ]
  end
}

#·······················································································································

setter @universalPropertyAndRoutineMapForContext insertLocalConstant
  ?let @lstring inLocalConstantName
  ?let @objectIR inConstantObjectIR
{
  let newValue = @possibleValuedObject.localConstant {!objectIR:inConstantObjectIR}
  with inLocalConstantName.string in !?mInternalPropertyAndRoutineMapForContext do
    if [mPossibleValuedObject isUndefined] then
      mObjectState = .hasUnreadValue
      mPossibleValuedObject = newValue
    else
      error inLocalConstantName : "object already defined in this context"
    end
  else
    [!?mInternalPropertyAndRoutineMapForContext insertKey
      !inLocalConstantName
      !.hasUnreadValue
      !false
      !newValue
      !{} # empty routine map
    ]
  end
  mLocalObjectList += !inLocalConstantName
}

#·······················································································································

setter @universalPropertyAndRoutineMapForContext insertGlobalVariable
  ?let @lstring inGlobalVariableName
  ?let @objectIR inObjectIR
  ?let @bool inWritable
{
  let newValue = @possibleValuedObject.globalVariableReference {!writable:inWritable !objectIR:inObjectIR}
  with inGlobalVariableName.string in !?mInternalPropertyAndRoutineMapForContext do
    if [mPossibleValuedObject isUndefined] then
      mPossibleValuedObject = newValue
    else
      error inGlobalVariableName : "object already defined in this context"
    end
  else
    [!?mInternalPropertyAndRoutineMapForContext insertKey
      !inGlobalVariableName
      !.noValue
      !false
      !newValue
      !{} # empty routine map
    ]
  end
}

#·······················································································································

setter @universalPropertyAndRoutineMapForContext insertLocalVariable
  ?let @lstring inLocalVariableName
  ?let @objectIR inObjectIR
  ?let @valuedObjectState inVariableInitialState
  ?let @bool inObjectShouldBeValuedAtEndOfScope
{
  let newValue = @possibleValuedObject.localVariableReference {!objectIR:inObjectIR}
  with inLocalVariableName.string in !?mInternalPropertyAndRoutineMapForContext do
    if [mPossibleValuedObject isUndefined] then
      mObjectState = inVariableInitialState
      mObjectShouldBeValuedAtEndOfScope = inObjectShouldBeValuedAtEndOfScope
      mPossibleValuedObject = newValue
    else
      error inLocalVariableName : "object already defined in this context"
    end
  else
    [!?mInternalPropertyAndRoutineMapForContext insertKey
      !inLocalVariableName
      !inVariableInitialState
      !inObjectShouldBeValuedAtEndOfScope
      !newValue
      !{} # empty routine map
    ]
  end
  mLocalObjectList += !inLocalVariableName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SEARCH METHODS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

setter @universalPropertyAndRoutineMapForContext searchValuedObjectForReadAccess
  ?let @lstring inValuedObjectName
  !@bool outIsPublic
  !@objectIR outObjectIR
{
  [mInternalPropertyAndRoutineMapForContext searchKey !inValuedObjectName ?let valueKind ?* ?let property ?*]
  switch property
  case undefined :
    error inValuedObjectName : "undefined in this context" : outIsPublic, outObjectIR
  case property (@bool isPublic @objectIR objectIR) :
    outIsPublic = isPublic
    outObjectIR = objectIR
  case module (@unifiedTypeMap-proxy type) :
    outIsPublic = true
    outObjectIR = .globalVariableReference {!type:type !plmName:inValuedObjectName}
  case register (@bool readable * @objectIR objectIR) :
    if readable then
      outIsPublic = true
      outObjectIR = objectIR
    else
      error inValuedObjectName : "the register cannot be read in this context" : outIsPublic, outObjectIR
    end
  case globalConstant (@objectIR objectIR) :
    outIsPublic = true
    outObjectIR = objectIR
  case localConstant (@objectIR objectIR) :
    outIsPublic = true
    outObjectIR = objectIR
    if valueKind == .hasUnreadValue then
      [!?mInternalPropertyAndRoutineMapForContext setMObjectStateForKey !.hasReadValue !inValuedObjectName]
    end
  case globalVariableReference (* @objectIR objectIR) :
    outIsPublic = true
    outObjectIR = objectIR
  case localVariableReference (@objectIR objectIR) :
    switch valueKind 
    case noValue :
      error inValuedObjectName : "'" + inValuedObjectName + "' has no value" : outIsPublic, outObjectIR
    case hasUnreadValue :
      [!?mInternalPropertyAndRoutineMapForContext setMObjectStateForKey !.hasReadValue !inValuedObjectName]
      outIsPublic = true
      outObjectIR = objectIR
    case hasReadValue :
      outIsPublic = true
      outObjectIR = objectIR
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

setter @universalPropertyAndRoutineMapForContext searchValuedObjectForWriteAccess
  ?let @lstring inValuedObjectName
  !@bool outIsPublic
  !@objectIR outObjectIR
{
  [mInternalPropertyAndRoutineMapForContext searchKey !inValuedObjectName ?let valueKind ?* ?let property ?*]
  switch property
  case undefined :
    error inValuedObjectName : "undefined in this context" : outIsPublic, outObjectIR
  case property (* *) :
    error inValuedObjectName : "a property cannot be written" : outIsPublic, outObjectIR
  case module (*) :
    error inValuedObjectName : "a module cannot be written" : outIsPublic, outObjectIR
  case register (* @bool writable @objectIR objectIR) :
    if writable then
      outIsPublic = true
      outObjectIR = objectIR
    else
      error inValuedObjectName : "the register cannot be written" : outIsPublic, outObjectIR
    end
  case globalConstant (*) :
    error inValuedObjectName : "a constant cannot be written" : outIsPublic, outObjectIR
  case localConstant (*) :
    error inValuedObjectName : "a constant cannot be written" : outIsPublic, outObjectIR
  case globalVariableReference (* *) :
    error inValuedObjectName : "a global variable cannot be written" : outIsPublic, outObjectIR
  case localVariableReference (@objectIR objectIR) :
    switch valueKind 
    case noValue :
      [!?mInternalPropertyAndRoutineMapForContext setMObjectStateForKey !.hasUnreadValue !inValuedObjectName]
      outIsPublic = true
      outObjectIR = objectIR
    case hasUnreadValue :
      warning inValuedObjectName : "previous value has been never read"
      outIsPublic = true
      outObjectIR = objectIR
    case hasReadValue :
      [!?mInternalPropertyAndRoutineMapForContext setMObjectStateForKey !.hasUnreadValue !inValuedObjectName]
      outIsPublic = true
      outObjectIR = objectIR
    end
  end
}

#·······················································································································

method @universalPropertyAndRoutineMapForContext searchRoutine
  ?let @lstring inRoutineName
  ?let @lstring inArgumentSignature
  !@string outLLVMInvocationRoutineName
  !@routineDescriptor outDescriptor
{
  [mInternalPropertyAndRoutineMapForContext searchKey !inRoutineName ?3* ?let @routineSignatureMapForContext routineMap]
  [routineMap searchKey !inArgumentSignature ?outLLVMInvocationRoutineName ?outDescriptor]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! INTERNAL MAPS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

map @internalUniversalPropertyAndRoutineMapForContext { # Key is a variable, property, routine name
#  @valuedObjectStateStack mStackState
  @valuedObjectState mObjectState
  @bool mObjectShouldBeValuedAtEndOfScope
  @possibleValuedObject mPossibleValuedObject
  @routineSignatureMapForContext mRoutineSignatureMapForContext

  insert insertKey error message "the '%K' routine is already declared in %L"
  search searchKey error message "there is no '%K' routine"
}

#·······················································································································

#list @valuedObjectStateStack {
#  @valuedObjectState objectState
#}

#·······················································································································

map @routineSignatureMapForContext { # argument signature is the key
  @string mLLVMInvocationRoutineName # Name used in LLVM for calling this routine
  @routineDescriptor mDescriptor

  insert insertKey error message "the '%K' routine signature is already declared in %L"
  search searchKey error message "there is no '%K' routine signature"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! VALUED OBJECT
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @possibleValuedObject {
  case undefined
  case property (@bool isPublic @objectIR objectIR)
  case module (@unifiedTypeMap-proxy type)
  case register (@bool readable @bool writable @objectIR objectIR)
  case globalConstant (@objectIR objectIR)
  case localConstant (@objectIR objectIR)
  case globalVariableReference (@bool writable @objectIR objectIR)
  case localVariableReference (@objectIR objectIR)
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! VALUED OBJECT STATE
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @valuedObjectState {
  case noValue
  case hasUnreadValue
  case hasReadValue
}

#·······················································································································

getter @valuedObjectState string -> @string {
  switch self
  case noValue : result = "no value"
  case hasUnreadValue : result = "unread value"
  case hasReadValue : result = "read value"
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
