#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @interruptionConfigurationList {
  @lstring mInterruptName
  @lbigint mInterruptIndex
}

#·······················································································································

list @configurationDeclarationList {
  @lbigint mPointerSize
  @lstring mPanicCodeTypeName
  @lstring mPanicLineTypeName
  @interruptionConfigurationList mInterruptionConfigurationList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension plm_target_specific_syntax {

  #·····················································································································

  rule <declaration> ?!@ast ioAST {
    $configuration$
    $integer$ ?let pointerSize
    $:$
    $typeName$ ?let panicCodeTypeName 
    $:$
    $typeName$ ?let panicLineTypeName
    @interruptionConfigurationList interruptionConfigurationList = {}
    ${$
    repeat
    while
      $identifier$ ?let interruptName
      $:$
      $integer$ ?let interruptIndex
      interruptionConfigurationList += !interruptName !interruptIndex
    end
    $}$
    ioAST.mConfigurationDeclarationList += !pointerSize !panicCodeTypeName !panicLineTypeName !interruptionConfigurationList
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @configurationDeclarationList noteTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  var first = true
  for config in self do
    if first then
      first = false
      let panicCodeTypeName = @lstring.new {!"$" + config.mPanicCodeTypeName !config.mPanicCodeTypeName.location}
      [!?ioGraph noteNode !panicCodeTypeName]
      let panicLineTypeName = @lstring.new {!"$" + config.mPanicLineTypeName !config.mPanicLineTypeName.location}
      [!?ioGraph noteNode !panicLineTypeName]
    else
      error config.mPointerSize : "duplicate configuration"
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  ENTER IN CONTEXT                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
# Duplicate configuration is detected in noteTypesInPrecedenceGraph

method @configurationDeclarationList enterInContext ?!context:@semanticContext ioContext {
  for config in self do
    if config.mPointerSize.bigint == 0G then
      error config.mPointerSize : "zero size pointer not allowed"
    else
      ioContext.mPointerSize = [config.mPointerSize.bigint uint]
    end
    ioContext.mPanicCodeType = @unifiedTypeMap-proxy.searchKey {!ioContext.mTypeMap !config.mPanicCodeTypeName}
    ioContext.mPanicLineType = @unifiedTypeMap-proxy.searchKey {!ioContext.mTypeMap !config.mPanicLineTypeName}
    for (name panicCode) in config.mInterruptionConfigurationList do
      if [panicCode.bigint fitsInUInt] then
        [!?ioContext.mAvailableInterruptMap insertKey !name ![panicCode.bigint uint]]
      else
        error panicCode : "value does not fit in $" + config.mPanicCodeTypeName
      end
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
