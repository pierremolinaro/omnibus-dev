#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @interruptionPanicCode {
  case noPanicCode
  case panicCode (@lbigint value)
}

#·······················································································································

list @interruptionConfigurationList {
  @lstring mInterruptName
  @interruptionPanicCode mInterruptionPanicCode
}

#·······················································································································

struct @targetParameters {
  @2lstringlist mPython_utilityToolList
  @lstring mPython_build
  @lstring mTargetPanic_LL_filePath
  @lstring mLinkerScript
  @lbigint mPointerSize
  @lbigint mSystemStackSize
  @lbigint mStackedUserRegisterOnInterruptByteSize

  @lstring mServiceHandler
  @lbigint mServicePushedRegisterByteSize

  @lbigint mSectionPushedRegisterByteSize 
  @lstring mSectionHandler
  @lstring mSectionDispatcherEntry
  @lstring mSectionDispatcherHeader
  @lstring mSectionDispatcherInvocation

  @lstringlist m_C_definitionFiles
  @lstringlist m_S_definitionFiles
  @lstringlist m_LL_definitionFiles
  
  @lstring mXtrInterruptHandler
  @lstring mUndefinedInterruptHandler
  
  @lstring mServiceDispatcherEntry
  @lstring mServiceDispatcherHeader
  @lstring mServiceEntry
}

#·······················································································································

class @configurationDeclarationAST : @abstractDeclaration {
  @lstring mPanicCodeTypeName
  @lstring mPanicLineTypeName
  @lstring mUnsignedIntegerTypeName
  @targetParameters mTargetParameters
  @interruptionConfigurationList mInterruptionConfigurationList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension plm_target_specific_syntax {

  #·····················································································································

  rule <key> ?let @string inKey {
    $identifier$ ?let key
    if key.string != inKey then
      error key : "invalid key" fixit { replace inKey }
    end
    $:$
  }

  #·····················································································································

  rule <configuration_start_symbol>
    ?!@ast ioAST
    !importClauseList:@lstringlist outImportedFileList
    !endOfSourceFile:@location outEndOfSourceFile
  {
    <key> !"PYTHON_UTILITIES"
    @2lstringlist python_utilityToolList = {}
    select
    or
      repeat
        $"string"$ ?let relativePath
        $->$
        $"string"$ ?let destinationFile
        python_utilityToolList += !relativePath !destinationFile
      while
        $,$
      end
    end
    <key> !"PYTHON_BUILD"
    $"string"$ ?let python_build
    <key> !"LINKER_SCRIPT"
    $"string"$ ?let linkerScript
    <key> !"PANIC"
    $\$type$ ?let panicCodeTypeName 
    $;$
    $\$type$ ?let panicLineTypeName
    $;$
    $"string"$ ?let targetPanic_LL_filePath
    <key> !"UINT_TYPE"
    $\$type$ ?let unsignedIntegerTypeName
    <key> !"POINTER_BIT_COUNT"
    $integer$ ?let pointerSize
    <key> !"SYSTEM_STACK_SIZE"
    $integer$ ?let systemStackSize
    <key> !"SERVICE"
    $"string"$ ?let serviceHandler
    $;$
    $"string"$ ?let serviceDispatcherEntry
    $;$
    $"string"$ ?let serviceDispatcherHeader
    $;$
    $"string"$ ?let serviceEntry
    $;$
    $integer$ ?let servicePushedRegisterByteSize
    <key> !"SECTION"
    $"string"$ ?let sectionDispatcherCode
    $;$
    $"string"$ ?let sectionDispatcherEntry
    $;$
    $"string"$ ?let sectionDispatcherHeader
    $;$
    $"string"$ ?let sectionDispatcherInvocation
    $;$
    $integer$ ?let sectionPushedRegisterByteSize
    <key> !"C_FILES"
    @lstringlist C_definitionFiles = {}
    repeat
      $"string"$ ?let fileRelativePath
      C_definitionFiles += !fileRelativePath
    while
      $,$
    end
    <key> !"S_FILES"
    @lstringlist S_definitionFiles = {}
    repeat
      $"string"$ ?let fileRelativePath
      S_definitionFiles += !fileRelativePath
    while
      $,$
    end
    <key> !"LL_FILES"
    @lstringlist LL_definitionFiles = {}
    repeat
      $"string"$ ?let fileRelativePath
      LL_definitionFiles += !fileRelativePath
    while
      $,$
    end
    outImportedFileList = {}
    <key> !"PLM_FILES"
    select
    or
      repeat
        $"string"$ ?let relativeFilePath
        outImportedFileList += !relativeFilePath
      while
        $,$
      end
    end
    <key> !"INTERRUPT_HANDLER"
    $"string"$ ?let xtrInterruptHandler
    $;$
    $"string"$ ?let undefinedInterruptHandler
    $;$
    $integer$ ?let stackedUserRegisterOnInterruptByteSize
    <interruptConfigList> ?let interruptionConfigurationList
     let parameters = @targetParameters.new {
      !python_utilityToolList
      !python_build
      !targetPanic_LL_filePath
      !linkerScript
      !pointerSize
      !systemStackSize
      !stackedUserRegisterOnInterruptByteSize
      !serviceHandler
      !servicePushedRegisterByteSize
      !sectionPushedRegisterByteSize
      !sectionDispatcherCode
      !sectionDispatcherEntry
      !sectionDispatcherHeader
      !sectionDispatcherInvocation
      !C_definitionFiles
      !S_definitionFiles
      !LL_definitionFiles
      !xtrInterruptHandler
      !undefinedInterruptHandler
      !serviceDispatcherEntry
      !serviceDispatcherHeader
      !serviceEntry
    }
   ioAST.mDeclarationListAST += !@configurationDeclarationAST.new {
      !panicCodeTypeName
      !panicLineTypeName
      !unsignedIntegerTypeName
      !parameters
      !interruptionConfigurationList
    }
    outEndOfSourceFile = .here
  }

 #·····················································································································

  rule <interruptConfigList> !@interruptionConfigurationList interruptionConfigurationList {
    interruptionConfigurationList = {}
    <key> !"INTERRUPTS"
    select
    or
      repeat
        $identifier$ ?let interruptName
        @interruptionPanicCode interruptionPanicCode
        select
          interruptionPanicCode = .noPanicCode
        or
          $->$
          $integer$ ?let panicCode
          interruptionPanicCode = .panicCode {!value:panicCode}
        end
        interruptionConfigurationList += !interruptName !interruptionPanicCode
      while
        $,$
      end
    end
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @configurationDeclarationAST addExtension
  ?!@extensionDeclarationListAST unused ioExtensionDeclarationListAST
  !@abstractDeclaration outNewDeclaration
{
  outNewDeclaration = self
}
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @configurationDeclarationAST enterInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  let rootNode =  @lstring.new {!"config" !mPanicCodeTypeName.location}
  [!?ioGraph addNode !rootNode !self]
  let panicCodeTypeName = llvmRegularTypeMangledNameFromName (!mPanicCodeTypeName)
  [!?ioGraph addEdge !rootNode !panicCodeTypeName]
  let panicLineTypeName = llvmRegularTypeMangledNameFromName (!mPanicLineTypeName)
  [!?ioGraph addEdge !rootNode !panicLineTypeName]
  let unsignedIntegerTypeName =llvmRegularTypeMangledNameFromName (!mUnsignedIntegerTypeName)
  [!?ioGraph addEdge !rootNode !unsignedIntegerTypeName]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @configurationDeclarationAST location -> @location outLocation {
  outLocation = mPanicCodeTypeName.location
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @configurationDeclarationAST keyRepresentationForErrorSignaling -> @string outRepresentation {
  outRepresentation = "config" 
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @configurationDeclarationAST enterRoutinesInContext
  ?procedureDeclarationList:let @functionDeclarationListAST unused inProcedureListAST
  ?!context:@semanticContext unused ioContext
  ?!staticStringMap:@staticStringMap unused ioGlobalLiteralStringMap
{
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  ENTER IN CONTEXT                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
# Duplicate configuration is detected in noteTypesInPrecedenceGraph

override method @configurationDeclarationAST enterInContext
  ?procedureDeclarationList:let @functionDeclarationListAST unused inProcedureListAST
  ?!context:@semanticContext ioContext
  ?!staticlistValues:@staticlistValues_listMap unused ioStaticListValueMap
  ?!staticStringMap:@staticStringMap unused ioGlobalLiteralStringMap
{
  @stringset panicCodeSet = {}
  if ioContext.mPointerSize != 0 then
    error mTargetParameters.mPointerSize : "duplicate configuration"
  elsif mTargetParameters.mPointerSize.bigint == 0G then
    error mTargetParameters.mPointerSize : "zero size pointer not allowed"
  else
    ioContext.mPointerSize = [mTargetParameters.mPointerSize.bigint uint]
  end
  ioContext.mTargetParameters = mTargetParameters
  ioContext.mPanicCodeType = @unifiedTypeMap-proxy.searchKey {!ioContext.mTypeMap !mPanicCodeTypeName}
  ioContext.mPanicLineType = @unifiedTypeMap-proxy.searchKey {!ioContext.mTypeMap !mPanicLineTypeName}
  ioContext.mUnsignedIntegerType = @unifiedTypeMap-proxy.searchKey {!ioContext.mTypeMap !mUnsignedIntegerTypeName}
  for (name panicCode) in mInterruptionConfigurationList do
    [!?ioContext.mAvailableInterruptMap insertKey !name !panicCode]
    switch panicCode
    case noPanicCode :
    case panicCode (value) :
      let s = [value.bigint string]
      if [panicCodeSet hasKey !s] then
        error value : "duplicate panic code"
      end
      panicCodeSet += !s
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @configurationDeclarationAST semanticAnalysis
  ?context:let @semanticContext unused inContext
  ?!temporary:@semanticTemporariesStruct unused ioTemporaries
  ?!intermediateCode:@intermediateCodeStruct unused ioIntermediateCodeStruct
{
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
