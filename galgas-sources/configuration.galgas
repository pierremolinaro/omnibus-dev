#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @interruptionPanicCode {
  case noCode
  case code (@lbigint value)
}

#·······················································································································

list @interruptionConfigurationList {
  public var @lstring mInterruptName
  public var @interruptionPanicCode mInterruptionPanicCode
}

#·······················································································································

struct @targetParameters {
  public var @location mConfigurationLocation
  
  public var @2lstringlist mPython_utilityToolList
  public var @lstring mPython_build

  public var @lstring mLinkerScript
  
  public var @uint mPointerSize

  public var @bool mHandleDynamicArray
 
  public var @lbigint mSystemStackSize
  public var @lbigint mStackedUserRegisterOnInterruptByteSize

  public var @lstring mNopInstructionStringInLLVM

  public var @lbigint mBitbandRegisterBaseAddress
  public var @lbigint mBitbandRegisterEndAddress
  public var @lbigint mBitbandRegisterRelocationAddress
  public var @lbigint mBitbandRegisterOffsetMultiplier
  public var @lbigint mBitbandRegisterBitMultiplier
  
  public var @lstring mSectionHandler
  public var @lbigint mSectionPushedRegisterByteSize 
  public var @lstring mSectionDispatcherHeader
  public var @lstring mSectionDispatcherEntry
  public var @lstring mSectionDispatcherInvocationFromAnyMode
  public var @lstring mSectionDispatcherInvocationFromUserMode

  public var @lstringlist m_C_definitionFiles
  public var @lstringlist m_S_definitionFiles
  public var @lstringlist m_LL_definitionFiles
  
  public var @lstring mXtrInterruptHandler
  public var @lstring mUndefinedInterruptHandler
  
  public var @lstring mServiceHandler
  public var @lbigint mServicePushedRegisterByteSize
  public var @lstring mServiceDispatcherEntry
  public var @lstring mServiceDispatcherHeader
  public var @lstring mServiceEntryNoReturnedValue
  public var @lstring mServiceEntryWithReturnValue
}

#·······················································································································

refclass @configurationDeclarationAST : @abstractDeclarationAST {
  public var @lstring mPanicCodeTypeName
  public var @lstring mPanicLineTypeName
  public var @targetParameters mTargetParameters
  public var @interruptionConfigurationList mInterruptionConfigurationList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_target_specific_syntax {

  #·····················································································································

  rule <configuration_key> ?let @string inKey {
    $identifier$ ?let key
    if key.string != inKey then
      error key : "invalid key (found '" + key + "', required '" + inKey + "'" fixit { replace inKey }
    end
    $:$
  }

  #·····················································································································

  rule <python_utility_tool_list> !@2lstringlist outPythonUtilityToolList {
    <configuration_key> !"PYTHON_UTILITIES"
    outPythonUtilityToolList = {}
    repeat
      while
      $"string"$ ?let relativePath
      $->$
      $"string"$ ?let destinationFile
      outPythonUtilityToolList += !relativePath !destinationFile
    end
  }

  #·····················································································································

  rule <configuration_start_symbol>
    ?!@ast ioAST
    !importClauseList:@lstringlist outImportedFileList
    !endOfSourceFile:@location outEndOfSourceFile
  {
    let loc = @location.here
    <python_utility_tool_list> ?let python_utilityToolList
    <configuration_key> !"PYTHON_BUILD"
    $"string"$ ?let python_build
    <configuration_key> !"LINKER_SCRIPT"
    $"string"$ ?let linkerScript
    <configuration_key> !"PANIC_CODE_TYPE_NAME"
    $\$type$ ?let panicCodeTypeName 
    <configuration_key> !"PANIC_LINE_TYPE_NAME"
    $\$type$ ?let panicLineTypeName
    <configuration_key> !"POINTER_BIT_COUNT"
    $integer$ ?let pointerSize
    <configuration_key> !"DYNAMIC_ARRAY"
    var @bool handleDynamicArray
    select
      $yes$
      handleDynamicArray = true
    or
      $no$
      handleDynamicArray = false
    end
    <configuration_key> !"SYSTEM_STACK_SIZE"
    $integer$ ?let systemStackSize
    <configuration_key> !"NOP"
    $"string"$ ?let nopInstructionStringInLLVM
    <configuration_key> !"BIT_BAND"
    var @lbigint bitbandRegisterBaseAddress
    var @lbigint bitbandRegisterEndAddress
    var @lbigint bitbandRegisterRelocationAddress
    var @lbigint bitbandRegisterOffsetMultiplier
    var @lbigint bitbandRegisterBitMultiplier
    select
      $no$
      bitbandRegisterBaseAddress = .new {!0G !.here}
      bitbandRegisterEndAddress = .new {!0G !.here}
      bitbandRegisterRelocationAddress = .new {!0G !.here}
      bitbandRegisterOffsetMultiplier = .new {!0G !.here}
      bitbandRegisterBitMultiplier= .new {!0G !.here}
    or
      $integer$ ?bitbandRegisterBaseAddress
      $integer$ ?bitbandRegisterEndAddress
      $integer$ ?bitbandRegisterRelocationAddress
      $integer$ ?bitbandRegisterOffsetMultiplier
      $integer$ ?bitbandRegisterBitMultiplier
    end
    <configuration_key> !"SERVICE_HANDLER"
    $"string"$ ?let serviceHandler
    <configuration_key> !"SERVICE_SYSTEM_STACK_SIZE"
    $integer$ ?let servicePushedRegisterByteSize
    <configuration_key> !"SERVICE_DISPATCHER_HEADER"
    $"string"$ ?let serviceDispatcherHeader
    <configuration_key> !"SERVICE_DISPATCHER_ENTRY"
    $"string"$ ?let serviceDispatcherEntry
    <configuration_key> !"SERVICE_ENTRY_NO_RETURNED_VALUE"
    $"string"$ ?let serviceEntryNoReturnedValue
    <configuration_key> !"SERVICE_ENTRY_WITH_RETURNED_VALUE"
    $"string"$ ?let serviceEntryReturnValue
    <configuration_key> !"SECTION_HANDLER"
    $"string"$ ?let sectionHandler
    <configuration_key> !"SECTION_SYSTEM_STACK_SIZE"
    $integer$ ?let sectionPushedRegisterByteSize
    <configuration_key> !"SECTION_DISPATCHER_HEADER"
    $"string"$ ?let sectionDispatcherHeader
    <configuration_key> !"SECTION_DISPATCHER_ENTRY"
    $"string"$ ?let sectionDispatcherEntry
    <configuration_key> !"SECTION_ENTRY_FROM_UNKNOWN_MODE"
    $"string"$ ?let sectionDispatcherInvocationFromAnyMode
    <configuration_key> !"SECTION_ENTRY_FROM_USER_MODE"
    $"string"$ ?let sectionDispatcherInvocationFromUserMode
    <configuration_key> !"INTERRUPT_HANDLER"
    $"string"$ ?let xtrInterruptHandler
    <configuration_key> !"INTERRUPT_USER_STACK_SIZE"
    $integer$ ?let stackedUserRegisterOnInterruptByteSize
    <configuration_key> !"UNUSED_INTERRUPT"
    $"string"$ ?let undefinedInterruptHandler
    <configuration_key> !"CPP_FILES"
    var @lstringlist C_definitionFiles = {}
    repeat
    while
      $"string"$ ?let fileRelativePath
      C_definitionFiles += !fileRelativePath
    end
    <configuration_key> !"S_FILES"
    var @lstringlist S_definitionFiles = {}
    repeat
    while
      $"string"$ ?let fileRelativePath
      S_definitionFiles += !fileRelativePath
    end
    <configuration_key> !"LL_FILES"
    var @lstringlist LL_definitionFiles = {}
    repeat
    while
      $"string"$ ?let fileRelativePath
      LL_definitionFiles += !fileRelativePath
    end
    outImportedFileList = {}
    <configuration_key> !"OMNIBUS_FILES"
    repeat
    while
      $"string"$ ?let relativeFilePath
      outImportedFileList += !relativeFilePath
    end
    <interruptConfigList> ?let interruptionConfigurationList ?let interruptConstantList
    var @uint ptrSize
    if pointerSize.bigint == 0G then
      error pointerSize : "zero size pointer is invalid" : ptrSize
    else
      ptrSize = [pointerSize.bigint uint]
    end
    let parameters = @targetParameters.new {
      !loc
      !python_utilityToolList
      !python_build
      !linkerScript
      !ptrSize
      !handleDynamicArray
      !systemStackSize
      !stackedUserRegisterOnInterruptByteSize
      !nopInstructionStringInLLVM
      !bitbandRegisterBaseAddress
      !bitbandRegisterEndAddress
      !bitbandRegisterRelocationAddress
      !bitbandRegisterOffsetMultiplier
      !bitbandRegisterBitMultiplier
      !sectionHandler
      !sectionPushedRegisterByteSize
      !sectionDispatcherHeader
      !sectionDispatcherEntry
      !sectionDispatcherInvocationFromAnyMode
      !sectionDispatcherInvocationFromUserMode
      !C_definitionFiles
      !S_definitionFiles
      !LL_definitionFiles
      !xtrInterruptHandler
      !undefinedInterruptHandler
      !serviceHandler
      !servicePushedRegisterByteSize
      !serviceDispatcherEntry
      !serviceDispatcherHeader
      !serviceEntryNoReturnedValue
      !serviceEntryReturnValue
    }
   ioAST.mDeclarationListAST += !@configurationDeclarationAST.new {
      !panicCodeTypeName
      !panicLineTypeName
      !parameters
      !interruptionConfigurationList
    }
    outEndOfSourceFile = .here
    ioAST.mDeclarationListAST += !@enumerationDeclarationAST.new {
      !.new {!isrSlotTypeName () !xtrInterruptHandler.location}
      !interruptConstantList
    }
  }

 #·····················································································································

  rule <interruptConfigList>
    !@interruptionConfigurationList interruptionConfigurationList
    !@enumerationConstantList outEnumerationConstantList
  {
    interruptionConfigurationList = {}
    outEnumerationConstantList = {}
    <configuration_key> !"INTERRUPTS"
    repeat
    while
      $identifier$ ?let interruptName
      var @interruptionPanicCode interruptionPanicCode
      select
        interruptionPanicCode = .noCode
      or
        $->$
        $integer$ ?let panicCode
        interruptionPanicCode = .code {!value:panicCode}
        outEnumerationConstantList += !interruptName !panicCode
      end
      interruptionConfigurationList += !interruptName !interruptionPanicCode
    end
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  PRECEDENCE GRAPH                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

func %once configurationNodeNameForPrecedenceGraph -> @string {
  result = "config"
}

#·······················································································································

func %once isrSlotTypeName -> @string {
  result = "ISRSlot"
}

#·······················································································································

override method @configurationDeclarationAST enterInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  let rootNode =  @lstring.new {!configurationNodeNameForPrecedenceGraph () !self.mPanicCodeTypeName.location}
  [!?ioGraph addNode !rootNode !self]
  [!?ioGraph addEdge !rootNode !self.mPanicCodeTypeName]
  [!?ioGraph addEdge !rootNode !self.mPanicCodeTypeName]
  [!?ioGraph addEdge !rootNode ![ctBoolTypeName () nowhere]]
}

#·······················································································································

override getter @configurationDeclarationAST locationForErrorSignaling -> @location outLocation {
  outLocation = self.mPanicCodeTypeName.location
}

#·······················································································································

override getter @configurationDeclarationAST keyRepresentationForErrorSignaling -> @string outRepresentation {
  outRepresentation = configurationNodeNameForPrecedenceGraph ()
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  ENTER IN CONTEXT                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
# Duplicate configuration is detected in noteTypesInPrecedenceGraph

override method @configurationDeclarationAST enterInContext
  ?!context:@semanticContext ioContext
  ?!@declarationDecorationList unused ioDecoratedDeclarationList
  ?!@decoratedRegularRoutineList unused ioDecoratedRoutineList
  ?!@routineListIR unused ioRoutineListIR
  ?!staticlistValues:@staticListInitializationMap unused ioStaticListValueMap
  ?!staticEntityMap:@staticEntityMap unused ioStaticEntityMap
  ?!registerUserAccess:@controlRegisterUserAccesMapAST unused ioControlRegisterUserAccesMapAST
  ?!userDefinedTypes:@userLLVMTypeDefinitionListIR unused ioUserLLVMTypeDefinitionListIR
{
#---
  if ioContext.mTargetParameters.mPointerSize != 0 then
    error self.mTargetParameters.mConfigurationLocation : "duplicate configuration"
  end
  ioContext.mTargetParameters = self.mTargetParameters
#--- Panic code type
  [ioContext.mTypeMap searchKey
    !lkey:self.mPanicCodeTypeName
    ?type:let panicCodeType
  ]
  ioContext.mPanicCodeType = panicCodeType
  if [ioContext.mPanicCodeType.kind isInteger] then
    [ioContext.mPanicCodeType.kind integer ?2* ?unsigned:let unsigned ?1*]
    if not unsigned then
      error self.mPanicCodeTypeName : "this type should be an unsigned integer type"
    end
  else
    error self.mPanicCodeTypeName : "this type should be an unsigned integer type"
  end
#--- Panic line type
  [ioContext.mTypeMap searchKey
    !lkey:self.mPanicLineTypeName
    ?type:let panicLineType
  ]
  ioContext.mPanicLineType = panicLineType
  if [ioContext.mPanicLineType.kind isInteger] then
    [ioContext.mPanicLineType.kind integer ?2* ?unsigned:let unsigned ?1*]
    if not unsigned then
      error self.mPanicLineTypeName : "this type should be an unsigned integer type"
    end
  else
    error self.mPanicLineTypeName : "this type should be an unsigned integer type"
  end
#--------------------------------- Interrupt panic codes
  var @stringset panicCodeSet = {}
  for (name panicCode) in self.mInterruptionConfigurationList do
    [!?ioContext.mAvailableInterruptMap insertKey !name !panicCode]
    switch panicCode
    case noCode :
    case code (value) :
      let s = [value.bigint string]
      if [panicCodeSet hasKey !s] then
        error value : "duplicate interrupt code"
      end
      panicCodeSet += !s
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
