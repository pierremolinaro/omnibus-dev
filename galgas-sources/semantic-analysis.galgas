#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#                                                                                                                       
#    S E M A N T I C    A N A L Y S I S                                                                                 
#                                                                                                                       
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

func checkAssignmentCompatibility
  ?source:let @objectIR inSourceValue
  ?targetAnnotationType:let @PLMType inOptionalTargetAnnotationType
  ?errorLocation:let @location inErrorLocation
  ?staticTypeAllowed:let @bool inStaticTypeAllowed
  -> @objectIR
{
  let sourceType = [inSourceValue type]
  if inOptionalTargetAnnotationType.plmTypeDescriptionName == sourceType.plmTypeDescriptionName then
    result = inSourceValue
  elsif [inOptionalTargetAnnotationType.kind isVoid] then
    result = inSourceValue
  elsif [sourceType.kind isStaticInteger] && [inOptionalTargetAnnotationType.kind isInteger] then
    [inSourceValue literalInteger ?1* ?value:let value]
    [inOptionalTargetAnnotationType.kind integer ?min:let minTarget ?max:let maxTarget ?2*]
    if (value < minTarget) || (maxTarget < value) then
      error inErrorLocation : "literal integer too large" : result
    else
      result = [inSourceValue withType !inOptionalTargetAnnotationType]
    end
  else
    error inErrorLocation
    : "object of type $" + inOptionalTargetAnnotationType.plmTypeDescriptionName
      + " cannot be assigned from expression of type $" + [inSourceValue plmTypeDescriptionName]
    : result
  end
  if not inStaticTypeAllowed then
    if [result plmTypeDescriptionName] == staticIntegerTypeName () then
      error inErrorLocation : "the $" + staticIntegerTypeName () + " static type is not allowed here"
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

abstract method @abstractDecoratedDeclaration semanticAnalysis
  ?context:let @semanticContext inContext
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!intermediateCode:@intermediateCodeStruct ioIntermediateCodeStruct

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

struct @semanticTemporariesStruct {
  @uint mTemporaryIndex
  @panicRoutinePriorityMap mPanicSetupRoutinePriorityMap 
  @panicRoutinePriorityMap mPanicLoopRoutinePriorityMap 
#  @subprogramInvocationGraph mSubprogramInvocationGraph
  @staticListInvokedFunctionSetMap mStaticArrayMapForTemporaries
  @stringset mInitializedDriverSet
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

setter @semanticTemporariesStruct newTempLLVMVar !@string outTempLLVMVar {
  outTempLLVMVar = "%temp." + mTemporaryIndex
  mTemporaryIndex ++
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc getNewTempValue
  ?let @PLMType inType
  ?!@semanticTemporariesStruct ioTemporaries
  !@objectIR outTempVariable
{
  if inType.kind == .void then
    outTempVariable = .void
  else
    [!?ioTemporaries newTempLLVMVar ?let llvmName]
    outTempVariable = .llvmValue {!type: inType !llvmName:llvmName}
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc getNewTempReference
  ?let @PLMType inType
  ?!@semanticTemporariesStruct ioTemporaries
  !@objectIR outTempVariable
{
  if inType.kind == .void then
    outTempVariable = .void
  else
    [!?ioTemporaries newTempLLVMVar ?let llvmName]
    outTempVariable = .reference {!type: inType !llvmName:llvmName}
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc semanticAnalysis
  ?sourceFile:let @string unused inSourceFile
  ?ast:let @ast inAST
  ?decoratedDeclarationList:let @declarationDecorationList inDecoratedDeclarationList
  ?decoratedRoutineList: let @decoratedRegularRoutineList inDecoratedRoutineList
  ?routineListIR:let @routineListIR inRoutineListIR
  ?context:let @semanticContext inSemanticContext
  ?endOfSourceFile:let @location inEndOfSourceFile
  ?staticEntityMap:let @staticEntityMap inStaticEntityMap
  !intermediateCode:@intermediateCodeStruct outIntermediateCodeStruct
{
  outIntermediateCodeStruct = .default
  outIntermediateCodeStruct.mRoutineListIR = inRoutineListIR
  outIntermediateCodeStruct.mTargetParameters = inSemanticContext.mTargetParameters
  outIntermediateCodeStruct.mStaticEntityMap = inStaticEntityMap
  outIntermediateCodeStruct.mGlobalTaskVariableList = inSemanticContext.mGlobalTaskVariableList
  for requiredProc in inAST.mRequiredProcListAST do
    let procedureMangledName = routineMangledNameFromAST (!"" !requiredProc.mName !requiredProc.mFormalArgumentList)
    outIntermediateCodeStruct.mRequiredProcedureSet += !procedureMangledName.string
  end 
#---
  @semanticTemporariesStruct temporaries = .default
  for (declaration) in inDecoratedDeclarationList do
    [declaration semanticAnalysis
      !context:inSemanticContext
      !?temporary:temporaries
      !?intermediateCode:outIntermediateCodeStruct
     ]
  end
  [inDecoratedRoutineList routineSemanticAnalysis
    !context:inSemanticContext
    !?temporary:temporaries
    !?intermediateCode:outIntermediateCodeStruct
   ]
  [inAST.mExternProcListAST externProcedureSemanticAnalysis
    !context:inSemanticContext
    !?temporary:temporaries
    !?intermediateCode:outIntermediateCodeStruct
  ]
#--- Add panic routines
  appendPanicRoutines (!?intermediateCode:outIntermediateCodeStruct)
#--- Check required procedure are defined
  checkRequiredProcedures (
    !ast:inAST
    !context:inSemanticContext
    !endOfSourceFile:inEndOfSourceFile
  )
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
