#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @fixedSizeArrayTypeDeclarationAST : @abstractDeclarationAST {
  @lstring mFixedSizeArrayType
  @lstring mElementTypeName
  @expressionAST mSizeExpression
  @location mSizeExpressionLocation
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <type_definition> ?!@ast ioAST !@lstring outTypeName {
    $[$
    <expression> !?ioAST ?let sizeExpression
    $:$
    <type_definition> !?ioAST ?let elementTypeName
    $]$
    let sizeExpressionLocation = @location.here
    outTypeName = ["anonymous." + ioAST.mTypeDeclarationIndex here]
    ioAST.mTypeDeclarationIndex ++
    ioAST.mDeclarationListAST += !@fixedSizeArrayTypeDeclarationAST.new {
      !outTypeName
      !elementTypeName
      !sizeExpression
      !sizeExpressionLocation
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @fixedSizeArrayTypeDeclarationAST enterInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  [!?ioGraph addNode !mFixedSizeArrayType !self]
  [!?ioGraph addEdge !mFixedSizeArrayType !mElementTypeName]
  [mSizeExpression addDependenceEdgeForStaticExpression !mFixedSizeArrayType !?ioGraph]
  let rootNode = @lstring.new {!configurationNodeNameForPrecedenceGraph () !mFixedSizeArrayType.location}
  [!?ioGraph addEdge !mFixedSizeArrayType !rootNode]
}

#·······················································································································

override getter @fixedSizeArrayTypeDeclarationAST keyRepresentationForErrorSignaling -> @string outRepresentation {
  outRepresentation = "anonymous $" + mFixedSizeArrayType 
}

#·······················································································································

override getter @fixedSizeArrayTypeDeclarationAST locationForErrorSignaling -> @location outLocation {
  outLocation = mFixedSizeArrayType.location
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  ENTER IN CONTEXT                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @fixedSizeArrayTypeDeclarationAST enterInContext
  ?!context:@semanticContext ioContext
  ?!@declarationDecorationList unused ioDecoratedDeclarationList
  ?!@decoratedRegularRoutineList unused ioDecoratedRoutineList
  ?!@routineListIR ioRoutineListIR
  ?!staticlistValues:@staticListInitializationMap unused ioStaticListValueMap
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!registerUserAccess:@controlRegisterUserAccesMapAST unused ioControlRegisterUserAccesMapAST
  ?!userDefinedTypes:@userLLVMTypeDefinitionListIR ioUserLLVMTypeDefinitionListIR
{
#--- Get element type
  [ioContext.mTypeMap searchKey !lkey:mElementTypeName ?type:let elementType]
  if not [elementType copyable] then
    error mElementTypeName : "an array element should be copyable"
  end
  if not [elementType instanciable] then
    error mElementTypeName : "an array element should be instanciable"
  end
#--- Compute array size
  computeStaticExpression (
    !?context: ioContext
    !?staticEntityMap:ioStaticEntityMap
    !expression:mSizeExpression
    !errorLocation:mSizeExpressionLocation
    !optionalContextualTypeName:["" nowhere]
    ?result:let @objectIR sizeExpressionIR
  )
  if not [sizeExpressionIR isLiteralInteger] then
    error mSizeExpressionLocation : "array size expression is a literal interger expression"
  else
    [sizeExpressionIR literalInteger ?1* ?value:let arraySize]
  #-------------------------------------------- Original type name
    let omnibusTypeDescriptionName = @lstring.new {!"[" + arraySize + " • " + elementType.omnibusTypeDescriptionName + "]" !mFixedSizeArrayType}
    let llvmBaseTypeName = @lstring.new {!elementType.llvmBaseTypeName + "-" + arraySize !mFixedSizeArrayType}
    if [ioContext.mTypeMap hasKey !omnibusTypeDescriptionName] then
      [ioContext.mTypeMap searchKey !lkey:omnibusTypeDescriptionName ?type: var type]
      [!?ioContext.mTypeMap insertType
        !lkey:mFixedSizeArrayType
        !type:type
      ]
    else
    #-------------------------------------------- Class method map : add "count" constructor
      @classConstantMap classConstantMap = {}
     [!?classConstantMap insertKey !.new {!"count" !mFixedSizeArrayType} !arraySize ![ctIntegerTypeName () nowhere]]
    #-------------------------------------------- Property map
      @propertyGetterMap arrayTypePropertyMap = {}
      [!?arrayTypePropertyMap insertKey # "count" property
        !.new {!"count" !omnibusTypeDescriptionName.location}
        !true # public
        !.constantProperty {!value:.literalInteger {!type: compileTimeIntegerType () !value:arraySize}}
      ]
    #-------------------------------------------- Constructor map
      @constructorMap constructorMap = {}
    #--- Constructor (!repeated)
      [!?constructorMap insertKey !["(!repeated)" nowhere] !{} !.arrayValue {!elementType:elementType !size:arraySize}]
    #--- Constructor (!!!!)
      var constructorName = "("
      for i in 0 ..< [arraySize uint] do
        constructorName += "!"
      end
      constructorName += ")"
      [!?constructorMap insertKey ![constructorName nowhere] !{} !.arrayValue {!elementType:elementType !size:arraySize}]
    #-------------------------------------------- Type attributes
      @omnibusTypeAttributes typeAttributes = .instanciable | .copyable
    #-------------------------------------------- Enter in type map
      let arrayType = @omnibusType.new {
        !propertyGetterMap:arrayTypePropertyMap
        !propertySetterMap: {}
        !classConstantMap:classConstantMap
        !constructorMap:constructorMap
        !subscript:.staticSubscript {!elementType:elementType !size:arraySize}
        !attributes:typeAttributes
        !omnibusTypeDescriptionName:omnibusTypeDescriptionName
        !kind:.staticArrayType {!elementType:elementType !size:arraySize}
        !llvmBaseTypeName:llvmBaseTypeName
      }
      [!?ioContext.mTypeMap insertType
        !lkey:mFixedSizeArrayType
        !type:arrayType
      ]
      [!?ioContext.mTypeMap insertType
        !lkey:omnibusTypeDescriptionName
        !type:arrayType
      ]
    #-------------------------------------------- GenerateLLVM type definition
      ioUserLLVMTypeDefinitionListIR += !@userLLVMStaticArrayTypeDefinitionIR.new {
        ![arrayType llvmTypeName]
        !elementType
        !arraySize
      }
    #---------- Enter assign repeated value to fixed array size function
      let assignValueToElements_functionName = assignValueToFixedSizeArrayElements_functionName (!arrayType)
      ioRoutineListIR += !@assignRepeatedValueToFixedSizeArrayElementsFunctionIR.new {
        ![assignValueToElements_functionName nowhere]
        !isRequired: true
        !warnsIfUnused: false
        !arrayType
        !elementType
        !arraySize
      }

#      let assignmentFunctionLLVMName =  assignmentOperatorFuncName (!arrayType !arrayType)
#      ioRoutineListIR += !@staticArrayTypeAssignFunctionIR.new {
#        ![assignmentFunctionLLVMName nowhere]
#        !isRequired: true
#        !warnsIfUnused: false
#        !arrayType
#      }
   #--- Enter assignment operator
      let key = assignmentOperatorKey (!arrayType !mFixedSizeArrayType.location !arrayType)
      [!?ioContext.mAssignmentOperatorMap insertKey !key !@fixedSizeArrayAssignmentOperatorUsage.new {!elementType !arraySize !assignValueToElements_functionName}]
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

func assignValueToFixedSizeArrayElements_functionName ?let @omnibusType inFixedSizeArrayType -> @string {
  result = "assign.value.to.fixed.size.array." + [inFixedSizeArrayType llvmBaseTypeName]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! USER LLVM TYPE DEFINITION
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @userLLVMStaticArrayTypeDefinitionIR : @userLLVMTypeDefinitionIR {
  @omnibusType mElementType
  @bigint mSize
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @userLLVMStaticArrayTypeDefinitionIR generateLLVMType ?!@string ioLLVMcode {
  ioLLVMcode += mLLVMDefinedTypeName + " = type [" + mSize + " x " + [mElementType llvmTypeName] + "]\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! CODE GENERATION: STATIC ARRAY ASSIGNMENT
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @fixedSizeArrayAssignmentOperatorUsage : @abstractAssignmentOperatorUsage {
  @omnibusType mElementType
  @uint mArraySize
  @string mAssignValueToElements_functionName
}

#·······················································································································

override method @fixedSizeArrayAssignmentOperatorUsage generateCode
    ?!@semanticTemporariesStruct ioTemporaries
    ?!@instructionListIR ioInstructionListIR
    ?!@allocaList unused ioAllocaList
    ?let @omnibusType inTargetType
    ?let @string inTargetLLVMName
    ?let @objectIR inSourcePossibleReference
    ?let @location unused inErrorLocation
{
  let arrayTypeName = [inTargetType llvmTypeName]
  let elementTypeName = [mElementType llvmTypeName]
  if [inSourcePossibleReference isLlvmArrayRepeatedDynamicValue] then
    [inSourcePossibleReference llvmArrayRepeatedDynamicValue ?2* ?value: let @objectIR value]
     var s = "call void @" + [mAssignValueToElements_functionName assemblerRepresentation]
     s += " (" + arrayTypeName + "* " + [value llvmName]
     s += ", " + elementTypeName + " " + [value llvmName]
     [!?ioInstructionListIR appendInstructionAsFreeString !s !{!mAssignValueToElements_functionName}]
  elsif [inSourcePossibleReference isLlvmArrayDynamicValues] then
    let uniqueValue = ioTemporaries.mTemporaryIndex
    ioTemporaries.mTemporaryIndex ++
    [inSourcePossibleReference llvmArrayDynamicValues ?1* ?values: let @operandIRList values]
    for (value) in values do (idx)
      let ptr = "%ptr." + uniqueValue + "." + idx
      var s = ptr + " = getelementptr " + arrayTypeName + ", " + arrayTypeName + "* " + inTargetLLVMName + ", i32 0, i32 " + idx
      [!?ioInstructionListIR appendInstructionAsFreeString !s !{}]
      s = "store " + elementTypeName + " " + [value llvmName] + ", " + elementTypeName + "* " + ptr
      [!?ioInstructionListIR appendInstructionAsFreeString !s !{}]    
    end
  else
#--- If source is a reference, load value
  @objectIR sourceValue = inSourcePossibleReference
  if [inSourcePossibleReference isReference] then
    getNewTempValue (!inTargetType !?ioTemporaries ?sourceValue)
    let s = [sourceValue llvmName] + " = load " + [inTargetType llvmTypeName] + ", "
          + [inTargetType llvmTypeName] + "* " + [inSourcePossibleReference llvmName]
    [!?ioInstructionListIR appendInstructionAsFreeString !s !{}]
  end
  #--- Perfom store
    let s = "store " + [inTargetType llvmTypeName] + " " + [sourceValue llvmName] 
          + ", " + [inTargetType llvmTypeName] + "* " + inTargetLLVMName
    [!?ioInstructionListIR appendInstructionAsFreeString !s !{}]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

#class @staticArrayTypeAssignFunctionIR : @abstractRoutineIR {
#  @omnibusType mFixedSizeArrayType
#}

#·······················································································································

#override method @staticArrayTypeAssignFunctionIR llvmCodeGeneration 
#  ?!@string ioLLVMcode
#  ?let @generationContext unused inGenerationContext
#  ?!@generationAdds ioGenerationAdds
#{

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @assignRepeatedValueToFixedSizeArrayElementsFunctionIR : @abstractRoutineIR {
  @omnibusType mFixedSizeArrayType
  @omnibusType mElementType
  @uint mArraySize
}

#·······················································································································

override method @assignRepeatedValueToFixedSizeArrayElementsFunctionIR llvmCodeGeneration 
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  let arrayTypeName = [mFixedSizeArrayType llvmTypeName]
  let elementTypeName = [mElementType llvmTypeName]
  ioLLVMcode += "define internal void @" + [mRoutineMangledName assemblerRepresentation]
  ioLLVMcode += " (" + arrayTypeName + " * %inTarget, " + elementTypeName + " %inRepeatedValue) nounwind optsize {\n"
  ioLLVMcode += "  %idx = alloca i32\n"
  ioLLVMcode += "  store i32 0, i32* %idx\n"
  ioLLVMcode += "  br label %loop\n"
  ioLLVMcode += "loop:\n"
  ioLLVMcode += "  %idxValue = load i32, i32* %idx\n"
  ioLLVMcode += "  %ptr = getelementptr " + arrayTypeName + ", " + arrayTypeName + "* %inTarget, i32 0, i32 %idxValue\n"
  ioLLVMcode += "  store " + elementTypeName + " %inRepeatedValue, " + elementTypeName + "* %ptr\n"
  ioLLVMcode += "  %nextIdxValue = add i32 %idxValue, 1\n"
  ioLLVMcode += "  store i32 %nextIdxValue, i32* %idx\n"
  ioLLVMcode += "  %continue = icmp ult i32 %nextIdxValue, " + mArraySize + "\n"
  ioLLVMcode += "  br i1 %continue, label %loop, label %exit\n"
  ioLLVMcode += "exit:\n"
  ioLLVMcode += "  ret void\n"
  ioLLVMcode += "}\n\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

#  let llvmTypeName = [mFixedSizeArrayType llvmTypeName]
#  let arcAssignmentList = [mFixedSizeArrayType arcList !{}]
#  ioLLVMcode += "define internal void @" + [mRoutineMangledName assemblerRepresentation]
#  ioLLVMcode += " (" + llvmTypeName + " * %inTarget, " + llvmTypeName + "* %inSource) nounwind optsize {\n"
#  ioLLVMcode += ";--- Handle ARC properties\n"
#  for (propertyName propertyType indexPath) in arcAssignmentList do
#    let propertyTypeLLVMName = [propertyType llvmTypeName]
#    let llvmVarName = "%arc.retain." + [propertyName assemblerRepresentation] + ioGenerationAdds.mUniqueIndex
#    ioGenerationAdds.mUniqueIndex ++
#    ioLLVMcode += "  " + llvmVarName + " = getelementptr inbounds " + llvmTypeName + ", " + llvmTypeName + "* %inSource, i32 0"
#    for (idx) in indexPath do
#      ioLLVMcode += ", i32 " + idx
#    end
#    ioLLVMcode += " ; " + propertyName + "\n"
#    let llvmLoadedVarName = llvmVarName + ".loaded"
#    ioLLVMcode += "  " + llvmLoadedVarName + " = load " + propertyTypeLLVMName + ", " + propertyTypeLLVMName + "* " + llvmVarName + "\n" 
#    ioLLVMcode += "  call void @arc.retain (" + propertyTypeLLVMName + " " + llvmLoadedVarName + ")\n" 
#  end
#  for (propertyName propertyType indexPath) in arcAssignmentList do
#    let llvmVarName = "%arc.release." + [propertyName assemblerRepresentation] + ioGenerationAdds.mUniqueIndex
#    ioGenerationAdds.mUniqueIndex ++
#    ioLLVMcode += "  " + llvmVarName + " = getelementptr inbounds " + llvmTypeName + ", " + llvmTypeName + "* %inTarget, i32 0"
#    for (idx) in indexPath do
#      ioLLVMcode += ", i32 " + idx
#    end
#    ioLLVMcode += " ; " + propertyName + "\n"
#    let propertyTypeLLVMName = [propertyType llvmTypeName]
#    let llvmLoadedVarName = llvmVarName + ".loaded"
#    ioLLVMcode += "  " + llvmLoadedVarName + " = load " + propertyTypeLLVMName + ", " + propertyTypeLLVMName + "* " + llvmVarName + "\n" 
#    ioLLVMcode += "  call void @arc.release (" + propertyTypeLLVMName + " " + llvmLoadedVarName + ")\n" 
#  end
#  ioLLVMcode += ";--- Compute type size\n"
#  ioLLVMcode += "  %sizeAsPtr = getelementptr inbounds " + llvmTypeName + ", " + llvmTypeName + "* null, i32 1\n"
#  ioLLVMcode += "  %byteCount = ptrtoint " + llvmTypeName + "* %sizeAsPtr to i32\n"
#  ioLLVMcode += "  %useLoadStore = icmp ule i32 %byteCount, 12\n"
#  ioLLVMcode += "  br i1 %useLoadStore, label %loadStore, label %copyBytesOrCopyWords\n"
#  ioLLVMcode += ";--- Copy\n"
#  ioLLVMcode += "loadStore:\n"
#  ioLLVMcode += "  %v = load " + llvmTypeName + ", " + llvmTypeName + "* %inSource\n" 
#  ioLLVMcode += "  store " + llvmTypeName + " %v, " + llvmTypeName + "* %inTarget\n" 
#  ioLLVMcode += "  br label %copy.end\n"
#  ioLLVMcode += "copyBytesOrCopyWords:\n"
#  ioLLVMcode += "  %byteCountAnd3 = and i32 %byteCount, 3\n"
#  ioLLVMcode += "  %byteCountIsMutipleOf4 = icmp eq i32 %byteCountAnd3, 0\n"
#  ioLLVMcode += "  br i1 %byteCountIsMutipleOf4, label %testTarget, label %copyBytes\n"
#  ioLLVMcode += "testTarget:\n"
#  ioLLVMcode += "  %targetAsUnsigned = ptrtoint " + llvmTypeName + "* %inTarget to i32\n"
#  ioLLVMcode += "  %targetAsUnsignedAnd3 = and i32 %targetAsUnsigned, 3\n"
#  ioLLVMcode += "  %targetAsUnsignedIsMutipleOf4 = icmp eq i32%targetAsUnsignedAnd3, 0\n"
#  ioLLVMcode += "  br i1 %targetAsUnsignedIsMutipleOf4, label %testSource, label %copyBytes\n"
#  ioLLVMcode += "testSource:\n"
#  ioLLVMcode += "  %sourceAsUnsigned = ptrtoint " + llvmTypeName + "* %inSource to i32\n"
#  ioLLVMcode += "  %sourceAsUnsignedAnd3 = and i32 %sourceAsUnsigned, 3\n"
#  ioLLVMcode += "  %sourceAsUnsignedIsMutipleOf4 = icmp eq i32 %sourceAsUnsignedAnd3, 0\n"
#  ioLLVMcode += "  br i1 %targetAsUnsignedIsMutipleOf4, label %copyWords, label %copyBytes\n"
#  ioLLVMcode += "copyWords:\n"
#  ioLLVMcode += "  %target32 = bitcast " + llvmTypeName + "* %inTarget to i32*\n"
#  ioLLVMcode += "  %source32 = bitcast " + llvmTypeName + "* %inSource to i32*\n"
#  ioLLVMcode += "  %wordCount = udiv i32 %byteCount, 4\n"
#  ioLLVMcode += "  br label %copyWords.loop\n"
#  ioLLVMcode += "copyWords.loop:\n"
#  ioLLVMcode += "  call void @copy.word.array (i32* %target32, i32* %source32, i32 %wordCount)\n"
#  ioLLVMcode += "  br label %copy.end\n"
#  ioLLVMcode += "copyBytes:\n"
#  ioLLVMcode += "  %target8 = bitcast " + llvmTypeName + "* %inTarget to i8*\n"
#  ioLLVMcode += "  %source8 = bitcast " + llvmTypeName + "* %inSource to i8*\n"
#  ioLLVMcode += "  call void @copy.byte.array (i8* %target8, i8* %source8, i32 %byteCount)\n"
#  ioLLVMcode += "  br label %copy.end\n"
#  ioLLVMcode += "copy.end:\n"
#  ioLLVMcode += "  ret void\n"
#  ioLLVMcode += "}\n\n"
#}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
