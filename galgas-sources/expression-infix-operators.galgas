#----------------------------------------------------------------------------------------------------------------------*
#  AST                                                                                                                 *
#----------------------------------------------------------------------------------------------------------------------*

enum @infixOperator {
  case equal
  case nonEqual
  case strictInf
  case strictSup
  case infEqual
  case supEqual
  case andOp
  case booleanAnd
  case orOp
  case booleanOrOp
  case xorOp
  case booleanXorOp
  case addOp
  case addOpNoOvf
  case subOp
  case subOpNoOvf
  case mulOp
  case mulOpNoOvf
  case divOp
  case divOpNoOvf
  case modOp
  case leftShiftOp
  case rightShiftOp
}

#----------------------------------------------------------------------------------------------------------------------*

class @infixOperatorExpressionAST : @expressionAST {
  @expressionAST mLeftExpression
  @location mOperatorLocation
  @infixOperator mOp
  @expressionAST mRightExpression
}

#----------------------------------------------------------------------------------------------------------------------*

class @asExpressionAST : @expressionAST {
  @expressionAST mExpression
  @lstring mTypeName
}

#----------------------------------------------------------------------------------------------------------------------*
#  SYNTAX                                                                                                              *
#----------------------------------------------------------------------------------------------------------------------*

syntax extension common_syntax {

  #--------------------------------------------------------------------------------------------------------------------*

  rule <comparison> !@expressionAST outExpression {
    <additive_expression> ?outExpression
    select
    or
      $as$
      $identifier$ ?let typeIdentifier
      outExpression = @asExpressionAST.new {
        !outExpression
        !typeIdentifier
      }
    or
      $==$
      let operatorLocation = @location.here
      <additive_expression> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.equal
        !rightExpression
      }
    or
      $!=$
      let operatorLocation = @location.here
      <additive_expression> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.nonEqual
        !rightExpression
      }
    or
      $<=$
      let operatorLocation = @location.here
      <additive_expression> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.infEqual
        !rightExpression
      }
    or
      $>=$
      let operatorLocation = @location.here
      <additive_expression> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.supEqual
        !rightExpression
      }
    or
      $<$
      let operatorLocation = @location.here
      <additive_expression> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.strictInf
        !rightExpression
      }
    or
      $>$
      let operatorLocation = @location.here
      <additive_expression> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.strictSup
        !rightExpression
      }
    end
  }

  #--------------------------------------------------------------------------------------------------------------------*

  rule <additive_expression> !@expressionAST outExpression {
    <multiplicative_expression> ?outExpression
    repeat
    while
      $|$
      let operatorLocation = @location.here
      <multiplicative_expression> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.orOp
        !rightExpression
      }
    while
      $^$
      let operatorLocation = @location.here
      <multiplicative_expression> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.xorOp
        !rightExpression
      }
    while
      $xor$
      let operatorLocation = @location.here
      <multiplicative_expression> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.booleanXorOp
        !rightExpression
      }
    while
      $+$
      let operatorLocation = @location.here
      <multiplicative_expression> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.addOp
        !rightExpression
      }
    while
      $&+$
      let operatorLocation = @location.here
      <multiplicative_expression> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.addOpNoOvf
        !rightExpression
      }
    while
      $-$
      let operatorLocation = @location.here
      <multiplicative_expression> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.subOp
        !rightExpression
      }
    while
      $&-$
      let operatorLocation = @location.here
      <multiplicative_expression> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.subOpNoOvf
        !rightExpression
      }
    while
      $or$
      let operatorLocation = @location.here
      <multiplicative_expression> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.booleanOrOp
        !rightExpression
      }
    while
      $<<$
      let operatorLocation = @location.here
      <multiplicative_expression> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.leftShiftOp
        !rightExpression
      }
    while
      $>>$
      let operatorLocation = @location.here
      <multiplicative_expression> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.rightShiftOp
        !rightExpression
      }
    end
  }

  #--------------------------------------------------------------------------------------------------------------------*

  rule <multiplicative_expression> !@expressionAST outExpression {
    <primary> ?outExpression
    repeat
    while
      $&$
      let operatorLocation = @location.here
      <primary> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.andOp
        !rightExpression
      }
    while
      $*$
      let operatorLocation = @location.here
      <primary> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.mulOp
        !rightExpression
      }
    while
      $&*$
      let operatorLocation = @location.here
      <primary> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.mulOpNoOvf
        !rightExpression
      }
    while
      $%$
      let operatorLocation = @location.here
      <primary> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.modOp
        !rightExpression
      }
    while
      $/$
      let operatorLocation = @location.here
      <primary> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.divOp
        !rightExpression
      }
    while
      $&/$
      let operatorLocation = @location.here
      <primary> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.divOpNoOvf
        !rightExpression
      }
    while
      $and$
      let operatorLocation = @location.here
      <primary> ?let rightExpression
      outExpression = @infixOperatorExpressionAST.new {
        !outExpression
        !operatorLocation
        !.booleanAnd
        !rightExpression
      }
    end
  }

  #--------------------------------------------------------------------------------------------------------------------*

}

#----------------------------------------------------------------------------------------------------------------------*
#  PRECEDENCE GRAPH FROM EXPRESSION                                                                                    *
#----------------------------------------------------------------------------------------------------------------------*

override method @infixOperatorExpressionAST addDependenceEdgeForStaticExpression
  ?let @lstring inConstantName
  ?!@semanticTypePrecedenceGraph ioGraph
{
  [mLeftExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
  [mRightExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
}

#----------------------------------------------------------------------------------------------------------------------*

override method @asExpressionAST addDependenceEdgeForStaticExpression
  ?let @lstring inConstantName
  ?!@semanticTypePrecedenceGraph ioGraph
{
  [mExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
}

#----------------------------------------------------------------------------------------------------------------------*
#  SEMANTICS                                                                                                           *
#----------------------------------------------------------------------------------------------------------------------*

method @infixOperatorMap checkWith
  ?leftType:let @unifiedTypeMap-proxy inLeftType
  ?rightType:let @unifiedTypeMap-proxy inRightType
  ?operatorLocation:let @location inOperatorLocation
  !resultType: @unifiedTypeMap-proxy outResultType
{
  let lkey = @lstring.new {![inLeftType key] + "." + [inRightType key] !inOperatorLocation}
  if [self hasKey !lkey.string] then
    [self searchKey !lkey ?outResultType]
  else
    error inOperatorLocation
      : "infix operation between '" + [inLeftType key] + " and " + [inRightType key] + "' is not defined"
      : outResultType
  end
}

#----------------------------------------------------------------------------------------------------------------------*

override method @infixOperatorExpressionAST analyzeExpression
  ?optionalReceiverType:let @receiverType inReceiverType
  ?optionalTargetType:let @unifiedTypeMap-proxy inOptionalTargetType
  ?context:let @semanticContext inContext
  ?modes:let @stringset inModeSet
  ?allowException:let @bool inAllowExceptions
  ?!temporary:@uint ioTemporaryIndex
  ?!staticStringMap:@globalLiteralStringMap ioGlobalLiteralStringMap
  ?!variableMap:@variableMap ioVariableMap
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !expressionType:@unifiedTypeMap-proxy outExpressionType
  !result:@variableKindIR outResult
{
#--- Analyze left expression
  [mLeftExpression analyzeExpression
    !optionalReceiverType:inReceiverType
    !optionalTargetType:inOptionalTargetType
    !context:inContext
    !modes:inModeSet
    !allowException:inAllowExceptions
    !?temporary:ioTemporaryIndex
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioVariableMap
    !?instructionListIR:ioInstructionGenerationList
    ?expressionType:let leftExpressionType
    ?result:@variableKindIR leftResult
  ]
#--- Analyze right expression
  [mRightExpression analyzeExpression
    !optionalReceiverType:inReceiverType
    !optionalTargetType:leftExpressionType
    !context:inContext
    !modes:inModeSet
    !allowException:inAllowExceptions
    !?temporary:ioTemporaryIndex
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioVariableMap
    !?instructionListIR:ioInstructionGenerationList
    ?expressionType:let rightExpressionType
    ?result:@variableKindIR rightResult
  ]
#--- Check left type
  @infixOperatorMap operatorMap
  @infixOperatorIR infixOperatorIR
  switch mOp 
  case equal :
    operatorMap = inContext.mEqualOperatorMap
    infixOperatorIR = .infix {!infixOperator:"=="}
  case nonEqual :
    operatorMap = inContext.mNonEqualOperatorMap
    infixOperatorIR = .infix {!infixOperator:"!="}
  case strictInf :
    operatorMap = inContext.mStrictInfOperatorMap
    infixOperatorIR = .infix {!infixOperator:"<"}
  case infEqual :
    operatorMap = inContext.mInfEqualOperatorMap
    infixOperatorIR = .infix {!infixOperator:"<="}
  case strictSup :
    operatorMap = inContext.mStrictSupOperatorMap
    infixOperatorIR = .infix {!infixOperator:">"}
  case supEqual :
    operatorMap = inContext.mSupEqualOperatorMap
    infixOperatorIR = .infix {!infixOperator:">="}
  case andOp :
    operatorMap = inContext.mAndOperatorMap
    infixOperatorIR = .infix {!infixOperator:"&"}
  case booleanAnd :
    operatorMap = inContext.mBooleanAndOperatorMap
    infixOperatorIR = .infix {!infixOperator:"&&"}
  case orOp :
    operatorMap = inContext.mOrOperatorMap
    infixOperatorIR = .infix {!infixOperator:"|"}
  case booleanOrOp :
    operatorMap = inContext.mBooleanOrOperatorMap
    infixOperatorIR = .infix {!infixOperator:"||"}
  case xorOp :
    operatorMap = inContext.mXorOperatorMap
    infixOperatorIR = .infix {!infixOperator:"^"}
  case booleanXorOp :
    operatorMap = inContext.mBooleanXorOperatorMap
    infixOperatorIR = .infix {!infixOperator:"^"}
  case addOp :
    if [inModeSet hasKey !exceptionModeName ()] then
      error mOperatorLocation : "operations that can generate exceptions are not allowed in $" + exceptionModeName () + " mode"
    end
    operatorMap = inContext.mAddOperatorMap
    if [option plm_options.noExceptionGeneration value] then
      infixOperatorIR = .infix {!infixOperator:"+"}
    else
      infixOperatorIR = .infixOvf {!builtinFunction:"__builtin_add_overflow" !code:3}
    end
  case addOpNoOvf :
    operatorMap = inContext.mAddNoOvfOperatorMap
    infixOperatorIR = .infix {!infixOperator:"+"}
  case subOp :
    if [inModeSet hasKey !exceptionModeName ()] then
      error mOperatorLocation : "operations that can generate exceptions are not allowed in $" + exceptionModeName () + " mode"
    end
    operatorMap = inContext.mSubNoOvfOperatorMap
    if [option plm_options.noExceptionGeneration value] then
      infixOperatorIR = .infix {!infixOperator:"-"}
    else
      infixOperatorIR = .infixOvf {!builtinFunction:"__builtin_sub_overflow" !code:4}
    end
  case subOpNoOvf :
    operatorMap = inContext.mSubOperatorMap
    infixOperatorIR = .infix {!infixOperator:"-"}
  case mulOp :
    if [inModeSet hasKey !exceptionModeName ()] then
      error mOperatorLocation : "operations that can generate exceptions are not allowed in $" + exceptionModeName () + " mode"
    end
    operatorMap = inContext.mMulOperatorMap
    if [option plm_options.noExceptionGeneration value] then
      infixOperatorIR = .infix {!infixOperator:"*"}
    else
      infixOperatorIR = .infixOvf {!builtinFunction:"__builtin_mul_overflow" !code:5}
    end
  case mulOpNoOvf :
    operatorMap = inContext.mMulNoOvfOperatorMap
    infixOperatorIR = .infix {!infixOperator:"*"}
  case divOp :
    if [inModeSet hasKey !exceptionModeName ()] then
      error mOperatorLocation : "operations that can generate exceptions are not allowed in $" + exceptionModeName () + " mode"
    end
    operatorMap = inContext.mDivOperatorMap
    if [option plm_options.noExceptionGeneration value] then
      infixOperatorIR = .infix {!infixOperator:"/"}
    else
      infixOperatorIR = .infixUnsignedDivide{!code:6}
    end
  case divOpNoOvf :
    operatorMap = inContext.mDivNoOvfOperatorMap
    infixOperatorIR = .infix {!infixOperator:"/"}
  case modOp :
    operatorMap = inContext.mModOperatorMap
    infixOperatorIR = .infix {!infixOperator:"%"}
  case leftShiftOp :
    operatorMap = inContext.mLeftShiftOperatorMap
    infixOperatorIR = .infix {!infixOperator:"<<"}
  case rightShiftOp :
    operatorMap = inContext.mRightShiftOperatorMap
    infixOperatorIR = .infix {!infixOperator:">>"}
  end
  [operatorMap checkWith
    !leftType:leftExpressionType
    !rightType:rightExpressionType
    !operatorLocation:mOperatorLocation
    ?resultType:outExpressionType
  ]
#---
  if [leftResult isLiteralUnsignedInteger] && [rightResult isLiteralUnsignedInteger] then
    [leftResult literalUnsignedInteger ?type:* ?value:let @uint64 leftValue]
    [rightResult literalUnsignedInteger ?type:* ?value:let @uint64 rightValue]
    @uint64 result
    switch mOp 
    case equal : result = [leftValue == rightValue uint64]
    case nonEqual : result = [leftValue != rightValue uint64]
    case strictInf : result = [leftValue < rightValue uint64]
    case infEqual : result = [leftValue <= rightValue uint64]
    case strictSup : result = [leftValue > rightValue uint64]
    case supEqual : result = [leftValue >= rightValue uint64]
    case andOp : result = leftValue & rightValue
    case booleanAnd : result = [(leftValue != 0L) && (rightValue != 0L) uint64]
    case orOp : result = leftValue | rightValue
    case booleanOrOp : result = [(leftValue != 0L) || (rightValue != 0L) uint64]
    case xorOp, booleanXorOp : result = leftValue ^ rightValue
    case addOp : result = leftValue &+ rightValue
    case addOpNoOvf : result = leftValue + rightValue
    case subOp : result = leftValue - rightValue
    case subOpNoOvf : result = leftValue &- rightValue
    case mulOp : result = leftValue * rightValue
    case mulOpNoOvf : result = leftValue &* rightValue
    case divOp : result = leftValue / rightValue
    case divOpNoOvf : result = leftValue &/ rightValue
    case modOp : result = leftValue mod rightValue
    case leftShiftOp  : result = leftValue << [rightValue uint]
    case rightShiftOp : result = leftValue >> [rightValue uint]
    end
    outResult = .literalUnsignedInteger {!type:outExpressionType !value:result}
  else
  #--- Temp var
    getNewTempVariable (!?ioTemporaryIndex ?outResult)
  #--- Generate code
    ioInstructionGenerationList += !@operatorInfixExpressionIR.new {
      !outExpressionType
      !outResult
      !leftResult
      !infixOperatorIR
      !rightResult
      !mOperatorLocation
    }
  end
}

#----------------------------------------------------------------------------------------------------------------------*
#! @asExpressionAST

override method @asExpressionAST analyzeExpression
  ?optionalReceiverType:let @receiverType inReceiverType
  ?optionalTargetType:let @unifiedTypeMap-proxy inOptionalTargetType
  ?context:let @semanticContext inContext
  ?modes:let @stringset inModeSet
  ?allowException:let @bool inAllowExceptions
  ?!temporary:@uint ioTemporaryIndex
  ?!staticStringMap:@globalLiteralStringMap ioGlobalLiteralStringMap
  ?!variableMap:@variableMap ioVariableMap
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !expressionType:@unifiedTypeMap-proxy outExpressionType
  !result:@variableKindIR outResult
{
#--- Analyze expression
  [mExpression analyzeExpression
    !optionalReceiverType:inReceiverType
    !optionalTargetType:inOptionalTargetType
    !context:inContext
    !modes:inModeSet
    !allowException:inAllowExceptions
    !?temporary:ioTemporaryIndex
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioVariableMap
    !?instructionListIR:ioInstructionGenerationList
    ?expressionType:let unused expressionType
    ?result:@variableKindIR expressionResult
  ]
#--- Analyze type
  outExpressionType = .searchKey {!inContext.mTypeMap !mTypeName}
#--- Temp var
  getNewTempVariable (!?ioTemporaryIndex ?outResult)
#--- Generate code
  ioInstructionGenerationList += !@asOperatorInstructionIR.new {
    !outExpressionType
    !outResult
    !expressionResult
  }
}

#----------------------------------------------------------------------------------------------------------------------*
#  CODE GENERATION                                                                                                     *
#----------------------------------------------------------------------------------------------------------------------*
#! @asOperatorInstructionIR
#----------------------------------------------------------------------------------------------------------------------*

class @asOperatorInstructionIR : @abstractInstructionIR {
  @unifiedTypeMap-proxy mTargetType
  @variableKindIR mTemporaryResultVariable
  @variableKindIR mOperand
}

#----------------------------------------------------------------------------------------------------------------------*

override method @asOperatorInstructionIR enterAccessibleEntities ?!@accessibleEntities ioAccessibleEntities {
  [mOperand enterAccessibleEntities !?ioAccessibleEntities]
}

#----------------------------------------------------------------------------------------------------------------------*

override getter @asOperatorInstructionIR instructionCode -> @string outCode {
  outCode = "const " + mangledNameForType (!name:[mTargetType key]) + " " + [mTemporaryResultVariable mangledName]
  outCode += " = ((" + mangledNameForType (!name:[mTargetType key]) + ") " + [mOperand mangledName] + ") ;\n"
}

#----------------------------------------------------------------------------------------------------------------------*
#! @operatorInfixExpressionIR
#----------------------------------------------------------------------------------------------------------------------*

enum @infixOperatorIR {
  case infix (@string infixOperator)
  case infixOvf (@string builtinFunction @uint code)
  case infixUnsignedDivide (@uint code)
}

#----------------------------------------------------------------------------------------------------------------------*

class @operatorInfixExpressionIR : @abstractInstructionIR {
  @unifiedTypeMap-proxy mTargetType
  @variableKindIR mResult
  @variableKindIR mLeftOperand
  @infixOperatorIR mOperator
  @variableKindIR mRighOperand
  @location mOperatorLocation
}

#----------------------------------------------------------------------------------------------------------------------*

override getter @operatorInfixExpressionIR instructionCode -> @string outCode {
  switch mOperator 
  case infix (@string infixOperator) :
    outCode = "const " + mangledNameForType (!name:[mTargetType key]) + " " + [mResult mangledName]
    outCode += " = " +  [mLeftOperand mangledName] + " " + infixOperator + " " + [mRighOperand mangledName] + " ;\n"
  case infixOvf (@string builtinFunction @uint code) :
    let ovfVar = "ovf" + [mOperatorLocation locationIndex]
    outCode = mangledNameForType (!name:[mTargetType key]) + " " + [mResult mangledName] + " ;\n"
    outCode += "const bool " + ovfVar + " = " + builtinFunction + " ("
      +  [mLeftOperand mangledName] + ", "
      + [mRighOperand mangledName]
      + ", &" + [mResult mangledName] + ") ;\n"
    outCode += "if (" + ovfVar + ") {\n"
    outCode += "  raise_exception (" + code
       + ", " + [[[mOperatorLocation file] lastPathComponent] utf8Representation]
       + ", " + [mOperatorLocation line] + ") ;\n"
    outCode += "}\n" 
  case infixUnsignedDivide (@uint code) :
    outCode  = "if (0 == " +  [mRighOperand mangledName] + ") {\n"
    outCode += "  raise_exception (" + code
      + ", " + [[[mOperatorLocation file] lastPathComponent] utf8Representation]
      + ", " + [mOperatorLocation line] + ") ;\n"
    outCode += "}\n"
    outCode += "const " + mangledNameForType (!name:[mTargetType key]) + " " + [mResult mangledName]
    outCode += " = " +  [mLeftOperand mangledName] + " / " + [mRighOperand mangledName] + " ;\n"
  end
}

#----------------------------------------------------------------------------------------------------------------------*

override method @operatorInfixExpressionIR enterAccessibleEntities ?!@accessibleEntities ioAccessibleEntities {
  [mLeftOperand enterAccessibleEntities !?ioAccessibleEntities]
  [mRighOperand enterAccessibleEntities !?ioAccessibleEntities]
}

#----------------------------------------------------------------------------------------------------------------------*
