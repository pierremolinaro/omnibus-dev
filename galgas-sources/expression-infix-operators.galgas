#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @infixOperator {
  case equal
  case nonEqual
  case strictInf
  case strictSup
  case infEqual
  case supEqual
  case andOp
  case orOp
  case xorOp
  case booleanXorOp
  case addOp
  case addOpNoOvf
  case subOp
  case subOpNoOvf
  case mulOp
  case mulOpNoOvf
  case divOp
  case divOpNoOvf
  case modOp
  case modOpNoOvf
  case leftShiftOp
  case rightShiftOp
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @infixOperatorExpressionAST : @expressionAST {
  @expressionAST mLeftExpression
  @location mOperatorLocation
  @infixOperator mOp
  @expressionAST mRightExpression
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH FROM EXPRESSION                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @infixOperatorExpressionAST addDependenceEdgeForStaticExpression
  ?let @lstring inConstantName
  ?!@semanticTypePrecedenceGraph ioGraph
{
  [mLeftExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
  [mRightExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @infixOperatorExpressionAST noteExpressionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  [mLeftExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
  [mRightExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @infixOperatorMap checkBinaryOperationWith
  ?leftType:let @PLMType inLeftType
  ?rightType:let @PLMType inRightType
  ?operatorLocation:let @location inOperatorLocation
  !resultType: @PLMType outResultType
  !op:@infixOperatorDescription outOperation
{
  let lkey = combineTypeNamesForInfixOperator (!inLeftType.plmTypeDescriptionName !inRightType.plmTypeDescriptionName)
  if [self hasKey !lkey.string] then
    [self searchKey !lkey ?outResultType ?outOperation]
  else
    error inOperatorLocation
      : "infix operation between " + inLeftType.plmTypeDescriptionName + " and " + inRightType.plmTypeDescriptionName + " is not defined"
      : outResultType, outOperation
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @infixOperatorExpressionAST analyzeExpression
  ?self:let @PLMType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?optionalTargetType:let @PLMType inOptionalTargetType
  ?context:let @semanticContext inContext
  ?mode:let @mode inCurrentMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !result:@objectIR outResult
{
#------------------- Analyze left expression
  [mLeftExpression analyzeExpression
    !self:inSelfType
    !routineAttributes:inRoutineAttributes
    !optionalTargetType:inOptionalTargetType
    !context:inContext
    !mode:inCurrentMode
    !?temporary:ioTemporaries
    !?staticEntityMap:ioStaticEntityMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:ioInstructionGenerationList
    ?result:@objectIR leftOperand
  ]
  [!?ioInstructionGenerationList appendLoadWhenReference
    !?ioTemporaries
    !?leftOperand
  ]  
#------------------- Analyze right expression
  [mRightExpression analyzeExpression
    !self:inSelfType
    !routineAttributes:inRoutineAttributes
    !optionalTargetType:[leftOperand type]
    !context:inContext
    !mode:inCurrentMode
    !?temporary:ioTemporaries
    !?staticEntityMap:ioStaticEntityMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:ioInstructionGenerationList
    ?result:@objectIR rightOperand
  ]
  [!?ioInstructionGenerationList appendLoadWhenReference
    !?ioTemporaries
    !?rightOperand
  ]  
#------------------- Get operator map
  let operatorMap = getInfixOperatorMap (!mOp !context:inContext !mode:inCurrentMode !mOperatorLocation)
#------------------- Check operand types
  [operatorMap checkBinaryOperationWith
    !leftType:[leftOperand type]
    !rightType:[rightOperand type]
    !operatorLocation:mOperatorLocation
    ?resultType:let resultType
    ?op:let @infixOperatorDescription binaryOperator
  ]
#------------------- Generate code
  [binaryOperator generateInfixOperatorCode
    !?ioTemporaries
    !?ioInstructionGenerationList
    !leftOperand
    !mOperatorLocation
    !rightOperand
    !resultType
    ?outResult
  ]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

private func getInfixOperatorMap
  ?let @infixOperator inOp
  ?context:let @semanticContext inContext
  ?mode:let @mode inCurrentMode
  ?let @location inOperatorLocation
  -> @infixOperatorMap outOperatorMap
{
  switch inOp 
  case equal :
    outOperatorMap = inContext.mEqualOperatorMap
  case nonEqual :
    outOperatorMap = inContext.mNonEqualOperatorMap
  case strictInf :
    outOperatorMap = inContext.mStrictInfOperatorMap
  case infEqual :
    outOperatorMap = inContext.mInfEqualOperatorMap
  case strictSup :
    outOperatorMap = inContext.mStrictSupOperatorMap
  case supEqual :
    outOperatorMap = inContext.mSupEqualOperatorMap
  case andOp :
    outOperatorMap = inContext.mAndOperatorMap
  case orOp :
    outOperatorMap = inContext.mOrOperatorMap
  case xorOp :
    outOperatorMap = inContext.mXorOperatorMap
  case booleanXorOp :
    outOperatorMap = inContext.mBooleanXorOperatorMap
  case addOp :
    if inCurrentMode == .panicMode then
      error inOperatorLocation : "operations that can generate panic are not allowed in " + panicModeName () + " mode"
    end
    if [option plm_options.noPanicGeneration value] then
      outOperatorMap = inContext.mAddNoOvfOperatorMap
    else
      outOperatorMap = inContext.mAddOperatorMap
    end
  case addOpNoOvf :
    outOperatorMap = inContext.mAddNoOvfOperatorMap
  case subOp :
    if inCurrentMode == .panicMode then
      error inOperatorLocation : "operations that can generate panic are not allowed in " + panicModeName () + " mode"
    end
    if [option plm_options.noPanicGeneration value] then
      outOperatorMap = inContext.mSubNoOvfOperatorMap
    else
      outOperatorMap = inContext.mSubOperatorMap
    end
  case subOpNoOvf :
    outOperatorMap = inContext.mSubNoOvfOperatorMap
  case mulOp :
    if inCurrentMode == .panicMode then
      error inOperatorLocation : "operations that can generate panic are not allowed in " + panicModeName () + " mode"
    end
    if [option plm_options.noPanicGeneration value] then
      outOperatorMap = inContext.mMulNoOvfOperatorMap
    else
      outOperatorMap = inContext.mMulOperatorMap
    end
  case mulOpNoOvf :
    outOperatorMap = inContext.mMulNoOvfOperatorMap
  case divOp :
    if inCurrentMode == .panicMode then
      error inOperatorLocation : "operations that can generate panic are not allowed in " + panicModeName () + " mode"
    end
    if [option plm_options.noPanicGeneration value] then
      outOperatorMap = inContext.mDivNoOvfOperatorMap
    else
      outOperatorMap = inContext.mDivOperatorMap
    end
  case divOpNoOvf :
    outOperatorMap = inContext.mDivNoOvfOperatorMap
  case modOp :
    if inCurrentMode == .panicMode then
      error inOperatorLocation : "operations that can generate panic are not allowed in " + panicModeName () + " mode"
    end
    if [option plm_options.noPanicGeneration value] then
      outOperatorMap = inContext.mModNoOvfOperatorMap
    else
      outOperatorMap = inContext.mModOperatorMap
    end
  case modOpNoOvf :
    outOperatorMap = inContext.mModNoOvfOperatorMap
  case leftShiftOp :
    outOperatorMap = inContext.mLeftShiftOperatorMap
  case rightShiftOp :
    outOperatorMap = inContext.mRightShiftOperatorMap
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

abstract class @infixOperatorDescription {
  @llvmBinaryOperation mOperator
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

abstract method @infixOperatorDescription generateInfixOperatorCode
  ?!@semanticTemporariesStruct ioTemporaries
  ?!@instructionListIR ioInstructionGenerationList
  ?let @objectIR inLeftOperand
  ?let @location inOperatorLocation
  ?let @objectIR inRightOperand
  ?let @PLMType inResultType
  !@objectIR outResultValue

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

getter @infixOperatorDescription performStaticOperation
  ?let @bigint inLeft
  ?let @bigint inRight
  -> @bigint
{
  switch mOperator
  case addNoOVF : result = inLeft + inRight
  case subNoOVF : result = inLeft - inRight
  case mulNoOVF : result = inLeft * inRight
  case udivNoOVF : result = inLeft / inRight
  case sdivNoOVF : result = inLeft / inRight
  case uremNoOVF : result = inLeft mod inRight
  case sremNoOVF : result = inLeft mod inRight

  case uaddOVF : result = inLeft + inRight
  case saddOVF : result = inLeft + inRight
  case usubOVF : result = inLeft - inRight
  case ssubOVF : result = inLeft - inRight
  case umulOVF : result = inLeft * inRight
  case smulOVF : result = inLeft * inRight
  case udivOVF : result = inLeft / inRight
  case sdivOVF : result = inLeft / inRight
  case uremOVF : result = inLeft mod inRight
  case sremOVF : result = inLeft mod inRight

  case and : result = inLeft & inRight
  case ior : result = inLeft | inRight
  case xor : result = inLeft ^ inRight
  case shl : result = inLeft << [inRight uint]
  case ashr : result = inLeft >> [inRight uint]
  case lshr : result = inLeft >> [inRight uint]
  
  case icmp_eq : result = [inLeft == inRight bigint]
  case icmp_ne : result = [inLeft != inRight bigint]
  case icmp_ult : result = [inLeft < inRight bigint]
  case icmp_ule : result = [inLeft <= inRight bigint]
  case icmp_ugt : result = [inLeft > inRight bigint]
  case icmp_uge : result = [inLeft >= inRight bigint]
  case icmp_slt : result = [inLeft < inRight bigint]
  case icmp_sle : result = [inLeft <= inRight bigint]
  case icmp_sgt : result = [inLeft > inRight bigint]
  case icmp_sge : result = [inLeft >= inRight bigint]

  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
