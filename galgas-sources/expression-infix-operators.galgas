#----------------------------------------------------------------------------------------------------------------------*
#  AST                                                                                                                 *
#----------------------------------------------------------------------------------------------------------------------*

enum @infixOperator {
  case equal
  case nonEqual
  case strictInf
  case strictSup
  case infEqual
  case supEqual
  case andOp
  case orOp
  case xorOp
  case booleanXorOp
  case addOp
  case addOpNoOvf
  case subOp
  case subOpNoOvf
  case mulOp
  case mulOpNoOvf
  case divOp
  case divOpNoOvf
  case modOp
  case modOpNoOvf
  case leftShiftOp
  case rightShiftOp
}

#----------------------------------------------------------------------------------------------------------------------*

class @infixOperatorExpressionAST : @expressionAST {
  @expressionAST mLeftExpression
  @location mOperatorLocation
  @infixOperator mOp
  @expressionAST mRightExpression
}

#----------------------------------------------------------------------------------------------------------------------*

class @booleanShortCircuitAndOperatorExpressionAST : @expressionAST {
  @expressionAST mLeftExpression
  @location mOperatorLocation
  @expressionAST mRightExpression
}

#----------------------------------------------------------------------------------------------------------------------*
#  PRECEDENCE GRAPH FROM EXPRESSION                                                                                    *
#----------------------------------------------------------------------------------------------------------------------*

override method @infixOperatorExpressionAST addDependenceEdgeForStaticExpression
  ?let @lstring inConstantName
  ?!@semanticTypePrecedenceGraph ioGraph
{
  [mLeftExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
  [mRightExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
}

#----------------------------------------------------------------------------------------------------------------------*

override method @booleanShortCircuitAndOperatorExpressionAST addDependenceEdgeForStaticExpression
  ?let @lstring inConstantName
  ?!@semanticTypePrecedenceGraph ioGraph
{
  [mLeftExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
  [mRightExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
}

#----------------------------------------------------------------------------------------------------------------------*
#  SEMANTICS                                                                                                           *
#----------------------------------------------------------------------------------------------------------------------*

method @infixOperatorMap checkWith
  ?leftType:let @unifiedTypeMap-proxy inLeftType
  ?rightType:let @unifiedTypeMap-proxy inRightType
  ?operatorLocation:let @location inOperatorLocation
  !resultType: @unifiedTypeMap-proxy outResultType
  !op:@infixOperatorDescription outOperation
{
  let lkey = combineTypeNamesForInfixOperator (![inLeftType key] ![inRightType key])
  if [self hasKey !lkey.string] then
    [self searchKey !lkey ?outResultType ?outOperation]
  else
    error inOperatorLocation
      : "infix operation between '" + [inLeftType key] + " and " + [inRightType key] + "' is not defined"
      : outResultType, outOperation
  end
}

#----------------------------------------------------------------------------------------------------------------------*

override method @infixOperatorExpressionAST analyzeExpression
  ?routineNameForInvocationGraph:let @lstring inRoutineNameForInvocationGraph
  ?optionalReceiverType:let @receiverType inReceiverType
  ?optionalTargetType:let @unifiedTypeMap-proxy inOptionalTargetType
  ?context:let @semanticContext inContext
  ?modes:let @stringset inModeSet
  ?allowException:let @bool inAllowExceptions
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@globalLiteralStringMap ioGlobalLiteralStringMap
  ?!variableMap:@variableMap ioVariableMap
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !type:@unifiedTypeMap-proxy outResultType
  !result:@valueIR outResultValue
{
#------------------- Analyze left expression
  [mLeftExpression analyzeExpression
    !routineNameForInvocationGraph:inRoutineNameForInvocationGraph
    !optionalReceiverType:inReceiverType
    !optionalTargetType:inOptionalTargetType
    !context:inContext
    !modes:inModeSet
    !allowException:inAllowExceptions
    !?temporary:ioTemporaries
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioVariableMap
    !?instructionListIR:ioInstructionGenerationList
    ?type:let @unifiedTypeMap-proxy leftType
    ?result:let @valueIR leftOperandValue
  ]
#------------------- Analyze right expression
  [mRightExpression analyzeExpression
    !routineNameForInvocationGraph:inRoutineNameForInvocationGraph
    !optionalReceiverType:inReceiverType
    !optionalTargetType:leftType
    !context:inContext
    !modes:inModeSet
    !allowException:inAllowExceptions
    !?temporary:ioTemporaries
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioVariableMap
    !?instructionListIR:ioInstructionGenerationList
    ?type:let @unifiedTypeMap-proxy rightType
    ?result:let @valueIR rightOperandValue
  ]
#------------------- Get operator map
  let operatorMap = getInfixOperatorMap (!mOp !context:inContext !modes:inModeSet !mOperatorLocation)
#------------------- Check operand types
  [operatorMap checkWith
    !leftType:leftType
    !rightType:rightType
    !operatorLocation:mOperatorLocation
    ?resultType:outResultType
    ?op:let @infixOperatorDescription binaryOperator
  ]
#------------------- Generate code
  [binaryOperator generateCode
    !?ioTemporaries
    !?ioInstructionGenerationList
    !leftOperandValue
    !leftType
    !rightOperandValue
    !rightType
    !mOperatorLocation
    ?outResultValue
  ]
}

#----------------------------------------------------------------------------------------------------------------------*

private func getInfixOperatorMap
  ?let @infixOperator inOp
  ?context:let @semanticContext inContext
  ?modes:let @stringset inModeSet
  ?let @location inOperatorLocation
  -> @infixOperatorMap outOperatorMap
{
  switch inOp 
  case equal :
    outOperatorMap = inContext.mEqualOperatorMap
  case nonEqual :
    outOperatorMap = inContext.mNonEqualOperatorMap
  case strictInf :
    outOperatorMap = inContext.mStrictInfOperatorMap
  case infEqual :
    outOperatorMap = inContext.mInfEqualOperatorMap
  case strictSup :
    outOperatorMap = inContext.mStrictSupOperatorMap
  case supEqual :
    outOperatorMap = inContext.mSupEqualOperatorMap
  case andOp :
    outOperatorMap = inContext.mAndOperatorMap
  case orOp :
    outOperatorMap = inContext.mOrOperatorMap
  case xorOp :
    outOperatorMap = inContext.mXorOperatorMap
  case booleanXorOp :
    outOperatorMap = inContext.mBooleanXorOperatorMap
  case addOp :
    if [inModeSet hasKey !exceptionModeName ()] then
      error inOperatorLocation : "operations that can generate exceptions are not allowed in $" + exceptionModeName () + " mode"
    end
    if [option plm_options.noExceptionGeneration value] then
      outOperatorMap = inContext.mAddNoOvfOperatorMap
    else
      outOperatorMap = inContext.mAddOperatorMap
    end
  case addOpNoOvf :
    outOperatorMap = inContext.mAddNoOvfOperatorMap
  case subOp :
    if [inModeSet hasKey !exceptionModeName ()] then
      error inOperatorLocation : "operations that can generate exceptions are not allowed in $" + exceptionModeName () + " mode"
    end
    if [option plm_options.noExceptionGeneration value] then
      outOperatorMap = inContext.mSubNoOvfOperatorMap
    else
      outOperatorMap = inContext.mSubOperatorMap
    end
  case subOpNoOvf :
    outOperatorMap = inContext.mSubOperatorMap
  case mulOp :
    if [inModeSet hasKey !exceptionModeName ()] then
      error inOperatorLocation : "operations that can generate exceptions are not allowed in $" + exceptionModeName () + " mode"
    end
    if [option plm_options.noExceptionGeneration value] then
      outOperatorMap = inContext.mMulNoOvfOperatorMap
    else
      outOperatorMap = inContext.mMulOperatorMap
    end
  case mulOpNoOvf :
    outOperatorMap = inContext.mMulNoOvfOperatorMap
  case divOp :
    if [inModeSet hasKey !exceptionModeName ()] then
      error inOperatorLocation : "operations that can generate exceptions are not allowed in $" + exceptionModeName () + " mode"
    end
    if [option plm_options.noExceptionGeneration value] then
      outOperatorMap = inContext.mDivNoOvfOperatorMap
    else
      outOperatorMap = inContext.mDivOperatorMap
    end
  case divOpNoOvf :
    outOperatorMap = inContext.mDivNoOvfOperatorMap
  case modOp :
    if [inModeSet hasKey !exceptionModeName ()] then
      error inOperatorLocation : "operations that can generate exceptions are not allowed in $" + exceptionModeName () + " mode"
    end
    if [option plm_options.noExceptionGeneration value] then
      outOperatorMap = inContext.mModNoOvfOperatorMap
    else
      outOperatorMap = inContext.mModOperatorMap
    end
  case modOpNoOvf :
    outOperatorMap = inContext.mModNoOvfOperatorMap
  case leftShiftOp :
    outOperatorMap = inContext.mLeftShiftOperatorMap
  case rightShiftOp :
    outOperatorMap = inContext.mRightShiftOperatorMap
  end
}

#----------------------------------------------------------------------------------------------------------------------*

override method @booleanShortCircuitAndOperatorExpressionAST analyzeExpression
  ?routineNameForInvocationGraph:let @lstring inRoutineNameForInvocationGraph
  ?optionalReceiverType:let @receiverType inReceiverType
  ?optionalTargetType:let @unifiedTypeMap-proxy inOptionalTargetType
  ?context:let @semanticContext inContext
  ?modes:let @stringset inModeSet
  ?allowException:let @bool inAllowExceptions
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@globalLiteralStringMap ioGlobalLiteralStringMap
  ?!variableMap:@variableMap ioVariableMap
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !type:@unifiedTypeMap-proxy outResultType
  !result:@valueIR outResultValue
{
#--- Analyze left expression
  @instructionListIR leftInstructionGenerationList = {}
  [mLeftExpression analyzeExpression
    !routineNameForInvocationGraph:inRoutineNameForInvocationGraph
    !optionalReceiverType:inReceiverType
    !optionalTargetType:inOptionalTargetType
    !context:inContext
    !modes:inModeSet
    !allowException:inAllowExceptions
    !?temporary:ioTemporaries
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioVariableMap
    !?instructionListIR:leftInstructionGenerationList
    ?type:let @unifiedTypeMap-proxy leftType
    ?result:let @valueIR leftValue
  ]
#--- Analyze right expression
  @instructionListIR rightInstructionGenerationList = {}
  [mRightExpression analyzeExpression
    !routineNameForInvocationGraph:inRoutineNameForInvocationGraph
    !optionalReceiverType:inReceiverType
    !optionalTargetType:leftType
    !context:inContext
    !modes:inModeSet
    !allowException:inAllowExceptions
    !?temporary:ioTemporaries
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioVariableMap
    !?instructionListIR:rightInstructionGenerationList
    ?type:let @unifiedTypeMap-proxy rightType
    ?result:let @valueIR rightValue
  ]
#--- Check left type is boolean
  if [leftType kind] != .boolean then
    error mOperatorLocation : "left operand should be boolean"
  end
#--- Check right type is boolean
  if [rightType kind] != .boolean then
    error mOperatorLocation : "left operand should be boolean"
  end
#--- Result type : bool 
  outResultType = inContext.mBooleanType
#--- Temp var
  getNewTempVariable (!outResultType !?ioTemporaries ?outResultValue)
#--- Generate code
  [!?ioInstructionGenerationList appendShortCircuitAndOperation
    !outResultValue
    !leftValue
    !leftInstructionGenerationList
    !rightValue
    !rightInstructionGenerationList
    !mOperatorLocation
  ]
}

#----------------------------------------------------------------------------------------------------------------------*

abstract class @infixOperatorDescription {
}

#----------------------------------------------------------------------------------------------------------------------*

abstract method @infixOperatorDescription generateCode
  ?!@semanticTemporariesStruct ioTemporaries
  ?!@instructionListIR ioInstructionGenerationList
  ?let @valueIR inLeftOperand
  ?let @unifiedTypeMap-proxy inLeftType
  ?let @valueIR inRightOperand
  ?let @unifiedTypeMap-proxy inRightType
  ?let @location inOperatorLocation
  !@valueIR outResultValue

#----------------------------------------------------------------------------------------------------------------------*
