#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @compileTimeConvertToBooleanAST : @abstractDeclarationAST {
  @lstring mReceiverName
  @lstring mReceiverTypeName
  @lstring mConverterName
  @ctExpressionAST mExpression
}

#·······················································································································

override getter @compileTimeConvertToBooleanAST locationForErrorSignaling -> @location outLocation {
  outLocation = self.mReceiverTypeName.location
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @llvmConvertToBooleanAST : @abstractDeclarationAST {
  @lstring mReceiverName
  @lstring mReceiverTypeName
  @lstring mConverterName
  @llvmGenerationInstructionList mInstructionList
}

#·······················································································································

override getter @llvmConvertToBooleanAST locationForErrorSignaling -> @location outLocation {
  outLocation = self.mReceiverTypeName.location
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {
  
  #·····················································································································

  rule <declaration> ?!@ast ioAST {
    $compiletime$
    $func$
    $($
    $identifier$ ?let receiverName
    $\$type$ ?let receiverTypeName
    $)$
    $identifier$ ?let converterName
    ${$
    <compile_time_expression> ?let @ctExpressionAST expression
    $}$
    ioAST.mDeclarationListAST += !@compileTimeConvertToBooleanAST.new {
      !receiverName
      !receiverTypeName
      !converterName
      !expression
    }
  }

  #·····················································································································

  rule <declaration> ?!@ast ioAST {
    <llvm_function_header> ?let receiverName ?let receiverTypeName ?let unused receiverGenericFormalParameterList
    $identifier$ ?let converterName
    ${$
    <llvm_instruction_list> ?let instructionList
    $}$
    ioAST.mDeclarationListAST += !@llvmConvertToBooleanAST.new {
      !receiverName
      !receiverTypeName
      !converterName
      !instructionList
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @compileTimeConvertToBooleanAST enterInPrecedenceGraph
  ?!@semanticTypePrecedenceGraph ioGraph
{
  let nodeName = @lstring.new {!self.mReceiverTypeName.string + "." + self.mConverterName !self.mReceiverTypeName.location}
  [!?ioGraph addNode !nodeName !self]
  [!?ioGraph addEdge !nodeName !self.mReceiverTypeName]
}

#·······················································································································

override getter @compileTimeConvertToBooleanAST keyRepresentationForErrorSignaling -> @string outRepresentation {
  outRepresentation = self.mReceiverTypeName.string + "." + self.mConverterName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @llvmConvertToBooleanAST enterInPrecedenceGraph
  ?!@semanticTypePrecedenceGraph ioGraph
{
  let nodeName = @lstring.new {!self.mReceiverTypeName.string + "." + self.mConverterName !self.mReceiverTypeName.location}
  [!?ioGraph addNode !nodeName !self]
  [!?ioGraph addEdge !nodeName !self.mReceiverTypeName]
}

#·······················································································································

override getter @llvmConvertToBooleanAST keyRepresentationForErrorSignaling -> @string outRepresentation {
  outRepresentation = self.mReceiverTypeName.string + "." + self.mConverterName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  ENTER IN CONTEXT                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @compileTimeConvertToBooleanAST enterInContext
  ?!context:@semanticContext ioContext
  ?!@declarationDecorationList unused ioDecoratedDeclarationList
  ?!@decoratedRegularRoutineList unused ioDecoratedRoutineList
  ?!@routineListIR unused ioRoutineListIR
  ?!staticlistValues:@staticListInitializationMap unused ioStaticListValueMap
  ?!staticEntityMap:@staticEntityMap unused ioStaticEntityMap
  ?!registerUserAccess:@controlRegisterUserAccesMapAST unused ioControlRegisterUserAccesMapAST
  ?!userDefinedTypes:@userLLVMTypeDefinitionListIR unused ioUserLLVMTypeDefinitionListIR
{
  if self.mConverterName.string != "convertToBoolean" then
    error self.mConverterName : "the implicit converter name should be 'convertToBoolean'"
  end
#--- Check types
  [@unifiedTypeMap-proxy makeProxy !?ioContext.mTypeMap !self.mReceiverTypeName ?let receiverTypeProxy]
  let receiverType = [receiverTypeProxy type]
  [receiverType checkIsCompileTimeType !self.mReceiverTypeName.location]
#--- Check native operator expression
  @ctCheckMap ctCheckMap = {}
  [!?ctCheckMap insertKey !self.mReceiverName]
  [self.mExpression checkCompileTimeExpression !ctCheckMap]
#--- Enter in infix operator map  
  let key = @lstring.new {![receiverType omnibusTypeDescriptionName] !self.mReceiverTypeName.location}
  let converter = @compileTimeImplicitConverterToBoolean.new {!self.mReceiverName !self.mExpression}
  [!?ioContext.mImplicitConversionToBooleanMap insertKey !key !converter]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @llvmConvertToBooleanAST enterInContext
  ?!context:@semanticContext ioContext
  ?!@declarationDecorationList unused ioDecoratedDeclarationList
  ?!@decoratedRegularRoutineList unused ioDecoratedRoutineList
  ?!@routineListIR unused ioRoutineListIR
  ?!staticlistValues:@staticListInitializationMap unused ioStaticListValueMap
  ?!staticEntityMap:@staticEntityMap unused ioStaticEntityMap
  ?!registerUserAccess:@controlRegisterUserAccesMapAST unused ioControlRegisterUserAccesMapAST
  ?!userDefinedTypes:@userLLVMTypeDefinitionListIR unused ioUserLLVMTypeDefinitionListIR
{
  if self.mConverterName.string != "convertToBoolean" then
    error self.mConverterName : "the implicit converter name should be 'convertToBoolean'"
  end
#--- Check types
  [@unifiedTypeMap-proxy makeProxy !?ioContext.mTypeMap !self.mReceiverTypeName ?let receiverTypeProxy]
  let receiverType = [receiverTypeProxy type]
  [receiverType checkIsNotCompileTimeType !self.mReceiverTypeName.location]
#--- Enter in infix operator map  
  let key = @lstring.new {![receiverType omnibusTypeDescriptionName] !self.mReceiverTypeName.location}
  let converter = @llvmImplicitConverterToBoolean.new {!self.mReceiverName !receiverType !self.mInstructionList}
  [!?ioContext.mImplicitConversionToBooleanMap insertKey !key !converter]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  GENERATE CODE                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

map @implicitConversionToBooleanMap { # Key is a type omnibus name
  @abstractImplicitConverterToBoolean mConverter
  insert insertKey error message "the '%K' type is already declared in %L"
  search searchKey error message "the '%K' type cannot be implicitly converted to boolean"
}

#·······················································································································

enum @implicitBooleanConversionResult {
  case compileTime (@bool value)
  case llvmVariable (@string name) 
}

#·······················································································································

getter @implicitBooleanConversionResult llvmName -> @string {
  switch self
  case compileTime (@bool value) : result = if value then 1 else 0 end
  case llvmVariable (@string name) : result = name
  end
}

#·······················································································································

abstract class @abstractImplicitConverterToBoolean {
}

#·······················································································································

abstract method @abstractImplicitConverterToBoolean generateConvertToBooleanCode
  ?let @objectIR inReceiverOperand
  ?let @location inErrorLocation
  ?!@semanticTemporariesStruct ioTemporaries
  ?!@instructionListIR ioInstructionGenerationList
  ?!alloca:@allocaList ioAllocaList
  !@implicitBooleanConversionResult outResult

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @compileTimeImplicitConverterToBoolean :  @abstractImplicitConverterToBoolean {
  @lstring mReceiverName
  @ctExpressionAST mExpression
}

#·······················································································································

override method @compileTimeImplicitConverterToBoolean generateConvertToBooleanCode
  ?let @objectIR inReceiverOperand
  ?let @location unused inErrorLocation
  ?!@semanticTemporariesStruct unused ioTemporaries
  ?!@instructionListIR unused ioInstructionGenerationList
  ?!alloca: @allocaList unused ioAllocaList
  !@implicitBooleanConversionResult outResult
{
  [inReceiverOperand literalInteger ?1* ?value: let receiverValue]
  @ctMap varMap = {}
  [!?varMap insertKey !self.mReceiverName !receiverValue]
  [self.mExpression computeCompileTimeExpression !varMap ?result: let result]
  outResult = .compileTime {!value: result != 0}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @llvmImplicitConverterToBoolean :  @abstractImplicitConverterToBoolean {
  @lstring mReceiverName
  @omnibusType mReceiverType
  @llvmGenerationInstructionList mInstructionList
}

#·······················································································································

override method @llvmImplicitConverterToBoolean generateConvertToBooleanCode
  ?let @objectIR inReceiverOperand
  ?let @location unused inErrorLocation
  ?!@semanticTemporariesStruct ioTemporaries
  ?!@instructionListIR ioInstructionGenerationList
  ?!alloca: @allocaList ioAllocaList
  !@implicitBooleanConversionResult outResult
{
  if [self.mInstructionList length] == 0 then
    outResult = .llvmVariable {!name: [inReceiverOperand llvmName]}
  else
    @assignmentGenerationVarMap varMap = {}
    [!?varMap insertKey !self.mReceiverName ![inReceiverOperand llvmName]]
    let resultLLVMName = "%temp" + ioTemporaries.mTemporaryIndex
    ioTemporaries.mTemporaryIndex ++
    [!?varMap insertKey !["result" nowhere] !resultLLVMName]
  
    @assignmentGenerationVarMap typeMap = {}
    [!?typeMap insertKey !self.mReceiverName ![self.mReceiverType llvmTypeName]]
  
    @stringlist generatedInstructions = {}
    [self.mInstructionList generateIRCode !varMap !typeMap !?generatedInstructions !?ioTemporaries.mTemporaryIndex !?ioAllocaList]
    for (str) in generatedInstructions do
      [!?ioInstructionGenerationList appendInstructionAsFreeString !str !{}]
    end
    outResult = .llvmVariable {!name: resultLLVMName}
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @implicitConversionToBooleanMap generateConvertToBooleanCode
  ?let @objectIR inReceiverOperand
  ?let @location inErrorLocation
  ?!@semanticTemporariesStruct ioTemporaries
  ?!@instructionListIR ioInstructionGenerationList
  ?!alloca:@allocaList ioAllocaList
  !@implicitBooleanConversionResult outResult
{
  let key = @lstring.new {![inReceiverOperand omnibusTypeDescriptionName] !inErrorLocation}
  [self searchKey !key ?let converter]
  [converter generateConvertToBooleanCode
    !inReceiverOperand
    !inErrorLocation
    !?ioTemporaries
    !?ioInstructionGenerationList
    !?alloca: ioAllocaList
    ?outResult
  ]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
