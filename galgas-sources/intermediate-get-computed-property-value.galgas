
setter @instructionListIR appendGetComputedPropertyValue
  ?!@semanticTemporariesStruct ioTemporaries
  ?let @objectIR inReceiverIR
  ?let @string inLLVMGetterName
  ?let @omnibusType inResultType
  !@objectIR outResultValueIR
{
 [!?ioTemporaries newTempLLVMVar ?let result_llvmName]
 outResultValueIR = .llvmNamedValue {!type:inResultType !llvmName:result_llvmName}
  self += !@getComputedPropertyValueIR.init {!inReceiverIR !inLLVMGetterName !outResultValueIR}
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

class @getComputedPropertyValueIR : @abstractInstructionIR {
  public var @objectIR mReceiverIR
  public var @string mLLVMGetterName
  public var @objectIR mResultValueIR
}

#·······················································································································

override method @getComputedPropertyValueIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  ioLLVMcode += "  " + [self.mResultValueIR llvmName] + " = call " + [[self.mResultValueIR type] llvmTypeName]
  ioLLVMcode += " @" + [self.mLLVMGetterName assemblerRepresentation] 
  ioLLVMcode += " (" + [[self.mReceiverIR type] llvmTypeName] + "* " + [self.mReceiverIR llvmName] + ")\n"
}

#·······················································································································

override method @getComputedPropertyValueIR enterAccessibleEntities
  ?!@stringset ioInvokedRoutineSet
  ?!@uint unused ioMaxBranchOfOnInstructions
{
  ioInvokedRoutineSet += !self.mLLVMGetterName
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
