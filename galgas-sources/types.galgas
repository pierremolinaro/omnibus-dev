#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#                                                                                                                       
#    P L M    T Y P E                                                                      
#                                                                                                                       
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

boolset @PLMTypeAttributes {
  instanciable, equatable, copyable, comparable
}

#·······················································································································

struct @PLMType {
  @propertyMap propertyMap %selector # Properties and methods
  @routineMapCTXT routineMap %selector
  @classConstantMap classConstantMap %selector # $type.cst, as enumeration values
  @constructorMap constructorMap %selector # $type (!... !...)
  @guardMapCTXT guardMap %selector
  @subscript subscript %selector
  @stringset assignmentSources %selector
  @PLMTypeAttributes attributes %selector # as instanciable, copyable, ...
  @string plmTypeDescriptionName %selector # used for type checking and type display
  @typeKind kind %selector
  @string llvmBaseTypeName %selector # used for type implementation
}

#·······················································································································

enum @subscript {
  case noSubscript
  case staticSubscript (@PLMType elementType @bigint size)
  case literalString
  
  default noSubscript
}

#·······················································································································

enum @typeKind {
  case void # No actual type, used for no annotation type, ...
  case boolean
  case literalString
  case enumeration (@uint bitCount)
  case structure (@propertyList propertyList)
  case syncTool
  case integer (@bigint min @bigint max @bool unsigned @uint bitCount)
  case staticInteger
  case opaque (@bigint bitCount)
  case staticArrayType (@PLMType elementType @bigint size)
  case dynamicArrayType (@PLMType elementType)
  case function (@mode mode
                 @routineTypedSignature signature
                 @unifiedTypeMap-proxy returnTypeProxy)
  default void
}

#·······················································································································

getter @PLMType llvmTypeName -> @string {
  switch self.kind
  case void :
    result = "void"
  case boolean :
    result = "i1"
  case literalString :
    result = "i8*"
  case enumeration (@uint bitCount) :
    result = "i" + bitCount
  case structure (*) :
    result = "%" + [[self llvmBaseTypeName] assemblerRepresentation]
  case syncTool :
    result = "%" + [[self llvmBaseTypeName] assemblerRepresentation]
  case integer (* * * @uint bitCount) :
    result = "i" + bitCount
  case staticInteger :
    result = "i32"
  case opaque (@bigint bitCount) :
    result = "i" + bitCount
  case staticArrayType (* *) :
    result = "%" + [[self llvmBaseTypeName] assemblerRepresentation]
  case dynamicArrayType (*) :
    result = "%ptrtype" # "i" + ptrSize
  case function (* signature returnTypeProxy) :
    if returnTypeProxy == .null then
      result = "void"
    else
      result = [[returnTypeProxy type] llvmTypeName]
    end
    result += "("
    for () in signature 
    do
      result += [[mTypeProxy type] llvmTypeName]
      switch mFormalArgumentPassingMode
      case inputOutput, output : result += "*"
      case input :
      end
    between result += ", "
    end
    result += ")*"
  end
}

#·······················································································································

func staticIntegerType -> @PLMType {
  result = .new {
    !propertyMap:{}
    !routineMap: {}
    !classConstantMap:{}
    !constructorMap:{}
    !guardMap:{}
    !subscript:.noSubscript
    !assignmentSources: {!staticIntegerTypeName ()}
    !attributes:.equatable | .copyable
    !plmTypeDescriptionName:staticIntegerTypeName ()
    !kind:.staticInteger
    !llvmBaseTypeName:staticIntegerTypeName ()
  }
}

#·······················································································································

func literalStringType -> @PLMType {
  result = .new {
    !propertyMap:{}
    !routineMap: {}
    !classConstantMap:{}
    !constructorMap:{}
    !guardMap:{}
    !subscript:.literalString
    !assignmentSources: {!staticStringTypeName ()}
    !attributes:.instanciable | .equatable | .copyable
    !plmTypeDescriptionName:staticStringTypeName () 
    !kind:.literalString
    !llvmBaseTypeName:staticStringTypeName ()
  }
}

#·······················································································································

func voidType -> @PLMType {
  result = .new {
    !propertyMap:{}
    !routineMap: {}
    !classConstantMap:{}
    !constructorMap:{}
    !guardMap:{}
    !subscript:.noSubscript
    !assignmentSources: {}
    !attributes:.none
    !plmTypeDescriptionName:""
    !kind:.void
    !llvmBaseTypeName:""
  }
}

#·······················································································································

getter @PLMType equatable -> @bool {
  result = [attributes equatable]
}

#·······················································································································

getter @unifiedTypeMap-proxy instanciable -> @bool {
  result = [[self type] instanciable]
}

#·······················································································································

getter @PLMType instanciable -> @bool {
  result = [attributes instanciable]
}

#·······················································································································

getter @unifiedTypeMap-proxy copyable -> @bool {
  result = [[self type] copyable]
}

#·······················································································································

getter @PLMType copyable -> @bool {
  result = [attributes copyable]
}

#·······················································································································

method @PLMType generateRetain
  ?let @string inPLMName
  ?!@string ioLLVMcode
  ?!@generationAdds ioGenerationAdds
{
  let llvmTypeName = [self llvmTypeName]
  let LLVMVariable =  llvmNameForLocalVariable (!inPLMName)
  if [self.kind isDynamicArrayType] then
    let tempLLVMVariable = "%temp." + [inPLMName assemblerRepresentation] + ".loaded." + ioGenerationAdds.mUniqueIndex
    ioGenerationAdds.mUniqueIndex ++
    ioLLVMcode += "  " + tempLLVMVariable + " = load " + llvmTypeName + ", " + llvmTypeName + "* " + LLVMVariable + "\n"
    ioLLVMcode += "  call void @arc.retain (" + llvmTypeName + " " + tempLLVMVariable + ")\n"
  else
    let arcAssignmentList = [self arcList !{}]
    for (propertyName propertyType indexPath) in arcAssignmentList do 
      let llvmVarName = "%arc.retain." + ioGenerationAdds.mUniqueIndex
      ioGenerationAdds.mUniqueIndex ++
      ioLLVMcode += "  " + llvmVarName + " = getelementptr inbounds " + llvmTypeName + ", " + llvmTypeName + "* " + LLVMVariable + ", i32 0"
      for (idx) in indexPath do
        ioLLVMcode += ", i32 " + idx
      end
      ioLLVMcode += " ; " + propertyName + "\n"
      let propertyTypeLLVMName = [propertyType llvmTypeName]
      let llvmLoadedVarName = llvmVarName + ".loaded"
      ioLLVMcode += "  " + llvmLoadedVarName + " = load " + propertyTypeLLVMName + ", " + propertyTypeLLVMName + "* " + llvmVarName + "\n" 
      ioLLVMcode += "  call void @arc.retain (" + propertyTypeLLVMName + " " + llvmLoadedVarName + ")\n" 
    end
  end
}

#·······················································································································

method @PLMType generateInsulate
  ?let @string inPLMName
  ?!@string ioLLVMcode
  ?!@generationAdds ioGenerationAdds
{
  let llvmTypeName = [self llvmTypeName]
  let LLVMVariable =  llvmNameForLocalVariable (!inPLMName)
  if [self.kind isDynamicArrayType] then
    let loadedVar = "%temp." + [inPLMName assemblerRepresentation] + ".loaded." + ioGenerationAdds.mUniqueIndex   
    let insulatedVar = "%temp." + [inPLMName assemblerRepresentation] + ".insulated." + ioGenerationAdds.mUniqueIndex   
    ioGenerationAdds.mUniqueIndex ++
    ioLLVMcode += "  " + loadedVar + " = load " + llvmTypeName + ", " + llvmTypeName + "* " + LLVMVariable + "\n"
    ioLLVMcode += "  " + insulatedVar + " = call " + llvmTypeName + " @arc.insulate (" + llvmTypeName + " " + loadedVar + ")\n"
    ioLLVMcode += "  store " + llvmTypeName + " " + insulatedVar + ", " + llvmTypeName + "* " + LLVMVariable + "\n"
  else
    let arcAssignmentList = [self arcList !{}]
    for (propertyName propertyType indexPath) in arcAssignmentList do 
      let llvmVarName = "%arc.release." + ioGenerationAdds.mUniqueIndex
      ioGenerationAdds.mUniqueIndex ++
      ioLLVMcode += "  " + llvmVarName + " = getelementptr inbounds " + llvmTypeName + ", " + llvmTypeName + "* " + LLVMVariable + ", i32 0"
      for (idx) in indexPath do
        ioLLVMcode += ", i32 " + idx
      end
      ioLLVMcode += " ; " + propertyName + "\n"
      let propertyTypeLLVMName = [propertyType llvmTypeName]
      let loadedVar = llvmVarName + ".loaded." + ioGenerationAdds.mUniqueIndex 
      let insulatedVar = llvmVarName + ".insulated." + ioGenerationAdds.mUniqueIndex 
      ioGenerationAdds.mUniqueIndex ++
      ioLLVMcode += "  " + loadedVar + " = load " + propertyTypeLLVMName + ", " + propertyTypeLLVMName + "* " + llvmVarName + "\n" 
      ioLLVMcode += "  " + insulatedVar + " = call " + propertyTypeLLVMName + " @arc.insulate (" + propertyTypeLLVMName + " " + loadedVar + ")\n" 
      ioLLVMcode += "  store " + propertyTypeLLVMName + " " + insulatedVar + ", " + propertyTypeLLVMName + "* " + llvmVarName + "\n"
    end
  end
}

#·······················································································································

method @PLMType generateRelease
  ?let @string inPLMName
  ?!@string ioLLVMcode
  ?!@generationAdds ioGenerationAdds
{
  let llvmTypeName = [self llvmTypeName]
  let LLVMVariable =  llvmNameForLocalVariable (!inPLMName)
  if [self.kind isDynamicArrayType] then
    let tempLLVMVariable = "%temp." + [inPLMName assemblerRepresentation] + ".loaded." + ioGenerationAdds.mUniqueIndex   
    ioGenerationAdds.mUniqueIndex ++
    ioLLVMcode += "  " + tempLLVMVariable + " = load " + llvmTypeName + ", " + llvmTypeName + "* " + LLVMVariable + "\n"
    ioLLVMcode += "  call void @arc.release (" + llvmTypeName + " " + tempLLVMVariable + ") ; §\n"
  else
    let arcAssignmentList = [self arcList !{}]
    for (propertyName propertyType indexPath) in arcAssignmentList do 
      let llvmVarName = "%arc.release." + ioGenerationAdds.mUniqueIndex
      ioGenerationAdds.mUniqueIndex ++
      ioLLVMcode += "  " + llvmVarName + " = getelementptr inbounds " + llvmTypeName + ", " + llvmTypeName + "* " + LLVMVariable + ", i32 0"
      for (idx) in indexPath do
        ioLLVMcode += ", i32 " + idx
      end
      ioLLVMcode += " ; " + propertyName + "\n"
      let propertyTypeLLVMName = [propertyType llvmTypeName]
      let llvmLoadedVarName = llvmVarName + ".loaded"
      ioLLVMcode += "  " + llvmLoadedVarName + " = load " + propertyTypeLLVMName + ", " + propertyTypeLLVMName + "* " + llvmVarName + "\n" 
      ioLLVMcode += "  call void @arc.release (" + propertyTypeLLVMName + " " + llvmLoadedVarName + ")\n" 
    end
  end
}

#·······················································································································

getter @PLMType descriptionForHTMLFile -> @string {
  switch self.kind
  case void :
    result = "Void"
  case boolean :
    result = "Boolean"
  case literalString :
    result = "Literal String"
  case enumeration (*) :
    result = "Enumeration"
  case structure (*) :
    result = "Structure"
  case syncTool :
    result = "Synchronization tool"
  case integer (* * @bool unsigned @uint bitCount) :
    result = "Integer (" + bitCount + " bit" + if bitCount > 1 then "s" else "" end
           + ", " + if unsigned then "unsigned" else "signed" end + ")"
  case staticInteger :
    result = "Static Integer"
  case opaque (@bigint bitCount) :
    result = "Opaque (" + bitCount + " bits)"
  case staticArrayType (* *) :
    result = "Static array"
  case dynamicArrayType (*) :
    result = "Dynamic array"
  case function (3*) :
    result = "Function"
  end
}

#·······················································································································

getter @PLMType defaultAlignment -> @uint{
  switch self.kind
  case void :
    result = 0
  case boolean :
    result = 1
  case literalString :
    result = 4
  case enumeration (bitCount) :
    if bitCount <= 8 then
      result = 1
    elsif bitCount <= 16 then
      result = 2
    else
      result = 4
    end
  case structure (*) :
    result = 4
  case syncTool :
    result = 4
  case integer (3* @uint bitCount) :
    if bitCount <= 8 then
      result = 1
    elsif bitCount <= 16 then
      result = 2
    else
      result = 4
    end
  case staticInteger :
    result = 0
  case opaque (@bigint bitCount) :
    if bitCount <= 8 then
      result = 1
    elsif bitCount <= 16 then
      result = 2
    else
      result = 4
    end
  case staticArrayType (elementType *) :
    result = [elementType defaultAlignment]
  case dynamicArrayType (*) :
    result = 4
  case function (3*) :
    result = 4
  end
}

#·······················································································································

list @arcAssignmentList {
  @string mPropertyName
  @PLMType mPropertyType
  @uintlist mPropertyIndexPath
}

#·······················································································································

getter @PLMType arcList ?let @uintlist inPropertyIndexPath -> @arcAssignmentList {
  result = {}
  if [kind isDynamicArrayType] then
    result += !"" !self !inPropertyIndexPath
  elsif [kind isStructure] then
    [kind structure ?propertyList:let @propertyList propertyList]
    for (propertyName propertyType) in propertyList do (idx)
      let indexPath = inPropertyIndexPath + {!idx}
      result += [propertyType arcList !indexPath]
    end
  end
}

#·······················································································································

getter @PLMType deinitNeeded -> @bool {
  result = [kind isDynamicArrayType]
  if [kind isStructure] then
    [kind structure ?propertyList:let @propertyList propertyList]
    for (propertyName propertyType) in propertyList while not result do
      result = [propertyType deinitNeeded]
    end
  end
}

#·······················································································································

shared map @unifiedTypeMap %selectors {
  @PLMType type
#  @uint index # This index allows to sort type for ensuring no forward LLVM type reference on generation
  @bool generate # Boolean value for ordering LLVM code generation for this type

  insert insertType error message "the '%K' type is already declared in %L"
  search searchKey error message "there is no '%K' type"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  BUILD TYPE MAP HTML FILE                                                                                                      
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc buildTypeMapHTMLFile
  ?let @unifiedTypeMap inTypeMap
  ?let @routineMapCTXT inRoutineMap
  ?sourceFile:let @lstring inSourceFile
{
#------------------------------------ Build an HTML file that contains all types
  let @string typeDumpFilePath = inSourceFile.string + ".types.html"
  if [option plm_options.emitTypeMap value] then
    @stringset firstLetterSet = {}
    for () in inTypeMap do
      firstLetterSet += ![[lkey.string characterAtIndex!0] string]
    end
    @string tableOfTypeString = ""
    @char currentFirstLetter = ' '
    for () type_ in inTypeMap do
      if currentFirstLetter != [type_lkey.string characterAtIndex !0] then
        currentFirstLetter = [type_lkey.string characterAtIndex !0]
        tableOfTypeString += "<br><a name=\"" + [currentFirstLetter uint] + "\"><b>" + currentFirstLetter + "</b></a><br>"
      end
      tableOfTypeString += linkForHTMLTypeMap (!type_lkey.string) + "<br>"
    end
    let @string typeDumpString = [filewrapper typeDumpGenerationTemplate.dump
      ![inSourceFile lastPathComponent]
      !inTypeMap
      !firstLetterSet
      !tableOfTypeString
      !inRoutineMap
    ]
    [typeDumpString writeToFileWhenDifferentContents !typeDumpFilePath ?*]
  else
    [@string deleteFileIfExists !typeDumpFilePath]
  end
}

#·······················································································································

filewrapper typeDumpGenerationTemplate in "../generation-templates/" {
}{
}{
  template dump "typeDump.html.galgasTemplate"
    ?@string PROJECT_NAME
    ?@unifiedTypeMap GLOBAL_TYPE_MAP
    ?@stringset FIRST_LETTER_SET
    ?@string TABLE_OF_TYPES_STRING
    ?@routineMapCTXT STANDALONE_ROUTINE_MAP
}

#·······················································································································

func linkForHTMLTypeMap ?let @string inTypeName -> @string {
  result = "<a class=\"header_link\" href=\"#" + inTypeName + "\">" + inTypeName + "</a>"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  LLVM TYPE GENERATION                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc declareLLVMTypes
  ?let @unifiedTypeMap inTypeMap
  ?let @userLLVMTypeDefinitionListIR inUserLLVMTypeDefinitionListIR
  ?let @uint inPointerSize
  ?!@generationAdds ioGenerationAdds
  ?!@string ioLLVMcode
{
#--- Generate LLVM type definition (using forward type is allowed for pointers)
  ioLLVMcode += llvmTitleComment (!"User Defined Types")
  ioLLVMcode += "%ptrtype = type i8* ; Generic pointer type for dynamic objects\n"
  for (type) in inUserLLVMTypeDefinitionListIR do
    [type generateLLVMType !?ioLLVMcode]
  end
  ioLLVMcode += "\n"
#--- Generate LLVM type specific code
  for (* type generate) in inTypeMap do
    if generate then
      switch type.kind
      case void :
      case boolean :
      case enumeration (*) :
      case integer (4*) :
      case structure (*) :
        generateLLVMcodeForStructure (
          !?ioLLVMcode
          !?ioGenerationAdds
          !type
          !inPointerSize
        )
      case syncTool :
      case literalString : 
      case staticInteger : 
      case opaque (*) :
      case staticArrayType (* arraySize) :
        generateLLVMCodeForStaticArrayType (
          !?ioLLVMcode
          !?ioGenerationAdds
          !type
          !arraySize
        )
      case dynamicArrayType (*) :
      case function (3*) :
      end
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! USER LLVM TYPE DEFINITION
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

abstract class @userLLVMTypeDefinitionIR {
  @string mLLVMDefinedTypeName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

abstract method @userLLVMTypeDefinitionIR generateLLVMType ?!@string ioLLVMcode

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @userLLVMTypeDefinitionListIR {
  @userLLVMTypeDefinitionIR mType
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
