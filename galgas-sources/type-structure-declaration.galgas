#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @structureDeclarationAST : @abstractDeclarationAST {
  public var @lstring mReceiverTypeName
  public var @lstring mOmnibusTypeSpecificName
  public var @lstring mLLVMBaseTypeName
  public var @lstringlist mAttributeListAST
  public var @bool mGenerateAssignmentRoutine
  public var @structurePropertyListAST mStructurePropertyListAST
  public var @bool mMayImplementDeinit
}

#·······················································································································

list @propertyAttributeList {
  public var @lstring mAttributeName
  public var @lbigint mAttributeValue
}

#·······················································································································

enum @propertyKindAST {
  case initializedStoredProperty (@expressionAST initExpression) # declared by "var = ···"
  case uninitializedStoredProperty # declared by "var", no initialization
  case initializedConstantProperty (@expressionAST initExpression) # declared by "let"
  case uninitializedConstantProperty # declared by "let", no initialization
  case readOnlyComputedProperty (@instructionListAST instructionList @location endOfInstructionList)
  case writeComputedProperty (@instructionListAST instructionList @location endOfInstructionList)
}

#·······················································································································

list @structurePropertyListAST {
  public var @lstring mPropertyName
  public var @propertyAttributeList mPropertyAttributeList
  public var @propertyVisibility mVisibility
  public var @lstring mPropertyTypeName
  public var @propertyKindAST mPropertyKind
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <private_or_public_struct_property_declaration>
    ?!@ast ioAST
    ?!@structurePropertyListAST ioPropertyListAST
  {
    var @propertyVisibility visibility
    select
      visibility = .isPrivate
    or
      $public$
      visibility = .isPublic
    end
    <struct_property_declaration> !?ioAST !visibility !?ioPropertyListAST
  }
  
  #·····················································································································

  rule <private_struct_property_declaration>
    ?!@ast ioAST
    ?!@structurePropertyListAST ioPropertyListAST
  {
    <struct_property_declaration> !?ioAST !.isPrivate !?ioPropertyListAST
  }

  #·····················································································································

  rule <struct_property_declaration>
    ?!@ast ioAST
    ?let @propertyVisibility inVisibility
    ?!@structurePropertyListAST ioPropertyListAST
  {
    $let$
    $identifier$ ?let propertyName
    var @lstring typeName
    select
      <type_definition> !?ioAST ?typeName
    or
      typeName = ["" nowhere]
    end
    select
      $=$
      <expression> !?ioAST ?let initExpression
      ioPropertyListAST +=
        !propertyName
        !{}
        !inVisibility
        !typeName 
        !.initializedConstantProperty{!initExpression:initExpression}
    or
      ioPropertyListAST +=
        !propertyName
        !{}
        !inVisibility
        !typeName 
        !.uninitializedConstantProperty
    end
  }

  #·····················································································································

  rule <struct_property_declaration>
    ?!@ast ioAST
    ?let @propertyVisibility inVisibility
    ?!@structurePropertyListAST ioPropertyListAST
  {
    $var$
    $identifier$ ?let propertyName
    var @propertyAttributeList propertyAttributeList = {}
    repeat
    while
      $@attribute$ ?let attribute
      $integer$ ?let value
      propertyAttributeList += !attribute !value
    end
    select
      <type_definition> !?ioAST  ?let typeName
      select
        ioPropertyListAST += !propertyName !{} !inVisibility !typeName !.uninitializedStoredProperty
      or
       <propertyGetterSetter>
         !?ioAST
         !inVisibility
         !propertyName
         !propertyAttributeList
         !typeName 
         !?ioPropertyListAST
      or
        $=$
        <expression> !?ioAST ?let initExpression
        ioPropertyListAST +=
          !propertyName
          !propertyAttributeList
          !inVisibility
          !typeName 
          !.initializedStoredProperty{!initExpression:initExpression}
      end
    or
      $=$
      <expression> !?ioAST ?let initExpression
      ioPropertyListAST +=
        !propertyName
        !propertyAttributeList
        !inVisibility
        !["" nowhere]
        !.initializedStoredProperty{!initExpression:initExpression}
    end
  }

  #·····················································································································

  rule <propertyGetterSetter>
    ?!@ast ioAST
    ?let @propertyVisibility inVisibility
    ?let @lstring inPropertyName
    ?let @propertyAttributeList inPropertyAttributeList
    ?let @lstring inTypeName
    ?!@structurePropertyListAST ioPropertyListAST
  {
    ${$  
    select
      var getterDefined = false
      var setterDefined = false
      repeat
        $@attribute$ ?let getterOrSetter
        ${$
        <instructionList> !?ioAST ?let instructionList
        let endOfInstructionList = @location.here
        $}$
        if getterOrSetter.string == "get" then
          ioPropertyListAST +=
            !inPropertyName
            !inPropertyAttributeList
            !inVisibility
            !inTypeName 
            !.readOnlyComputedProperty{
              !instructionList: instructionList
              !endOfInstructionList: endOfInstructionList
            }
          if getterDefined then
            error getterOrSetter : "duplicate getter definition"
          end
          getterDefined = true
        elsif getterOrSetter.string == "set" then
          ioPropertyListAST +=
            !inPropertyName
            !inPropertyAttributeList
            !inVisibility
            !inTypeName 
            !.writeComputedProperty{
              !instructionList: instructionList
              !endOfInstructionList: endOfInstructionList
            }
          if setterDefined then
            error getterOrSetter : "duplicate setter definition"
          end
          setterDefined = true
        else
          error getterOrSetter : "invalid attribute" fixit { remove replace "@set" replace "@get" }
        end
      while
      end
      if setterDefined && not getterDefined then
        error .here : "a getter should be defined"
      elsif not setterDefined && not getterDefined then
        error .here : "a getter should be defined, and optionaly a setter"
      end
    or # getter only
      <instructionList> !?ioAST ?let readInstructionList
      let endOfReadInstructionList = @location.here
      ioPropertyListAST +=
        !inPropertyName
        !inPropertyAttributeList
        !inVisibility
        !inTypeName 
        !.readOnlyComputedProperty{
          !instructionList: readInstructionList
          !endOfInstructionList: endOfReadInstructionList
        }
    end
    $}$
  }
  
  #·····················································································································

  rule <declaration> ?!@ast ioAST {
    $struct$
    $\$type$ ?let structureName
    var @lstringlist attributeList = {}
    repeat
    while
      $@attribute$ ?let attribute
      attributeList += !attribute
    end 
    ${$
    var @structurePropertyListAST fieldList = {}
    repeat
    while
      <private_or_public_struct_property_declaration> !?ioAST !?fieldList
    while
      <function_declaration> !?ioAST !receiverTypeName: structureName
    end
    $}$
    ioAST.mDeclarationListAST += !@structureDeclarationAST.new {
      !structureName
      !structureName
      !structureName
      !attributeList
      !true # Generate assignment routine
      !fieldList
      !true
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  PRECEDENCE GRAPH                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @structureDeclarationAST enterInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
#--- Add structure type name as node
  [!?ioGraph addNode !self.mReceiverTypeName !self]
#--- Add property types
  for (3* fieldTypeName propertyKind) in self.mStructurePropertyListAST do
    if fieldTypeName.string != "" then
      [!?ioGraph addEdge !self.mReceiverTypeName !fieldTypeName]
    end
    switch propertyKind
    case uninitializedStoredProperty, uninitializedConstantProperty :
    case initializedStoredProperty (initExpression) :
      [initExpression addDependenceEdgeForStaticExpression !self.mReceiverTypeName !?ioGraph]
    case initializedConstantProperty (initExpression) :
      [initExpression addDependenceEdgeForStaticExpression !self.mReceiverTypeName !?ioGraph]
    case readOnlyComputedProperty (instructionList *) :
      [instructionList noteInstructionListTypesInPrecedenceGraph !?ioGraph]
    case writeComputedProperty (writeInstructionList *) :
      [writeInstructionList noteInstructionListTypesInPrecedenceGraph !?ioGraph]
    end
  end
}

#·······················································································································

override getter @structureDeclarationAST keyRepresentationForErrorSignaling -> @string outRepresentation {
  outRepresentation = self.mReceiverTypeName 
}

#·······················································································································

override getter @structureDeclarationAST locationForErrorSignaling -> @location outLocation {
  outLocation = self.mReceiverTypeName.location
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  ENTER IN CONTEXT                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @propertyList {
  public var @string mPropertyName
  public var @omnibusType mType
}

#·······················································································································

sortedlist @sortedOperandIRList {
  public var @objectIR mOperand
  public var @uint mIndex
}{
  mIndex <
}

#·······················································································································

method @structurePropertyListAST-element enterPropertyInContext
  ?!context:@semanticContext ioContext
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?receiverTypeName:let @lstring inReceiverTypeName
  ?omnibusTypeName: let @lstring inOmnibusTypeSpecificName
  ?receiverLLVMBaseTypeName:let @string inReceiverLLVMBaseTypeName
  ?!@decoratedRegularRoutineList ioDecoratedRoutineList
  ?!@propertyList ioPropertyList
  ?!@sortedOperandIRList sortedOperandIRList
  ?!@constructorSignature constructorSignature
  ?!@string ioConstructorKey
  ?!@bool canBeCopied
{
#---------- Attributes
  var @lbigint explicitAlignment = .new {!0G !.nowhere}
  var alignmentDefined = false
  for (attribute align) in self.mPropertyAttributeList do
    if attribute.string != "align" then
      error attribute : "only the @align attribute is allowed here"
    elsif alignmentDefined then
      error attribute : "@align attribute already defined"
    else
    #--- Check alignment is a power of 2
      if align.bigint != (1G << [align.bigint - 1G bitCountForUnsignedRepresentation]) then
        error align : "alignment should be a power of 2"
      else
        explicitAlignment = align
        alignmentDefined = true
      end
    end
  end
#---------- Property type
  let @omnibusType optionalPropertyType = if self.mPropertyTypeName.string == ""
    then voidType ()
    else [@unifiedTypeMap-entry.searchKey {!ioContext.mTypeMap !self.mPropertyTypeName} type]
  end
#---------- Init expression
  switch self.mPropertyKind
  case initializedConstantProperty (initExpression) :
    computeStaticExpression (
      !?context: ioContext
      !?staticEntityMap:ioStaticEntityMap
      !expression: initExpression
      !errorLocation: self.mPropertyName.location
      !optionalContextualTypeName: self.mPropertyTypeName
      ?result:let @objectIR expressionIR
    )
    var @omnibusType actualPropertyType
    if optionalPropertyType.kind != .void then
      actualPropertyType = optionalPropertyType
    else
      actualPropertyType = [expressionIR type]
    end
    if not [actualPropertyType instanciable] then
      error self.mPropertyName : actualPropertyType.omnibusTypeDescriptionName + " type is not instanciable"
    end
    if not [actualPropertyType copyable] then
      canBeCopied = false
    end
    [!?ioContext insertGetter
      !omnibusTypeName: inOmnibusTypeSpecificName
      !self.mPropertyName
      !self.mVisibility
      !.constantProperty {!value:[expressionIR withType !actualPropertyType]}
    ]
  case initializedStoredProperty (initExpression) :
    computeStaticExpression (
      !?context: ioContext
      !?staticEntityMap:ioStaticEntityMap
      !expression: initExpression
      !errorLocation: self.mPropertyName.location
      !optionalContextualTypeName: self.mPropertyTypeName
      ?result:let @objectIR expressionIR
    )
    var @omnibusType actualPropertyType
    if optionalPropertyType.kind != .void then
      actualPropertyType = optionalPropertyType
    else
      actualPropertyType = [expressionIR type]
    end
    if [actualPropertyType.kind isCompileTimeBool] then
      actualPropertyType = boolType ()
    elsif [actualPropertyType.kind isCompileTimeInteger] then
      error self.mPropertyName
      : "$" + actualPropertyType.omnibusTypeDescriptionName + " type is a compile time type, and is not instanciable"
    elsif not [actualPropertyType instanciable] then
      error self.mPropertyName : actualPropertyType.omnibusTypeDescriptionName + " type is not instanciable"
    end
    if not [actualPropertyType copyable] then
      canBeCopied = false
    end
    let propertyIndex = [ioPropertyList length]
    ioPropertyList += !self.mPropertyName.string !actualPropertyType
    [!?ioContext insertGetter
      !omnibusTypeName: inOmnibusTypeSpecificName
      !self.mPropertyName
      !self.mVisibility
      !.storedProperty{!type:actualPropertyType !index:propertyIndex}
    ]
    [!?ioContext insertPropertySetter
      !inReceiverTypeName
      !self.mPropertyName
      !self.mVisibility
      !.storedProperty{!type:actualPropertyType !index:propertyIndex}
    ]
    sortedOperandIRList += ![expressionIR withType !actualPropertyType] !propertyIndex
  case uninitializedStoredProperty :
    if not [optionalPropertyType instanciable] then
      error self.mPropertyName : optionalPropertyType.omnibusTypeDescriptionName + " type is not instanciable"
    end
    let propertyIndex = [ioPropertyList length]
    ioPropertyList += !self.mPropertyName.string !optionalPropertyType
    [!?ioContext insertGetter
      !omnibusTypeName: inOmnibusTypeSpecificName
      !self.mPropertyName
      !self.mVisibility
      !.storedProperty{!type: optionalPropertyType!index:propertyIndex}
    ]
    [!?ioContext insertPropertySetter
      !inReceiverTypeName
      !self.mPropertyName
      !self.mVisibility
      !.storedProperty{!type:optionalPropertyType !index:propertyIndex}
    ]
    constructorSignature += !self.mPropertyName.string !optionalPropertyType !propertyIndex
    ioConstructorKey += "!" + self.mPropertyName
  case uninitializedConstantProperty :
    if not [optionalPropertyType instanciable] then
      error self.mPropertyName : optionalPropertyType.omnibusTypeDescriptionName + " type is not instanciable"
    end
    let propertyIndex = [ioPropertyList length]
    ioPropertyList += !self.mPropertyName.string !optionalPropertyType
    [!?ioContext insertGetter
      !omnibusTypeName: inOmnibusTypeSpecificName
      !self.mPropertyName
      !self.mVisibility
      !.storedProperty{!type: optionalPropertyType!index:propertyIndex}
    ]
    constructorSignature += !self.mPropertyName.string !optionalPropertyType !propertyIndex
    ioConstructorKey += "!" + self.mPropertyName
  case readOnlyComputedProperty (instructionList endOfInstructionList) :
    if not [optionalPropertyType instanciable] then
      error self.mPropertyName : optionalPropertyType.omnibusTypeDescriptionName + " type is not instanciable"
    end
    let llvmRoutineName = getterLLVMName (!inReceiverLLVMBaseTypeName !self.mPropertyName)
    let modeDictionary = [@mode.anyMode routineLLVMDictionaryForFunction !llvmRoutineName]
    [!?ioContext insertGetter
      !omnibusTypeName: inOmnibusTypeSpecificName
      !self.mPropertyName
      !self.mVisibility
      !.computedProperty{
        !typeProxy: @unifiedTypeMap-entry.searchKey {!ioContext.mTypeMap !self.mPropertyTypeName}
        !modeDictionary:modeDictionary
      }
    ]
    let functionLLVMName = @lstring.new {
      !llvmRoutineName
      !self.mPropertyName.location
    }
    ioDecoratedRoutineList +=
      !receiverTypeName: inReceiverTypeName
      !mode:.anyMode
      !isRequired: false
      !routineKind: .function
      !warnIfUnused: false
      !exportedRoutine: false
      !routineAttributes: .none
      !routineMangledLLVMName: functionLLVMName
      !formalArgumentList: {}
      !warningOnUnusedArgs: false 
      !instructionList
      !endOfInstructionList
      !returnTypeName: self.mPropertyTypeName
  case writeComputedProperty (writeInstructionList endOfWriteInstructionList) :
    if not [optionalPropertyType instanciable] then
      error self.mPropertyName : optionalPropertyType.omnibusTypeDescriptionName + " type is not instanciable"
    end
    let llvmGetterRoutineName = getterLLVMName (!inReceiverLLVMBaseTypeName !self.mPropertyName)
    let getterModeDictionary = [@mode.anyMode routineLLVMDictionaryForFunction !llvmGetterRoutineName]
    let llvmSetterRoutineName = setterLLVMName (!inReceiverLLVMBaseTypeName !self.mPropertyName)
    let setterModeDictionary = [@mode.anyMode routineLLVMDictionaryForFunction !llvmSetterRoutineName]
    [!?ioContext insertPropertySetter
      !inReceiverTypeName
      !self.mPropertyName
      !self.mVisibility
      !.computedProperty {
        !typeProxy: @unifiedTypeMap-entry.searchKey {!ioContext.mTypeMap !self.mPropertyTypeName}
        !getterModeDictionary: getterModeDictionary
        !setterModeDictionary: setterModeDictionary
      }
    ]
    let functionLLVMName = @lstring.new {
      !llvmSetterRoutineName
      !self.mPropertyName.location
    }
    let @routineFormalArgumentListAST formalArgumentList = {
      !.input
      !["" nowhere]
      !self.mPropertyTypeName
      !.new {!"newValue" !endOfWriteInstructionList}
    }
    ioDecoratedRoutineList +=
      !receiverTypeName: inReceiverTypeName
      !mode:.anyMode
      !isRequired: false
      !routineKind: .function
      !warnIfUnused: false
      !exportedRoutine: false
      !routineAttributes: .mutating
      !routineMangledLLVMName: functionLLVMName
      !formalArgumentList: formalArgumentList
      !warningOnUnusedArgs: false 
      !writeInstructionList
      !endOfWriteInstructionList
      !returnTypeName: ["" nowhere]
  end
}

#·······················································································································

override method @structureDeclarationAST enterInContext
  ?!context:@semanticContext ioContext
  ?!@declarationDecorationList unused ioDecoratedDeclarationList
  ?!@decoratedRegularRoutineList ioDecoratedRoutineList
  ?!@routineListIR ioRoutineListIR
  ?!staticlistValues:@staticListInitializationMap unused ioStaticListValueMap
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!registerUserAccess:@controlRegisterUserAccesMapAST unused ioControlRegisterUserAccesMapAST
  ?!userDefinedTypes:@userLLVMTypeDefinitionListIR ioUserLLVMTypeDefinitionListIR
{
#---------------------------------------- Properties
  var @propertyList propertyList = {}
#  @propertyGetterMap propertyGetterMap = {}
  var @sortedOperandIRList sortedOperandIRList = {}
  var @constructorSignature constructorSignature = {}
  var @string constructorKey = "("
  var canBeCopied = true
  for property in self.mStructurePropertyListAST do
    [property enterPropertyInContext
      !?context:ioContext
      !?staticEntityMap:ioStaticEntityMap
      !receiverTypeName:self.mReceiverTypeName
      !omnibusTypeName: self.mOmnibusTypeSpecificName
      !receiverLLVMBaseTypeName: self.mLLVMBaseTypeName
      !?ioDecoratedRoutineList
      !?propertyList
#      !?propertyGetterMap
      !?sortedOperandIRList
      !?constructorSignature
      !?constructorKey
      !?canBeCopied
    ]
  end
  constructorKey += ")"
#---------------------------------------- Constructor map
  var @constructorMap constructorMap = {}
  [!?constructorMap insertKey
    !.new {!constructorKey !self.mReceiverTypeName.location}
    !constructorSignature
    !.structure {!sortedOperandList:sortedOperandIRList}
  ]
  [!?ioContext.mTypeConstructorMap insertKey !self.mOmnibusTypeSpecificName !constructorMap]
#---------------------------------------- Attribute instanciable, copyable
  var typeAttributes = @omnibusTypeAttributes.instanciable
#---------------------------------------- Check structure is copyable
  for (s) in self.mAttributeListAST do
    if s.string == copyableAttribute () then
      if [typeAttributes copyable] then
        error s : "duplicated attribute"
      elsif not canBeCopied then
        error self.mReceiverTypeName : "this structure cannot be copied, one field is not copyable"
      end
    else
      error s : "only @copyable attribute is allowed here"
    end
  end
  if self.mGenerateAssignmentRoutine & canBeCopied then
    typeAttributes = typeAttributes | .copyable
  end
#---------------------------------------- Enter in type map
  let structureType = @omnibusType.new {
#    !propertyGetterMap: propertyGetterMap
    !subscript:.noSubscript
    !attributes:typeAttributes
    !omnibusTypeDescriptionName:self.mOmnibusTypeSpecificName
    !kind:.structure {!propertyList:propertyList}
    !llvmBaseTypeName:self.mLLVMBaseTypeName
  }
  [!?ioContext.mTypeMap insertType
    !lkey:self.mReceiverTypeName
    !type:structureType
  ]
#-------------------------------------------- GenerateLLVM type definition
  ioUserLLVMTypeDefinitionListIR += !@userLLVMSructureTypeDefinitionIR.new {
    ![structureType llvmTypeName]
    !propertyList
  }
#----------------- Assignment
#--- Enter assignment function from references
  let assignmentFunctionLLVMName = assignmentOperatorFuncName (!structureType !structureType)
  ioRoutineListIR += !@structureTypeAssignFunctionIR.new {
    ![assignmentFunctionLLVMName nowhere]
    !isRequired: false
    !warnsIfUnused: false
    !structureType
  }
#--- Enter assignment operator
  let key = assignmentOperatorKey (!structureType !.nowhere !structureType)
  let operatorUsage = @structureAssignmentOperatorUsage.new {!structureType}
  [!?ioContext.mAssignmentOperatorMap insertKey !key !operatorUsage]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! USER LLVM TYPE DEFINITION
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @userLLVMSructureTypeDefinitionIR : @userLLVMTypeDefinitionIR {
  public var @propertyList mPropertyList
}

#·······················································································································

override method @userLLVMSructureTypeDefinitionIR generateLLVMType ?!@string ioLLVMcode {
  ioLLVMcode += self.mLLVMDefinedTypeName + " = type {"
  for (* mType) in self.mPropertyList
    do ioLLVMcode += [mType llvmTypeName]
    between ioLLVMcode += ", "
  end
  ioLLVMcode += "}\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! CODE GENERATION: ASSIGNMENT

refclass @structureAssignmentOperatorUsage : @abstractAssignmentOperatorUsage {
  public var @omnibusType mStructureType
}

#·······················································································································

override method @structureAssignmentOperatorUsage generateCode
    ?!@semanticTemporariesStruct ioTemporaries
    ?!@instructionListIR ioInstructionListIR
    ?!@allocaList unused ioAllocaList
    ?let @omnibusType inTargetType
    ?let @string inTargetLLVMName
    ?let @objectIR inSourcePossibleReference
    ?let @location inErrorLocation
    ?initializedTarget: let @bool inTargetIsInitialized
{
  let structTypeName = [inTargetType llvmTypeName]
  let arcAssignmentList = [inTargetType arcList !{}]
  if not inTargetIsInitialized then
   for (propertyName propertyType indexPath) in arcAssignmentList do 
      let llvmPropertyPtrName = "%arc.init." + [inErrorLocation locationIndex]
      var s = "  " + llvmPropertyPtrName + " = getelementptr inbounds " + structTypeName + ", " + structTypeName + "* " + inTargetLLVMName + ", i32 0"
      for (idx) in indexPath do
        s += ", i32 " + idx
      end
      s += " ; " + propertyName + "\n"
      [!?ioInstructionListIR appendInstructionAsFreeString !s !{}]
      let propertyTypeLLVMName = [propertyType llvmTypeName]
      s = "store " + propertyTypeLLVMName + " null, " + propertyTypeLLVMName + "* " + llvmPropertyPtrName 
      [!?ioInstructionListIR appendInstructionAsFreeString !s !{}]
    end
  end
  switch inSourcePossibleReference
  case llvmArrayRepeatedDynamicValue (3*) :
    error inErrorLocation : "invalid structure assignment «llvmArrayRepeatedDynamicValue»"
  case llvmArrayDynamicValues (2*) :
    error inErrorLocation : "invalid structure assignment «llvmArrayDynamicValues»"
  case llvmArrayStaticValues (3*) :
    error inErrorLocation : "invalid structure assignment «llvmArrayStaticValues»"
  case llvmArrayRepeatedStaticValue (3* @uint idx) :
    let assignmentFunctionLLVMName =  assignmentOperatorFuncName (!inTargetType !inTargetType)
    let s = " call void  @" + [assignmentFunctionLLVMName assemblerRepresentation] 
        + " (" + [inTargetType llvmTypeName] + "* " + inTargetLLVMName 
          + ", " + [inTargetType llvmTypeName] + "* @gconst." + idx + ")"
    [!?ioInstructionListIR appendInstructionAsFreeString !s !{!assignmentFunctionLLVMName}]
  case reference (2*) :
    let assignmentFunctionLLVMName = assignmentOperatorFuncName (!inTargetType !inTargetType)
    let s = " call void  @" + [assignmentFunctionLLVMName assemblerRepresentation] 
        + " (" + [inTargetType llvmTypeName] + "* " + inTargetLLVMName 
          + ", " + [inTargetType llvmTypeName] + "* " + [inSourcePossibleReference llvmName] + ")"
    [!?ioInstructionListIR appendInstructionAsFreeString !s !{!assignmentFunctionLLVMName}]
  case llvmStructureValue (* sortedOperandValues) :
    let uniqueValue = ioTemporaries.mTemporaryIndex
    ioTemporaries.mTemporaryIndex ++
    for (operand idx) in sortedOperandValues do
      let ptr = "%ptr." + uniqueValue + "." + idx
      var s = ptr + " = getelementptr " + structTypeName + ", " + structTypeName + "* " + inTargetLLVMName + ", i32 0, i32 " + idx
      [!?ioInstructionListIR appendInstructionAsFreeString !s !{}]
      s = "store " + [operand llvmTypeName] + " " + [operand llvmName] + ", " + [operand llvmTypeName] + "* " + ptr
      [!?ioInstructionListIR appendInstructionAsFreeString !s !{}]    
    end
  case literalInteger (2*) :
    error inErrorLocation : "invalid structure assignment «literalInteger»"
  case literalString (2*) :
    error inErrorLocation : "invalid structure assignment «literalString»"
  case llvmNamedValue (2*) :
    error inErrorLocation : "invalid structure assignment «llvmArrayStaticValues»"
  case null (*) :
    error inErrorLocation : "invalid structure assignment «llvmValue»"
  case void:
    error inErrorLocation : "invalid structure assignment «void»"
  case zero (*) :
    error inErrorLocation : "invalid structure assignment «zero»"
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @structureTypeAssignFunctionIR : @abstractRoutineIR {
  public var @omnibusType mStructureType
}

#·······················································································································

override method @structureTypeAssignFunctionIR llvmCodeGeneration 
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds ioGenerationAdds
{
  let arcAssignmentList = [self.mStructureType arcList !{}]
  let structureTypeLLVMTypeName = [self.mStructureType llvmTypeName]
  ioLLVMcode += "define internal void @" + [self.mRoutineMangledName assemblerRepresentation]
  ioLLVMcode += " (" + structureTypeLLVMTypeName + " * %inTarget, "
  ioLLVMcode += structureTypeLLVMTypeName + "* %inSource) nounwind optsize {\n"
  ioLLVMcode += ";--- Handle ARC properties\n"
  for (propertyName propertyType indexPath) in arcAssignmentList do
    let propertyTypeLLVMName = [propertyType llvmTypeName]
    let llvmVarName = "%arc.retain." + [propertyName assemblerRepresentation] + ioGenerationAdds.mUniqueIndex
    ioGenerationAdds.mUniqueIndex ++
    ioLLVMcode += "  " + llvmVarName + " = getelementptr inbounds " + structureTypeLLVMTypeName + ", " + structureTypeLLVMTypeName + "* %inSource, i32 0"
    for (idx) in indexPath do
      ioLLVMcode += ", i32 " + idx
    end
    ioLLVMcode += " ; " + propertyName + "\n"
    let llvmLoadedVarName = llvmVarName + ".loaded"
    ioLLVMcode += "  " + llvmLoadedVarName + " = load " + propertyTypeLLVMName + ", " + propertyTypeLLVMName + "* " + llvmVarName + "\n" 
    ioLLVMcode += "  call void @arc.retain (" + propertyTypeLLVMName + " " + llvmLoadedVarName + ")\n" 
  end
  for (propertyName propertyType indexPath) in arcAssignmentList do
    let llvmVarName = "%arc.release." + [propertyName assemblerRepresentation] + ioGenerationAdds.mUniqueIndex
    ioGenerationAdds.mUniqueIndex ++
    ioLLVMcode += "  " + llvmVarName + " = getelementptr inbounds " + structureTypeLLVMTypeName + ", " + structureTypeLLVMTypeName + "* %inTarget, i32 0"
    for (idx) in indexPath do
      ioLLVMcode += ", i32 " + idx
    end
    ioLLVMcode += " ; " + propertyName + "\n"
    let propertyTypeLLVMName = [propertyType llvmTypeName]
    let llvmLoadedVarName = llvmVarName + ".loaded"
    ioLLVMcode += "  " + llvmLoadedVarName + " = load " + propertyTypeLLVMName + ", " + propertyTypeLLVMName + "* " + llvmVarName + "\n" 
    ioLLVMcode += "  call void @arc.release (" + propertyTypeLLVMName + " " + llvmLoadedVarName + ")\n" 
  end
  ioLLVMcode += ";--- Compute type size\n"
  ioLLVMcode += "  %sizeAsPtr = getelementptr inbounds " + structureTypeLLVMTypeName + ", " + structureTypeLLVMTypeName + "* null, i32 1\n"
  ioLLVMcode += "  %byteCount = ptrtoint " + structureTypeLLVMTypeName + "* %sizeAsPtr to i32\n"
  ioLLVMcode += "  %useLoadStore = icmp ule i32 %byteCount, 12\n"
  ioLLVMcode += "  br i1 %useLoadStore, label %loadStore, label %copyBytesOrCopyWords\n"
  ioLLVMcode += ";--- Copy\n"
  ioLLVMcode += "loadStore:\n"
  ioLLVMcode += "  %v = load " + structureTypeLLVMTypeName + ", " + structureTypeLLVMTypeName + "* %inSource\n" 
  ioLLVMcode += "  store " + structureTypeLLVMTypeName + " %v, " + structureTypeLLVMTypeName + "* %inTarget\n" 
  ioLLVMcode += "  br label %copy.end\n"
  ioLLVMcode += "copyBytesOrCopyWords:\n"
  ioLLVMcode += "  %byteCountAnd3 = and i32 %byteCount, 3\n"
  ioLLVMcode += "  %byteCountIsMutipleOf4 = icmp eq i32 %byteCountAnd3, 0\n"
  ioLLVMcode += "  br i1 %byteCountIsMutipleOf4, label %testTarget, label %copyBytes\n"
  ioLLVMcode += "testTarget:\n"
  ioLLVMcode += "  %targetAsUnsigned = ptrtoint " + structureTypeLLVMTypeName + "* %inTarget to i32\n"
  ioLLVMcode += "  %targetAsUnsignedAnd3 = and i32 %targetAsUnsigned, 3\n"
  ioLLVMcode += "  %targetAsUnsignedIsMutipleOf4 = icmp eq i32%targetAsUnsignedAnd3, 0\n"
  ioLLVMcode += "  br i1 %targetAsUnsignedIsMutipleOf4, label %testSource, label %copyBytes\n"
  ioLLVMcode += "testSource:\n"
  ioLLVMcode += "  %sourceAsUnsigned = ptrtoint " + structureTypeLLVMTypeName + "* %inSource to i32\n"
  ioLLVMcode += "  %sourceAsUnsignedAnd3 = and i32 %sourceAsUnsigned, 3\n"
  ioLLVMcode += "  %sourceAsUnsignedIsMutipleOf4 = icmp eq i32 %sourceAsUnsignedAnd3, 0\n"
  ioLLVMcode += "  br i1 %targetAsUnsignedIsMutipleOf4, label %copyWords, label %copyBytes\n"
  ioLLVMcode += "copyWords:\n"
  ioLLVMcode += "  %target32 = bitcast " + structureTypeLLVMTypeName + "* %inTarget to i32*\n"
  ioLLVMcode += "  %source32 = bitcast " + structureTypeLLVMTypeName + "* %inSource to i32*\n"
  ioLLVMcode += "  %temp = add i32 %byteCount, 3\n"
  ioLLVMcode += "  %wordCount = udiv i32 %temp, 4\n"
  ioLLVMcode += "  br label %copyWords.loop\n"
  ioLLVMcode += "copyWords.loop:\n"
  ioLLVMcode += "  call void @copy.word.array (i32* %target32, i32* %source32, i32 %wordCount)\n"
  ioLLVMcode += "  br label %copy.end\n"
  ioLLVMcode += "copyBytes:\n"
  ioLLVMcode += "  %target8 = bitcast " + structureTypeLLVMTypeName + "* %inTarget to i8*\n"
  ioLLVMcode += "  %source8 = bitcast " + structureTypeLLVMTypeName + "* %inSource to i8*\n"
  ioLLVMcode += "  call void @copy.byte.array (i8* %target8, i8* %source8, i32 %byteCount)\n"
  ioLLVMcode += "  br label %copy.end\n"
  ioLLVMcode += "copy.end:\n"
  ioLLVMcode += "  ret void\n"
  ioLLVMcode += "}\n\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
