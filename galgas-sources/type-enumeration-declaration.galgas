#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @enumerationConstantList {
  public var @lstring mConstantName
  public var @bigint mConstantValue
}

#·······················································································································

refclass @enumerationDeclarationAST : @abstractDeclarationAST {
  public var @lstring mEnumerationName
  public var @enumerationConstantList mCaseNameList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <declaration> ?!@ast ioAST {
    $enum$
    $\$type$ ?let enumerationName
    ${$
    var @enumerationConstantList enumCaseList = {}
    var idx = 0
    repeat
      $case$
      $identifier$ ?let constantName
      enumCaseList += !constantName !idx
      idx = idx + 1
    while
    end
    $}$
    ioAST.mDeclarationListAST += !@enumerationDeclarationAST.new {!enumerationName !enumCaseList}
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  PRECEDENCE GRAPH                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @enumerationDeclarationAST enterInPrecedenceGraph
  ?!@semanticTypePrecedenceGraph ioGraph
{
  [!?ioGraph addNode !self.mEnumerationName !self]
#--- Mark dependance from $uintN used for value property
  var @bigint maxValue = 0
  for (* value) in self.mCaseNameList do
    if maxValue < value then
      maxValue = value
    end
  end
  var @uint representationBitCount = 0
  loop (@uint.max) while maxValue > 0 do
    representationBitCount += 1 ;
    maxValue = maxValue / 2
  end
  [!?ioGraph addEdge !self.mEnumerationName !["u" + representationBitCount nowhere]]
}

#·······················································································································

override getter @enumerationDeclarationAST locationForErrorSignaling -> @location outLocation {
  outLocation = self.mEnumerationName.location
}

#·······················································································································

override getter @enumerationDeclarationAST keyRepresentationForErrorSignaling -> @string outRepresentation {
  outRepresentation = "enum " + self.mEnumerationName 
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  ENTER IN CONTEXT                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @enumerationDeclarationAST enterInContext
  ?!context:@semanticContext ioContext
  ?!@declarationDecorationList unused ioDecoratedDeclarationList
  ?!@decoratedRegularRoutineList unused ioDecoratedRoutineList
  ?!@routineListIR ioRoutineListIR
  ?!staticlistValues:@staticListInitializationMap unused ioStaticListValueMap
  ?!staticEntityMap:@staticEntityMap unused ioStaticEntityMap
  ?!registerUserAccess:@controlRegisterUserAccesMapAST unused ioControlRegisterUserAccesMapAST
  ?!userDefinedTypes:@userLLVMTypeDefinitionListIR unused ioUserLLVMTypeDefinitionListIR
{
#---------------- Build constant map
  var @constantMap enumConstantMap = {}
  var @bigint maxValue = 0
  for (name value) in self.mCaseNameList do
    [!?enumConstantMap insertKey !name !value !self.mEnumerationName]
    if maxValue < value then
      maxValue = value
    end
  end
  [!?ioContext.mTypeConstantMap insertKey
    !self.mEnumerationName
    !enumConstantMap
  ]
#---------------- Compute representation bit count
  var @uint representationBitCount = 0
  loop (@uint.max) while maxValue > 0 do
    representationBitCount += 1 ;
    maxValue = maxValue / 2
  end
#---------------- Property map: add uintN() accessor
  let integerTypeProxy = [ioContext.mTypeMap searchKey
    !.new {!"u" + representationBitCount !self.mEnumerationName}
  ]
  [!?ioContext addComputedPropertyGetter
    !omnibusTypeName: self.mEnumerationName
    !llvmBaseTypeName:self.mEnumerationName
    !propertyName: .new {!"u" + representationBitCount !self.mEnumerationName}
    !mode:.anySafeMode
    !visibility: .isPublic
    !resultTypeProxy:integerTypeProxy
    ?routineLLVMName: let conversionToUIntN
  ]
  ioRoutineListIR += !@enumToUintRoutineIR.new {
    !conversionToUIntN
    !isRequired:false
    !warnsIfUnused:false
    !"i" + representationBitCount # enumerationType llvmTypeName]
  }
#---------------- Insert enumeration in type map
  let enumerationType = @omnibusType.new {
    !subscript:.noSubscript
    !attributes:.instanciable | .copyable
    !omnibusTypeDescriptionName: self.mEnumerationName
    !kind:.enumeration {!bitCount:representationBitCount}
    !llvmBaseTypeName: self.mEnumerationName
  }
  [!?ioContext.mTypeMap insertType !self.mEnumerationName !type:enumerationType]
#--- Enter assignment operator
  let key = assignmentOperatorKey (!enumerationType !self.mEnumerationName.location !enumerationType)
  [!?ioContext.mAssignmentOperatorMap insertKey !key !@simpleCopyAssignmentOperatorUsage.new]
#---------------- Infix operators
#  enterEnumerationComparisonOperators (!typeName:mEnumerationName !?context:ioContext)
  let boolType = boolType ()
#--- Enter == infix operator
  addIntegerInfixOperator (!.equal !enumerationType !"icmp eq" !?ioContext.mInfixOperatorMap !boolType)
#--- Enter ≤ infix operator
  addIntegerInfixOperator (
    !.infEqual
    !enumerationType
    !"icmp ule"
    !?ioContext.mInfixOperatorMap
    !boolType
  )
#--- Enter < infix operator
  addIntegerInfixOperator (
    !.lessThan
    !enumerationType
    !"icmp ult"
    !?ioContext.mInfixOperatorMap
    !boolType
  )
}

#·······················································································································

#proc enterEnumerationComparisonOperators
#  ?typeName:@lstring inTypeName
#  ?!context:@semanticContext ioContext
#{
#  let key = combineTypeNamesForInfixOperator (!inTypeName.string !inTypeName.string)
#  let booleanType = [ioContext booleanType !.nowhere]
#  [!?ioContext.mEqualOperatorMap insertKey
#    !key
#    !booleanType
#    !@infixObjectObjectOperatorDescription.new {!.icmp_eq}
#  ]
#  [!?ioContext.mStrictInfOperatorMap insertKey
#    !key
#    !booleanType
#    !@infixObjectObjectOperatorDescription.new {!.icmp_ult}
#  ]
#  [!?ioContext.mInfEqualOperatorMap insertKey
#    !key
#    !booleanType
#    !@infixObjectObjectOperatorDescription.new {!.icmp_ule}
#  ]
#}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  CODE GENERATION                                                                                                    
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @enumToUintRoutineIR : @abstractRoutineIR {
  public var @string mEnumerationLLVMTypeName
}

#·······················································································································

override method @enumToUintRoutineIR llvmCodeGeneration 
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  let routineLLVMName = llvmNameForFunction (!self.mRoutineMangledName.string)
  ioLLVMcode += "define internal " + self.mEnumerationLLVMTypeName + " @" + routineLLVMName
  ioLLVMcode += " (" + self.mEnumerationLLVMTypeName + "* %inValue) nounwind {\n"
  ioLLVMcode += "  %r = load " + self.mEnumerationLLVMTypeName + ", " + self.mEnumerationLLVMTypeName + "* %inValue\n"
  ioLLVMcode += "  ret " + self.mEnumerationLLVMTypeName + " %r\n"
  ioLLVMcode += "}\n\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

