#—————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#—————————————————————————————————————————————————————————————————————————————————————————————————

class @llvmAssignmentOperatorDeclarationAST : @abstractDeclarationAST {
  public var @location mOperatorLocation
  public var @lstring mTargetVariableName
  public var @lstring mTargetTypeName
  public var @genericFormalParameterList mTargetGenericFormalParameterList
  public var @lstring mSourceVariableName
  public var @lstring mSourceTypeName
  public var @genericFormalParameterList mSourceGenericFormalParameterList
  public var @ctExpressionAST mWhereClause
  public var @llvmGenerationInstructionList mLLVMInstructionList
}

#·······················································································································

override getter @llvmAssignmentOperatorDeclarationAST locationForErrorSignaling -> @location outLocation {
  outLocation = self.mOperatorLocation
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

#func buildAssignmentOperatorDeclaration ?let @string inTypeName -> @abstractDeclarationAST {
#  @llvmGenerationInstructionList llvmInstructionList = @[]
#  llvmInstructionList += !@llvmVarInstruction.new {!["LOADED" nowhere]}
#  @llvmGenerationInstructionElementList instructionElements = @[]
#  instructionElements += !.symbol {!name: ["LOADED" nowhere]}
#  instructionElements += !.string {!string: " = load "}
#  instructionElements += !.type {!name: ["SRC" nowhere]}
#  instructionElements += !.string {!string: ", "}
#  instructionElements += !.type {!name: ["SRC" nowhere]}
#  instructionElements += !.string {!string: "* "}
#  instructionElements += !.symbol {!name: ["SRC" nowhere]}
#  llvmInstructionList += !@llvmGenerationInstruction.new {!{instructionElements}}
#  instructionElements = @[]
#  instructionElements += !.string {!string: "store "}
#  instructionElements += !.type {!name: ["SRC" nowhere]}
#  instructionElements += !.string {!string: " "}
#  instructionElements += !.symbol {!name: ["LOADED" nowhere]}
#  instructionElements += !.string {!string: ", "}
#  instructionElements += !.type {!name: ["TGT" nowhere]}
#  instructionElements += !.string {!string: "* "}
#  instructionElements += !.symbol {!name: ["TGT" nowhere]}
#  llvmInstructionList += !@llvmGenerationInstruction.new {!{instructionElements}}
#  result = @llvmAssignmentOperatorDeclarationAST.new {
#    !["" nowhere]
#    !["TGT" nowhere]
#    ![inTypeName nowhere]
#    !@[]
#    !["SRC" nowhere]
#    ![inTypeName nowhere]
#    !@[]
#    !@ctTrueExpressionAST.new
#    !llvmInstructionList
#  }
#}

#—————————————————————————————————————————————————————————————————————————————————————————————————

#func buildAssignmentOperatorFrom_ctInt ?let @string inTypeName -> @abstractDeclarationAST {
#  @llvmGenerationInstructionList llvmInstructionList = @[]
#  @llvmGenerationInstructionElementList instructionElements = @[]
#  instructionElements += !.string {!string: "store "}
#  instructionElements += !.type {!name: ["TGT" nowhere]}
#  instructionElements += !.string {!string: " "}
#  instructionElements += !.symbol {!name: ["SRC" nowhere]}
#  instructionElements += !.string {!string: ", "}
#  instructionElements += !.type {!name: ["TGT" nowhere]}
#  instructionElements += !.string {!string: "* "}
#  instructionElements += !.symbol {!name: ["TGT" nowhere]}
#  llvmInstructionList += !@llvmGenerationInstruction.new {!{instructionElements}}
#  result = @llvmAssignmentOperatorDeclarationAST.new {
#    !["" nowhere]
#    !["TGT" nowhere]
#    ![inTypeName nowhere]
#    !@[]
#    !["SRC" nowhere]
#    ![ctIntegerTypeName () nowhere]
#    !@[]
#    !@ctTrueExpressionAST.new
#    !llvmInstructionList
#  }
#}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#!  SYNTAX                                                                                                               
#—————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <llvm_function_header>
    !@lstring outTargetVariableName
    !@lstring outTargetTypeName
    !@genericFormalParameterList outTargetGenericFormalParameterList
  {
    $llvm$
    $func$
    $($
    $identifier$ ?outTargetVariableName
    $\$type$ ?outTargetTypeName
    <generic_formal_arguments> ?outTargetGenericFormalParameterList
    $)$
  }
  
  #·····················································································································

  rule <declaration> ?!@ast ioAST {
    <llvm_function_header> ?let targetVariableName ?let targetTypeName ?let targetGenericFormalParameterList
    let operatorLocation = @location.here
    $=$
    $($
    $identifier$ ?let sourceVariableName
    $\$type$ ?let sourceTypeName
    <generic_formal_arguments> ?let sourceGenericFormalParameterList
    $)$
    <generic_where_clause> ?let whereClause
    ${$
    <llvm_instruction_list> ?let instructionList
    $}$
    ioAST.mDeclarationListAST += !@llvmAssignmentOperatorDeclarationAST {
      !operatorLocation
      !targetVariableName
      !targetTypeName
      !targetGenericFormalParameterList
      !sourceVariableName
      !sourceTypeName
      !sourceGenericFormalParameterList
      !whereClause
      !instructionList
    }
  }

  #·····················································································································

}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH                                                                                                     
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @llvmAssignmentOperatorDeclarationAST enterInPrecedenceGraph
  ?!@semanticTypePrecedenceGraph ioGraph
{
  let nodeName = @lstring {!self.mTargetTypeName.string + " = " + self.mSourceTypeName !self.mOperatorLocation}
  [!?ioGraph addNode !nodeName !self]
  [!?ioGraph addEdge !nodeName !self.mTargetTypeName]
  [!?ioGraph addEdge !nodeName !self.mSourceTypeName]
}

#·······················································································································

override getter @llvmAssignmentOperatorDeclarationAST keyRepresentationForErrorSignaling -> @string outRepresentation {
  outRepresentation = "Assignment " + self.mTargetTypeName + " = " + self.mSourceTypeName
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#!   Assignment operator
#—————————————————————————————————————————————————————————————————————————————————————————————————

func assignmentOperatorFuncName ?let @omnibusType inTargetType ?let @omnibusType inSourceType  -> @string {
  result = "assign." + inTargetType.llvmBaseTypeName + ".ref.from." + inSourceType.llvmBaseTypeName + ".ref"
}

#·······················································································································

func assignmentOperatorKey ?let @omnibusType inTargetType
                           ?let @location inErrorLocation
                           ?let @omnibusType inSourceType -> @lstring {
  result = @lstring {
    !inTargetType.omnibusTypeDescriptionName + " = " + inSourceType.omnibusTypeDescriptionName
    !inErrorLocation
  }
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#!  ENTER IN CONTEXT                                                                                                     
#—————————————————————————————————————————————————————————————————————————————————————————————————

abstract class @abstractAssignmentOperatorUsage {
}

#·······················································································································

abstract method @abstractAssignmentOperatorUsage generateCode
    ?!@semanticTemporariesStruct ioTemporaries
    ?!@instructionListIR ioInstructionListIR
    ?!@allocaList ioAllocaList
    ?let @omnibusType inTargetType
    ?let @string inTargetLLVMName
    ?let @objectIR inSourcePossibleReference
    ?let @location inErrorLocation
    ?initializedTarget: let @bool inTargetIsInitialized

#·······················································································································

map @assignmentOperatorMap {
  public var @abstractAssignmentOperatorUsage mOperatorUsage
  insert insertKey error message "the '%K' assignment is already declared in %L"
  search searchKey error message "there is no '%K' assignment"
}

#·······················································································································

override method @llvmAssignmentOperatorDeclarationAST enterInContext
  ?!context:@semanticContext ioContext
  ?!@declarationDecorationList unused ioDecoratedDeclarationList
  ?!@decoratedRegularRoutineList unused ioDecoratedRoutineList
  ?!@routineListIR ioRoutineListIR
  ?!staticlistValues:@staticListInitializationMap unused ioStaticListValueMap
  ?!staticEntityMap:@staticEntityMap unused ioStaticEntityMap
  ?!registerUserAccess:@controlRegisterUserAccesMapAST unused ioControlRegisterUserAccesMapAST
  ?!userDefinedTypes:@userLLVMTypeDefinitionListIR unused ioUserLLVMTypeDefinitionListIR
{
#--- Build formal generic constant parameter map
  var @ctCheckMap formalGenericConstantParameterMap = @[]
  [self.mTargetGenericFormalParameterList buildFormalGenericConstantMap !?formalGenericConstantParameterMap]
  [self.mSourceGenericFormalParameterList buildFormalGenericConstantMap !?formalGenericConstantParameterMap]
#--- Check Where clause
  [self.mWhereClause checkCompileTimeExpression !formalGenericConstantParameterMap]
#--- Check instruction list
  [!?formalGenericConstantParameterMap insertKey !self.mTargetVariableName]
  [!?formalGenericConstantParameterMap insertKey !self.mSourceVariableName]
  [self.mLLVMInstructionList checkInstructionList !formalGenericConstantParameterMap]
#--- Types
  [!?ioContext.mTypeMap makeEntry !self.mTargetTypeName ?let targetTypeProxy]
  let targetType = [targetTypeProxy type]
  [!?ioContext.mTypeMap makeEntry !self.mSourceTypeName ?let sourceTypeProxy]
  let @omnibusType sourceType = [sourceTypeProxy type]
#---
  let key = assignmentOperatorKey (!targetType !self.mOperatorLocation !sourceType)
  let operatorUsage = @llvmAssignmentOperatorUsage {
    !targetType
    !self.mTargetVariableName
    !self.mTargetGenericFormalParameterList
    !sourceType
    !self.mSourceVariableName
    !self.mSourceGenericFormalParameterList
    !self.mWhereClause
    !self.mLLVMInstructionList
  }
  [!?ioContext.mAssignmentOperatorMap insertKey
    !key 
    !operatorUsage
  ]
#--- Generate function if
#       - none type is generic
#       - source type is not a compile time type
  if ([self.mTargetGenericFormalParameterList count] == 0)
   && ([self.mSourceGenericFormalParameterList count] == 0)
   && not [sourceType.kind isCompileTimeInteger]
   && not [sourceType.kind isCompileTimeBool] then
    var @assignmentGenerationVarMap varMap = @[]
    [!?varMap insertKey !self.mTargetVariableName !"%" + self.mTargetVariableName]
    [!?varMap insertKey !self.mSourceVariableName !"%" + self.mSourceVariableName]
    var @assignmentGenerationVarMap typeMap = @[]
    [!?typeMap insertKey !self.mTargetVariableName ![targetType llvmTypeName]]
    [!?typeMap insertKey !self.mSourceVariableName ![sourceType llvmTypeName]]
    var @stringlist generatedInstructions = @[]
    var @allocaList allocaList = @[]
    var @uint temporaryIndex = 0
    [self.mLLVMInstructionList generateIRCode !varMap !typeMap !?generatedInstructions !?temporaryIndex !?allocaList]
    let routine = @assignmentRoutineIR {
      ! @{!assignmentOperatorFuncName (!targetType !sourceType) !self.mOperatorLocation}
      !isRequired: false
      !warnsIfUnused: false
      !targetType
      !self.mTargetVariableName.string
      !sourceType
      !self.mSourceVariableName.string
      !generatedInstructions
      !allocaList
    }
    ioRoutineListIR += !routine
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#   CODE GENERATION
#—————————————————————————————————————————————————————————————————————————————————————————————————

class @llvmAssignmentOperatorUsage : @abstractAssignmentOperatorUsage {
  public var @omnibusType mTargetType
  public var @lstring mTargetVarName
  public var @genericFormalParameterList mTargetGenericFormalParameterList
  public var @omnibusType mSourceTpe
  public var @lstring mSourceVarName
  public var @genericFormalParameterList mSourceGenericFormalParameterList
  public var @ctExpressionAST mWhereClause
  public var @llvmGenerationInstructionList mInstructionList
}

#·······················································································································

override method @llvmAssignmentOperatorUsage generateCode
    ?!@semanticTemporariesStruct ioTemporaries
    ?!@instructionListIR ioInstructionListIR
    ?!@allocaList ioAllocaList
    ?let @omnibusType inTargetType
    ?let @string inTargetLLVMName
    ?let @objectIR inSourcePossibleReference
    ?let @location unused inErrorLocation
    ?initializedTarget: let @bool unused inTargetIsInitialized
{
  if ([self.mTargetGenericFormalParameterList count] == 0)
    && ([self.mSourceGenericFormalParameterList count] == 0)
    && not [[inSourcePossibleReference type].kind isCompileTimeInteger]
    && not [[inSourcePossibleReference type].kind isCompileTimeBool] then
      let llvmRoutineName = assignmentOperatorFuncName (!inTargetType ![inSourcePossibleReference type])
      let s = "call void @" + [llvmRoutineName assemblerRepresentation]
            + " (" + [inTargetType llvmTypeName] + "* " + inTargetLLVMName
            + ", " + [[inSourcePossibleReference type] llvmTypeName] + "* " + [inSourcePossibleReference llvmName] + ")"
      [!?ioInstructionListIR appendInstructionAsFreeString !s !@[llvmRoutineName]]
    else
    [inSourcePossibleReference extractReference ?type:let sourceType ?llvmName: let srcLLVMName]
    var @assignmentGenerationVarMap varMap = @[]
    [!?varMap insertKey !self.mTargetVarName !inTargetLLVMName]
    [!?varMap insertKey !self.mSourceVarName !srcLLVMName]
    var @assignmentGenerationVarMap typeMap = @[]
    [!?typeMap insertKey !self.mTargetVarName ![inTargetType llvmTypeName]]
    [!?typeMap insertKey !self.mSourceVarName ! [sourceType llvmTypeName]]
    var @stringlist generatedInstructions = @[]
    [self.mInstructionList generateIRCode !varMap !typeMap !?generatedInstructions !?ioTemporaries.mTemporaryIndex !?ioAllocaList]
    for (str) in generatedInstructions do
     [!?ioInstructionListIR appendInstructionAsFreeString !str !@[]]
    end
  end
}

#·······················································································································

setter @instructionListIR appendAssignmentIR
    ?!@semanticTemporariesStruct ioTemporaries
    ?let @assignmentOperatorMap inAssignmentOperatorMap
    ?!@allocaList ioAllocaList
    ?let @omnibusType inTargetType
    ?let @string inTargetLLVMName
    ?let @objectIR inSourcePossibleReference
    ?let @location inErrorLocation
    ?initializedTarget: let @bool inTargetIsInitialized
{
  let key = assignmentOperatorKey (!inTargetType !inErrorLocation ![inSourcePossibleReference type])
  [inAssignmentOperatorMap searchKey !key ?let operatorUsage]
  var @instructionListIR instructions = @[]
  [operatorUsage generateCode
    !?ioTemporaries
    !?instructions
    !?ioAllocaList
    !inTargetType
    !inTargetLLVMName
    !inSourcePossibleReference
    !inErrorLocation
    !initializedTarget: inTargetIsInitialized
  ]
  self += instructions
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

class @assignmentRoutineIR : @abstractRoutineIR {
  public var @omnibusType mTargetType
  public var @string mTargetVarName
  public var @omnibusType mSourceType
  public var @string mSourceVarName
  public var @stringlist mGeneratedInstructions
  public var @allocaList mAllocaList
}

#·······················································································································

override method @assignmentRoutineIR llvmCodeGeneration 
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  ioLLVMcode += "define internal void @" + [self.mRoutineMangledName assemblerRepresentation]
  ioLLVMcode += " (" + [self.mTargetType llvmTypeName] + "* %" + self.mTargetVarName
             + ", " + [self.mSourceType llvmTypeName] + "* %" + self.mSourceVarName + ") nounwind {\n"
  [self.mAllocaList generateAllocaList !?ioLLVMcode]
  for (instruction) in self.mGeneratedInstructions do
    ioLLVMcode += "  " + instruction + "\n"
  end
  ioLLVMcode += "  ret void\n"
  ioLLVMcode += "}\n\n"
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

class @simpleAssignmentCopyRoutineIR : @abstractRoutineIR {
  public var @omnibusType mTargetType
}

#·······················································································································

override method @simpleAssignmentCopyRoutineIR llvmCodeGeneration 
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  let typeName = [self.mTargetType llvmTypeName]
  let targetVarName = "%targetPtr"
  let sourceVarName = "%sourcePtr"
  ioLLVMcode += "define internal void @" + [self.mRoutineMangledName assemblerRepresentation]
  ioLLVMcode += " (" + typeName + "* " + targetVarName
             + ", " + typeName + "* " + sourceVarName + ") nounwind {\n"
  ioLLVMcode += "  %value = load " + typeName + ", " + typeName + "* " + sourceVarName + "\n"
  ioLLVMcode += "  store " + typeName + " %value, " + typeName + "* " + targetVarName + "\n"
  ioLLVMcode += "  ret void\n"
  ioLLVMcode += "}\n\n"
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

class @simpleCopyAssignmentOperatorUsage : @abstractAssignmentOperatorUsage {
}

#·······················································································································

override method @simpleCopyAssignmentOperatorUsage generateCode
    ?!@semanticTemporariesStruct ioTemporaries
    ?!@instructionListIR ioInstructionListIR
    ?!@allocaList unused ioAllocaList
    ?let @omnibusType inTargetType
    ?let @string inTargetLLVMName
    ?let @objectIR inSourcePossibleReference
    ?let @location unused inErrorLocation
    ?initializedTarget: let @bool unused inTargetIsInitialized
{
#--- If source is a reference, load value
  var @objectIR sourceValue = inSourcePossibleReference
  if [inSourcePossibleReference isReference] then
    getNewTempValue (!inTargetType !?ioTemporaries ?sourceValue)
    let s = [sourceValue llvmName] + " = load " + [inTargetType llvmTypeName] + ", "
          + [inTargetType llvmTypeName] + "* " + [inSourcePossibleReference llvmName]
    [!?ioInstructionListIR appendInstructionAsFreeString !s !@[]]
  end
#--- Perfom store
  let s = "store " + [inTargetType llvmTypeName] + " " + [sourceValue llvmName] 
        + ", " + [inTargetType llvmTypeName] + "* " + inTargetLLVMName
  [!?ioInstructionListIR appendInstructionAsFreeString !s !@[]]
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
