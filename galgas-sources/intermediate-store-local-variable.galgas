
setter @instructionListIR appendStoreLocalVariable
  ?let @string inLocalVarName
  ?let @unifiedTypeMap-proxy inTargetVarType
  ?let @objectIR inSourceValue
{
  self += !@storeLocalVariableIR.new {!inLocalVarName !inTargetVarType !inSourceValue}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @storeLocalVariableIR : @abstractInstructionIR {
  @string mLocalVarName
  @unifiedTypeMap-proxy mTargetVarType
  @objectIR mSourceValue
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @storeLocalVariableIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  let llvmType = [mTargetVarType llvmTypeName]
  ioLLVMcode += "  store "
  ioLLVMcode += llvmType + " " + [mSourceValue llvmName] + ", " + llvmType + " * %" + llvmNameForLocalVariable (!mLocalVarName) + "\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @storeLocalVariableIR enterAccessibleEntities 
  ?!@accessibleEntities unused ioAccessibleEntities
  ?!@uint unused ioMaxBranchOfOnInstructions
{
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

setter @instructionListIR appendStoreTemporaryReference
  ?let @unifiedTypeMap-proxy inTargetVarType
  ?let @string inLLVMName
  ?let @objectIR inSourceValue
  ?volatile:let @bool inVolatile
{
  self += !@storeFromTemporaryReferenceIR.new {!inTargetVarType !inLLVMName !inSourceValue !inVolatile}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @storeFromTemporaryReferenceIR : @abstractInstructionIR {
  @unifiedTypeMap-proxy mTargetVarType
  @string mLLVMName
  @objectIR mSourceValue
  @bool mVolatile
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @storeFromTemporaryReferenceIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  let llvmType = [mTargetVarType llvmTypeName]
  ioLLVMcode += "  store "
  if mVolatile then
    ioLLVMcode += "volatile "
  end
  ioLLVMcode += llvmType + " " + [mSourceValue llvmName] + ", " + llvmType + " * %" + mLLVMName + "\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @storeFromTemporaryReferenceIR enterAccessibleEntities 
  ?!@accessibleEntities unused ioAccessibleEntities
  ?!@uint unused ioMaxBranchOfOnInstructions
{
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
