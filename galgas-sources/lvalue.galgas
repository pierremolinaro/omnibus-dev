#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  AST                                                                                                                  
#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @accessInAssignmentAST {
  case property (@lstring name)
  case arrayAccess (@expressionAST index @location endOfIndex @bool checkIndexExpression)
}

#·······················································································································

list @accessInAssignmentListAST {
  @accessInAssignmentAST mAccess
}


#·······················································································································

enum @LValueOperandAST {
  case noOperand
  case property (@lstring name @LValueOperandAST next)
  case arrayAccess (@expressionAST index @location endOfIndex @bool checkIndexExpression @LValueOperandAST next)
}

#·······················································································································

struct @LValueAST {
  @lstring mIdentifier # self is represented by an empty string, with actual "self" location
  @LValueOperandAST mOperand
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension plm_syntax {

  #·····················································································································

  rule <lvalue> ?!@ast ioAST !@LValueAST outAssignmentTargetAST {
    @lstring receiver
    select
      $identifier$ ?receiver
    or
      $self$
      receiver = ["" here]
    end
    <lvalue_operand> !?ioAST ?let operand
    outAssignmentTargetAST = .new {!receiver !operand}
  }
  
  #·····················································································································

  rule <lvalue_operand> ?!@ast unused ioAST !@LValueOperandAST outOperand {
    outOperand = .noOperand
  }

  #·····················································································································

  rule <lvalue_operand> ?!@ast ioAST !@LValueOperandAST outOperand {
    $.$
    $identifier$ ?let propertyName
    <lvalue_operand> !?ioAST ?let operand
    outOperand = .property {!name:propertyName !next:operand}
  }
  
  #·····················································································································

  rule <lvalue_operand> ?!@ast ioAST !@LValueOperandAST outOperand {
    $[$
    <expression> !?ioAST ?let expression
    let endOfIndex = @location.here
    @bool checkIndexExpression
    select
      $]$
      checkIndexExpression = true
    or
      $]!$
      checkIndexExpression = false
    end
    <lvalue_operand> !?ioAST ?let operand
    outOperand = .arrayAccess {
      !index:expression
      !endOfIndex:endOfIndex
      !checkIndexExpression:checkIndexExpression
      !next:operand
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @LValueAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  [mOperand noteInstructionTypesInPrecedenceGraph !?ioGraph]
}

#·······················································································································

method @LValueOperandAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  switch self
  case noOperand :
  case property (* @LValueOperandAST next) :
    [next noteInstructionTypesInPrecedenceGraph !?ioGraph]
  case arrayAccess (@expressionAST indexExpression 2* @LValueOperandAST next) :
    [indexExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
    [next noteInstructionTypesInPrecedenceGraph !?ioGraph]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

struct @LValueRepresentation {
  @PLMType type
  @string llvmName
}

#·······················································································································

method @LValueAST analyzeLValue
  ?self:let @PLMType inSelfType
  ?forAddressOf:let @bool inIsAddressOf
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !@LValueRepresentation outInternalRepresentation
{
  if mIdentifier.string == "" then # Receiver is "self"
    if inSelfType.kind == .void then
      error mIdentifier : "self is not available in this context" : outInternalRepresentation
    elsif not [inRoutineAttributes mutating] && not inIsAddressOf then
      error mIdentifier
      : "cannot mutate properties, current method is not declared with @" + mutatingAttribute () + " attribute"
      : outInternalRepresentation
    else
      analyzeSelfLValue (
        !self:inSelfType
        !routineAttributes:inRoutineAttributes
        !context:inContext
        !mode:inMode
        !?temporary: ioTemporaries
        !?staticEntityMap:ioStaticEntityMap
        !?variableMap: ioUniversalMap
        !?alloca: ioAllocaList
        !?instructionListIR: ioInstructionGenerationList
        !mOperand
        ?outInternalRepresentation
      )
    end
  else
    [ioUniversalMap searchEntity !mIdentifier ?let @valuedObject entity]
    switch entity
    case task (*) :
      error mIdentifier : "a task has no value" : outInternalRepresentation
    case driver (* *) :
      error mIdentifier : "a driver has no value" : outInternalRepresentation
    case globalConstant (*) :
      error mIdentifier : "a global constant cannot be written" : outInternalRepresentation
    case localConstant (3*) :
      error mIdentifier : "a local constant cannot be written" : outInternalRepresentation
    case globalSyncInstance (2*) :
      error mIdentifier : "a global sync instance is not allowed here" : outInternalRepresentation
    case localVariable (@PLMType variableType @lstring plmName) :
      analyzeVariableInLValue (
        !self: inSelfType
        !readAccess:inIsAddressOf
        !routineAttributes:inRoutineAttributes
        !context: inContext
        !mode: inMode
        !?temporary: ioTemporaries
        !?staticEntityMap:ioStaticEntityMap
        !?variableMap: ioUniversalMap
        !?alloca: ioAllocaList
        !?instructionListIR: ioInstructionGenerationList
        !variableName: mIdentifier
        !llvmName:llvmNameForLocalVariable (!plmName)
        !type: variableType
        !mOperand
        ?outInternalRepresentation
      )
    end
  end
}

#·······················································································································

private proc analyzeVariableInLValue
  ?self:let @PLMType inSelfType
  ?readAccess:let @bool inIsReadAccess # If false, write access
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  ?variableName:let @lstring inVariableName
  ?llvmName:let @string inLLVMName
  ?type:let @PLMType inVariableType
  ?let @LValueOperandAST inOperand
  !@LValueRepresentation outInternalRepresentation
{
  var currentType = inVariableType
  var currentLLVMName = inLLVMName
  switch inOperand
  case noOperand :
    if inIsReadAccess then
      [!?ioUniversalMap searchValuedObjectForReadAccess !inVariableName ?*]
    else
      [!?ioUniversalMap searchValuedObjectForWriteAccess !inVariableName ?*]
    end
  case property (@lstring name @LValueOperandAST nextOperand) :
    if inIsReadAccess then
      [!?ioUniversalMap searchValuedObjectForReadAccess !inVariableName ?*]
    else
      [!?ioUniversalMap searchValuedObjectForReadWriteAccess !inVariableName ?*]
    end
    handlePropertyAccess (
      !self: inSelfType
      !?currentType
      !?currentLLVMName
      !routineAttributes: inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap: ioStaticEntityMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !?instructionListIR: ioInstructionGenerationList
      !name
      !nextOperand
    )
  case arrayAccess (indexExpression endOfIndex checkIndexExpression nextOperand) :
    if inIsReadAccess then
      [!?ioUniversalMap searchValuedObjectForReadAccess !inVariableName ?*]
    else
      [!?ioUniversalMap searchValuedObjectForReadWriteAccess !inVariableName ?*]
    end
    handleArrayAccess (
      !self: inSelfType
      !?currentType
      !?currentLLVMName
      !routineAttributes: inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap: ioStaticEntityMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !?instructionListIR: ioInstructionGenerationList
      !indexExpression
      !endOfIndex
      !checkIndexExpression
      !nextOperand
    )
  end
  outInternalRepresentation = .new {!currentType !currentLLVMName}
}

#·······················································································································

private proc handlePropertyAccess
  ?self:let @PLMType inSelfType
  ?!@PLMType ioCurrentType
  ?!@string ioCurrentLLVMName
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  ?let @lstring inPropertyName
  ?let @LValueOperandAST inNextOperand
{
  let @propertySetterMap propertySetterMap = ioCurrentType.propertySetterMap
  [propertySetterMap searchKey !inPropertyName ?* ?let @propertySetterKind propertyAccess]
  switch propertyAccess
  case computedProperty (3*) :
    error inPropertyName : "a computed property cannot be used here" : ioCurrentType, ioCurrentLLVMName
  case storedProperty (@PLMType propertyType @uint index) :
    [!?ioTemporaries newTempLLVMVar ?let newLLVMvariable]
    [!?ioInstructionGenerationList appendGetUniversalPropertyReference
      !ioCurrentType
      !ioCurrentLLVMName
      !newLLVMvariable
      !index
      !inPropertyName
    ]
    ioCurrentType = propertyType
    ioCurrentLLVMName = newLLVMvariable
    handleNextOperand (
      !self: inSelfType
      !?ioCurrentType
      !?ioCurrentLLVMName
      !inNextOperand
      !routineAttributes: inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap: ioStaticEntityMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !?instructionListIR: ioInstructionGenerationList
    )
  end
}

#·······················································································································

private proc handleArrayAccess
  ?self:let @PLMType inSelfType
  ?!@PLMType ioCurrentType
  ?!@string ioCurrentLLVMName
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  ?let @expressionAST inIndexExpression
  ?let @location inEndOfIndex
  ?let @bool inCheckIndexExpression
  ?let @LValueOperandAST inNextOperand
{
  switch ioCurrentType.subscript
  case noSubscript :
    error inEndOfIndex
    : "the receiver type is " + [ioCurrentType plmTypeDescriptionName] + " and does not accept subscripting"
  case literalString :
    error inEndOfIndex
    : "the receiver type is literal string and does not accept subscripting"
    : ioCurrentType, ioCurrentLLVMName
  case staticSubscript (@PLMType elementType @bigint arraySize) :
    handleArraySubscriptNew (
      !self: inSelfType
      !routineAttributes:inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap:ioStaticEntityMap
      !?variableMap:ioUniversalMap
      !?alloca: ioAllocaList
      !inIndexExpression 
      !inEndOfIndex
      !inCheckIndexExpression
      !arraySize:arraySize
      !elementType:elementType
      !?ioInstructionGenerationList
      ?let @objectIR indexIR
    )
    [!?ioTemporaries newTempLLVMVar ?let newLLVMvariable]
    [!?ioInstructionGenerationList appendGetUniversalArrayElementReference
      !ioCurrentType
      !ioCurrentLLVMName
      !elementType
      !newLLVMvariable
      !indexIR
    ]
    ioCurrentType = elementType
    ioCurrentLLVMName = newLLVMvariable
    handleNextOperand (
      !self: inSelfType
      !?ioCurrentType
      !?ioCurrentLLVMName
      !inNextOperand
      !routineAttributes: inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap: ioStaticEntityMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !?instructionListIR: ioInstructionGenerationList
    )
  end
}

#·······················································································································

private proc analyzeSelfLValue
  ?self:let @PLMType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  ?let @LValueOperandAST inOperand
  !@LValueRepresentation outInternalRepresentation
{
  var currentType = inSelfType
  var currentLLVMName = llvmNameForSelf ()
  switch inOperand
  case noOperand :
  case property (@lstring name @LValueOperandAST next) :
    handlePropertyAccess (
      !self: inSelfType
      !?currentType
      !?currentLLVMName
      !routineAttributes: inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap: ioStaticEntityMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !?instructionListIR: ioInstructionGenerationList
      !name
      !next
    )
  case arrayAccess (indexExpression endOfIndex checkIndexExpression nextOperand) :
    handleArrayAccess (
      !self: inSelfType
      !?currentType
      !?currentLLVMName
      !routineAttributes: inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap: ioStaticEntityMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !?instructionListIR: ioInstructionGenerationList
      !indexExpression
      !endOfIndex
      !checkIndexExpression
      !nextOperand
    )
  end
  outInternalRepresentation = .new {!currentType !currentLLVMName}
}

#·······················································································································

private proc handleNextOperand
  ?self:let @PLMType inSelfType
  ?!@PLMType ioCurrentType
  ?!@string ioCurrentLLVMName
  ?let @LValueOperandAST inOperand
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
  switch inOperand
  case noOperand :
  case property (@lstring name @LValueOperandAST next) :
    handlePropertyAccess (
      !self: inSelfType
      !?ioCurrentType
      !?ioCurrentLLVMName
      !routineAttributes: inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap: ioStaticEntityMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !?instructionListIR: ioInstructionGenerationList
      !name
      !next
    )
  case arrayAccess (indexExpression endOfIndex checkIndexExpression nextOperand) :
    handleArrayAccess (
      !self: inSelfType
      !?ioCurrentType
      !?ioCurrentLLVMName
      !routineAttributes: inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap: ioStaticEntityMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !?instructionListIR: ioInstructionGenerationList
      !indexExpression
      !endOfIndex
      !checkIndexExpression
      !nextOperand
    )
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
