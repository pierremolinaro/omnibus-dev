#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @controlRegisterAssignmentOperatorKind {
  case assignment
  case assignmentWithOperator (@infixOperator infixOperator @location operatorLocation)
}

#·······················································································································

class @controlRegisterAssignmentInstructionAST : @instructionAST {
  @lstring mRegisterGroupName
  @lstring mRegisterName
  @registerIndexAST mRegisterIndex
  @controlRegisterAssignmentOperatorKind mAssignmentKind
  @expressionAST mSourceExpression
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension plm_syntax {

  #·····················································································································

  rule <control_register_lvalue>
    ?!@ast ioAST
    !@lstring outRegisterGroupName
    !@lstring outRegisterName
    !@registerIndexAST outRegisterIndex
  {
    $identifier$ ?outRegisterGroupName
    $:$
    $identifier$ ?outRegisterName
    select
      outRegisterIndex = .noIndex
    or
      $[$
      <expression> !?ioAST ?let expression
      let endOfIndex = @location.here
      $]$
      outRegisterIndex = .index {!index:expression !endOfIndex:endOfIndex}
    end
  }

  #·····················································································································

  rule <instruction> ?!@ast ioAST ?!@instructionListAST ioInstructionList {
    <control_register_lvalue> !?ioAST ?let registerGroupName ?let registerName ?let registerIndexAST
  #--- Assignment or assignment operator
    @controlRegisterAssignmentOperatorKind assignmentKind
    select
      $=$
      assignmentKind = .assignment
    or
      <assignment_combined_with_operator> ?let infixOperator ?let operatorLocation
      assignmentKind = .assignmentWithOperator {!infixOperator:infixOperator !operatorLocation:operatorLocation}
    end
  #--- Source expression
    <expression> !?ioAST ?let sourceExpression
  #---
    ioInstructionList += !@controlRegisterAssignmentInstructionAST.new {
      !registerGroupName.location
      !registerGroupName
      !registerName
      !registerIndexAST
      !assignmentKind
      !sourceExpression
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @controlRegisterAssignmentInstructionAST noteInstructionTypesInPrecedenceGraph
  ?!@semanticTypePrecedenceGraph ioGraph
{
  [mSourceExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
  switch mRegisterIndex
  case noIndex :
  case index (indexExpression *) :
    [indexExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @controlRegisterAssignmentInstructionAST instructionSemanticAnalysis
  ?self:let @PLMType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?routineNameForInvocationGraph:let @lstring inCallerNameForInvocationGraph
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
#-------------------- Search register group name in entity map
  [ioUniversalMap searchEntity !mRegisterGroupName ?let entity]
#-------------------- Search register name in register group map
  @controlRegisterMap registerMap
  @registerGroupKind groupKind
  switch entity
  case task (*) :
    error mRegisterGroupName : "the \":\" access is reserved to control register" : registerMap, groupKind
  case driver (2*) :
    error mRegisterGroupName : "the \":\" access is reserved to control register" : registerMap, groupKind
  case globalConstant (*) :
    error mRegisterGroupName : "the \":\" access is reserved to control register" : registerMap, groupKind
  case localConstant (3*) :
    error mRegisterGroupName : "the \":\" access is reserved to control register" : registerMap, groupKind
  case globalSyncInstance (2*) :
    error mRegisterGroupName : "the \":\" access is reserved to control register" : registerMap, groupKind
  case localVariable (2*) :
    error mRegisterGroupName : "the \":\" access is reserved to control register" : registerMap, groupKind
  case registerGroup (theRegisterMap theGroupKind) :
    registerMap = theRegisterMap
    groupKind = theGroupKind
  end
  [registerMap searchKey
    !mRegisterName
    ?let @PLMType registerType
    ?let @bool readOnly
    ?let @bool userAccess
    ?2*
    ?let @bigint addressOffset
    ?2*
    ?let @uint registerArraySize # 0 if not an array
    ?let @uint registerElementSize # 0 if not an array
  ]
  @bigint registerAddress
  switch groupKind
  case single (@bigint baseAddress) :
    registerAddress = baseAddress + addressOffset
  case arrayGroup (@lbigintlist baseAddresses) :
    registerAddress = 0 + addressOffset
  end
#-------------------- Check access
  if readOnly then
    error mRegisterName
      : "this control register cannot be modified (declared with @ro attribute)"
  elsif not userAccess && ([inMode isUserMode] || [inMode isAnyMode]) then
    error mRegisterName : "this control register is not accessible in user mode"
  end
#-------------------- Analyze source expression
  [mSourceExpression analyzeExpression
    !self:inSelfType
    !routineAttributes:inRoutineAttributes
    !routineNameForInvocationGraph:inCallerNameForInvocationGraph
    !optionalTargetType:registerType
    !context:inContext
    !mode:inMode
    !?temporary:ioTemporaries
    !?staticEntityMap:ioStaticEntityMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:ioInstructionGenerationList
    ?result:@objectIR sourceOperand
  ]
#-------------------- Load source expression
  [!?ioInstructionGenerationList appendLoadWhenReference
    !?ioTemporaries
    !?sourceOperand
  ]
#-------------------- Analyze control register target
  @string llvmName
  switch mRegisterIndex
  case noIndex :
    if registerArraySize == 0 then
      let regName = mRegisterGroupName.string + ":" + mRegisterName
      [!?ioInstructionGenerationList appendEnterRegisterAddress !?ioTemporaries ?llvmName !registerAddress !regName]
    else
      error mRegisterName : "the control register is an array" : llvmName
    end
  case index (indexExpression endOfIndex) :
    handleArraySubscriptNew (
      !self: inSelfType
      !routineAttributes:inRoutineAttributes
      !routineNameForInvocationGraph: inCallerNameForInvocationGraph
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap:ioStaticEntityMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !indexExpression 
      !endOfIndex
      !arraySize:registerArraySize
      !elementType:registerType
      !?ioInstructionGenerationList
      ?let @objectIR indexIR
    )
    if [indexIR isLiteralInteger] then # subscript expression is constant
      [indexIR literalInteger ?1* ?value:let @bigint idx]
      let addr = registerAddress + idx * registerElementSize
      let registerName = mRegisterGroupName.string + ":" + mRegisterName + "[" + idx + "]"
      [!?ioInstructionGenerationList appendEnterRegisterAddress !?ioTemporaries ?llvmName !addr !registerName]
    else
      [!?ioInstructionGenerationList appendComputeSubscriptedVolatileRegisterAddress
        !?ioTemporaries
        ?llvmName
        !indexIR
        !registerAddress
        !registerElementSize
      ]
    end
  end
#-------------------- Perform assignment
  let t = [sourceOperand type]
  if not [t copyable] then
    error mRegisterGroupName : "source expression is not copyable"
  else
    switch mAssignmentKind
    case assignment :
      [!?ioInstructionGenerationList appendStoreIndirectVolatileRegister !registerType !llvmName !sourceOperand]
    case assignmentWithOperator (infixOperator operatorLocation) :
    #--- Load register
      getNewTempValue (!registerType !?ioTemporaries ?let registerValueIR)
      [!?ioInstructionGenerationList appendLoadIndirectVolatileRegister !registerValueIR !llvmName]
    #--- Perform operation
      let operatorMap = getInfixOperatorMap (!infixOperator !context:inContext !mode:inMode !operatorLocation)
      [operatorMap checkBinaryOperationWith
        !leftType:[registerValueIR type]
        !rightType:[sourceOperand type]
        !operatorLocation:operatorLocation
        ?resultType:let resultType
        ?op:let @infixOperatorDescription binaryOperator
      ]
      [binaryOperator generateInfixOperatorCode
        !?ioTemporaries
        !?ioInstructionGenerationList
        !registerValueIR
        !operatorLocation
        !sourceOperand
        !resultType
        ?let operationResult
      ]
    #--- Store register
      [!?ioInstructionGenerationList appendStoreIndirectVolatileRegister !registerType !llvmName !operationResult]
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
