#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   PREFIX OPERATOR MAP
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

map @prefixOperatorMap {
  @omnibusType mReceiverType
  @omnibusType mResultType
  @prefixOperatorUsage mOperator
  insert insertKey error message "the '%K' infix operator is already declared in %L"
  search searchKey error message "there is no '%K' infix operator"
}

#·······················································································································

abstract class @prefixOperatorUsage {}

#·······················································································································

abstract method @prefixOperatorUsage generateCode
  ?let @objectIR inReceiverOperand
  ?let @location inOperatorLocation
  ?let @omnibusType inResultType
  ?noPanicCode: let @bool inDoNotGeneratePanicCode
  ?!@semanticTemporariesStruct ioTemporaries
  ?!@instructionListIR ioInstructionGenerationList
  !@objectIR outResultValue

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @prefixOperatorMap generateInfixOperatorCode
  ?let @objectIR inReceiverOperand
  ?let @prefixOperator inPrefixOperator
  ?let @location inOperatorLocation
  ?safeMode: let @bool inSafeMode
  ?noPanicCode: let @bool inDoNotGeneratePanicCode
  ?!@semanticTemporariesStruct ioTemporaries
  ?!@instructionListIR ioInstructionGenerationList
  !@objectIR outResultValue
{
#--- Is operator allowed in safe mode ?
  if inSafeMode then
    switch inPrefixOperator 
    case minusNoOvf, notOp, bitWiseComplement :
    case minusOp :
      error inOperatorLocation : "this operator is not allowed in safe mode"
    end
  end
#---
  let key = prefixOperatorMapKey (![inReceiverOperand type] !inPrefixOperator !inOperatorLocation)
  [self searchKey !key ?1* ?let resultType ?let operatorUsage]
  [operatorUsage generateCode
    !inReceiverOperand
    !inOperatorLocation
    !resultType
    !noPanicCode: inDoNotGeneratePanicCode
    !?ioTemporaries
    !?ioInstructionGenerationList
    ?outResultValue
  ]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

