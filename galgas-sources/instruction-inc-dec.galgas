#----------------------------------------------------------------------------------------------------------------------*
#  AST                                                                                                                 *
#----------------------------------------------------------------------------------------------------------------------*

enum @incDecKind {
  case incWithOverflowCheck
  case decWithOverflowCheck
  case incNoOverflowCheck
  case decNoOverflowCheck
}

#----------------------------------------------------------------------------------------------------------------------*

class @incDecInstructionAST : @instructionAST {
  @incDecKind mKind
  @lstring mVarName
}

#----------------------------------------------------------------------------------------------------------------------*
#  SYNTAX                                                                                                              *
#----------------------------------------------------------------------------------------------------------------------*

syntax extension common_syntax {

  #--------------------------------------------------------------------------------------------------------------------*

  rule <instruction> ?!@instructionListAST ioInstructionList ?!@labelMap unused ioLabelMap {
    $identifier$ ?let varName
    $++$
    ioInstructionList += !@incDecInstructionAST.new {
      !.incWithOverflowCheck
      !varName
    }
  }

  #--------------------------------------------------------------------------------------------------------------------*

  rule <instruction> ?!@instructionListAST ioInstructionList?!@labelMap unused ioLabelMap {
    $identifier$ ?let varName
    $--$
    ioInstructionList += !@incDecInstructionAST.new {
      !.decWithOverflowCheck
      !varName
    }
  }

  #--------------------------------------------------------------------------------------------------------------------*

  rule <instruction> ?!@instructionListAST ioInstructionList?!@labelMap unused ioLabelMap {
    $identifier$ ?let varName
    $&++$
    ioInstructionList += !@incDecInstructionAST.new {
      !.incNoOverflowCheck
      !varName
    }
  }

  #--------------------------------------------------------------------------------------------------------------------*

  rule <instruction> ?!@instructionListAST ioInstructionList?!@labelMap unused ioLabelMap {
    $identifier$ ?let varName
    $&--$
    ioInstructionList += !@incDecInstructionAST.new {
      !.decNoOverflowCheck
      !varName
    }
  }

  #--------------------------------------------------------------------------------------------------------------------*

}

#----------------------------------------------------------------------------------------------------------------------*
#  SEMANTICS                                                                                                           *
#----------------------------------------------------------------------------------------------------------------------*

override method @incDecInstructionAST analyze
  ?routineNameForInvocationGraph:let @lstring unused inRoutineNameForInvocationGraph
  ?optionalReceiverType:let @receiverType unused inReceiverType
  ?context:let @semanticContext inContext
  ?modes:let @stringset inModeSet
  ?allowException:let @bool unused inAllowExceptions
  ?!temporary:@semanticTemporariesStruct unused ioTemporaryIndex
  ?!staticStringMap:@globalLiteralStringMap unused ioGlobalLiteralStringMap
  ?!variableMap:@variableMap ioVariableMap
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
#--- Get target type
  [!?ioVariableMap searchForReadWriteAccess
    !mVarName
    ?let type
    ?let accessIsAllowed
    ?let @string unused variableCName
    ?let @variableKindIR variable
    ?3*
  ]
  if not accessIsAllowed then
    error mVarName : "routine has no access right on the this variable"
  end
#--- Check type inc / dec operator
  if not [inContext.mTypeNameSetForIncDec hasKey ![type key]] then
    error mVarName : [type key] + " type does not handle incrementation/decrementation operator"
  end
  @bigint min
  @bigint max
  switch [type kind]
  case boolean, boolset, literalString :
    error mVarName : "internal error" : min, max
  case enumeration (*) :
    error mVarName : "internal error" : min, max
  case structure (*) :
    error mVarName : "internal error" : min, max
  case integer (kMin kMax * *) :
    min = kMin
    max = kMax
  end
#--- Exception allowed ?
  switch mKind
  case incNoOverflowCheck, decNoOverflowCheck :
  case incWithOverflowCheck, decWithOverflowCheck :
    if [inModeSet hasKey !exceptionModeName ()] then
      error mVarName : "operations that can generate exceptions are not allowed in $" + exceptionModeName () + " mode"
    end
  end
#--- Code generation
  ioInstructionGenerationList += !@incDecInstructionIR.new {
   !min
   !max
   !mKind
   !variable
   !mVarName.location
 }
}

#----------------------------------------------------------------------------------------------------------------------*
#  CODE GENERATION                                                                                                     *
#----------------------------------------------------------------------------------------------------------------------*

class @incDecInstructionIR : @abstractInstructionIR {
  @bigint mMin
  @bigint mMax
  @incDecKind mKind
  @variableKindIR mVariable
  @location mVariableLocation
}

#----------------------------------------------------------------------------------------------------------------------*

override getter @incDecInstructionIR instructionCode -> @string outCode {
  switch mKind
  case incWithOverflowCheck :
    outCode = ""
    if not [option plm_options.noExceptionGeneration value] then
      outCode += "if (" + [mVariable mangledName] + " == " + mMax + ") {\n"
      outCode += "  raise_exception (1, "
        + [[[mVariableLocation file] lastPathComponent] utf8Representation] + ", "
        + [mVariableLocation line] + ") ;\n"
      outCode += "}\n"
    end
    outCode += [mVariable mangledName] + " ++ ;\n"
  case decWithOverflowCheck :
    outCode = ""
    if not [option plm_options.noExceptionGeneration value] then
      outCode += "if (" + [mVariable mangledName] + " == " + mMin + ") {\n"
      outCode += "  raise_exception (2, "
        + [[[mVariableLocation file] lastPathComponent] utf8Representation]
        + ", " + [mVariableLocation line] + ") ;\n"
      outCode += "}\n"
    end
    outCode += [mVariable mangledName] + " -- ;\n"
  case incNoOverflowCheck :
    outCode = [mVariable mangledName] + " ++ ;\n"
  case decNoOverflowCheck :
    outCode = [mVariable mangledName] + " -- ;\n"
  end
}

#----------------------------------------------------------------------------------------------------------------------*

override method @incDecInstructionIR llvmInstructionCode ?!@string ioCode {
  switch mKind
  case incWithOverflowCheck :
    if not [option plm_options.noExceptionGeneration value] then
      ioCode += "if (" + [mVariable mangledName] + " == " + mMax + ") {\n"
      ioCode += "  raise_exception (1, "
        + [[[mVariableLocation file] lastPathComponent] utf8Representation] + ", "
        + [mVariableLocation line] + ") ;\n"
      ioCode += "}\n"
    end
    ioCode += [mVariable mangledName] + " ++ ;\n"
  case decWithOverflowCheck :
    if not [option plm_options.noExceptionGeneration value] then
      ioCode += "if (" + [mVariable mangledName] + " == " + mMin + ") {\n"
      ioCode += "  raise_exception (2, "
        + [[[mVariableLocation file] lastPathComponent] utf8Representation]
        + ", " + [mVariableLocation line] + ") ;\n"
      ioCode += "}\n"
    end
    ioCode += [mVariable mangledName] + " -- ;\n"
  case incNoOverflowCheck :
    ioCode += [mVariable mangledName] + " ++ ;\n"
  case decNoOverflowCheck :
    ioCode += [mVariable mangledName] + " -- ;\n"
  end
}

#----------------------------------------------------------------------------------------------------------------------*

override method @incDecInstructionIR enterAccessibleEntities ?!@accessibleEntities ioAccessibleEntities {
  [mVariable enterAccessibleEntities !?ioAccessibleEntities]
}

#----------------------------------------------------------------------------------------------------------------------*
