#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @registerIntegerFieldListAST {
  @lstring mFieldName
  @expressionAST mExpression
  @location mExpressionLocation
}

#·······················································································································

class @registerConstantExpressionAST : @expressionAST {
  @lstring mRegisterName
  @lstringlist mMaskFieldNames
  @registerIntegerFieldListAST mFieldValues
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension common_syntax {

  #·····················································································································

  rule <primary> !@expressionAST outExpression {
    $typeName$ ?let registerNameAsTypeName
    ${$
    @lstringlist booleanFieldNames = {}
    @registerIntegerFieldListAST integerFieldValues = {}
    repeat
      $identifier$ ?let registerFieldName
      select
        booleanFieldNames += !registerFieldName
      or
        $:$
        <expression> ?let expression
        integerFieldValues += !registerFieldName !expression !.here
      end
    while
      $,$
    end
    $}$
    outExpression = @registerConstantExpressionAST.new {!registerNameAsTypeName !booleanFieldNames !integerFieldValues}
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH FROM EXPRESSION                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @registerConstantExpressionAST addDependenceEdgeForStaticExpression
  ?let @lstring unused inConstantName
  ?!@semanticTypePrecedenceGraph unused ioGraph
{
#  [!?ioGraph addEdge !inConstantName !mRegisterName]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @registerConstantExpressionAST noteExpressionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph unused ioGraph {
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @registerConstantExpressionAST analyzeExpression
  ?self:let @unifiedTypeMap-proxy inSelfType
  ?directAccessToPropertiesAllowed:let @bool inDirectAccessToPropertiesAllowed
  ?inGuard:let @bool inGuard
  ?routineNameForInvocationGraph:let @lstring inCallerNameForInvocationGraph
  ?optionalTargetType:let @unifiedTypeMap-proxy inOptionalTargetType
  ?context:let @semanticContext inContext
  ?modes:let @stringset inModeSet
  ?allowPanic:let @bool inAllowPanic
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@staticStringMap ioGlobalLiteralStringMap
  ?!variableMap:@variableMap ioVariableMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !result:@operandIR outResult
{
#--- Search variable
  [inContext.mControlRegisterMap searchKey
    !mRegisterName
    ?let type
    ?*
    ?*
    ?let @controlRegisterBitSliceAccessMap unused registerBitSliceMap
    ?let @controlRegisterFieldMap registerFieldMap
    ?let @bigint unused registerAddress
    ?*
    ?let registerBitCount
  ]
#------------------------------------------------------ Fields masks
  @bigint accumulatedFieldStaticValues = 0G
  for (fieldName) in mMaskFieldNames do
    [registerFieldMap searchKey
      !fieldName
      ?let @uint fieldBitIndex
      ?let @uint fieldBitCount
    ]
    let mask = (1G << fieldBitCount) - 1G
    accumulatedFieldStaticValues = accumulatedFieldStaticValues | (mask << fieldBitIndex)
  end
#------------------------------------------------------ Valuated fields
  @operandList operandList = {}
  for (fieldName expression expressionLocation) in mFieldValues do
    [registerFieldMap searchKey
      !fieldName
      ?let @uint fieldBitIndex
      ?let @uint fieldBitCount
    ]
  #--- Analyze expression
    [expression analyzeExpression
      !self:inSelfType
      !directAccessToPropertiesAllowed:inDirectAccessToPropertiesAllowed
      !inGuard:inGuard
      !routineNameForInvocationGraph:inCallerNameForInvocationGraph
      !optionalTargetType:inOptionalTargetType
      !context:inContext
      !modes:inModeSet
      !allowPanic:inAllowPanic
      !?temporary:ioTemporaries
      !?staticStringMap:ioGlobalLiteralStringMap
      !?variableMap:ioVariableMap
      !?alloca:ioAllocaList
      !?instructionListIR:ioInstructionGenerationList
      ?result:let @operandIR expressionResult
    ]
  #--- Check expression type
    switch [expressionResult.mType kind]
    case arrayType (* *) :
      error expressionLocation: "an unsigned integer expression is required here"
    case opaque (*) :
      error expressionLocation: "an unsigned integer expression is required here"
    case enumeration (*) :
      error expressionLocation: "an unsigned integer expression is required here"
    case structure (* *) :
      error expressionLocation: "an unsigned integer expression is required here"
    case boolset, boolean :
      error expressionLocation: "an unsigned integer expression is required here"
    case literalString :
      error expressionLocation: "an unsigned integer expression is required here"
    case staticInteger :
      [expressionResult.mValue literalInteger ?value:let @bigint value]
      if value < 0G then
        error expressionLocation: "this integer expression should be positive"
      elsif value < (1G << fieldBitCount) then
        accumulatedFieldStaticValues = accumulatedFieldStaticValues | (value << fieldBitIndex)
      else
        error expressionLocation: "expression too large (should be < " + (1G << fieldBitCount)
      end
    case integer (@bigint unused min @bigint unused max @bool unsigned @uint expressionBitCount) :
      if not unsigned then
        error expressionLocation: "an unsigned integer expression is required here"
      else
        let assertGenerated = expressionBitCount > fieldBitCount
        if assertGenerated && [inModeSet hasKey !panicModeName ()] then
          error expressionLocation
            : "operations that can generate panic are not allowed in `" + panicModeName () + " mode"
        end
      #--- Generate an upper bound check

      #--- Perform an bit extend to register type
        var partialResult = expressionResult
        if expressionBitCount < registerBitCount then
          getNewTempVariable (!type !?ioTemporaries ?let extendedResult)
          [!?ioInstructionGenerationList appendExtend !extendedResult !partialResult]
          partialResult = extendedResult
        end
      #--- Perform left shift
        getNewTempVariable (!type !?ioTemporaries ?let shiftedResult)
        [!?ioInstructionGenerationList appendShiftLeft !shiftedResult !partialResult !fieldBitIndex]
      #---- Accumulate 
        operandList += !shiftedResult
      end
    end
  end
#------------------------------------------------------ Result
  outResult = .new {!type !.literalInteger {!value:accumulatedFieldStaticValues}}
  for (operand) in operandList do
    getNewTempVariable (!type !?ioTemporaries ?let newResult)
    [!?ioInstructionGenerationList appendBinaryOperation
      !newResult.mValue
      !newResult.mType
      !.here
      !outResult.mValue
      !.ior
      !operand.mValue
    ] 
    outResult = newResult
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
