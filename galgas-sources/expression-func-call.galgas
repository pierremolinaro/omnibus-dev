#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                 *
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @functionCallEffectiveParameterList {
  @lstring mSelector
  @expressionAST mExpression
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @functionCallInExpressionAST : @expressionAST {
  @lstring mReceiverName
  @lstring mFunctionName
  @functionCallEffectiveParameterList mParameterList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                              *
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension common_syntax {

  rule <primary> !@expressionAST outExpression {
    @lstring receiverName
    @lstring functionName
    select
      $self$
      receiverName = .new {!"self" !.here}
      $.$
      $identifier$ ?functionName
    or
      $identifier$ ?let x
      select
        functionName = x
        receiverName = ["" nowhere]
      or
        $.$
        receiverName = x
        $identifier$ ?functionName
      end
    end
    $($
    @functionCallEffectiveParameterList parameterList = {}
    repeat
    while
      $!$ ?let selector
      <expression> ?let expression
      parameterList += !selector !expression
    end
    $)$
    outExpression = @functionCallInExpressionAST.new {!receiverName !functionName !parameterList}
  }

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH FROM EXPRESSION                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @functionCallInExpressionAST addDependenceEdgeForStaticExpression
  ?let @lstring unused inConstantName
  ?!@semanticTypePrecedenceGraph unused ioGraph
{
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @functionCallInExpressionAST analyzeExpression
  ?routineNameForInvocationGraph:let @lstring inRoutineNameForInvocationGraph
  ?optionalTargetType:let @unifiedTypeMap-proxy unused inOptionalTargetType
  ?context:let @semanticContext inContext
  ?modes:let @stringset inModeSet
  ?allowException:let @bool inAllowExceptions
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@staticStringMap ioGlobalLiteralStringMap
  ?!variableMap:@variableMap ioVariableMap
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !result:@operandIR outResult
{
#--- Search proc in proc map
  @procCallEffectiveParameterListIR effectiveParameterListIR = {}
  @modeMap functionModeMap
  @funcSignature functionSignature
  @unifiedTypeMap-proxy functionResultType
  @string functionMangledName = ""
  if mReceiverName.string != "" then
    [!?ioVariableMap searchForReadAccess
      !mReceiverName
      ?let type
      ?*
      ?@objectInMemoryIR variableKind
      ?4*
    ]
  #--- Load variable
    effectiveParameterListIR += !.outputInput !["" nowhere] ![variableKind address] 
    functionMangledName += [type key] + "."
    [[type functionMap] searchKey
      !mFunctionName
      ?functionModeMap
      ?functionSignature
      ?functionResultType
    ]
  else
    [inContext.mFunctionMap searchKey
      !mFunctionName
      ?functionModeMap
      ?functionSignature
      ?functionResultType
    ]
  end
  functionMangledName += mFunctionName.string
#--- Enter in invocation graph
  [!?ioTemporaries.mSubprogramInvocationGraph addEdge
    !inRoutineNameForInvocationGraph
    !.new {!"func " + functionMangledName !mFunctionName.location}
  ]
#--- Check mode
  checkMode (
    !requiredModes:inModeSet
    !possibleModes:[functionModeMap keySet]
    !error:mFunctionName.location
  )
#---
  var s = llvmNameForFunction (!name:mFunctionName.string) + " ("
  if [functionSignature length] != [mParameterList length] then
    error mFunctionName : "this function declares " + [functionSignature length] 
      + " formal parameters, the call names " + [mParameterList length] + " effective arguments"
  else
    for formal in functionSignature, actual in mParameterList
    do
      if actual.mSelector.string != formal.mFormalSelector.string then
        var s = "!"
        if formal.mFormalSelector.string != "" then
          s += formal.mFormalSelector.string + ":"
        end
        error actual.mSelector : "required selector: " + s
      end
      [actual.mExpression analyzeExpression
        !routineNameForInvocationGraph:inRoutineNameForInvocationGraph
        !optionalTargetType:formal.mArgumentType
        !context:inContext
        !modes:inModeSet
        !allowException:inAllowExceptions
        !?temporary:ioTemporaries
        !?staticStringMap:ioGlobalLiteralStringMap
        !?variableMap:ioVariableMap
        !?instructionListIR:ioInstructionGenerationList
        ?result:@operandIR argumentIR
      ]
      if [argumentIR.mType key] == [formal.mArgumentType key] then
        effectiveParameterListIR +=
          !.output
          !formal.mFormalSelector
          !argumentIR
      elsif [[argumentIR.mType kind] isLiteralInteger] && [[formal.mArgumentType kind] isInteger] then
        [argumentIR.mValue literalInteger ?value:let @bigint value]
        [[formal.mArgumentType kind] integer ?min:let @bigint min ?max:let @bigint max ?unsigned:* ?bitCount:*]
        if (min <= value) && (value <= max) then
          effectiveParameterListIR += !.output !formal.mFormalSelector !.new {!formal.mArgumentType !argumentIR.mValue}
        else
          error actual.mSelector: "the literal integer cannot fit in an $" + [formal.mArgumentType key]
        end
      else
        error actual.mSelector : "the actual parameter has the $" + [argumentIR.mType key]
         + ", but the formal argument has the $" + [formal.mArgumentType key] + " type"
      end
    between 
      s += ", "
    end
  end
  s += ")"
#--- New LLVM variable for function call result
  getNewTempVariable (!functionResultType !?ioTemporaries ?outResult)
#--- Generate code
  ioInstructionGenerationList += !@functionCallIR.new {
    !outResult
    !functionMangledName
    !effectiveParameterListIR
  }
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @functionCallIR : @abstractInstructionIR {
  @operandIR mResult
  @string mFunctionName
  @procCallEffectiveParameterListIR mArgumentList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @functionCallIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  ioLLVMcode += "  " + [mResult.mValue llvmName] + " = call " + [mResult.mType llvmTypeName]
  ioLLVMcode += " @" + llvmNameForFunction (!name:mFunctionName) + " ("
  for () in mArgumentList do
    switch mEffectiveParameterPassingMode
    case input:
      ioLLVMcode += [mParameter.mType llvmTypeName] + "* " + [mParameter.mValue llvmName]
    case output:
      ioLLVMcode += [mParameter.mType llvmTypeName] + " " + [mParameter.mValue llvmName]
    case outputInput:
      ioLLVMcode += [mParameter.mType llvmTypeName] + "* " + [mParameter.mValue llvmName]
    end
  between
    ioLLVMcode += ", "
  end
  ioLLVMcode += ")\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @functionCallIR enterAccessibleEntities ?!@accessibleEntities ioAccessibleEntities {
  ioAccessibleEntities.mFunctionSet += !mFunctionName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
