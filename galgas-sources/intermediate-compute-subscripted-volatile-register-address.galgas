
setter @instructionListIR appendComputeSubscriptedVolatileRegisterAddress
  ?let @string inLLVMResultVarName
  ?let @objectIR inIndexIR
  ?let @bigint inAddress
  ?let @uint inElementSize
{
  self += !@computeSubscriptedVolatileRegisterAddress.new {!inLLVMResultVarName !inIndexIR !inAddress !inElementSize}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @computeSubscriptedVolatileRegisterAddress : @abstractInstructionIR {
  @string mLLVMResultVarName
  @objectIR mIndexResult
  @bigint mRegisterBaseAddress
  @uint mElementSize
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @computeSubscriptedVolatileRegisterAddress llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds ioGenerationAdds
{
  let registerIndexName = "%register.index." + ioGenerationAdds.mUniqueIndex
  let scriptedRegisterOffsetName = "%register.offset." + ioGenerationAdds.mUniqueIndex
  ioGenerationAdds.mUniqueIndex ++
#--- Needs to perform an extend to i32 ?
  [[mIndexResult type] integer ?min:* ?max:* ?unsigned:* ?bitCount:let bitCount ?1*]
  if bitCount < 32 then
    ioLLVMcode += "  " + registerIndexName + " = zext " + [mIndexResult  llvmTypeName]
               + " " + [mIndexResult llvmName] + " to i32 ; extend index to i32\n"
  end
#--- Generation
  ioLLVMcode += "  " + scriptedRegisterOffsetName + " = mul i32 "
             + if bitCount < 32 then registerIndexName else [mIndexResult llvmName] end
             + ", " + mElementSize + " ; multiply index by size " + mElementSize + "\n"
  ioLLVMcode += "  %" + mLLVMResultVarName + " = add i32 " + scriptedRegisterOffsetName
             + ", " + mRegisterBaseAddress + " ; Adding base address " + [mRegisterBaseAddress hexString] + "\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @computeSubscriptedVolatileRegisterAddress enterAccessibleEntities 
  ?!@accessibleEntities unused ioAccessibleEntities
  ?!@uint unused ioMaxBranchOfOnInstructions
{
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
