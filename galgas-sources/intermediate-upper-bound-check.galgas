

setter @instructionListIR appendUpperBoundCheck
  ?let @objectIR inSource
  ?let @bigint inUpperBoundPlusOne
  ?panicCode:let @uint inPanicCode
  ?let @location inLocation
{
  self += !@upperBoundCheckIR.new {!inSource !inUpperBoundPlusOne !inPanicCode !inLocation}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @upperBoundCheckIR : @abstractInstructionIR {
  @objectIR mSource
  @bigint mUpperBoundPlusOne
  @uint mPanicCode
  @location mLocation
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @upperBoundCheckIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
#  [!?ioGenerationAdds.mStaticEntityMap findOrAddStaticString 
#    ![[[mLocation file] lastPathComponent] stringByDeletingPathExtension]
#    ?let staticStringIndex
#  ]
  let llvmType = [[mSource type] llvmTypeName]
  let llvmSourceName = [mSource llvmName]
  let llvmLabelName = [mSource name]
  ioLLVMcode += "  " + llvmSourceName + ".isOk = icmp ult " + llvmType + " " + llvmSourceName + ", " + mUpperBoundPlusOne + "\n"
  ioLLVMcode += "  br i1 " + llvmSourceName + ".isOk, label %" + llvmLabelName + ".ovf, label %" + llvmLabelName + ".ok\n\n"
  ioLLVMcode += llvmLabelName + ".ovf:\n"
  ioLLVMcode += "  call void @" + [panicRoutineNameForLocationFile (!mLocation) assemblerRepresentation] + " ("
  ioLLVMcode += [inGenerationContext.mPanicLineLLVMType llvmTypeName] + " " + [mLocation line] + ", "
  ioLLVMcode += [inGenerationContext.mPanicCodeLLVMType llvmTypeName] + " " + mPanicCode + ")\n"
  ioLLVMcode += "  unreachable\n\n"
  ioLLVMcode += llvmLabelName + ".ok:\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @upperBoundCheckIR enterAccessibleEntities
  ?!@stringset ioInvokedRoutineSet
  ?!@uint unused ioMaxBranchOfOnInstructions
{
  ioInvokedRoutineSet += !panicRoutineNameForLocationFile (!mLocation)
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
