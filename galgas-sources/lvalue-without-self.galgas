#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @accessInAssignmentAST {
  case property (@lstring name)
  case arrayAccess (@expressionAST index @location endOfIndex)
}

#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @accessInAssignmentListAST {
  @accessInAssignmentAST mAccess
}

#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

struct @LValueWithoutSelfAST {
  @lstring mIdentifier
  @accessInAssignmentListAST mAccessList
}

#·······················································································································

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension plm_syntax {

  #·····················································································································

  rule <lvalue_without_self> !@LValueWithoutSelfAST outAssignmentTargetAST {
  #--- Object
    $identifier$ ?let @lstring identifier
  #--- Properties and subscript
    @accessInAssignmentListAST accessList = {}
    repeat
    while
      $.$
      $identifier$ ?let propertyName
      accessList += !.property {!name:propertyName}
    while
      $[$
      <expression> ?let expression
      let endOfIndex = @location.here
      $]$
      accessList += !.arrayAccess {!index:expression !endOfIndex:endOfIndex}
    end
    outAssignmentTargetAST = .new {!identifier !accessList}
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @LValueWithoutSelfAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  for (access) in mAccessList do
    switch access
    case property (*) :
    case arrayAccess (@expressionAST indexExpression *) :
      [indexExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @targetAccessKind {
  case read
  case write
  case readWrite
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @LValueWithoutSelfAST type
  ?variableMap:let @universalPropertyAndRoutineMapForContext inUniversalMap
  !@PLMType outType
{
  [inUniversalMap searchValuedObjectType !mIdentifier ?outType]
  for (access) in mAccessList do
    switch access
    case property (@lstring propertyName) :
      if [outType isStructure] then
        [outType structure ?2* ?universalMap:let universalMap ?3*]
        [universalMap searchValuedObject !propertyName ?let propertyObject]
        outType = [propertyObject type]
      else
        error propertyName : "receiver has no property" : outType
      end
    case arrayAccess (* *) :
      if [outType isArrayType] then
        [outType arrayType ?1* ?elementType:outType ?4*]
      end
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @LValueWithoutSelfAST analyzeLValueNoSelf
  ?self:let @PLMType inSelfType
  ?propertiesAreMutable:let @bool unused inRoutineCanMutateProperties
  ?routineNameForInvocationGraph:let @lstring inCallerNameForInvocationGraph
  ?context:let @semanticContext inContext
  ?mode:let @mode inCurrentMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@staticStringMap ioGlobalLiteralStringMap
  ?!variableMap:@universalPropertyAndRoutineMapForContext ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !@internalRepresentation outInternalRepresentation
{
  [ioUniversalMap searchEntity !mIdentifier ?let @possibleValuedObject entity]
  switch entity
  case undefined :
    error mIdentifier : "undefined identifier" :outInternalRepresentation
  case property (*****) :
    error mIdentifier : "property ??" : outInternalRepresentation
  case module (*) :
    error mIdentifier : "a module cannot be written" : outInternalRepresentation
  case register (* writable registerType registerAddress * arraySize elementSize) : # 0 if not an array
    analyzeControlRegisterInLValue (
      !self: inSelfType
      !routineNameForInvocationGraph: inCallerNameForInvocationGraph
      !context: inContext
      !mode: inCurrentMode
      !?temporary: ioTemporaries
      !?staticStringMap: ioGlobalLiteralStringMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !?instructionListIR: ioInstructionGenerationList
      !registerName: mIdentifier
      !registerType: registerType
      !writable: writable
      !registerAddress: registerAddress
      !registerArraySize: arraySize
      !elementSize: elementSize
      !accessList: mAccessList
      ?outInternalRepresentation
    )
  case globalConstant (*) :
    error mIdentifier : "a global constant cannot be written" : outInternalRepresentation
  case localConstant (**) :
    error mIdentifier : "a local constant cannot be written" : outInternalRepresentation
  case globalVariable (@bool writable @PLMType variableType @lstring unused plmName) :
    if not writable then
      error mIdentifier : "a global variable cannot be written in this context" : outInternalRepresentation
    else
      analyzeGloabalVariableInLValue (
        !self: inSelfType
        !routineNameForInvocationGraph: inCallerNameForInvocationGraph
        !context: inContext
        !mode: inCurrentMode
        !?temporary: ioTemporaries
        !?staticStringMap: ioGlobalLiteralStringMap
        !?variableMap: ioUniversalMap
        !?alloca: ioAllocaList
        !?instructionListIR: ioInstructionGenerationList
        !variableName: mIdentifier
        !registerType: variableType
        !accessList: mAccessList
        ?outInternalRepresentation
      )
    end
  case localVariable (@PLMType unused type @lstring unused plmName) :
    outInternalRepresentation = .standAloneIdentifier {!identifier:mIdentifier}
    for (access) in mAccessList do
      switch access
      case property (@lstring propertyName) :
        handlePropertyAccessInExpression (
          !?outInternalRepresentation
          !propertyName
          !routineNameForInvocationGraph:inCallerNameForInvocationGraph
          !optionalTargetType:.void
          !context:inContext
          !mode:inCurrentMode
          !?temporary:ioTemporaries
          !?staticStringMap:ioGlobalLiteralStringMap
          !?variableMap:ioUniversalMap
          !?alloca:ioAllocaList
          !?instructionListIR:ioInstructionGenerationList
        )
      case arrayAccess (@expressionAST indexExpression @location endOfIndex) :
        handleArrayAccessInExpression (
          !?outInternalRepresentation
          !indexExpression
          !endOfIndex
          !self:inSelfType
          !inGuard:false
          !routineNameForInvocationGraph:inCallerNameForInvocationGraph
          !optionalTargetType:.void
          !context:inContext
          !mode:inCurrentMode
          !?temporary:ioTemporaries
          !?staticStringMap:ioGlobalLiteralStringMap
          !?variableMap:ioUniversalMap
          !?alloca:ioAllocaList
          !?instructionListIR:ioInstructionGenerationList
        )
      end
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

private proc analyzeControlRegisterInLValue
  ?self:let @PLMType inSelfType
  ?routineNameForInvocationGraph:let @lstring inCallerNameForInvocationGraph
  ?context:let @semanticContext inContext
  ?mode:let @mode inCurrentMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@staticStringMap ioGlobalLiteralStringMap
  ?!variableMap:@universalPropertyAndRoutineMapForContext ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  ?registerName:let @lstring inRegisterName
  ?registerType:let @PLMType inRegisterType
  ?writable:let @bool inWritable
  ?registerAddress:let @uint inRegisterAddress
  ?registerArraySize:let @uint inArraySize
  ?elementSize:let @uint inElementSize
  ?accessList:let @accessInAssignmentListAST inAccessList
  !@internalRepresentation outInternalRepresentation
{
 if not inWritable then
   error inRegisterName : "the control register is not writable in this context" : outInternalRepresentation
 elsif [inAccessList length] == 0 then # Simple access to a control register for writing
   if inArraySize == 0 then
     outInternalRepresentation = .volatileAbsoluteReference {!type:inRegisterType !address:inRegisterAddress}
   else
     error inRegisterName : "the control register is an array" : outInternalRepresentation
   end
 elsif [inAccessList length] == 1 then # Simple access to a control register for writing
   [inAccessList first ?let access]
   switch access
   case property (@lstring propertyName) :
     error propertyName : "writing a slice of a control register is not allowed" : outInternalRepresentation
   case arrayAccess (@expressionAST indexExpression @location endOfIndex) :
     handleArraySubscriptNew (
       !self: inSelfType
       !inGuard: false
       !routineNameForInvocationGraph: inCallerNameForInvocationGraph
       !context: inContext
       !mode: inCurrentMode
       !?temporary: ioTemporaries
       !?staticStringMap: ioGlobalLiteralStringMap
       !?variableMap: ioUniversalMap
       !?alloca: ioAllocaList
       !indexExpression 
       !endOfIndex
       !arraySize:inArraySize
       !elementType:inRegisterType
       !? ioInstructionGenerationList
       ?let @objectIR indexIR
     )
     if [indexIR isLiteralInteger] then # subscript expression is constant
        [indexIR literalInteger ?1* ?value:let @bigint idx]
        let addr = inRegisterAddress + idx * inElementSize
        outInternalRepresentation = .volatileAbsoluteReference {!type:inRegisterType !address:addr}      
     else
       let llvmName = "reg.addr." + ioTemporaries.mTemporaryIndex
       ioTemporaries.mTemporaryIndex ++
       [!?ioInstructionGenerationList appendComputeSubscriptedVolatileRegisterAddress
         !llvmName
         !indexIR
         !inRegisterAddress
         !inElementSize
       ]
       outInternalRepresentation = .volatileIndirectReference {!type:inRegisterType !llvmName:llvmName}      
     end
    end
  else
    error inRegisterName : "only subscripting is allowed in control register assignment" : outInternalRepresentation
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

private proc analyzeGloabalVariableInLValue
  ?self:let @PLMType inSelfType
  ?routineNameForInvocationGraph:let @lstring inCallerNameForInvocationGraph
  ?context:let @semanticContext inContext
  ?mode:let @mode inCurrentMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@staticStringMap ioGlobalLiteralStringMap
  ?!variableMap:@universalPropertyAndRoutineMapForContext ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  ?variableName:let @lstring inVariableName
  ?registerType:let @PLMType inVariableType
  ?accessList:let @accessInAssignmentListAST inAccessList
  !@internalRepresentation outInternalRepresentation
{
  if [inAccessList length] == 0 then
    [!?ioUniversalMap searchValuedObjectForWriteAccess !inVariableName ?*]
    outInternalRepresentation = .indirectReference {!type:inVariableType !plmName:inVariableName !isGlobalVariable:true}
  else
    var currentType = inVariableType
    [!?ioUniversalMap searchValuedObjectForReadWriteAccess !inVariableName ?*]
    outInternalRepresentation = .standAloneIdentifier {!identifier:inVariableName}
    for (access) in inAccessList do
      switch access
      case property (@lstring propertyName) :
        if [currentType isStructure] then
#          [currentType structure ?2* ?universalMap:let structureMap ?3*]
#          var m = structureMap
#          [!?m searchValuedObjectForReadAccess !propertyName ?let @objectIR propertyIR]
#          let newObject = @objectIR.temporaryReference {
#             !type:[propertyIR type]
#             !llvmName:.new {!"temp." + ioTemporaries.mTemporaryIndex !propertyName}
#             !sliceMap:{}
#             !volatile:false
#          }
#          ioTemporaries.mTemporaryIndex ++
#          [!?ioInstructionGenerationList appendGetPropertyReference
#            !newObject
#            !currentPointer
#            !propertyName
#          ]
#          ioInternalRepresentation = .structureMember {!propertyName:inPropertyName !currentPointerIR:newObject}
#          currentType = [propertyIR type]


        else
          error propertyName : "this should be a structure instance"
        end
        handlePropertyAccessInExpression (
          !?outInternalRepresentation
          !propertyName
          !routineNameForInvocationGraph:inCallerNameForInvocationGraph
          !optionalTargetType:.void
          !context:inContext
          !mode:inCurrentMode
          !?temporary:ioTemporaries
          !?staticStringMap:ioGlobalLiteralStringMap
          !?variableMap:ioUniversalMap
          !?alloca:ioAllocaList
          !?instructionListIR:ioInstructionGenerationList
        )
      case arrayAccess (@expressionAST indexExpression @location endOfIndex) :
        handleArrayAccessInExpression (
          !?outInternalRepresentation
          !indexExpression
          !endOfIndex
          !self:inSelfType
          !inGuard:false
          !routineNameForInvocationGraph:inCallerNameForInvocationGraph
          !optionalTargetType:.void
          !context:inContext
          !mode:inCurrentMode
          !?temporary:ioTemporaries
          !?staticStringMap:ioGlobalLiteralStringMap
          !?variableMap:ioUniversalMap
          !?alloca:ioAllocaList
          !?instructionListIR:ioInstructionGenerationList
        )
      end
    end
  end
 }

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
