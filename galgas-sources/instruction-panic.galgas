#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @panicInstructionAST : @instructionAST {
  @location mThrowLocation
  @expressionAST mCodeExpression
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension common_syntax {

  #·····················································································································

  rule <instruction> ?!@instructionListAST ioInstructionList ?!@labelMap unused ioLabelMap {
    $panic$
    let loc = @location.here
    <expression> ?let @expressionAST codeExpression
    ioInstructionList += !loc !@panicInstructionAST.new {!loc !codeExpression}
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @panicInstructionAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  [mCodeExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @panicInstructionAST analyze
  ?self:let @unifiedTypeMap-proxy inSelfType
  ?propertiesAreMutable:let @bool unused inRoutineCanMutateProperties
  ?directAccessToPropertiesAllowed:let @bool inDirectAccessToPropertiesAllowed
  ?routineNameForInvocationGraph:let @lstring inCallerNameForInvocationGraph
  ?context:let @semanticContext inContext
  ?modes:let @stringset inModeSet
  ?allowPanic:let @bool inAllowPanic
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@staticStringMap ioGlobalLiteralStringMap
  ?!variableMap:@variableMap ioVariableMap
  ?!localVariableMap:@localVariableMap ioLocalVariableMap
  ?!namedObjectMap:@namedObjectMap ioNamedObjectMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
#--- Panic allowed ?
  if [inModeSet hasKey !panicModeName ()] then
    error mThrowLocation : "operations that can generate panic are not allowed in `" + panicModeName () + " mode"
  end
#--- Analyze expression
  @instructionListIR unusedInstructionListIR = {}
  [mCodeExpression analyzeExpression
    !self:inSelfType
    !directAccessToPropertiesAllowed:inDirectAccessToPropertiesAllowed
    !inGuard:false
    !routineNameForInvocationGraph:inCallerNameForInvocationGraph
    !optionalTargetType:inContext.mPanicCodeType
    !context:inContext
    !modes:inModeSet
    !allowPanic:inAllowPanic
    !?temporary:ioTemporaries
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioVariableMap
    !?localVariableMap: ioLocalVariableMap
    !?namedObjectMap: ioNamedObjectMap
    !?alloca:ioAllocaList
    !?instructionListIR:unusedInstructionListIR
    ?result:@valueIR result
  ]
#--- Expression type check
#--- Expression is static ?
  if ([unusedInstructionListIR length] > 0)
     || not [result isLiteralInteger]
     || ([result key] != staticIntegerTypeName ()) then
    error mThrowLocation : "throw expression should be a literal integer"
  else
    [[inContext.mPanicCodeType kind] integer ?min:let min ?max:let max ?unsigned:* ?bitCount:*]
    [result literalInteger ?1* ?value:let throwValue]
    if (throwValue < min) || (throwValue > max) then
      error mThrowLocation : "panic expression cannot be represented by an `" +[inContext.mPanicCodeType key]
    elsif not [option plm_options.noPanicGeneration value] then
      ioInstructionGenerationList += !@panicInstructionIR.new {
        !mThrowLocation
        !throwValue
      }
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  CODE GENERATION                                                                                                      
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @panicInstructionIR : @abstractInstructionIR {
  @location mThrowLocation
  @bigint mPanicCode
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @panicInstructionIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext inGenerationContext
  ?!@generationAdds ioGenerationAdds
{
  [!?ioGenerationAdds.mStaticStringMap findOrAddStaticString
    ![[[mThrowLocation file] lastPathComponent] stringByDeletingPathExtension]
    ?let staticStringIndex
  ]
  ioLLVMcode += "  call void @raise_panic." + staticStringIndex + " ("
  ioLLVMcode += inGenerationContext.mPanicLineLLVMType + " " + [mThrowLocation line] + ", "
  ioLLVMcode += inGenerationContext.mPanicCodeLLVMType + " " + mPanicCode + ")\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @panicInstructionIR enterAccessibleEntities
  ?!@accessibleEntities unused ioAccessibleEntities
  ?!@uint unused ioMaxBranchOfOnInstructions
{
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   PANIC INSTRUCTION                                                                                                   
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @panicWithLineAndFileInstructionIR : @abstractInstructionIR {
  @bigint mPanicCode
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @panicWithLineAndFileInstructionIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  ioLLVMcode += "  call void @raise_panic ("
  ioLLVMcode += inGenerationContext.mPanicCodeLLVMType + " " + mPanicCode + ", "
  ioLLVMcode += inGenerationContext.mPanicLineLLVMType + " %LINE, i8* %FILE)\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @panicWithLineAndFileInstructionIR enterAccessibleEntities
  ?!@accessibleEntities unused ioAccessibleEntities
  ?!@uint unused ioMaxBranchOfOnInstructions
{
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
