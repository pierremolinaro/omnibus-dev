
enum @llvmBinaryOperation {
  case addNoOVF
  case subNoOVF
  case mulNoOVF
  case udivNoOVF
  case sdivNoOVF
  case uremNoOVF
  case sremNoOVF

  case uaddOVF
  case saddOVF
  case usubOVF
  case ssubOVF
  case umulOVF
  case smulOVF
  case udivOVF
  case sdivOVF
  case uremOVF
  case sremOVF

  case and
  case ior
  case xor
  case shl
  case ashr
  case lshr
  
  case icmp_eq
  case icmp_ne
  case icmp_ult
  case icmp_ule
  case icmp_ugt
  case icmp_uge
  case icmp_slt
  case icmp_sle
  case icmp_sgt
  case icmp_sge
}

#----------------------------------------------------------------------------------------------------------------------*

setter @instructionListIR appendBinaryOperation
  ?let @valueIR inTarget
  ?let @unifiedTypeMap-proxy inOperandType
  ?let @location inLocation
  ?let @valueIR inLeft
  ?let @llvmBinaryOperation inOperation
  ?let @valueIR inRight
{
  self += !@binaryOperationIR.new {!inTarget !inOperandType !inLeft !inOperation !inRight !inLocation}
}

#----------------------------------------------------------------------------------------------------------------------*

class @binaryOperationIR : @abstractInstructionIR {
  @valueIR mTarget
  @unifiedTypeMap-proxy mOperandType
  @valueIR mLeft
  @llvmBinaryOperation mOperation
  @valueIR mRight
  @location mLocation
}

#----------------------------------------------------------------------------------------------------------------------*

override method @binaryOperationIR enterAccessibleEntities ?!@accessibleEntities unused ioAccessibleEntities {
}

#----------------------------------------------------------------------------------------------------------------------*

method @binaryOperationIR enterCodeForOverflowOperation 
  ?let @string inOperation
  ?let @uint inCode
  ?!@string ioCode
  ?let @generationContext inGenerationContext
  ?!@generationAdds ioGenerationAdds
{
  [!?ioGenerationAdds.mStaticStringMap findOrAddStaticString ![[mLocation file] lastPathComponent] ?let staticStringIndex]
  let llvmType = [mOperandType llvmTypeName]
  ioCode += "  " + mTarget + ".r = call {" + llvmType + ", i1} @llvm." + inOperation + ".with.overflow." + llvmType
  ioCode += " (" + llvmType + " " + mLeft + ", " + llvmType + " " + mRight + ")\n"
  ioCode += "  " + mTarget + ".hasOvf = extractvalue {" + llvmType + ", i1} " + mTarget + ".r, 1\n"
  let labelName = [mTarget name]
  ioCode += "  br i1 " + mTarget + ".hasOvf, label %" + labelName + ".ovf, label %" + labelName + ".noovf\n\n"
  ioCode += labelName + ".ovf:\n"
  ioCode += "  %" + labelName + ".str = load i8*, i8** " + literalStringName (!staticStringIndex) + "\n"
  ioCode += "  call void @raise_exception ("
  ioCode += inGenerationContext.mExceptionCodeLLVMType + " " + inCode + ", "
  ioCode += "i8* %" + labelName + ".str, "
  ioCode += inGenerationContext.mExceptionLineLLVMType + " " + [mLocation locationIndex] + ")\n"
  ioCode += "  unreachable\n\n"
  ioCode += labelName + ".noovf:\n"
  ioCode += "  " + mTarget + " = extractvalue {" + llvmType + ", i1} " + mTarget + ".r, 0\n"
  ioGenerationAdds.mIntrinsicsDeclarationSet += !"declare {" + llvmType + ", i1} @llvm." + inOperation + ".with.overflow."
     + llvmType + " (" + llvmType + " %a, " + llvmType + " %b)"
}

#----------------------------------------------------------------------------------------------------------------------*

override method @binaryOperationIR llvmInstructionCode
  ?!@string ioCode
  ?let @generationContext inGenerationContext
  ?!@generationAdds ioGenerationAdds
{
  let llvmType = [mOperandType llvmTypeName]
  switch mOperation
  case addNoOVF :
    ioCode += "  " + mTarget + " = add " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case subNoOVF :
    ioCode += "  " + mTarget + " = sub " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case mulNoOVF :
    ioCode += "  " + mTarget + " = mul " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case udivNoOVF :
    ioCode += "  " + mTarget + " = udiv " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case sdivNoOVF :
    ioCode += "  " + mTarget + " = sdiv " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case uremNoOVF :
    ioCode += "  " + mTarget + " = urem " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case sremNoOVF :
    ioCode += "  " + mTarget + " = srem " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case uaddOVF :
    [self enterCodeForOverflowOperation
      !"uadd"
      !10
      !?ioCode
      !inGenerationContext
      !?ioGenerationAdds
    ]
  case saddOVF :
    [self enterCodeForOverflowOperation
      !"sadd"
      !10
      !?ioCode
      !inGenerationContext
      !?ioGenerationAdds
    ]
  case usubOVF :
    [self enterCodeForOverflowOperation
      !"usub"
      !11
      !?ioCode
      !inGenerationContext
      !?ioGenerationAdds
    ]
  case ssubOVF :
    [self enterCodeForOverflowOperation
      !"ssub"
      !11
      !?ioCode
      !inGenerationContext
      !?ioGenerationAdds
    ]
  case umulOVF :
    [self enterCodeForOverflowOperation
      !"umul"
      !12
      !?ioCode
      !inGenerationContext
      !?ioGenerationAdds
    ]
  case smulOVF :
    [self enterCodeForOverflowOperation
      !"smul"
      !12
      !?ioCode
      !inGenerationContext
      !?ioGenerationAdds
    ]
  case udivOVF :
    [self enterCodeForOverflowOperation
      !"udiv"
      !13
      !?ioCode
      !inGenerationContext
      !?ioGenerationAdds
    ]
  case sdivOVF :
    [self enterCodeForOverflowOperation
      !"sdiv"
      !13
      !?ioCode
      !inGenerationContext
      !?ioGenerationAdds
    ]
  case uremOVF :
    [self enterCodeForOverflowOperation
      !"urem"
      !14
      !?ioCode
      !inGenerationContext
      !?ioGenerationAdds
    ]
  case sremOVF :
    [self enterCodeForOverflowOperation
      !"srem"
      !14
      !?ioCode
      !inGenerationContext
      !?ioGenerationAdds
    ]
  case and :
    ioCode += "  " + mTarget + " = and " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case ior :
    ioCode += "  " + mTarget + " = or " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case xor :
    ioCode += "  " + mTarget + " = xor " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case shl :
    ioCode += "  " + mTarget + " = shl " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case ashr :
    ioCode += "  " + mTarget + " = ashr " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case lshr :
    ioCode += "  " + mTarget + " = lshr " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case icmp_eq :
    ioCode += "  " + mTarget + " = icmp eq " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case icmp_ne :
    ioCode += "  " + mTarget + " = icmp ne " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case icmp_ult :
    ioCode += "  " + mTarget + " = icmp ult " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case icmp_ule :
    ioCode += "  " + mTarget + " = icmp ule " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case icmp_ugt :
    ioCode += "  " + mTarget + " = icmp ugt " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case icmp_uge :
    ioCode += "  " + mTarget + " = icmp uge " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case icmp_slt :
    ioCode += "  " + mTarget + " = icmp slt " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case icmp_sle :
    ioCode += "  " + mTarget + " = icmp sle " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case icmp_sgt :
    ioCode += "  " + mTarget + " = icmp sgt " + llvmType + " " + mLeft + ", " + mRight + "\n"
  case icmp_sge :
    ioCode += "  " + mTarget + " = icmp sge " + llvmType + " " + mLeft + ", " + mRight + "\n"
  end
}

#----------------------------------------------------------------------------------------------------------------------*
