#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @typedConstantCall : @expressionAST {
  @lstring mOptionalTypeName
  @lstring mConstructorName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension common_syntax {

  #·····················································································································

  rule <primary> !@expressionAST outExpression {
    @lstring optionalTypeName
    select
      optionalTypeName = .new {!"" !.here}
    or
      $typeName$ ?optionalTypeName
    end
    $.$
    $identifier$ ?let constantName
    outExpression = @typedConstantCall.new {!optionalTypeName !constantName}
  }

  #·····················································································································
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH FROM EXPRESSION                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @typedConstantCall addDependenceEdgeForStaticExpression
  ?let @lstring inConstantName
  ?!@semanticTypePrecedenceGraph ioGraph
{
  if mOptionalTypeName.string != "" then
    [!?ioGraph addEdge !inConstantName !.new{!"$" + mOptionalTypeName !mOptionalTypeName.location}]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @typedConstantCall noteExpressionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  if mOptionalTypeName.string != "" then
    [!?ioGraph noteNode !.new{!"$" + mOptionalTypeName !mOptionalTypeName.location}]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @typedConstantCall analyzeExpression
  ?self:let @unifiedTypeMap-proxy unused inSelfType
  ?directAccessToPropertiesAllowed:let @bool unused inDirectAccessToPropertiesAllowed
  ?inGuard:let @bool unused inGuard
  ?routineNameForInvocationGraph:let @lstring unused inCallerNameForInvocationGraph
  ?optionalTargetType:let @unifiedTypeMap-proxy inOptionalTargetType
  ?context:let @semanticContext inContext
  ?modes:let @stringset unused inModeSet
  ?allowPanic:let @bool unused inAllowPanic
  ?!temporary:@semanticTemporariesStruct unused ioTemporaries
  ?!staticStringMap:@staticStringMap unused ioGlobalLiteralStringMap
  ?!variableMap:@variableMap unused ioVariableMap
  ?!namedObjectMap:@namedObjectMap unused ioNamedObjectMap
  ?!alloca:@allocaList unused ioAllocaList
  ?!instructionListIR:@instructionListIR unused ioInstructionGenerationList
  !result:@objectIR outResult
{
#--- Analyze constructor source type
  @unifiedTypeMap-proxy inferredResultType
  if mOptionalTypeName.string == "" then
    inferredResultType = inOptionalTargetType
  else
    inferredResultType = .searchKey {!inContext.mTypeMap !mOptionalTypeName}
  end
  if inferredResultType == .null then
    error mConstructorName : "cannot infer type" : outResult
  else
    let classConstantMap = [inferredResultType classConstantMap]
    [classConstantMap searchKey !mConstructorName ?outResult]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
