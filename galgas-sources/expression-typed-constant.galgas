#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @typedConstantCallAST : @expressionAST {
  @lstring mOptionalTypeName
  @lstring mConstructorName
  @primaryInExpressionAccessListAST mAccessList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension plm_syntax {

  #·····················································································································

  rule <primary> ?!@ast ioAST !@expressionAST outExpression {
    @lstring optionalTypeName
    select
      optionalTypeName = .new {!"" !.here}
    or
      <type_definition> !?ioAST ?optionalTypeName
    end
    $.$
    $identifier$ ?let constantName
  #--- Properties and access
    <expression_access_list> !?ioAST ?let accessList
  #---
    outExpression = @typedConstantCallAST.new {!optionalTypeName !constantName !accessList}
  }

  #·····················································································································
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH FROM EXPRESSION                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @typedConstantCallAST addDependenceEdgeForStaticExpression
  ?let @lstring inConstantName
  ?!@semanticTypePrecedenceGraph ioGraph
{
  if mOptionalTypeName.string != "" then
    [!?ioGraph addEdge !inConstantName !llvmTypeNameFromPLMname (!mOptionalTypeName)]
  end
  for (access) in mAccessList do
    switch access
    case property (@lstring unused name) :
    case arrayAccess (@expressionAST indexExpression *) :
      [indexExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
    case funcCall (* @effectiveArgumentListAST arguments *) :
      for () in arguments do
        switch mEffectiveParameterKind
        case input (*):
        case inputWithType (* @lstring typeName *) :
          if typeName.string != "" then
            [!?ioGraph noteNode !llvmTypeNameFromPLMname (!typeName)]
          end
        case output (expression *) : [expression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
        case outputInput (*) :
        case outputInputSelfVariable (*) :
        end 
      end
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @typedConstantCallAST noteExpressionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  if mOptionalTypeName.string != "" then
    [!?ioGraph noteNode !llvmTypeNameFromPLMname (!mOptionalTypeName)]
  end
  for (access) in mAccessList do
    switch access
    case property (@lstring unused name) :
    case arrayAccess (@expressionAST indexExpression *) :
      [indexExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
    case funcCall (* @effectiveArgumentListAST arguments *) :
      for () in arguments do
        switch mEffectiveParameterKind
        case input (*):
        case inputWithType (* @lstring typeName *) :
          if typeName.string != "" then
            [!?ioGraph noteNode !llvmTypeNameFromPLMname (!typeName)]
          end
        case output (expression *) : [expression noteExpressionTypesInPrecedenceGraph !?ioGraph]
        case outputInput (*) :
        case outputInputSelfVariable (*) :
        end 
      end
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @typedConstantCallAST analyzeExpression
  ?self:let @PLMType inSelfType
  ?contextFlags:let @contextFlags inContextFlags
  ?routineNameForInvocationGraph:let @lstring inCallerNameForInvocationGraph
  ?optionalTargetType:let @PLMType inOptionalTargetType
  ?context:let @semanticContext inContext
  ?mode:let @mode inCurrentMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@staticStringMap ioGlobalLiteralStringMap
  ?!variableMap:@universalValuedObjectMapForContext ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !result:@objectIR outResult
{
#--- Analyze constructor source type
  @PLMType inferredResultType
  if mOptionalTypeName.string == "" then
    inferredResultType = inOptionalTargetType
  else
    inferredResultType = [@unifiedTypeMap-proxy.searchKey {!inContext.mTypeMap !mOptionalTypeName} type]
  end
  if inferredResultType.kind == .void then
    error mConstructorName : "cannot infer type" : outResult
  else
    let classConstantMap = inferredResultType.classConstantMap
    [classConstantMap searchKey !mConstructorName ?let @bigint value ?let classTypeName]
    let resultType = [@unifiedTypeMap-proxy.searchKey {!inContext.mTypeMap !classTypeName} type]
    outResult = .literalInteger {!type:resultType !value:value}
  end
#--- Analyze access list
  if [mAccessList length] > 0 then
  #--- procAnalyzeAccesListInExpression works on reference, not value
  #    So we create articially a temporary reference for resulting value
    getNewTempReference (![outResult type] !?ioTemporaries ?let newTemporaryReference)
  #--- ... add it into alloc list...
    ioAllocaList += ![newTemporaryReference llvmName] ![newTemporaryReference llvmTypeName]
  #--- ... and store resulting value into it
    [!?ioInstructionGenerationList appendStoreTemporaryReference
      ![newTemporaryReference type]
      ![newTemporaryReference llvmName]
      !outResult
    ]
    outResult = newTemporaryReference
  #---
    procAnalyzeAccesListInExpression (
      !self: inSelfType
      !contextFlags:inContextFlags
      !routineNameForInvocationGraph: inCallerNameForInvocationGraph
      !optionalTargetType: inOptionalTargetType
      !context: inContext
      !mode: inCurrentMode
      !?temporary: ioTemporaries
      !?staticStringMap: ioGlobalLiteralStringMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !?instructionListIR: ioInstructionGenerationList
      !mAccessList
      !?result: outResult
    )
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
