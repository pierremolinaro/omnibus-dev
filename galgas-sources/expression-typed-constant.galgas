#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @typedConstantCallAST : @expressionAST {
  @lstring mOptionalTypeName
  @lstring mConstructorName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SOLVE ENTITIES                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @typedConstantCallSE : @expressionSE {
  @lstring mOptionalTypeName
  @lstring mConstructorName
}

#·······················································································································

override method @typedConstantCallAST solveEntities
  ?context:let @semanticContext unused inSemanticContext
  !@expressionSE outExpression
{
  outExpression = @typedConstantCallSE.new {!mOptionalTypeName !mConstructorName}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension plm_syntax {

  #·····················································································································

  rule <primary> !@expressionAST outExpression {
    @lstring optionalTypeName
    select
      optionalTypeName = .new {!"" !.here}
    or
      $\$type$ ?optionalTypeName
    end
    $.$
    $identifier$ ?let constantName
    outExpression = @typedConstantCallAST.new {!optionalTypeName !constantName}
  }

  #·····················································································································
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH FROM EXPRESSION                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @typedConstantCallAST addDependenceEdgeForStaticExpression
  ?let @lstring inConstantName
  ?!@semanticTypePrecedenceGraph ioGraph
{
  if mOptionalTypeName.string != "" then
    [!?ioGraph addEdge !inConstantName !llvmRegularTypeMangledNameFromName (!mOptionalTypeName)]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @typedConstantCallAST noteExpressionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  if mOptionalTypeName.string != "" then
    [!?ioGraph noteNode !llvmRegularTypeMangledNameFromName (!mOptionalTypeName)]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @typedConstantCallAST analyzeExpression
  ?self:let @PLMType unused inSelfType
  ?inGuard:let @bool unused inGuard
  ?routineNameForInvocationGraph:let @lstring unused inCallerNameForInvocationGraph
  ?optionalTargetType:let @PLMType inOptionalTargetType
  ?context:let @semanticContext inContext
  ?mode:let @mode unused inCurrentMode
  ?!temporary:@semanticTemporariesStruct unused ioTemporaries
  ?!staticStringMap:@staticStringMap unused ioGlobalLiteralStringMap
  ?!variableMap:@universalPropertyAndRoutineMapForContext unused ioUniversalMap
  ?!alloca:@allocaList unused ioAllocaList
  ?!instructionListIR:@instructionListIR unused ioInstructionGenerationList
  !result:@objectIR outResult
{
#--- Analyze constructor source type
  @PLMType inferredResultType
  if mOptionalTypeName.string == "" then
    inferredResultType = inOptionalTargetType
  else
    inferredResultType = [@unifiedTypeMap-proxy.searchKey {!inContext.mTypeMap !mOptionalTypeName} type]
  end
  if inferredResultType == .void then
    error mConstructorName : "cannot infer type" : outResult
  else
    let classConstantMap = [inferredResultType classConstantMap]
    [classConstantMap searchKey !mConstructorName ?let @bigint value ?let classTypeName]
    let resultType = [@unifiedTypeMap-proxy.searchKey {!inContext.mTypeMap !classTypeName} type]
    outResult = .literalInteger {!type:resultType !value:value}
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
