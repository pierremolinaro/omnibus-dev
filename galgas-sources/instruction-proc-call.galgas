#----------------------------------------------------------------------------------------------------------------------*
#  AST                                                                                                                 *
#----------------------------------------------------------------------------------------------------------------------*

enum @procEffectiveParameterPassingModeAST {
  case input (@lstring name)
  case output (@expressionAST exp @location endOfExp)
  case outputInput (@lstring name)
}

#----------------------------------------------------------------------------------------------------------------------*

getter @procEffectiveParameterPassingModeAST passingModeForActualSelector ?@lstring inSelector -> @string outResult {
  switch self
  case input (*) : outResult = "?"
  case output (* *) : outResult = "!"
  case outputInput (*) : outResult = "!?"
  end
  if inSelector.string != "" then
    outResult += inSelector.string + ":"
  end
}

#----------------------------------------------------------------------------------------------------------------------*

list @procEffectiveParameterListAST {
  @procEffectiveParameterPassingModeAST mEffectiveParameterKind
  @lstring mSelector
}

#----------------------------------------------------------------------------------------------------------------------*

class @procCallInstructionAST : @instructionAST {
  @lstring mProcRoutineName
  @procEffectiveParameterListAST mEffectiveParameterList
}

#----------------------------------------------------------------------------------------------------------------------*
#  SYNTAX                                                                                                              *
#----------------------------------------------------------------------------------------------------------------------*

syntax extension common_syntax {

  rule <instruction> ?!@instructionListAST ioInstructionList ?!@labelMap unused ioLabelMap {
    $identifier$ ?let procRoutineName
    @procEffectiveParameterListAST effectiveParameterList = {}
    $($
    repeat
    while
      $!$ ?let selector
      <expression> ?let expression
      effectiveParameterList += !.output{!exp:expression !endOfExp:.here} !selector
    while
      $!?$ ?let selector
      $identifier$ ?let effectiveParameterName
      effectiveParameterList += !.outputInput{!name:effectiveParameterName} !selector
    while
      $?$ ?let selector
      $identifier$ ?let effectiveParameterName
      effectiveParameterList += !.input{!name:effectiveParameterName} !selector
    end
    $)$
    ioInstructionList += !@procCallInstructionAST.new {
      !procRoutineName
      !effectiveParameterList
    }
  }

}

#----------------------------------------------------------------------------------------------------------------------*
#  SEMANTICS                                                                                                           *
#----------------------------------------------------------------------------------------------------------------------*

list @procEffectiveParameterList {
  @procEffectiveParameterPassingModeAST mEffectiveParameterPassingMode
  @lstring mSelector
  @unifiedTypeMap-proxy mParameterType
}

#----------------------------------------------------------------------------------------------------------------------*

override method @procCallInstructionAST analyze
  ?routineNameForInvocationGraph:let @lstring inRoutineNameForInvocationGraph
  ?optionalReceiverType:let @receiverType inReceiverType
  ?context:let @semanticContext inContext
  ?modes:let @stringset inModeSet
  ?allowException:let @bool inAllowExceptions
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@globalLiteralStringMap ioGlobalLiteralStringMap
  ?!variableMap:@variableMap ioVariableMap
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
#--- Build proc mangled name
  @string procMangledName = mProcRoutineName.string
  for () in mEffectiveParameterList do
    switch mEffectiveParameterKind 
    case input (*) :
      procMangledName += "!" ;
    case output (* *) :
      procMangledName += "?" ;
    case outputInput (*) :
      procMangledName += "?!" ;
    end
    procMangledName += mSelector.string + ":"
  end
#--- Search proc in proc map
  [inContext.mProcedureMap searchKey
    !.new {!procMangledName !mProcRoutineName.location}
    ?*
    ?let procedureModeMap
    ?let @procedureSignature procSignature
    ?*
  ]
#--- Enter in invocation graph
  [!?ioTemporaries.mSubprogramInvocationGraph addEdge
    !inRoutineNameForInvocationGraph
    !.new {!"proc " + procMangledName !mProcRoutineName.location}
  ]
#--- Check parameters
  @procEffectiveParameterList parameterList = {}
  @procCallEffectiveParameterListIR procCallEffectiveParameterListIR = {}
  for () formal_ in procSignature, () in mEffectiveParameterList do
    switch mEffectiveParameterKind 
    case input (@lstring name) :
      [!?ioVariableMap searchForWriteAccess
        !name
        ?let type
        ?let accessIsAllowed
        ?let unused parameterAccess
        ?let isCopiable
        ?*
        ?let canBeUsedAsInputParameter
      ]
      if not accessIsAllowed then
        error name : "routine has no access right on the this variable"
      end
      if not canBeUsedAsInputParameter then
        error name : "this variable cannot be used as input parameter"
      end
      if not isCopiable then
        error name : "the '" + name + "' variable is not copiable"
      end
      parameterList += !mEffectiveParameterKind !mSelector !type
      procCallEffectiveParameterListIR += !.input !mSelector !type !.llvmLocalObject {!name:mangledNameForLocalVariable (!name.string)}
    case output (@expressionAST expression @location endOfExp) :
      [expression analyzeExpression
        !routineNameForInvocationGraph:inRoutineNameForInvocationGraph
        !optionalReceiverType:inReceiverType
        !optionalTargetType:formal_mType
        !context:inContext
        !modes:inModeSet
        !allowException:inAllowExceptions
        !?temporary:ioTemporaries
        !?staticStringMap:ioGlobalLiteralStringMap
        !?variableMap:ioVariableMap
        !?instructionListIR:ioInstructionGenerationList
        ?type:let expressionType
        ?result:let @operandIR expressionResult
      ]
      checkAssignmentCompatibility (
        !sourceType:expressionType
        !sourceValue:expressionResult
        !targetType:formal_mType
        !errorLocation:endOfExp
      )
      parameterList += !mEffectiveParameterKind !mSelector !formal_mType
      procCallEffectiveParameterListIR += !.output !mSelector !formal_mType !expressionResult
    case outputInput (@lstring name) :
      [!?ioVariableMap searchForReadWriteAccess
        !name
        ?let type
        ?let accessIsAllowed
        ?let unused parameterAccess
        ?2*
        ?let canBeUsedAsInputParameter
      ]
      if not canBeUsedAsInputParameter then
        error name : "this variable cannot be used as output/input parameter"
      end
      if not accessIsAllowed then
        error name : "routine has no access right on the this variable"
      end
      parameterList += !mEffectiveParameterKind !mSelector !type
      procCallEffectiveParameterListIR += !.outputInput !mSelector !type !.llvmLocalObject {!name:mangledNameForLocalVariable (!name.string)}
    end
  end
#--- Check mode
  checkMode (
    !requiredModes:inModeSet
    !possibleModes:[procedureModeMap keySet]
    !error:mProcRoutineName.location
  )
  if [procSignature length] != [parameterList length] then
    error mProcRoutineName: "this proc requires " + [procSignature length] + " argument(s), this call names "
      + [procSignature length] + " argument(s)"
  else
    for () formal_ in procSignature, () actual_ in parameterList do
      if [formal_mType key] != [actual_mParameterType key] then
        error actual_mSelector : "the actual parameter type is '" + [actual_mParameterType key] +
           "', and is incompatible with the formal type '" + [formal_mType key] + "'"
      end
      let requiredPassingMode = [formal_mFormalArgumentPassingMode requiredActualPassingModeForSelector !formal_mSelector]
      let testedPassingMode = [actual_mEffectiveParameterPassingMode passingModeForActualSelector !actual_mSelector]
      if requiredPassingMode != testedPassingMode then
        error actual_mSelector : "the required selector is '" + requiredPassingMode + "'"
      end
    end  
  end
#--- Code generation
  ioInstructionGenerationList += !@procCallInstructionIR.new {
    !mProcRoutineName.string
    !procCallEffectiveParameterListIR
  }
}

#----------------------------------------------------------------------------------------------------------------------*
#  CODE GENERATION                                                                                                     *
#----------------------------------------------------------------------------------------------------------------------*

enum @procEffectiveParameterPassingModeIR {
  case input
  case output
  case outputInput
}

#----------------------------------------------------------------------------------------------------------------------*

list @procCallEffectiveParameterListIR {
  @procEffectiveParameterPassingModeIR mEffectiveParameterPassingMode
  @lstring mSelector
  @unifiedTypeMap-proxy mParameterType
  @operandIR mParameter
}

#----------------------------------------------------------------------------------------------------------------------*

class @procCallInstructionIR : @abstractInstructionIR {
  @string mProcName
  @procCallEffectiveParameterListIR mParameters
}

#----------------------------------------------------------------------------------------------------------------------*

override method @procCallInstructionIR llvmInstructionCode
  ?!@string ioCode
  ?let @generationContext unused inGenerationContext
  ?!@stringset unused ioIntrinsicsDeclarationSet
{
  ioCode += ";--- call " + mangledNameForProcedure (!mProcName) + " ()\n"
  ioCode += "  call void @" + mangledNameForProcedure (!mProcName) + " ("
  for () in mParameters do
    switch mEffectiveParameterPassingMode
    case input:
      ioCode += [mParameterType llvmTypeName] + "* " + [mParameter string]
    case output:
      ioCode += [mParameterType llvmTypeName] + " " + [mParameter string]
    case outputInput:
      ioCode += [mParameterType llvmTypeName] + "* " + [mParameter string]
    end
  between
    ioCode += ", "
  end
  ioCode += ")\n"
}

#----------------------------------------------------------------------------------------------------------------------*

override method @procCallInstructionIR enterAccessibleEntities ?!@accessibleEntities ioAccessibleEntities {
  ioAccessibleEntities.mProcedureSet += !mProcName
}

#----------------------------------------------------------------------------------------------------------------------*
