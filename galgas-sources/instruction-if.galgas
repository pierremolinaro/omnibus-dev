#----------------------------------------------------------------------------------------------------------------------*
#  AST                                                                                                                 *
#----------------------------------------------------------------------------------------------------------------------*

class @ifInstructionAST : @instructionAST {
  @expressionAST mTestExpression
  @location mTestExpressionEndLocation
  @instructionListAST mThenInstructionList
  @location mEndOfThenInstructionList
  @instructionListAST mElseInstructionList
  @location mEndOfElseInstructionList
  @location mEndOf_if_instruction
}

#----------------------------------------------------------------------------------------------------------------------*
#  SYNTAX                                                                                                              *
#----------------------------------------------------------------------------------------------------------------------*

syntax extension common_syntax {

  #--------------------------------------------------------------------------------------------------------------------*

  rule <instruction> ?!@instructionListAST ioInstructionList ?!@labelMap ioLabelMap {
    $if$
    @lstring ifLabel
    select
      ifLabel = .new {!"" !.here}
    or
      $attribute$ ?ifLabel
      [!?ioLabelMap insertKey !ifLabel]
    end
    <if_instruction> ?let ifInstruction !?ioLabelMap !ifLabel
    ioInstructionList += !ifInstruction
    $end$
    @lstring endLabel
    select
      endLabel = .new {!"" !.here}
    or
      $attribute$ ?endLabel
    end
    if ifLabel.string != endLabel.string then
      error endLabel : "'end' label does not match 'if' label, it should be "
         + (if ifLabel.string != "" then "@" + ifLabel.string else "empty" end)
    end 
  }
  
  #--------------------------------------------------------------------------------------------------------------------*

  rule <if_instruction> !@ifInstructionAST outIfInstruction ?!@labelMap ioLabelMap ?let @lstring ifLabel {
    <expression> ?let @expressionAST testExpression
    let testExpressionEndLocation = @location.here
    $then$
    <instructionList> ?let thenInstructionList !?ioLabelMap
    let endOfThenBranch = @location.here
    @instructionListAST elseInstructionList
    select
      elseInstructionList = {}
    or
      $else$
      @lstring elsifLabel
      select
        elsifLabel = .new {!"" !.here}
      or
        $attribute$ ?elsifLabel
      end
      if ifLabel.string != elsifLabel.string then
        error elsifLabel : "'else' label does not match 'if' label, it should be "
           + (if ifLabel.string != "" then "@" + ifLabel.string else "empty" end)
      end 
      <instructionList> ?elseInstructionList !?ioLabelMap
    or
      $elsif$
      @lstring elsifIfLabel
      select
        elsifIfLabel = .new {!"" !.here}
      or
        $attribute$ ?elsifIfLabel
      end
      if ifLabel.string != elsifIfLabel.string then
        error elsifIfLabel : "'elsif' label does not match 'if' label, it should be "
           + (if ifLabel.string != "" then "@" + ifLabel.string else "empty" end)
      end 
      <if_instruction> ?let ifInstruction !?ioLabelMap !ifLabel
      elseInstructionList = {!ifInstruction}
    end
    let endOfElseBranch = @location.here
    outIfInstruction = .new {
      !testExpression
      !testExpressionEndLocation
      !thenInstructionList
      !endOfThenBranch
      !elseInstructionList
      !endOfElseBranch
      !.here
    }
  }

  #--------------------------------------------------------------------------------------------------------------------*

}

#----------------------------------------------------------------------------------------------------------------------*
#  SEMANTICS                                                                                                           *
#----------------------------------------------------------------------------------------------------------------------*

override method @ifInstructionAST analyze
  ?routineNameForInvocationGraph:let @lstring inRoutineNameForInvocationGraph
  ?optionalReceiverType:let @receiverType inReceiverType
  ?context:let @semanticContext inContext
  ?modes:let @stringset inModeSet
  ?allowException:let @bool inAllowExceptions
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@globalLiteralStringMap ioGlobalLiteralStringMap
  ?!variableMap:@variableMap ioVariableMap
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
#--- Analyze test expression
  [mTestExpression analyzeExpression
    !routineNameForInvocationGraph:inRoutineNameForInvocationGraph
    !optionalReceiverType:inReceiverType
    !optionalTargetType:.null
    !context:inContext
    !modes:inModeSet
    !allowException:inAllowExceptions
    !?temporary:ioTemporaries
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioVariableMap
    !?instructionListIR:ioInstructionGenerationList
    ?result:let @operandIR testResult
  ]
#--- Check inferred type
  if [testResult.mType kind] != .boolean then
    error mTestExpressionEndLocation : "test expression type is '" + [testResult.mType key] + "', it should be boolean"
  end
  if [testResult.mValue isStatic] then
    error mTestExpressionEndLocation : "test expression type should not be static"
  end
  [!?ioVariableMap openOverrideForSelectBlock]
#--- Analyze 'then' instruction list
  @instructionListIR thenInstructionGenerationList = {}
  [mThenInstructionList analyzeBranchInstructionList
    !routineNameForInvocationGraph:inRoutineNameForInvocationGraph
    !endOfInstructionList:mEndOfThenInstructionList
    !optionalReceiverType:inReceiverType
    !context:inContext
    !modes:inModeSet
    !allowException:inAllowExceptions
    !?temporary:ioTemporaries
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioVariableMap
    !?instructionListIR:thenInstructionGenerationList
  ]
#--- Analyze 'else' instruction list
  @instructionListIR elseInstructionGenerationList = {}
  [mElseInstructionList analyzeBranchInstructionList
    !routineNameForInvocationGraph:inRoutineNameForInvocationGraph
    !endOfInstructionList:mEndOfElseInstructionList
    !optionalReceiverType:inReceiverType
    !context:inContext
    !modes:inModeSet
    !allowException:inAllowExceptions
    !?temporary:ioTemporaries
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioVariableMap
    !?instructionListIR:elseInstructionGenerationList
  ]
  [!?ioVariableMap closeOverride !mEndOf_if_instruction]
#--- Code generation
  ioInstructionGenerationList += !@ifInstructionIR.new {
    !testResult
    !mTestExpressionEndLocation
    !thenInstructionGenerationList
    !elseInstructionGenerationList
 }
}

#----------------------------------------------------------------------------------------------------------------------*
#  CODE GENERATION                                                                                                     *
#----------------------------------------------------------------------------------------------------------------------*

class @ifInstructionIR : @abstractInstructionIR {
  @operandIR mTestVariable
  @location mLocation
  @instructionListIR mThenInstructionGenerationList
  @instructionListIR mElseInstructionGenerationList
}

#----------------------------------------------------------------------------------------------------------------------*

override method @ifInstructionIR llvmInstructionCode
  ?!@string ioCode
  ?let @generationContext inGenerationContext
  ?!@generationAdds ioGenerationAdds
{
  ioCode += ";--- if (" + mTestVariable.mValue + ") {\n"
  let labelTrue = "if." + [mLocation locationIndex] + ".true"
  let labelFalse = "if." + [mLocation locationIndex] + ".false"
  let labelEnd = "if." + [mLocation locationIndex] + ".end"
  ioCode += "  br i1 " + mTestVariable.mValue + ", label %" + labelTrue + ", label %" + labelFalse + "\n\n"
#--- True branch
  ioCode += labelTrue + ":\n"
  [mThenInstructionGenerationList instructionListLLVMCode !?ioCode !inGenerationContext !?ioGenerationAdds]
  ioCode += "  br label %" + labelEnd + "\n\n"
#--- false branch
  ioCode += labelFalse + ":\n"
  [mElseInstructionGenerationList instructionListLLVMCode !?ioCode !inGenerationContext !?ioGenerationAdds]
  ioCode += "  br label %" + labelEnd + "\n\n"
#--- end
  ioCode += labelEnd + ":\n"
}

#----------------------------------------------------------------------------------------------------------------------*

override method @ifInstructionIR enterAccessibleEntities ?!@accessibleEntities ioAccessibleEntities {
  [mThenInstructionGenerationList enterAccessibleEntities !?ioAccessibleEntities]
  [mElseInstructionGenerationList enterAccessibleEntities !?ioAccessibleEntities]
}

#----------------------------------------------------------------------------------------------------------------------*
