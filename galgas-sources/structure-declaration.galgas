#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @structureDeclaration : @abstractDeclaration {
  @lstring mStructureName
  @lstringlist mAttributeList
  @structureFieldListAST mStructureFieldListAST
  @procedureDeclarationListAST mProcedureDeclarationListAST
  @functionDeclarationListAST mFunctionDeclarationListAST
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @structureDeclaration location -> @location outLocation {
  outLocation = mStructureName.location
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @structureFieldListAST {
  @lstring mFieldName
  @lstring mFieldTypeName
  @expressionAST mInitExpression
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension common_syntax {

  #·····················································································································

  rule <declaration> ?!@ast ioAST {
    $struct$
    $typeName$ ?let structureName
    @lstringlist attributeList = {}
    repeat
    while
      $attribute$ ?let attribute
      attributeList += !attribute
    end 
    ${$
    @procedureDeclarationListAST procedureDeclarationListAST = {}
    @structureFieldListAST fieldList = {}
    @functionDeclarationListAST functionDeclarationListAST = {}
    repeat
    while
      $var$
      $identifier$ ?let fieldName
      $typeName$ ?let typeName
      $=$
      <expression> ?let initExpression
      fieldList += !fieldName !typeName !initExpression
    while
      <procedure> !?procedureDeclarationListAST
    while
      $func$
      <function_declaration> !?functionDeclarationListAST
    while
      $;$
    end
    $}$
    ioAST.mDeclarationList += !@structureDeclaration.new {
      !structureName
      !attributeList
      !fieldList
      !procedureDeclarationListAST
      !functionDeclarationListAST
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  ADD EXTENSION                                                                                                        
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @structureDeclaration addExtension
  ?!@extensionDeclarationListAST ioExtensionDeclarationListAST
  !@abstractDeclaration outNewDeclaration
{
  @structureDeclaration newDeclaration = self
  let currentExtensionDeclarationListAST = ioExtensionDeclarationListAST
  ioExtensionDeclarationListAST = {}
  for (typeName procedures fieldList functions) in currentExtensionDeclarationListAST do
    if mStructureName.string == typeName.string then
      newDeclaration = .new {
        ![newDeclaration mStructureName]
        ![newDeclaration mAttributeList]
        ![newDeclaration mStructureFieldListAST] + fieldList
        ![newDeclaration mProcedureDeclarationListAST] + procedures
        ![newDeclaration mFunctionDeclarationListAST] + functions
      }
    else
      ioExtensionDeclarationListAST += !typeName !procedures !fieldList !functions
    end
  end
  outNewDeclaration = newDeclaration
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @structureDeclaration enterInPrecedenceGraph
  ?!@semanticTypePrecedenceGraph ioGraph
{
  [!?ioGraph addNode !mStructureName !self]
  for (* fieldTypeName *) in mStructureFieldListAST do
    if fieldTypeName.string != "" then
      [!?ioGraph addEdge !mStructureName !fieldTypeName]
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override getter @structureDeclaration keyRepresentation -> @string outRepresentation {
  outRepresentation = "struct " + mStructureName 
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  ENTER IN CONTEXT                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @propertyList {
  @string mPropertyName
  @unifiedTypeMap-proxy mType
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

map @propertyMap {
  @unifiedTypeMap-proxy mTypeProxy
  @uint mIndex
  insert insertKey error message "the '%K' property is already declared"
  search searchKey error message "there is no '%K' property"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @structureDeclaration enterInContext
  ?procedureDeclarationList:let @procedureDeclarationListAST unused inProcedureListAST
  ?!context:@semanticContext ioContext
  ?!staticStringMap:@staticStringMap unused ioGlobalLiteralStringMap
{
#--- Attribute "unique"
  var copiable = true
  for (s) in mAttributeList do
    if s.string == "unique" then
      if not copiable then
        error s : "duplicated attribute"
      end
      copiable = false
    else
      error s : "unknown attribute"
    end
  end
#--- Properties
  @propertyList propertyList = {}
  @propertyMap propertyMap = {}
  for () in mStructureFieldListAST do
    [@unifiedTypeMap-proxy makeProxy !?ioContext.mTypeMap !mFieldTypeName ?let fieldTypeNameProxy]
    propertyList += !mFieldName.string !fieldTypeNameProxy
    [!?propertyMap insertKey !mFieldName !fieldTypeNameProxy ![propertyMap count]]
  end
#--- Procedures
  @procedureMap procedureMap = {}
  for procedure in mProcedureDeclarationListAST do
    @procedureSignature signature = {}
    for (formalArgumentPassingMode selector formalArgumentTypeName *) in procedure.mProcFormalArgumentList do
      [@unifiedTypeMap-proxy makeProxy !?ioContext.mTypeMap !formalArgumentTypeName ?let typeProxy]
      signature += !formalArgumentPassingMode !selector.string !typeProxy
    end
  #--- Check modes
    @modeMap procedureModeMap = {}
    for mode in procedure.mProcedureModeList do
      [ioContext.mModeMap searchKey !mode.mValue]
      [!?procedureModeMap insertKey !mode.mValue]
    end
    [!?procedureMap insertKey
      !procedure.mProcedureName
      !procedure.mMutating
      !procedureModeMap
      !signature
      !false # not weak
    ]
  end
#--- Functions
  @functionMap functionMap = {}
  for function in mFunctionDeclarationListAST do
    @funcSignature signature = {}
    for (selector formalArgumentTypeName *) in function.mFuncFormalArgumentList do
      [@unifiedTypeMap-proxy makeProxy !?ioContext.mTypeMap !formalArgumentTypeName ?let typeProxy]
      signature += !selector !typeProxy
    end
  #--- Mode map
    @modeMap functionModeMap = {}
    for () in function.mFunctionModeList do
      [!?functionModeMap insertKey !mValue] ;
    end
  #--- Result type
    [@unifiedTypeMap-proxy makeProxy !?ioContext.mTypeMap !function.mResultTypeName ?let resultType]
  #--- Enter in context
    [!?functionMap insertKey
      !function.mFunctionName
      !functionModeMap
      !signature
      !resultType
    ]
  end
#--- Enter in type map
  [!?ioContext.mTypeMap insertKey
    !lkey:mStructureName
    !llvmTypeName:"%struct." + mStructureName.string
    !kind:.structure {!propertyMap:propertyMap !propertyList:propertyList}
    !typedConstantMap:{}
    !procedureMap:procedureMap
    !functionMap:functionMap
    !copiable:copiable
    !equatable:false
    !comparable:false
    !enumerationType:.null # Not enumerable
  ]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  INIT ANALYSIS                                                                                                        
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @structureDeclaration initAnalysis
  ?!context:@semanticContext ioContext
{
  @staticStringMap globalLiteralStringMap = {} # Unused here
  @operandIRList operandIRList = {}
  for () in mStructureFieldListAST do
  #--- Field type
    let @unifiedTypeMap-proxy fieldType = if mFieldTypeName.string == ""
      then .null
      else .searchKey {!ioContext.mTypeMap !mFieldTypeName}
    end
  #--- Init expression
    @instructionListIR initInstructionGenerationList = {}
    @semanticTemporariesStruct temporaries = .default
    [mInitExpression analyzeExpression
      !routineNameForInvocationGraph:.new {!mStructureName.string + " init" !mStructureName.location}
      !optionalTargetType:fieldType
      !context:ioContext
      !modes:{}
      !allowException:false
      !?temporary:temporaries
      !?staticStringMap:globalLiteralStringMap
      !?variableMap:*
      !?instructionListIR:initInstructionGenerationList
      ?result:@operandIR expressionIR
    ]
    if not [expressionIR.mValue isStatic] then
      error mFieldName : "initialization expression cannot be computed statically"
    end
    @unifiedTypeMap-proxy type
    if fieldType != .null then
      type = fieldType
    else
      type = expressionIR.mType
    end
    operandIRList += !.new {!type !expressionIR.mValue}
  end
  [!?ioContext.mConstructorMap insertKey !mStructureName !operandIRList]
#--- Add to type initializer map
#  @initializerMap initializerMap = {}
#  [!?initializerMap insertKey !["" nowhere] !operandIRList] # Implicit initializer
#  [!?ioContext.mTypeInitializerMap insertKey !mStructureName !"struct." + mStructureName !initializerMap]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTIC ANALYSIS                                                                                                    
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @structureDeclaration semanticAnalysis
  ?context:let @semanticContext inContext
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!intermediateCode:@intermediateCodeStruct ioIntermediateCodeStruct
{
  let structureType = @unifiedTypeMap-proxy.searchKey {!inContext.mTypeMap !mStructureName}
  @propertyMap propertyMap
  switch [structureType kind]
  case structure (propMap *) : propertyMap = propMap
  case enumeration (*) : propertyMap = {}
  case integer (* * * *) : propertyMap = {}
  case literalInteger : propertyMap = {}
  case boolean : propertyMap = {}
  case boolset : propertyMap = {}
  case literalString : propertyMap = {}
  end
#--- Analyze procedures
  for procedure in mProcedureDeclarationListAST do
  #--- Enter subprogram invocation graph
    let procMangledName = "proc " + mStructureName + "." + procedure.mProcedureName
    let procNameForInvocationGraph = @lstring.new {!procMangledName !procedure.mProcedureName.location}
    if not [ioTemporaries.mSubprogramInvocationGraph isNodeDefined !procNameForInvocationGraph.string] then
      [!?ioTemporaries.mSubprogramInvocationGraph addNode
        !procNameForInvocationGraph
        !procedure.mProcedureName
      ]
    end
  #--- Check modes
    @stringset procedureModeSet = {}
    for mode in procedure.mProcedureModeList do
      [inContext.mModeMap searchKey !mode.mValue]
      procedureModeSet += !mode.mValue.string
    end
  #--- Variable map
    @variableMap variableMap = initialVariableMap (
      !routineMangledName:procMangledName
      !context:inContext
      !globalsAreConstant:false
    )
  #--- Add "self" to variable map
    if procedure.mMutating then
      [!?variableMap insertDefinedLocalVariable
        !.new {!"self" !.here}
        !structureType
        !true # mReadAccessAllowed
        !.localValue {!type:structureType !name:"self"}
        !true # copiable
        !{} # fieldMap
        !true # mCanBeUsedAsInputParameter
        !false # mIsConstant
      ]
    else
      [!?variableMap insertUsedLocalConstant
        !.new {!"self" !.here}
        !structureType
        !true # mReadAccessAllowed
        !.localValue {!type:structureType !name:"self"}
        !true # copiable
        !{} # fieldMap
        !false # mCanBeUsedAsInputParameter
        !true # mIsConstant
      ]
    end
  #--- Analyse formal arguments
    @procFormalArgumentListForGeneration formalArguments = {}
    formalArguments += !.inputOutput !structureType !mangledNameForLocalVariable (!"self")
    enterFormalArguments (
      !context:inContext
      !procedure.mProcFormalArgumentList
      !?variableMap
      !?formalArguments
    )
  #--- Analyze instruction list
    @instructionListIR instructionGenerationList = {}
    [procedure.mInstructionList analyzeRoutineInstructionList
      !routineNameForInvocationGraph:procNameForInvocationGraph
      !context:inContext
      !modes:procedureModeSet
      !allowException:not [procedureModeSet hasKey !panicModeName ()]
      !?temporary:ioTemporaries
      !?staticStringMap:ioIntermediateCodeStruct.mStaticStringMap
      !?variableMap:variableMap
      !?generatedCode:instructionGenerationList
    ]
    [variableMap checkAutomatonStates !procedure.mEndOfProcLocation]
  #--- Check attributes
    @bool warnIfUnused = true
    for (attribute) in procedure.mProcedureAttributeList do
      if attribute.string == noWarningIfUnusedAttribute () then
        if not warnIfUnused then
          error attribute : "duplicated attribute"
        end
        warnIfUnused = false
      else
        error attribute : "unknown attribute"
      end
    end
  #--- Enter in map
    [!?ioIntermediateCodeStruct.mProcedureMapIR insertKey
      !.new {!mStructureName.string + "." + procedure.mProcedureName !procedure.mProcedureName.location}
      !formalArguments
      !instructionGenerationList
      !false # [ioIntermediateCodeStruct.mRequiredProcedureSet hasKey !mProcedureName.string]
      !warnIfUnused
      !false
      !false
    ]  
  end
#--- Analyze functions
  for function in mFunctionDeclarationListAST do
  #--- Check modes
    @stringset functionModeSet = {}
    for mode in function.mFunctionModeList do
      [inContext.mModeMap searchKey !mode.mValue]
      functionModeSet += !mode.mValue.string
    end
  #--- Check attributes
    @bool warnIfUnused = true
    for (attribute) in function.mFunctionAttributeList do
      if attribute.string == noWarningIfUnusedAttribute () then
        if not warnIfUnused then
          error attribute : "duplicated attribute"
        end
        warnIfUnused = false
      else
        error attribute : "unknown attribute"
      end
    end
  #--- Variable map
    @variableMap variableMap = initialVariableMap (
      !routineMangledName:mangledNameForFunction (!name:function.mFunctionName.string)
      !context:inContext
      !globalsAreConstant:true
    )
  #--- Add "self" variable to variable map
    [!?variableMap insertUsedLocalConstant
      !.new {!"self" !.here}
      !structureType
      !true # mReadAccessAllowed
      !.localValue {!type:structureType !name:"self"}
      !true # copiable
      !{} # fieldMap
      !false # mCanBeUsedAsInputParameter
      !true # mIsConstant
    ]
  #--- Add 'result" variable
    let resultVarName = @lstring.new {!"result" !function.mResultTypeName.location}
    let resultType = @unifiedTypeMap-proxy.searchKey {!inContext.mTypeMap !function.mResultTypeName}
    [!?variableMap insertOutputFormalArgument
      !resultVarName
      !resultType
      !true
      !.localValue {!type:resultType !name:resultVarName.string}
      !true
      !{}
      !true
      !false # Is not constant
    ]
  #--- Analyse formal arguments
    @procFormalArgumentListForGeneration formalArguments = {}
    formalArguments += !.inputOutput !structureType !mangledNameForLocalVariable (!"self")
    for (* formalArgumentTypeName formalArgumentName) in function.mFuncFormalArgumentList do
      let typeProxy = @unifiedTypeMap-proxy.searchKey {!inContext.mTypeMap !formalArgumentTypeName}
      [!?variableMap insertConstantInputFormalArgument
        !formalArgumentName
        !typeProxy
        !true
        !.localValue {!type:typeProxy !name:formalArgumentName.string}
        ![typeProxy copiable]
        !{}
        !false
        !true # Is constant
      ]
      formalArguments += !.input !typeProxy !mangledNameForLocalVariable (!formalArgumentName.string)
    end
  #---
    let routineNameForInvocationGraph = @lstring.new{
      !"func " + mStructureName + "." + function.mFunctionName.string
      !function.mFunctionName.location
    }
    if not [ioTemporaries.mSubprogramInvocationGraph isNodeDefined !routineNameForInvocationGraph.string] then
      [!?ioTemporaries.mSubprogramInvocationGraph addNode
        !routineNameForInvocationGraph
        !routineNameForInvocationGraph
      ]
    end
  #--- Analyze instruction list
    @instructionListIR instructionGenerationList = {}
    [function.mInstructionList analyzeRoutineInstructionList
      !routineNameForInvocationGraph:routineNameForInvocationGraph
      !context:inContext
      !modes:functionModeSet
      !allowException:not [functionModeSet hasKey !panicModeName ()]
      !?temporary:ioTemporaries
      !?staticStringMap:ioIntermediateCodeStruct.mStaticStringMap
      !?variableMap:variableMap
      !?generatedCode:instructionGenerationList
    ]
  #---
    [variableMap checkAutomatonStates !function.mEndOfFuncLocation]
  #--- Code generation
    [!?ioIntermediateCodeStruct.mFunctionMapIR insertKey
      !.new {!mStructureName.string + "." + function.mFunctionName !function.mFunctionName.location}
      !formalArguments
      !instructionGenerationList
      !resultType
      !resultVarName.string
      !warnIfUnused
    ]  
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  CODE GENERATION                                                                                                      
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc declareLLVMstructureTypes ?let @unifiedTypeMap inTypeMap ?!@string ioLLVMcode {
  for () in inTypeMap do
    if [kind isStructure] then
      [kind structure ?propertyMap:* ?propertyList:let propertyList]
      ioLLVMcode += llvmTypeName + " = type {"
      for () in propertyList
        do ioLLVMcode += [mType llvmTypeName]
        between ioLLVMcode += ", "
      end
      ioLLVMcode += "}\n\n"
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

#proc declareLLVMstructureInitRoutines ?let @typeInitializerMap inTypeInitializerMap ?!@string ioLLVMcode {
#  for (* llvmTypeName initializerMap) in inTypeInitializerMap do
#    for (initializerName propertyList) in initializerMap do
#      ioLLVMcode += "define internal %" + llvmTypeName + " @" + llvmTypeName + "." + initializerName + " () nounwind {\n"
#      var s = "undef"
#      for () in propertyList do (idx)
#        let target = "%result." + idx
#        ioLLVMcode += "  " + target + " = insertvalue %" + llvmTypeName + " " + s
#        ioLLVMcode += ", " + [mOperand.mType llvmTypeName] + " " + [mOperand.mValue llvmName] + ", " + idx + "\n"
#        s = target
#      end
#      ioLLVMcode += "  ret %" + llvmTypeName + " " + s + "\n"
#      ioLLVMcode += "}\n\n"
#    end
#  end
#}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

