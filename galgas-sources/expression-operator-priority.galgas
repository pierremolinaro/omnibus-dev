#—————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#—————————————————————————————————————————————————————————————————————————————————————————————————

abstract refclass @expressionAST {
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#—————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <expression> ?!@ast ioAST !@expressionAST outExpression {
    <expression_logical_and> !?ioAST ?outExpression
    repeat
    while
      $||$
      let operatorLocation = @location.here
      <expression_logical_and> !?ioAST ?let rightExpression
    #--- x or y equivalent to not (not x and not y)
      let andExpression = @booleanShortCircuitAndOperatorExpressionAST.init {
        !@prefixOperatorExpressionAST.init {!operatorLocation !.notOp !outExpression}
        !operatorLocation
        !@prefixOperatorExpressionAST.init {!operatorLocation !.notOp !rightExpression}
      }
      outExpression = @prefixOperatorExpressionAST.init {
        !operatorLocation
        !.notOp
        !andExpression
      }
    end
  }

#  #·····················································································································
#
#  rule <expression_logical_xor> ?!@ast ioAST !@expressionAST outExpression {
#    <expression_logical_and> !?ioAST ?outExpression
#    repeat
#    while
#      $xor$
#      let operatorLocation = @location.here
#      <expression_logical_and> !?ioAST ?let rightExpression
#      outExpression = @omnibusInfixOperatorExpressionAST.new {
#        !outExpression
#        !operatorLocation
#        !.booleanXorOp
#        !rightExpression
#      }
#    end
#  }
#
  #·····················································································································

  rule <expression_logical_and> ?!@ast ioAST !@expressionAST outExpression {
    <expression_bitwise_or> !?ioAST ?outExpression
    repeat
    while
      $&&$
      let operatorLocation = @location.here
      <expression_bitwise_or> !?ioAST ?let rightExpression
      outExpression = @booleanShortCircuitAndOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !rightExpression
      }
    end
  }

  #·····················································································································

  rule <expression_bitwise_or> ?!@ast ioAST !@expressionAST outExpression {
    <expression_bitwise_xor> !?ioAST ?outExpression
    repeat
    while
      $|$
      let operatorLocation = @location.here
      <expression_bitwise_xor> !?ioAST ?let rightExpression
      outExpression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.bitWiseOrOp
        !rightExpression
      }
    end
  }

  #·····················································································································

  rule <expression_bitwise_xor> ?!@ast ioAST !@expressionAST outExpression {
    <expression_bitwise_and> !?ioAST ?outExpression
    repeat
    while
      $^$
      let operatorLocation = @location.here
      <expression_bitwise_and> !?ioAST ?let rightExpression
      outExpression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.bitWiseXorOp
        !rightExpression
      }
    end
   }

  #·····················································································································

  rule <expression_bitwise_and> ?!@ast ioAST !@expressionAST outExpression {
    <expression_equality> !?ioAST ?outExpression
    repeat
    while
      $&$
      let operatorLocation = @location.here
      <expression_equality> !?ioAST ?let rightExpression
      outExpression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.bitWiseAndOp
        !rightExpression
      }
    end
  }

  #·····················································································································

  rule <expression_equality> ?!@ast ioAST !@expressionAST outExpression {
    <expression_comparison> !?ioAST ?outExpression
    select
    or
      $==$
      let operatorLocation = @location.here
      <expression_comparison> !?ioAST ?let rightExpression
      outExpression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.equal
        !rightExpression
      }
    or
      $≠$
      let operatorLocation = @location.here
      <expression_comparison> !?ioAST ?let rightExpression
      let expression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.equal
        !rightExpression
      }
      outExpression = @prefixOperatorExpressionAST.init {
        !operatorLocation
        !.notOp
        !expression
      }
    end
  }

  #·····················································································································

  rule <expression_comparison> ?!@ast ioAST !@expressionAST outExpression {
    <expression_shift> !?ioAST ?outExpression
    select
    or
      $≤$
      let operatorLocation = @location.here
      <expression_shift> !?ioAST ?let rightExpression
      outExpression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.infEqual
        !rightExpression
      }
    or
      $≥$
      let operatorLocation = @location.here
      <expression_shift> !?ioAST ?let rightExpression
      let expression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.lessThan
        !rightExpression
      }
      outExpression = @prefixOperatorExpressionAST.init {
        !operatorLocation
        !.notOp
        !expression
      }
    or
      $<$
      let operatorLocation = @location.here
      <expression_shift> !?ioAST ?let rightExpression
      outExpression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.lessThan
        !rightExpression
      }
    or
      $>$
      let operatorLocation = @location.here
      <expression_shift> !?ioAST ?let rightExpression
      let expression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.infEqual
        !rightExpression
      }
      outExpression = @prefixOperatorExpressionAST.init {
        !operatorLocation
        !.notOp
        !expression
      }
    end
  }

  #·····················································································································

  rule <expression_shift> ?!@ast ioAST !@expressionAST outExpression {
    <expression_addition> !?ioAST ?outExpression
    repeat
    while
      $<<$
      let operatorLocation = @location.here
      <expression_addition> !?ioAST ?let rightExpression
      outExpression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.leftShiftOp
        !rightExpression
      }
    while
      $>>$
      let operatorLocation = @location.here
      <expression_addition> !?ioAST ?let rightExpression
      outExpression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.rightShiftOp
        !rightExpression
      }
    end
  }

  #·····················································································································

  rule <expression_addition> ?!@ast ioAST !@expressionAST outExpression {
    <expression_product> !?ioAST ?outExpression
    repeat
    while
      $+$
      let operatorLocation = @location.here
      <expression_product> !?ioAST ?let rightExpression
      outExpression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.addOp
        !rightExpression
      }
    while
      $+%$
      let operatorLocation = @location.here
      <expression_product> !?ioAST ?let rightExpression
      outExpression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.addOpNoOvf
        !rightExpression
      }
    while
      $-$
      let operatorLocation = @location.here
      <expression_product> !?ioAST ?let rightExpression
      outExpression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.subOp
        !rightExpression
      }
    while
      $-%$
      let operatorLocation = @location.here
      <expression_product> !?ioAST ?let rightExpression
      outExpression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.subOpNoOvf
        !rightExpression
      }
    end
  }

  #·····················································································································

  rule <expression_product> ?!@ast ioAST !@expressionAST outExpression {
    <primary> !?ioAST ?outExpression
    repeat
    while
      $*$
      let operatorLocation = @location.here
      <primary> !?ioAST ?let rightExpression
      outExpression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.mulOp
        !rightExpression
      }
    while
      $*%$
      let operatorLocation = @location.here
      <primary> !?ioAST ?let rightExpression
      outExpression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.mulOpNoOvf
        !rightExpression
      }
    while
      $%$
      let operatorLocation = @location.here
      <primary> !?ioAST ?let rightExpression
      outExpression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.modOp
        !rightExpression
      }
    while
      $!%$
      let operatorLocation = @location.here
      <primary> !?ioAST ?let rightExpression
      outExpression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.modOpNoOvf
        !rightExpression
      }
    while
      $/$
      let operatorLocation = @location.here
      <primary> !?ioAST ?let rightExpression
      outExpression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.divOp
        !rightExpression
      }
    while
      $!/$
      let operatorLocation = @location.here
      <primary> !?ioAST ?let rightExpression
      outExpression = @omnibusInfixOperatorExpressionAST.init {
        !outExpression
        !operatorLocation
        !.divOpNoOvf
        !rightExpression
      }
    end
  }

  #·····················································································································

  rule <primary> ?!@ast ioAST !@expressionAST outExpression {
    $~$
    let operatorLocation = @location.here
    <primary> !?ioAST ?let expression
    outExpression = @prefixOperatorExpressionAST.init {
      !operatorLocation
      !.bitWiseComplement
      !expression
    }
  }

  #·····················································································································

  rule <primary> ?!@ast ioAST !@expressionAST outExpression {
    $not$
    let operatorLocation = @location.here
    <primary> !?ioAST ?let expression
    outExpression = @prefixOperatorExpressionAST.init {
      !operatorLocation
      !.notOp
      !expression
    }
  }

  #·····················································································································

  rule <primary> ?!@ast ioAST !@expressionAST outExpression {
    $-$
    let operatorLocation = @location.here
    <primary> !?ioAST ?let expression
    outExpression = @prefixOperatorExpressionAST.init {
      !operatorLocation
      !.minusOp
      !expression
    }
  }

  #·····················································································································

  rule <primary> ?!@ast ioAST !@expressionAST outExpression {
    $-%$
    let operatorLocation = @location.here
    <primary> !?ioAST ?let expression
    outExpression = @prefixOperatorExpressionAST.init {
      !operatorLocation
      !.minusNoOvf
      !expression
    }
  }

  #·····················································································································

  rule <primary> ?!@ast ioAST !@expressionAST outExpression {
    $($
    <expression> !?ioAST ?outExpression
    $)$
  }

  #·····················································································································

}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#—————————————————————————————————————————————————————————————————————————————————————————————————

abstract method @expressionAST noteExpressionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#—————————————————————————————————————————————————————————————————————————————————————————————————

abstract method @expressionAST analyzeExpression
  ?self:let @omnibusType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?optionalTargetType:let @omnibusType inTargetType
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !result:@objectIR outResult

#—————————————————————————————————————————————————————————————————————————————————————————————————
