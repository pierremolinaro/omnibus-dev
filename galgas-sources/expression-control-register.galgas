#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! AST                                                                                                                  
#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @registerInExpressionAST : @expressionAST {
  public var @controlRegisterLValueAST mControlRegisterLValue
  public var @lstring mFieldName # Empty string if no field access
}

#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SYNTAX                                                                                                               
#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <primary> ?!@ast ioAST !@expressionAST outExpression {
    <control_register_lvalue> !?ioAST ?let registerLValue
    let @lstring fieldName
    select
      fieldName = ["" nowhere]
    or
      $.$
      $identifier$ ?fieldName
    end
#    $)$
  #--- AST
    outExpression = @registerInExpressionAST.init {!registerLValue !fieldName}
  }

  #·····················································································································

}

#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! PRECEDENCE GRAPH FROM EXPRESSION                                                                                     
#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @registerInExpressionAST addDependenceEdgeForStaticExpression
  ?let @lstring inConstantName
  ?!@semanticTypePrecedenceGraph ioGraph
{
  [!?ioGraph addEdge !inConstantName !self.mControlRegisterLValue.mRegisterGroupName]
  [self.mControlRegisterLValue addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#! NOTE TYPES                                                                                                           
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @registerInExpressionAST noteExpressionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  [self.mControlRegisterLValue noteExpressionTypesInPrecedenceGraph !?ioGraph]
}

#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! SEMANTICS                                                                                                            
#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @registerInExpressionAST analyzeExpression
  ?self:let @omnibusType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?optionalTargetType:let @omnibusType unused inOptionalTargetType
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !result:@objectIR outResult
{
  [self.mControlRegisterLValue controlRegisterLValueSemanticAnalysis
    !writeAccess:false
    !self: inSelfType
    !routineAttributes: inRoutineAttributes
    !context: inContext
    !mode: inMode
    !?temporary: ioTemporaries
    !?staticEntityMap: ioStaticEntityMap
    !?variableMap: ioUniversalMap
    !?alloca: ioAllocaList
    !?instructionListIR: ioInstructionGenerationList
    ?registerType:let registerType
    ?registerBitCount:*
    ?sliceMap:let sliceMap
    ?llvmRegisterAddressName:let llvmRegisterAddressName
  ]
#-------------------- Load register
  getNewTempValue (!registerType !?ioTemporaries ?outResult)
  [!?ioInstructionGenerationList appendLoadIndirectVolatileRegister !outResult !llvmRegisterAddressName]
#-------------------- Field access
  if self.mFieldName.string != "" then
    [sliceMap searchKey
      !self.mFieldName
      ?let @llvmBinaryOperation accessOperator
      ?let @bigint accessRightOperand
      ?let @omnibusType resultType
    ]
    getNewTempValue (!resultType !?ioTemporaries ?let resultIR)
    [!?ioInstructionGenerationList appendBinaryOperation
      !resultIR
      ![outResult type]
      !self.mFieldName.location
      !outResult
      !accessOperator
      !.literalInteger {!type: compileTimeIntegerType () !value:accessRightOperand}
    ]
    outResult = resultIR
  end
}

#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
