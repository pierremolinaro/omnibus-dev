#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @terminusInfixOperatorEnumeration {
 case equal           # ==
 case strictInf       # <

 case booleanOr       # or
 case booleanXor      # xor

 case bitWiseAnd      # &
 case bitWiseOr       # |
 case xor             # ^

 case add             # +
# case sub             # -
 case mul             # *
 case div             # /
 case modulo          # mod

 case divNoOvf        # &/
 case moduloNoOvf     # &%
 case leftShift       # <<
 case rightShift      # >>
}

#·······················································································································

getter @terminusInfixOperatorEnumeration string -> @string {
 switch self
 case equal     : result = "=="
 case strictInf : result = "<"
 case booleanOr  : result = "or"
 case bitWiseAnd : result = "&"
 case bitWiseOr : result = "|"
 case xor       : result = "^"
 case booleanXor: result = "xor"
 case add       : result = "+"
 case mul       : result = "*"
 case div       : result = "/"
 case divNoOvf  : result = "!&"
 case modulo    : result = "%"
 case moduloNoOvf: result = "!%"
 case leftShift  : result = "<<"
 case rightShift : result = ">>"
 end
}

#·······················································································································

class @nativeInfixOperatorAST : @abstractDeclarationAST {
  @terminusInfixOperatorEnumeration mInfixOperator
  @location mInfixOperatorLocation
  @lstring mLeftOperandName
  @lstring mLeftTypeName
  @lstring mRightOperandName
  @lstring mRightTypeName
  @ctExpressionAST mExpression
  @lstring mResultTypeName
}

#·······················································································································

override getter @nativeInfixOperatorAST locationForErrorSignaling -> @location outLocation {
  outLocation = mInfixOperatorLocation
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <infixOperator> !@terminusInfixOperatorEnumeration outOperator {
    select
      $==$
      outOperator = .equal
    or
      $<$
      outOperator = .strictInf
    or
      $or$
      outOperator = .booleanOr
    or
      $&$
      outOperator = .bitWiseAnd
    or
      $|$
      outOperator = .bitWiseOr
    or
      $^$
      outOperator = .xor
    or
      $xor$
      outOperator = .booleanXor
    or
      $+$
      outOperator = .add
    or
      $*$
      outOperator = .mul
    or
      $/$
      outOperator = .div
    or
      $!/$
      outOperator = .divNoOvf
    or
      $%$
      outOperator = .modulo
    or
      $!%$
      outOperator = .moduloNoOvf
    or
      $<<$
      outOperator = .leftShift
    or
      $>>$
      outOperator = .rightShift
    end
  }
  
  #·····················································································································

  rule <declaration> ?!@ast ioAST {
    $compiletime$
    $func$
    $($
    $identifier$ ?let leftArgument
    $\$type$ ?let leftType
    $)$
    let operationLocation = @location.here
    <infixOperator> ?let infixOperator
    $($
    $identifier$ ?let rightArgument
    $\$type$ ?let rightType
    $)$
    $->$
    $\$type$ ?let resultType
    ${$
    <native_expression> ?let @ctExpressionAST expression
    $}$
    ioAST.mDeclarationListAST += !@nativeInfixOperatorAST.new {
      !infixOperator
      !operationLocation
      !leftArgument
      !leftType
      !rightArgument
      !rightType
      !expression
      !resultType
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @nativeInfixOperatorAST enterInPrecedenceGraph
  ?!@semanticTypePrecedenceGraph ioGraph
{
  let nodeName = @lstring.new {![mInfixOperator string] + "." + mLeftTypeName + "." + mRightTypeName !mInfixOperatorLocation}
  [!?ioGraph addNode !nodeName !self]
  [!?ioGraph addEdge !nodeName !mLeftTypeName]
  [!?ioGraph addEdge !nodeName !mRightTypeName]
  [!?ioGraph addEdge !nodeName !mResultTypeName]
}

#·······················································································································

override getter @nativeInfixOperatorAST keyRepresentationForErrorSignaling -> @string outRepresentation {
  outRepresentation = mLeftTypeName.string + " " + mInfixOperator + " " + mRightTypeName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  ENTER IN CONTEXT                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @nativeInfixOperatorAST enterInContext
  ?!context:@semanticContext ioContext
  ?!@declarationDecorationList unused ioDecoratedDeclarationList
  ?!@decoratedRegularRoutineList unused ioDecoratedRoutineList
  ?!@routineListIR unused ioRoutineListIR
  ?!staticlistValues:@staticListInitializationMap unused ioStaticListValueMap
  ?!staticEntityMap:@staticEntityMap unused ioStaticEntityMap
  ?!registerUserAccess:@controlRegisterUserAccesMapAST unused ioControlRegisterUserAccesMapAST
  ?!userDefinedTypes:@userLLVMTypeDefinitionListIR unused ioUserLLVMTypeDefinitionListIR
{
#--- Check types
  [@unifiedTypeMap-proxy makeProxy !?ioContext.mTypeMap !mLeftTypeName ?let leftType]
  [[leftType type] checkIsCompileTimeType !mLeftTypeName.location]
  [@unifiedTypeMap-proxy makeProxy !?ioContext.mTypeMap !mRightTypeName ?let rightType]
  [[rightType type] checkIsCompileTimeType !mRightTypeName.location]
  [@unifiedTypeMap-proxy makeProxy !?ioContext.mTypeMap !mResultTypeName ?let resultType]
  [[resultType type] checkIsCompileTimeType !mResultTypeName.location]
#--- Check native operator expression
  @ctCheckMap ctCheckMap = {}
  [!?ctCheckMap insertKey !mLeftOperandName]
  [!?ctCheckMap insertKey !mRightOperandName]
  [mExpression checkCompileTimeExpression !ctCheckMap]
#--- Enter in infix operator map  
  let key = @lstring.new {
    ![[leftType type] omnibusTypeDescriptionName] + " " + mInfixOperator + " " + [[rightType type] omnibusTypeDescriptionName]
    !mInfixOperatorLocation
  }
  let definition = @terminusNativeInfixOperator.new {
    !mInfixOperatorLocation
    !mLeftOperandName
    !mRightOperandName
    !mExpression
  }
  [!?ioContext.mTerminusInfixOperatorMap insertKey !key ![leftType type] ![rightType type] ![resultType type] !definition]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   INFIX OPERATOR MAP
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

map @terminusInfixOperatorMap {
  @omnibusType mLeftType
  @omnibusType mRightType
  @omnibusType mResultType
  @terminusInfixOperator mOperator
  insert insertKey error message "the '%K' infix operator is already declared in %L"
  search searchKey error message "there is no '%K' infix operator"
}

#·······················································································································

abstract class @terminusInfixOperator {}

#·······················································································································

class @terminusNativeInfixOperator : @terminusInfixOperator {
  @location mInfixOperatorLocation
  @lstring mLeftOperandName
  @lstring mRightOperandName
  @ctExpressionAST mExpression
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

