#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @getElementPtrIndexKind {
  case absolute (@uint index)
  case indirect (@operandIR index)
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @elementPtrList {
  @getElementPtrIndexKind mIndex
  @string mFieldName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

setter @instructionListIR appendGetElementPtrFromSelf
  ?let @objectInMemoryIR inTarget
  ?let @unifiedTypeMap-proxy inSelfType
  ?let @elementPtrList inElementList
{
  if inSelfType == .null then
    error .nowhere : "<<INTERNAL ERROR>>"
  end
  self += !@getElementPtrFromSelfIR.new {!inTarget !inSelfType !inElementList}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @getElementPtrFromSelfIR : @abstractInstructionIR {
  @objectInMemoryIR mTarget
  @unifiedTypeMap-proxy mSelfType
  @elementPtrList mElementList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @getElementPtrFromSelfIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  let llvmType = [mSelfType llvmTypeName]
  ioLLVMcode += "  " + [mTarget llvmName] + " = getelementptr inbounds "
  ioLLVMcode += llvmType + ", " + llvmType + "* %self, i32 0"
  for (idx *) in mElementList do
    switch idx
    case absolute (index) : ioLLVMcode += ", i32 " + index
    case indirect (index) : ioLLVMcode += ", " + [index.mType llvmTypeName] + " " + [index.mValue llvmName]
    end
  end
  ioLLVMcode += " ;"
  for (* fieldName) in mElementList do
    ioLLVMcode += " " + fieldName
  end
  ioLLVMcode += "\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @getElementPtrFromSelfIR enterAccessibleEntities
  ?!@accessibleEntities unused ioAccessibleEntities
  ?!@uint unused ioMaxBranchOfOnInstructions
{
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
