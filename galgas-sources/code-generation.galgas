#----------------------------------------------------------------------------------------------------------------------*
#                                                                                                                      *
#    C O D E    G E N E R A T I O N                                                                                    *
#                                                                                                                      *
#----------------------------------------------------------------------------------------------------------------------*

getter @literalStructureList literalValue -> @string result {
  result = "{"
  for (ls) in self do
    switch ls
    case unsignedInteger (@bigint value) :
      result += [value string]
    case signedInteger (@bigint value) :
      result += [value string]
    case string (* @string name) :
      result += name
    case structure (@literalStructureList valueList) :
      result += [valueList literalValue]
    end
  between result += ", "
  end
  result += "}"
}

#----------------------------------------------------------------------------------------------------------------------*

getter @objectInMemoryIR mangledName -> @string outResult {
  switch self
  case register (* @lstring name *) :
    outResult = mangledNameForRegister (!name:name.string)
  case globalVariable (* @string name *) :
    outResult = mangledNameForGlobalVariable (!name)
  case localValue (* @lstring name) :
    outResult = mangledNameForLocalVariable (!name.string)
  case globalConstant (* @string name) :
    outResult = mangledNameForGlobalVariable (!name)
  case localAccess (@lstring access) :
    outResult = access.string
  case literalStructure (@literalStructureList s) :
    outResult = [s literalValue]
  end
}

#----------------------------------------------------------------------------------------------------------------------*
#! @abstractExpressionIR
#----------------------------------------------------------------------------------------------------------------------*

abstract class @abstractExpressionIR {
}

#----------------------------------------------------------------------------------------------------------------------*
#! @abstractInstructionIR
#----------------------------------------------------------------------------------------------------------------------*

abstract class @abstractInstructionIR {
}

#----------------------------------------------------------------------------------------------------------------------*

abstract method @abstractInstructionIR llvmInstructionCode
  ?!@string ioCode
  ?let @generationContext inGenerationContext
  ?!@stringset ioIntrinsicsDeclarationSet

#----------------------------------------------------------------------------------------------------------------------*
#! @instructionListIR
#----------------------------------------------------------------------------------------------------------------------*

list @instructionListIR {
  @abstractInstructionIR mInstructionGeneration
}

#----------------------------------------------------------------------------------------------------------------------*
#! @abstractTypeIR
#----------------------------------------------------------------------------------------------------------------------*

abstract getter @abstractTypeIR headerCodeGenerationForType -> @string outCode

#----------------------------------------------------------------------------------------------------------------------*
#! codeGeneration
#----------------------------------------------------------------------------------------------------------------------*

func %once llvmSeparatorLine -> @string outResult {
  outResult  = ";-------------------------------------------------------------------------"
  outResult += "---------------------------------------------*\n"
}

#----------------------------------------------------------------------------------------------------------------------*

func llvmTitleComment ?@string inTitle -> @string outTitle {
  outTitle  = llvmSeparatorLine ()
  outTitle += ";    " + [inTitle stringByRightPadding !114!' '] + "*\n"
  outTitle += llvmSeparatorLine () + "\n"
}

#----------------------------------------------------------------------------------------------------------------------*

proc codeGeneration
  ?currentDir:let @string inCurrentDirectory
  ?outputFile:let @string inSourceFileName
  ?eof:let @location inEndOfSourceFileLocation
  ?intermediateCode:let @intermediateCodeStruct inIntermediateCodeStruct
  ?target:let @lstring inTargetName
  ?exceptionCodeType:let @unifiedTypeMap-proxy inExceptionCodeType
  ?exceptionLineType:let @unifiedTypeMap-proxy inExceptionLineType
{
  let homeDir = @string.homeDirectory + "/plm-products/"
#--- Create source directory (if does not exist)
  let productDirectory = homeDir + [[inSourceFileName stringByDeletingPathExtension] stringByReplacingStringByString !"/" !"+"]
  let sourceDirectory = productDirectory + "/sources"
  [sourceDirectory makeDirectory] ;
#--- Generate target files
  generateTarget (
    !currentDir:inCurrentDirectory
    !productDirectory:productDirectory
    !targetName:inTargetName
  )
#--- Generate LLVM file
  generateLLVMfile (
    !currentDir:inCurrentDirectory
    !productDir:productDirectory
    !eof:inEndOfSourceFileLocation
    !intermediateCode:inIntermediateCodeStruct
    !target:inTargetName
    !exceptionCodeType:inExceptionCodeType
    !exceptionLineType:inExceptionLineType
  )
}

#----------------------------------------------------------------------------------------------------------------------*

struct @generationContext {
  @string mExceptionCodeLLVMType
  @string mExceptionLineLLVMType
}

#----------------------------------------------------------------------------------------------------------------------*

proc generateLLVMfile
  ?currentDir:let @string inCurrentDirectory
  ?productDir:let @string inProductDirectory
  ?eof:let @location inEndOfSourceFileLocation
  ?intermediateCode:let @intermediateCodeStruct inIntermediateCodeStruct
  ?target:let @lstring inTargetName
  ?exceptionCodeType:let @unifiedTypeMap-proxy inExceptionCodeType
  ?exceptionLineType:let @unifiedTypeMap-proxy inExceptionLineType
{
  let @generationContext generationContext = .new {
    ![inExceptionCodeType llvmTypeName]
    ![inExceptionLineType llvmTypeName]
  }
  let sourceDirectory = inProductDirectory + "/sources"
  var code = ""
#----------------------------------------- Include target specific file
  code += llvmTitleComment (!"Target specific code")
  let targetSpecificFileContents = getTargetTextFile (
     !currentDir:inCurrentDirectory
     !from:inTargetName.string + "/target.ll"
  )
  code += targetSpecificFileContents
  code += llvmTitleComment (!"Static Strings")
  for () in inIntermediateCodeStruct.mGlobalLiteralStringMap do
    code += "static const char * " + mLiteralStringCname + " = " + [lkey.string utf8Representation] + " ;\n"
  end
  code += "\n"
#----------------------------------------- Global constants
  for globalConstant in inIntermediateCodeStruct.mGlobalConstantMap
    before code += llvmTitleComment (!"Global constants")
    do [globalConstant generateLLVM !?code]
    after code += "\n"
  end
#----------------------------------------- Global variables
  for globalVariable in inIntermediateCodeStruct.mGlobalVariableMap
    before code += llvmTitleComment (!"Global variables")
    do [globalVariable generateLLVM !?code]
    after code += "\n"
  end
#----------------------------------------- Boot
  @stringset intrinsicsDeclarationSet = {}
  code += llvmTitleComment (!"Boot")
  code += "define internal void @boot () nounwind {\n"
  for boot in inIntermediateCodeStruct.mBootList do
    [boot.mInstructionListIR instructionListLLVMCode !?code !generationContext !?intrinsicsDeclarationSet]
  end
  code += ";---\n"
  code += "  ret void\n"
  code += "}\n\n"
#----------------------------------------- Init
  code += llvmTitleComment (!"Init")
  code += "define internal void @init () nounwind {\n"
  for init in inIntermediateCodeStruct.mInitList do
    [init.mInstructionListIR instructionListLLVMCode !?code !generationContext !?intrinsicsDeclarationSet]
  end
  code += ";---\n"
  code += "  ret void\n"
  code += "}\n\n"
#----------------------------------------- Procedures
  for procedure in inIntermediateCodeStruct.mProcedureMapIR do
    [procedure llvmCodeGeneration !?code !generationContext !?intrinsicsDeclarationSet]
  end
#----------------------------------------- Functions
  for function in inIntermediateCodeStruct.mFunctionMapIR do
    [function implementationCodeGeneration !?code !generationContext !?intrinsicsDeclarationSet]
  end
#----------------------------------------- Exception
  if not [option plm_options.noExceptionGeneration value] then
    code += getTargetTextFile (
       !currentDir:inCurrentDirectory
       !from:inTargetName.string + "/target-exception.ll"
    )
    code += llvmTitleComment (!"Raise Exception internal")
    code += "define internal void @raise_exception_internal ("
    code += generationContext.mExceptionCodeLLVMType + " %in.CODE, "
    code += generationContext.mExceptionLineLLVMType + " %in.LINE) nounwind noreturn {\n"
    code += "  %" + mangledNameForLocalVariable (!"CODE") + " = alloca " + generationContext.mExceptionCodeLLVMType + "\n"
    code += "  store " + generationContext.mExceptionCodeLLVMType + " %in.CODE, "
         + generationContext.mExceptionCodeLLVMType + "* %" + mangledNameForLocalVariable (!"CODE") + "\n"
    code += "  %" + mangledNameForLocalVariable (!"LINE") + " = alloca " + generationContext.mExceptionLineLLVMType + "\n"
    code += "  store " + generationContext.mExceptionLineLLVMType + " %in.LINE, "
         + generationContext.mExceptionLineLLVMType + "* %" + mangledNameForLocalVariable (!"LINE") + "\n"
    [inIntermediateCodeStruct.mExceptionSetupInstructionListIR instructionListLLVMCode
      !?code
      !generationContext
      !?intrinsicsDeclarationSet
    ]
    code += "  br label %exception.loop\n\n"
    code += "exception.loop:\n"
    [inIntermediateCodeStruct.mExceptionLoopInstructionListIR instructionListLLVMCode
      !?code
      !generationContext
      !?intrinsicsDeclarationSet
    ]
    code += "  br label %exception.loop\n"
    code += "}\n\n"
  end
#----------------------------------------- Intrinsics
  if [intrinsicsDeclarationSet count] > 0 then
    code += llvmTitleComment (!"LLVM intrinsics")
    for (s) in intrinsicsDeclarationSet do
      code += s + "\n"
    end
    code += "\n"
  end
#----------------------------------------- Write LLVM file
  code += llvmSeparatorLine ()
  [code writeToFileWhenDifferentContents !sourceDirectory + "/source-plm.ll" ?*]
#----------------------------------------- Perform LLVM compilation
  if (@uint.errorCount == 0) && not [option plm_options.compileOnly value] then
    let script = if [option plm_options.performFlashing value] then "run" else "build" end
    let fullScript = "python " + inProductDirectory + "/" + script + ".py"
    @sint result = [fullScript system]
    if result != 0S then
      error inEndOfSourceFileLocation: "error during LLVM compilation or flashing"
    end
  end
}

#----------------------------------------------------------------------------------------------------------------------*

