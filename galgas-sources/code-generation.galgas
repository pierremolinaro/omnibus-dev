#----------------------------------------------------------------------------------------------------------------------*
#                                                                                                                      *
#    C O D E    G E N E R A T I O N                                                                                    *
#                                                                                                                      *
#----------------------------------------------------------------------------------------------------------------------*

getter @objectInMemoryIR mangledName -> @string outResult {
  switch self
  case register (* @lstring name *) :
    outResult = mangledNameForRegister (!name:name.string)
  case globalVariable (* @string name *) :
    outResult = mangledNameForGlobalVariable (!name)
  case localValue (* @string name) :
    outResult = mangledNameForLocalVariable (!name)
  case globalConstant (* @string name) :
    outResult = mangledNameForGlobalVariable (!name)
  case fieldAccess (@unifiedTypeMap-proxy unused objectType
                    @string unused objectName
                    @unifiedTypeMap-proxy unused fielfType
                    @uint unused fieldIndex) :
    outResult = "<<@objectInMemoryIR mangledName:fieldAccess>>"
  end
}

#----------------------------------------------------------------------------------------------------------------------*
#! @abstractExpressionIR
#----------------------------------------------------------------------------------------------------------------------*

abstract class @abstractExpressionIR {
}

#----------------------------------------------------------------------------------------------------------------------*
#! @generationAdds
#----------------------------------------------------------------------------------------------------------------------*

struct @generationAdds {
  @stringset mIntrinsicsDeclarationSet
  @staticStringMap mStaticStringMap
}

#----------------------------------------------------------------------------------------------------------------------*
#! @abstractInstructionIR
#----------------------------------------------------------------------------------------------------------------------*

abstract class @abstractInstructionIR {
}

#----------------------------------------------------------------------------------------------------------------------*

abstract method @abstractInstructionIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext inGenerationContext
  ?!@generationAdds ioGenerationAdds

#----------------------------------------------------------------------------------------------------------------------*
#! @instructionListIR
#----------------------------------------------------------------------------------------------------------------------*

list @instructionListIR {
  @abstractInstructionIR mInstructionGeneration
}

#----------------------------------------------------------------------------------------------------------------------*
#! codeGeneration
#----------------------------------------------------------------------------------------------------------------------*

func %once llvmSeparatorLine -> @string outResult {
  outResult  = ";-------------------------------------------------------------------------"
  outResult += "---------------------------------------------*\n"
}

#----------------------------------------------------------------------------------------------------------------------*

func llvmTitleComment ?@string inTitle -> @string outTitle {
  outTitle  = llvmSeparatorLine ()
  outTitle += ";    " + [inTitle stringByRightPadding !114!' '] + "*\n"
  outTitle += llvmSeparatorLine () + "\n"
}

#----------------------------------------------------------------------------------------------------------------------*

func %once asSeparatorLine -> @string outResult {
  outResult  = "@-------------------------------------------------------------------------"
  outResult += "---------------------------------------------*\n"
}

#----------------------------------------------------------------------------------------------------------------------*

func asTitleComment ?@string inTitle -> @string outTitle {
  outTitle  = asSeparatorLine ()
  outTitle += "@    " + [inTitle stringByRightPadding !114!' '] + "*\n"
  outTitle += asSeparatorLine () + "\n"
}

#----------------------------------------------------------------------------------------------------------------------*

proc codeGeneration
  ?currentDir:let @string inCurrentDirectory
  ?outputFile:let @string inSourceFileName
  ?eof:let @location inEndOfSourceFileLocation
  ?intermediateCode:let @intermediateCodeStruct inIntermediateCodeStruct
  ?typeMap:let @unifiedTypeMap inTypeMap
  ?target:let @lstring inTargetName
  ?exceptionCodeType:let @unifiedTypeMap-proxy inExceptionCodeType
  ?exceptionLineType:let @unifiedTypeMap-proxy inExceptionLineType
  ?sourceFileSet:let @stringset inSourceFileAbsolutePathSet
{
  let homeDir = @string.homeDirectory + "/plm-products/"
#--- Create source directory (if does not exist)
  let productDirectory = homeDir + [[inSourceFileName stringByDeletingPathExtension] stringByReplacingStringByString !"/" !"+"]
  let sourceDirectory = productDirectory + "/sources"
  [sourceDirectory makeDirectory] ;
#--- Generate target files
  generateTarget (
    !currentDir:inCurrentDirectory
    !productDirectory:productDirectory
    !targetName:inTargetName
  )
#--- Generate LLVM file
  generateLLVMfile (
    !currentDir:inCurrentDirectory
    !productDir:productDirectory
    !eof:inEndOfSourceFileLocation
    !intermediateCode:inIntermediateCodeStruct
    !typeMap:inTypeMap
    !target:inTargetName
    !exceptionCodeType:inExceptionCodeType
    !exceptionLineType:inExceptionLineType
    !sourceFileSet:inSourceFileAbsolutePathSet
  )
}

#----------------------------------------------------------------------------------------------------------------------*

struct @generationContext {
  @string mExceptionCodeLLVMType
  @string mExceptionLineLLVMType
}

#----------------------------------------------------------------------------------------------------------------------*

proc generateLLVMfile
  ?currentDir:let @string inCurrentDirectory
  ?productDir:let @string inProductDirectory
  ?eof:let @location inEndOfSourceFileLocation
  ?intermediateCode:let @intermediateCodeStruct inIntermediateCodeStruct
  ?typeMap:let @unifiedTypeMap inTypeMap
  ?target:let @lstring inTargetName
  ?exceptionCodeType:let @unifiedTypeMap-proxy inExceptionCodeType
  ?exceptionLineType:let @unifiedTypeMap-proxy inExceptionLineType
  ?sourceFileSet:let @stringset inSourceFileAbsolutePathSet
{
  @generationAdds generationAdds = .default
  generationAdds.mStaticStringMap = inIntermediateCodeStruct.mStaticStringMap
  let @generationContext generationContext = .new {
    ![inExceptionCodeType llvmTypeName]
    ![inExceptionLineType llvmTypeName]
  }
  let sourceDirectory = inProductDirectory + "/sources"
  var llvmCode = ""
  var asCode = ""
#----------------------------------------- Include assembler specific file
  asCode += asTitleComment (!"Target specific code")
  asCode += getTargetTextFile (
     !currentDir:inCurrentDirectory
     !from:inTargetName.string + "/target.s"
  )
  asCode += "\n"
#----------------------------------------- Include LLVM specific file
  llvmCode += llvmTitleComment (!"Target specific code")
  llvmCode += getTargetTextFile (
     !currentDir:inCurrentDirectory
     !from:inTargetName.string + "/target.ll"
  )
  llvmCode += "\n"
#----------------------------------------- Structure types
  llvmCode += llvmTitleComment (!"Structure types")
  declareLLVMstructures (!inTypeMap !?llvmCode)
  llvmCode += "\n"
#----------------------------------------- Global constants
  for globalConstant in inIntermediateCodeStruct.mGlobalConstantMap
    before llvmCode += llvmTitleComment (!"Global constants")
    do [globalConstant generateLLVM !?llvmCode]
    after llvmCode += "\n"
  end
#----------------------------------------- Global variables
  for globalVariable in inIntermediateCodeStruct.mGlobalVariableMap
    before llvmCode += llvmTitleComment (!"Global variables")
    do [globalVariable generateLLVM !?llvmCode]
    after llvmCode += "\n"
  end
#----------------------------------------- Boot
  llvmCode += llvmTitleComment (!"Boot")
  llvmCode += "define internal void @boot () nounwind {\n"
  for boot in inIntermediateCodeStruct.mBootList do
    [boot.mInstructionListIR instructionListLLVMCode !?llvmCode !generationContext !?generationAdds]
  end
  llvmCode += ";---\n"
  llvmCode += "  ret void\n"
  llvmCode += "}\n\n"
#----------------------------------------- Init
  [inIntermediateCodeStruct.mInitList generateLLVMinitCode
    !?llvmCode
    !inIntermediateCodeStruct.mProcedureMapIR
    !generationContext
    !?generationAdds
  ]
#----------------------------------------- Procedures
  for procedure in inIntermediateCodeStruct.mProcedureMapIR do
    [procedure llvmCodeGeneration !?llvmCode !?asCode !generationContext !?generationAdds]
  end
#----------------------------------------- Functions
  for function in inIntermediateCodeStruct.mFunctionMapIR do
    [function implementationCodeGeneration !?llvmCode !generationContext !?generationAdds]
  end
#----------------------------------------- Exception
  if not [option plm_options.noExceptionGeneration value] then
    llvmCode += getTargetTextFile (
       !currentDir:inCurrentDirectory
       !from:inTargetName.string + "/target-exception.ll"
    )
    llvmCode += llvmTitleComment (!"Raise Exception internal")
    llvmCode += "define internal void @raise_exception_internal ("
    llvmCode += generationContext.mExceptionLineLLVMType + " %in.LINE, "
    llvmCode += generationContext.mExceptionCodeLLVMType + " %in.CODE, "
    llvmCode += "i8* %in.FILE) nounwind noreturn {\n"
  #--- First argument: line
    llvmCode += "  %" + mangledNameForLocalVariable (!"LINE") + " = alloca "
    llvmCode += generationContext.mExceptionLineLLVMType + "\n"
    llvmCode += "  store " + generationContext.mExceptionLineLLVMType + " %in.LINE, "
    llvmCode += generationContext.mExceptionLineLLVMType + "* %" + mangledNameForLocalVariable (!"LINE") + "\n"
  #--- Second argument : code
    llvmCode += "  %" + mangledNameForLocalVariable (!"CODE")
    llvmCode += " = alloca " + generationContext.mExceptionCodeLLVMType + "\n"
    llvmCode += "  store " + generationContext.mExceptionCodeLLVMType + " %in.CODE, "
    llvmCode += generationContext.mExceptionCodeLLVMType + "* %" + mangledNameForLocalVariable (!"CODE") + "\n"
  #--- Third argument : file    
    llvmCode += "  %" + mangledNameForLocalVariable (!"FILE") + " = alloca i8*\n"
    llvmCode += "  store i8* %in.FILE, i8** %" + mangledNameForLocalVariable (!"FILE") + "\n"
    [inIntermediateCodeStruct.mExceptionSetupInstructionListIR instructionListLLVMCode
      !?llvmCode
      !generationContext
      !?generationAdds
    ]
    llvmCode += "  br label %exception.loop\n\n"
    llvmCode += "exception.loop:\n"
    [inIntermediateCodeStruct.mExceptionLoopInstructionListIR instructionListLLVMCode
      !?llvmCode
      !generationContext
      !?generationAdds
    ]
    llvmCode += "  br label %exception.loop\n"
    llvmCode += "}\n\n"
  #--- File specific routines
    for (filePath) in inSourceFileAbsolutePathSet do
      [!?generationAdds.mStaticStringMap findOrAddStaticString
        ![[filePath lastPathComponent] stringByDeletingPathExtension]
        ?let staticStringIndex
      ]
      let routineName = "@raise_exception." + staticStringIndex
      llvmCode += llvmTitleComment (!routineName + " (" + [filePath lastPathComponent] + ")")
      llvmCode += "define internal void " + routineName + " (i32 %inSourceLine, i32 %inCode) nounwind noreturn {\n"
      llvmCode += "  %str.FILE = load i8*, i8** @string." + staticStringIndex + "\n"
      llvmCode += "  call void @raise_exception (i32 %inSourceLine, i32 %inCode, i8* %str.FILE)\n"
      llvmCode += "  unreachable\n"
      llvmCode += "}\n\n"
    end
  end
#----------------------------------------- Intrinsics
  if [generationAdds.mIntrinsicsDeclarationSet count] > 0 then
    llvmCode += llvmTitleComment (!"LLVM intrinsics")
    for (s) in generationAdds.mIntrinsicsDeclarationSet do
      llvmCode += s + "\n"
    end
    llvmCode += "\n"
  end
#----------------------------------------- Static Strings
  if [generationAdds.mStaticStringMap count] > 0 then
    llvmCode += llvmTitleComment (!"Static strings")
    for () in generationAdds.mStaticStringMap do
      let lgStr = [[lkey.string length] + 1 string]
      llvmCode += literalCharacterArrayName (!mIndex) + " = private unnamed_addr constant ["
      llvmCode += lgStr + " x i8] c\""
      llvmCode += [lkey.string utf8RepresentationWithoutDelimiters]
      llvmCode += "\\00\", align 1\n"
      llvmCode += literalStringName (!mIndex) + " = private constant i8* getelementptr inbounds (["
      llvmCode += lgStr + " x i8], [" + lgStr + " x i8]* "
      llvmCode += literalCharacterArrayName (!mIndex) + ", i32 0, i32 0), align 4\n\n"
    end
  end
#----------------------------------------- Write LLVM file
  llvmCode += llvmSeparatorLine ()
  [llvmCode writeToFileWhenDifferentContents !sourceDirectory + "/source-plm.ll" ?*]
#----------------------------------------- Write assembler file
  asCode += asSeparatorLine ()
  [asCode writeToFileWhenDifferentContents !sourceDirectory + "/source-plm.s" ?*]
#----------------------------------------- Perform compilation
  if (@uint.errorCount == 0) && not [option plm_options.compileOnly value] then
    let script = if [option plm_options.performFlashing value] then "run" else "build" end
    let fullScript = "python " + inProductDirectory + "/" + script + ".py"
    @sint result = [fullScript system]
    if result != 0S then
      error inEndOfSourceFileLocation: "error during LLVM compilation or flashing"
    end
  end
}

#----------------------------------------------------------------------------------------------------------------------*

