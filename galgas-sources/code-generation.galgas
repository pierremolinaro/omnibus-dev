#----------------------------------------------------------------------------------------------------------------------*
#                                                                                                                      *
#    I N T E R M E D I A T E    C O D E    G E N E R A T I O N                                                         *
#                                                                                                                      *
#----------------------------------------------------------------------------------------------------------------------*
#----------------------------------------------------------------------------------------------------------------------*
#! @abstractExpressionGeneration
#----------------------------------------------------------------------------------------------------------------------*

abstract class @abstractExpressionGeneration {
}

#----------------------------------------------------------------------------------------------------------------------*

abstract getter @abstractExpressionGeneration expressionCode -> @string outCode

#----------------------------------------------------------------------------------------------------------------------*
#! @inLineExpressionGeneration
#----------------------------------------------------------------------------------------------------------------------*

class @inLineExpressionGeneration : @abstractExpressionGeneration {
  @string mCode
}

#----------------------------------------------------------------------------------------------------------------------*

override getter @inLineExpressionGeneration expressionCode -> @string outCode {
  outCode = mCode
}

#----------------------------------------------------------------------------------------------------------------------*
#! @abstractInstructionGeneration
#----------------------------------------------------------------------------------------------------------------------*

abstract class @abstractInstructionGeneration {
}

#----------------------------------------------------------------------------------------------------------------------*

abstract getter @abstractInstructionGeneration instructionCode -> @string outCode

#----------------------------------------------------------------------------------------------------------------------*
#! @instructionGenerationList
#----------------------------------------------------------------------------------------------------------------------*

list @instructionGenerationList {
  @abstractInstructionGeneration mInstructionGeneration
}

#----------------------------------------------------------------------------------------------------------------------*
#! @abstractGeneration
#----------------------------------------------------------------------------------------------------------------------*

abstract getter @abstractTypeIR headerCodeGenerationForType -> @string outCode

#----------------------------------------------------------------------------------------------------------------------*

abstract class @abstractGeneration {
}

#----------------------------------------------------------------------------------------------------------------------*

abstract getter @abstractGeneration headerCodeGeneration -> @string outCode

#----------------------------------------------------------------------------------------------------------------------*

abstract getter @abstractGeneration implementationCodeGeneration -> @string outCode

#----------------------------------------------------------------------------------------------------------------------*
#! @orderedGenerationList
#----------------------------------------------------------------------------------------------------------------------*

list @orderedGenerationList {
  @abstractGeneration mGeneration
}

#----------------------------------------------------------------------------------------------------------------------*

func %once separatorLine -> @string outResult {
  outResult  = "//------------------------------------------------------------------------"
  outResult += "---------------------------------------------*\n"
}

#----------------------------------------------------------------------------------------------------------------------*
#  @registerMapIR mRegisterMap
#  @globalVariableMapIR mGlobalVariableMap
#  @globalConstantMapIR mGlobalConstantMap
#  @typeMapIR mTypeMapIR
#  @procedureMapIR mProcedureMapIR
#  @functionMapIR mFunctionMapIR

proc codeGeneration
  ?outputFile:let @string inSourceFileName
  ?generation:let @orderedGenerationList unused inGenerationList
  ?intermediateCode:let @intermediateCodeStruct inIntermediateCodeStruct
  ?target:let @lstring inTargetName
{
# log "ioIntermediateCodeStruct.mProcedureMapIR" : ioIntermediateCodeStruct.mProcedureMapIR
#--- Create source directory (if does not exist)
  let targetDirectory = [inSourceFileName stringByDeletingPathExtension] + "+" + inTargetName
  let sourceDirectory = targetDirectory + "/sources"
  [sourceDirectory makeDirectory] ;
#--- Generate header file
  let baseName = sourceDirectory + "/plm"
  var header = "#ifndef PLM_HEADER_DEFINED\n"
  header += "#define PLM_HEADER_DEFINED\n\n"
  header += separatorLine () + "\n"
  header += "#include <stdint.h>\n"
  header += "#include <stddef.h>\n\n"
  for register in inIntermediateCodeStruct.mRegisterMap do
    header += [register headerCodeGenerationForRegister]
  end
  for globalConstant in inIntermediateCodeStruct.mGlobalConstantMap do
    header += [globalConstant headerCodeGeneration]
  end
  for @typeMapIR-element type in inIntermediateCodeStruct.mTypeMapIR do
    header += [type.mType headerCodeGenerationForType]
  end
  for procedure in inIntermediateCodeStruct.mProcedureMapIR do
    header += [procedure headerCodeGeneration]
  end
  for function in inIntermediateCodeStruct.mFunctionMapIR do
    header += [function headerCodeGeneration]
  end
  header += separatorLine () + "\n"
  header += "#endif\n"
  [header writeToFileWhenDifferentContents !baseName + ".h" ?*]
#--- Generate C file
  var code = "#include \"plm.h\"\n\n"
  code += separatorLine ()
  code += "//   Static Strings                                                                                                    *\n"
  code += separatorLine () + "\n"
  for () in inIntermediateCodeStruct.mGlobalLiteralStringMap do
    code += "static const char * " + mLiteralStringCname + " = " + [lkey.string utf8Representation] + " ;\n"
  end
  code += "\n"
  for globalVariable in inIntermediateCodeStruct.mGlobalVariableMap do
    code += [globalVariable implementationCodeGeneration]
  end
  for procedure in inIntermediateCodeStruct.mProcedureMapIR do
    code += [procedure implementationCodeGeneration]
  end
  for function in inIntermediateCodeStruct.mFunctionMapIR do
    code += [function implementationCodeGeneration]
  end
  code += separatorLine ()
  [code writeToFileWhenDifferentContents !baseName + ".c" ?*]
#--- Generate target files
  generateTargets (
    !targetDirectory:targetDirectory
    !targetName:inTargetName
  )
}

#----------------------------------------------------------------------------------------------------------------------*

