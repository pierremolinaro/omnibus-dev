#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @guardedCommandAST {
  case boolean (@bool isExitCommand # if false, it is an "exit" command
                @expressionAST expression
                @location endOfExpression)

  case boolAndSync (@bool isExitCommand # if false, it is an "exit" command
                    @expressionAST expression
                    @bool warnsOnStaticExpression
                    @location endOfExpression
                    @lbool usesSelf
                    @lstringlist nameList
                    @effectiveArgumentListAST effectiveParameterListAST)
}

#·······················································································································

list @syncInstructionBranchListAST {
  @guardedCommandAST mGuardedCommand
  @instructionListAST mInstructionList
  @location mEndOfBranch
}

#·······················································································································

class @syncInstructionAST : @instructionAST {
  @syncInstructionBranchListAST mBranchList
  @location mEndOf_on_instruction
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension plm_syntax {

  #·····················································································································

  rule <guarded_command> ?!@ast ioAST !@guardedCommandAST outGuardedCommand {
    @expressionAST expression
    @location endOfExp
    @bool warnsOnStaticExpression
    select
      $($
      <expression> !?ioAST ?expression
      endOfExp = .here
      $)$
      warnsOnStaticExpression = true
    or
      expression = @literalBooleanInExpressionAST.new {!true}
      endOfExp = .here
      warnsOnStaticExpression = false
    end
    @lbool usesSelf
    select
      usesSelf = .new {!false !.here}
    or
      $self$
      usesSelf = .new {!true !.here}
      $.$
    end
    @lstringlist nameList = {}
    repeat
      $identifier$ ?var name
      nameList += !name
    while
      $.$
    end
    <effective_parameters> !?ioAST ?let effectiveParameterList ?*
    @bool isExitCommand
    select
      $exit$
      isExitCommand = false
    or
      isExitCommand = true
    end
    outGuardedCommand = .boolAndSync {
      !isExitCommand:isExitCommand
      !expression:expression
      !warnsOnStaticExpression:warnsOnStaticExpression
      !endOfExpression:endOfExp
      !usesSelf:usesSelf
      !nameList:nameList
      !effectiveParameterListAST:effectiveParameterList
    }
  }
  
  #·····················································································································

  rule <guarded_command> ?!@ast ioAST !@guardedCommandAST outGuardedCommand {
    $($
    <expression> !?ioAST ?let expression
    let endOfExp = @location.here
    $)$
    @bool isExitCommand
    select
      $exit$
      isExitCommand = false
    or
      isExitCommand = true
    end
    outGuardedCommand = .boolean {
      !isExitCommand:isExitCommand
      !expression:expression
      !endOfExpression:endOfExp
    }
  }
  
  #·····················································································································

  rule <instruction> ?!@ast ioAST ?!@instructionListAST ioInstructionList {
    $event$
    let startLocation = @location.here
    @syncInstructionBranchListAST instructionBranchList = {}
    repeat
      <guarded_command> !?ioAST ?let @guardedCommandAST guardedCommand
      ${$
      <instructionList> !?ioAST ?let instructionList
      instructionBranchList += !guardedCommand !instructionList !.here
      $}$
    while
      $else$
      $event$
    end
    ioInstructionList += !@syncInstructionAST.new {!startLocation !instructionBranchList !.here}
  }
  
  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @syncInstructionAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  for (* instructionList *) in mBranchList do
    [instructionList noteInstructionListTypesInPrecedenceGraph !?ioGraph]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @syncInstructionAST instructionSemanticAnalysis
  ?self:let @PLMType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
#-------------------- Guards
  @guardedCommandIRList guardedCommandIRList = {}
  for (guardedCommand instructionList endOfBranch) in mBranchList do
    switch guardedCommand
    case boolean (isContinue expression endOfExpression) :
      @instructionListIR guardInstructionGenerationList = {}
      [expression analyzeExpression
        !self:inSelfType
        !routineAttributes: inRoutineAttributes | .guard
        !optionalTargetType:voidType ()
        !context:inContext
        !mode:inMode
        !?temporary:ioTemporaries
        !?staticEntityMap:ioStaticEntityMap
        !?variableMap:ioUniversalMap
        !?alloca:ioAllocaList
        !?instructionListIR:guardInstructionGenerationList
        ?result:@objectIR sourceOperand
      ]
      if [sourceOperand plmTypeDescriptionName] != boolTypeName () then
        error endOfExpression : "guarded expression should be boolean"
      elsif [sourceOperand isStatic] then
        warning endOfExpression : "guarded expression is static"
      end
      [!?guardInstructionGenerationList appendLoadWhenReference
        !?ioTemporaries
        !?sourceOperand
      ]
      guardedCommandIRList += !.booleanGuard {
        !isExitCommand:isContinue
        !instructionGenerationList:guardInstructionGenerationList
        !result:sourceOperand
      }
    case boolAndSync (isContinue exp warnsOnStaticExpression endOfExp usesSelf nameList parameterList) :
      @instructionListIR boolExpInstructionGenerationList = {}
    #--- Analyze guard boolean expression
      [exp analyzeExpression
        !self:inSelfType
        !routineAttributes:.guard
        !optionalTargetType:voidType ()
        !context:inContext
        !mode:inMode
        !?temporary:ioTemporaries
        !?staticEntityMap:ioStaticEntityMap
        !?variableMap:ioUniversalMap
        !?alloca:ioAllocaList
        !?instructionListIR:boolExpInstructionGenerationList
        ?result:@objectIR boolExpressionResult
      ]
      if [boolExpressionResult plmTypeDescriptionName] != boolTypeName () then
        error endOfExp : "guarded expression should be boolean"
      elsif warnsOnStaticExpression && [boolExpressionResult isStatic] then
        warning endOfExp : "guarded expression is static"
      end
      [!?boolExpInstructionGenerationList appendLoadWhenReference
        !?ioTemporaries
        !?boolExpressionResult
      ]
    #--- Analyze synchronization
      @instructionListIR guardInstructionGenerationList = {}
      @procCallEffectiveParameterListIR guardEffectiveParameterListIR
      @lstring guardMangledName
      if usesSelf.bool then # self.p1.p2. … (…)
        if [inSelfType.kind isVoid] then
          error usesSelf : "'self' is not available in this context" : guardEffectiveParameterListIR, guardMangledName
        else
          var propertyList = nameList
          [!?propertyList popLast ?let guardName]
          @objectIR currentObject = .reference {!type:inSelfType !llvmName:llvmNameForSelf ()}
          for (propertyName) in propertyList do
            handlePropertyAccessInExpression (
              !?currentObject
              !propertyName
              !inMode
              !?temporary:ioTemporaries
              !?alloca: ioAllocaList
              !?instructionListIR:ioInstructionGenerationList
            )
          end
          analyzeGuardCall (
            !self:inSelfType
            !routineAttributes:inRoutineAttributes
            !receiver:currentObject
            !guardName:guardName
            !effective:parameterList
            !context:inContext
            !requiredMode:inMode
            !?temporary:ioTemporaries
            !?staticEntityMap:ioStaticEntityMap
            !?variableMap:ioUniversalMap
            !?alloca:ioAllocaList
            !?instructionListIR:guardInstructionGenerationList
            ?effectiveIR:guardEffectiveParameterListIR
            ?guardMangledName: guardMangledName
          )
        end
      elsif [nameList length] == 1 then # Standalone guard: g (…)
        error [nameList mValueAtIndex !0]
        : "Standalone guard are not handled"
        : guardMangledName, guardEffectiveParameterListIR
      else # Global variable method, or driver method: g.h. … (…)
        var propertyList = nameList
        [!?propertyList popFirst ?let globalReceiverName]
        [!?propertyList popLast ?let guardName]
        [ioUniversalMap searchValuedObject
          !globalReceiverName
          !inMode
          !ioTemporaries.mInitializedDriverSet
          ?var currentObjectIR
        ]
        for (propertyName) in propertyList do
          handlePropertyAccessInExpression (
            !?currentObjectIR
            !propertyName
            !inMode
            !?temporary:ioTemporaries
            !?alloca: ioAllocaList
            !?instructionListIR:ioInstructionGenerationList
          )
        end
        analyzeGuardCall (
          !self:inSelfType
          !routineAttributes:inRoutineAttributes
          !receiver:currentObjectIR
          !guardName:guardName
          !effective:parameterList
          !context:inContext
          !requiredMode:inMode
          !?temporary:ioTemporaries
          !?staticEntityMap:ioStaticEntityMap
          !?variableMap:ioUniversalMap
          !?alloca:ioAllocaList
          !?instructionListIR:guardInstructionGenerationList
          ?effectiveIR:guardEffectiveParameterListIR
          ?guardMangledName: guardMangledName
        )
      end
      if [boolExpressionResult isLiteralInteger] then
        [boolExpressionResult literalInteger ?1* ?value:let @bigint value]
        if value == 1G then
          guardedCommandIRList += !.sync {
            !isExitCommand:isContinue
            !guardMangledName:guardMangledName.string
            !guardInstructionGenerationList:guardInstructionGenerationList
            !effectiveParameterListIR:guardEffectiveParameterListIR
          }
        else
          error guardMangledName : "false guard not handled yet"
        end
      else
        guardedCommandIRList += !.boolAndSync {
          !isExitCommand:isContinue
          !boolExpInstructionGenerationList:boolExpInstructionGenerationList
          !boolExpResult:boolExpressionResult
          !guardMangledName:guardMangledName.string
          !guardInstructionGenerationList:guardInstructionGenerationList
          !effectiveParameterListIR:guardEffectiveParameterListIR
        }
      end
    end
  end
#-------------------- Instruction lists
  [!?ioUniversalMap openOverrideForSelectBlock]
  @syncInstructionBranchListIR syncInstructionBranchListIR = {}
  for (* instructionList endOfBranch) in mBranchList, (guardedCommandIR) in guardedCommandIRList do
    @instructionListIR branchInstructionGenerationList = {}
    [instructionList analyzeBranchInstructionList
      !self:inSelfType
      !routineAttributes:inRoutineAttributes
      !endOfInstructionList:endOfBranch
      !context:inContext
      !mode:inMode
      !?temporary:ioTemporaries
      !?staticEntityMap:ioStaticEntityMap
      !?variableMap:ioUniversalMap
      !?alloca:ioAllocaList
      !?instructionListIR:branchInstructionGenerationList
    ]
    syncInstructionBranchListIR += !guardedCommandIR !branchInstructionGenerationList
  end
  [!?ioUniversalMap closeOverride !mEndOf_on_instruction]
#-------------------- Code generation
  ioInstructionGenerationList += !@syncInstructionIR.new {!mInstructionLocation !syncInstructionBranchListIR}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc analyzeGuardCall
  ?self:let @PLMType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?receiver:let @objectIR inReceiver
  ?guardName:let @lstring inGuardName
  ?effective:let @effectiveArgumentListAST inEffectiveParameterList
  ?context:let @semanticContext inContext
  ?requiredMode:let @mode inRequiredMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !effectiveIR:@procCallEffectiveParameterListIR outEffectiveParameterListIR
  !guardMangledName:@lstring outGuardMangledName
{
#  let guardMangledName = guardUserMangledNameFromCall (![inReceiver type].llvmBaseTypeName !inGuardName !inEffectiveParameterList)
  let guardMangledName = [inEffectiveParameterList mangledName !inGuardName]
  let guardMap = [[inReceiver type] guardMap]
  [guardMap searchKey !guardMangledName ?let isPublic ?let @routineTypedSignature formalSignature ?let guardRoutineUserLLVMName ?*]
#--- Check accessibility
  if not isPublic then
    error inGuardName : "this guard is not public": outGuardMangledName, outEffectiveParameterListIR
  else  
    outGuardMangledName = guardRoutineUserLLVMName
  #--- Analyze effective parameters
    outEffectiveParameterListIR = {!.outputInput !inReceiver}
    analyzeEffectiveParameters (
      !self:inSelfType
      !routineAttributes:inRoutineAttributes
      !formal:formalSignature
      !effective:inEffectiveParameterList
      !errorLocation:inGuardName.location
      !context:inContext
      !requiredMode:inRequiredMode
      !?temporary:ioTemporaries
      !?staticEntityMap:ioStaticEntityMap
      !?variableMap:ioUniversalMap
      !?alloca:ioAllocaList
      !?instructionListIR:ioInstructionGenerationList
      !?effectiveIR:outEffectiveParameterListIR
    )
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  CODE GENERATION                                                                                                      
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @guardedCommandIR {
  case booleanGuard (@bool isExitCommand # if false, it is an "exit" command
                     @instructionListIR instructionGenerationList
                     @objectIR result)

  case sync        (@bool isExitCommand # if false, it is an "exit" command
                    @string guardMangledName
                    @instructionListIR guardInstructionGenerationList
                    @procCallEffectiveParameterListIR effectiveParameterListIR)

  case boolAndSync (@bool isExitCommand # if false, it is an "exit" command
                    @instructionListIR boolExpInstructionGenerationList
                    @objectIR boolExpResult
                    @string guardMangledName
                    @instructionListIR guardInstructionGenerationList
                    @procCallEffectiveParameterListIR effectiveParameterListIR)
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @syncInstructionBranchListIR {
  @guardedCommandIR mGuardedCommand
  @instructionListIR mInstructionGenerationList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @guardedCommandIRList {
  @guardedCommandIR mGuardedCommand
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @syncInstructionIR : @abstractInstructionIR {
  @location mSelectInstructionLocation
  @syncInstructionBranchListIR mOnInstructionBranchListIR
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @syncInstructionIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext inGenerationContext
  ?!@generationAdds ioGenerationAdds
{
  ioGenerationAdds.mUsesGuards = true
  let startLabel = "select." + ioGenerationAdds.mUniqueIndex
  ioGenerationAdds.mUniqueIndex ++
#---
  ioLLVMcode += "  br label %" + startLabel + ".start\n\n"
  let startLabelName = startLabel + ".start" 
  ioLLVMcode += startLabelName + ":\n"
  let exitLabelName = startLabel + ".exit"
  let selectLabelName = startLabel + ".select"
  let errorLabelName = startLabel + ".error"
  var currentStartBranchLabel = startLabelName
  for branch in mOnInstructionBranchListIR do (idx)
    let acceptanceVarName = "%" + startLabel + ".accept." + idx
    @bool isWhileGuardedCommand
    switch branch.mGuardedCommand
    case booleanGuard (isExitCommand instructionGenerationList @objectIR result) :
      isWhileGuardedCommand = isExitCommand
      [instructionGenerationList instructionListLLVMCode !?ioLLVMcode !inGenerationContext !?ioGenerationAdds]
      let acceptedLabelName = startLabel + "." + idx + ".true"
      let rejectedLabelName = startLabel + "." + idx + ".false"
      ioLLVMcode += "  br i1 " + [result llvmName] + ", label %" + acceptedLabelName + ", label %" + rejectedLabelName + "\n\n"
      ioLLVMcode += acceptedLabelName + ":\n"
      ioLLVMcode += "  call void @accept.guard ()\n"
      ioLLVMcode += "  br label %" + rejectedLabelName + "\n\n"
      ioLLVMcode += rejectedLabelName + ":\n"
      ioLLVMcode += "  " + acceptanceVarName + " = or i1 " + [result llvmName] + ", 0 ; assignment\n"
    case boolAndSync (isExitCommand expInstructionList expResult guardMangledName guardInstructionList effectiveParameterList) :
      isWhileGuardedCommand = isExitCommand
      [expInstructionList instructionListLLVMCode !?ioLLVMcode !inGenerationContext !?ioGenerationAdds]
      let testOkLabelName = startLabel + "." + idx + ".boolexp.true"
      let testExitLabelName = startLabel + "." + idx + ".test.exit"
      ioLLVMcode += "  br i1 " + [expResult llvmName] + ", label %" + testOkLabelName + ", label %" + testExitLabelName + "\n\n"
      ioLLVMcode += testOkLabelName + ":\n"
      [guardInstructionList instructionListLLVMCode !?ioLLVMcode !inGenerationContext !?ioGenerationAdds]
      let guardAcceptationLabelName = startLabel + "." + idx + ".guard.acceptation"
      ioLLVMcode += "  %" + guardAcceptationLabelName + " = call i1 @" + [guardMangledName assemblerRepresentation]
      ioLLVMcode += " ("
      for () in effectiveParameterList do
        switch mEffectiveParameterPassingMode
        case input:
          ioLLVMcode += [mParameter llvmTypeName] + " * " + [mParameter llvmName]
        case output:
          ioLLVMcode += [mParameter llvmTypeName] + " " + [mParameter llvmName]
        case outputInput:
          ioLLVMcode += [mParameter llvmTypeName] + " * " + [mParameter llvmName]
        end
      between
        ioLLVMcode += ", "
      end
      ioLLVMcode += ")\n"
      ioLLVMcode += "  br label %" + testExitLabelName + "\n\n"
      ioLLVMcode += testExitLabelName + ":\n"
      ioLLVMcode += "  " + acceptanceVarName + " = phi i1 "
      ioLLVMcode += "[%" + guardAcceptationLabelName + ", %" + testOkLabelName + "], "
      ioLLVMcode += "[false, %" + currentStartBranchLabel + "]\n"
    case sync (isExitCommand guardMangledName guardInstructionList effectiveParameterList) :
      isWhileGuardedCommand = isExitCommand
      [guardInstructionList instructionListLLVMCode !?ioLLVMcode !inGenerationContext !?ioGenerationAdds]
      ioLLVMcode += "  " + acceptanceVarName + " = call i1 @" + [guardMangledName assemblerRepresentation]
      ioLLVMcode += " ("
      for () in effectiveParameterList do
        switch mEffectiveParameterPassingMode
        case input:
          ioLLVMcode += [mParameter llvmTypeName] + " * " + [mParameter llvmName]
        case output:
          ioLLVMcode += [mParameter llvmTypeName] + " " + [mParameter llvmName]
        case outputInput:
          ioLLVMcode += [mParameter llvmTypeName] + " * " + [mParameter llvmName]
        end
      between
        ioLLVMcode += ", "
      end
      ioLLVMcode += ")\n"
    end
    let acceptedLabelName = startLabel + ".accepted." + idx
    let rejectedLabelName = startLabel + ".rejected." + idx
    ioLLVMcode += "  br i1 " + acceptanceVarName + ", label %" + acceptedLabelName + ", label %" + rejectedLabelName + "\n\n"
    ioLLVMcode += acceptedLabelName + ":\n"
    [branch.mInstructionGenerationList instructionListLLVMCode !?ioLLVMcode !inGenerationContext !?ioGenerationAdds]
    ioLLVMcode += "  br label %" + if isWhileGuardedCommand then startLabelName else exitLabelName end + "\n\n"
    ioLLVMcode += rejectedLabelName + ":\n"
    currentStartBranchLabel = rejectedLabelName
  end
  ioLLVMcode += "  %" + selectLabelName + " = call i1 @" + llvmNameForServiceCall (!waitForGuardChangeFunctionName ()) + " ()\n"
  ioLLVMcode += "  br i1 %" + selectLabelName + ", label %" + startLabelName + ", label %" + errorLabelName + "\n\n"
  ioLLVMcode += errorLabelName + ":\n" # Error, all guards are closed
  if [option plm_options.noPanicGeneration value] then
    ioLLVMcode += "  br label %" + exitLabelName + "\n\n" # No panic generation, pass instruction
  else
    ioLLVMcode += "  call void @" + [panicRoutineNameForLocationFile (!mSelectInstructionLocation) assemblerRepresentation] + " ("
    ioLLVMcode += [inGenerationContext.mPanicLineLLVMType llvmTypeName] + " " + [mSelectInstructionLocation line] + ", "
    ioLLVMcode += [inGenerationContext.mPanicCodeLLVMType llvmTypeName] + " " + panicCodeForClosedSync () + ")\n"
    ioLLVMcode += "  unreachable\n\n"
  end
  ioLLVMcode += exitLabelName + ":\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @syncInstructionIR enterAccessibleEntities
  ?!@stringset ioInvokedRoutineSet
  ?!@uint ioMaxBranchOfOnInstructions
{
  let branchCount = [mOnInstructionBranchListIR length]
  if ioMaxBranchOfOnInstructions < branchCount then
    ioMaxBranchOfOnInstructions = branchCount
  end
  for branch in mOnInstructionBranchListIR do
    [branch.mInstructionGenerationList enterAccessibleEntities !?ioInvokedRoutineSet !?ioMaxBranchOfOnInstructions]
    switch branch.mGuardedCommand
    case booleanGuard (* instructionGenerationList *) :
      [instructionGenerationList enterAccessibleEntities !?ioInvokedRoutineSet !?ioMaxBranchOfOnInstructions]
    case sync (* guardMangledName guardInstructionGenerationList *) :
      [guardInstructionGenerationList enterAccessibleEntities !?ioInvokedRoutineSet !?ioMaxBranchOfOnInstructions]
      ioInvokedRoutineSet += !guardMangledName
    case boolAndSync (* instructionGenerationList * guardMangledName guardInstructionGenerationList *) :
      [instructionGenerationList enterAccessibleEntities !?ioInvokedRoutineSet !?ioMaxBranchOfOnInstructions]
      [guardInstructionGenerationList enterAccessibleEntities !?ioInvokedRoutineSet !?ioMaxBranchOfOnInstructions]
      ioInvokedRoutineSet += !guardMangledName
    end
  end
  if not [option plm_options.noPanicGeneration value] then
    ioInvokedRoutineSet += !panicRoutineNameForLocationFile (!mSelectInstructionLocation)
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
