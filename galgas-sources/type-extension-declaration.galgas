#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @extensionDeclarationListAST {
  @lstring mTypeName
  @functionDeclarationListAST mProcedureDeclarationListAST
  @structurePropertyListAST mPropertyList
  @systemRoutineDeclarationListAST mSVCListAST
  @guardDeclarationListAST mGuardListAST
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension plm_syntax {

  #·····················································································································

  rule <declaration> ?!@ast ioAST {
    $extension$
    $\$type$ ?let typeName
    ${$
    @functionDeclarationListAST procedureDeclarationListAST = {}
    @structurePropertyListAST propertyList = {}
    @systemRoutineDeclarationListAST svcListAST = {}
    @guardDeclarationListAST guardListAST = {}
    repeat
    while
      <property_in_extension> !?ioAST !?propertyList
    while
      <function> !?ioAST !?procedureDeclarationListAST
    while
     <system_routine> !?ioAST !?svcListAST
    while
      <guard> !?ioAST !?guardListAST
    while
      $;$
    end
    $}$
    ioAST.mExtensionDeclarationListAST +=
      !typeName
      !procedureDeclarationListAST
      !propertyList
      !svcListAST
      !guardListAST
  }

  #·····················································································································

  rule <property_in_extension> ?!@ast ioAST ?!@structurePropertyListAST ioPropertyList {
    @bool public
    select
      public = false
    or
      $public$
      public = true
    end
    @bool constant
    select
      $var$
      constant = false
    or
      $let$
      constant = true
    end
    $identifier$ ?let fieldName
    <type_definition> !?ioAST ?let typeName
    $=$
    <expression> !?ioAST ?let initExpression
    ioPropertyList += !fieldName !constant !public !typeName !.expression{!initExpression:initExpression}
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

abstract method @abstractDeclarationAST addExtension
  ?!@extensionDeclarationListAST ioExtensionDeclarationListAST
  ?!@extendStaticArrayDeclarationAST ioExtendStaticArrayDeclarationAST
  !@abstractDeclarationAST outNewDeclaration

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
