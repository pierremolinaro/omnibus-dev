#—————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

enum @registerGroupIndexAST {
  case noIndex
  case index (@expressionAST index @location endOfIndex @bool checkIndexExpression)
}

#·······················································································································

enum @registerIndexAST {
  case noIndex
  case index (@expressionAST index @location endOfIndex @bool checkIndexExpression)
}

#·······················································································································

struct @controlRegisterLValueAST {
  public var @lstring mRegisterGroupName
  public var @registerGroupIndexAST mGroupIndex
  public var @lstring mRegisterName
  public var @registerIndexAST mRegisterIndex
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#—————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <control_register_lvalue> ?!@ast ioAST !@controlRegisterLValueAST outRegisterLValue {
    $©group$ ?let registerGroupName
    let @registerGroupIndexAST groupIndex
    select
      groupIndex = .noIndex
    or
      $[$
      <expression> !?ioAST ?let expression
      let endOfIndex = @location.here
      let @bool checkIndexExpression
      select
        $]$
        checkIndexExpression = true
      or
        $]!$
        checkIndexExpression = true
      end
      groupIndex = .index {!index:expression !endOfIndex:endOfIndex !checkIndexExpression:checkIndexExpression}
    end
    $.$
    $identifier$ ?let registerName
    let @registerIndexAST registerIndex
    select
      registerIndex = .noIndex
    or
      $[$
      <expression> !?ioAST ?let expression
      let endOfIndex = @location.here
      let @bool checkIndexExpression
      select
        $]$
        checkIndexExpression = true
      or
        $]!$
        checkIndexExpression = false
      end
      registerIndex = .index {
        !index:expression
        !endOfIndex:endOfIndex
        !checkIndexExpression:checkIndexExpression
      }
    end
    outRegisterLValue = .init {!registerGroupName !groupIndex !registerName !registerIndex}
  }

  #·····················································································································

}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#!  NOTE TYPES                                                                                                           
#—————————————————————————————————————————————————————————————————————————————————————————————————

method @controlRegisterLValueAST noteExpressionTypesInPrecedenceGraph
  ?!@semanticTypePrecedenceGraph ioGraph
{
  switch self.mGroupIndex
  case noIndex :
  case index (indexExpression * *) :
    [indexExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
  end
  switch self.mRegisterIndex
  case noIndex :
  case index (indexExpression * *) :
    [indexExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
  end
}

#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! PRECEDENCE GRAPH FROM EXPRESSION                                                                                     
#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @controlRegisterLValueAST addDependenceEdgeForStaticExpression
  ?let @lstring inConstantName
  ?!@semanticTypePrecedenceGraph ioGraph
{
  [!?ioGraph addEdge !inConstantName !self.mRegisterGroupName]
  switch self.mGroupIndex
  case noIndex :
  case index (@expressionAST indexExpression * *) :
    [indexExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
  end
  switch self.mRegisterIndex
  case noIndex :
  case index (@expressionAST indexExpression * *) :
    [indexExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#!  SEMANTICS                                                                                                            
#—————————————————————————————————————————————————————————————————————————————————————————————————

method @controlRegisterLValueAST controlRegisterLValueSemanticAnalysis
  ?writeAccess:let @bool inWriteAccess
  ?self:let @omnibusType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !registerType: @omnibusType outRegisterType
  !registerBitCount:@uint outRegisterTypeBitCount
  !sliceMap:@sliceMap outSliceMap
  !llvmRegisterAddressName:@string outllvmRegisterAddressName
{
#-------------------- Search register group name in entity map
  [inContext.mControlRegisterGroupMap searchKey !self.mRegisterGroupName ?let groupKind ?let controlRegisterMap]
#-------------------- Search register name
  [controlRegisterMap searchKey
    !self.mRegisterName
    ?outRegisterType
    ?let @bool readOnly
    ?let @bool userAccess
    ?outSliceMap
    ?*
    ?let @bigint addressOffset
    ?*
    ?outRegisterTypeBitCount
    ?let @uint registerArraySize # 0 if not an array
    ?let @uint registerElementSize # 0 if not an array
  ]
#-------------------- Check access
  if inWriteAccess & readOnly then
    error self.mRegisterName
      : "this control register cannot be modified (declared with @ro attribute)"
  elsif not userAccess && ([inMode isUserMode] || [inMode isAnyMode]) then
    error self.mRegisterName : "this control register is not accessible in user mode"
  end
#-------------------- Group index ?
  let @string registerAddress
  switch groupKind
  case single (@bigint baseAddress) :
    if [self.mGroupIndex isIndex] then
      error self.mRegisterGroupName : "subscripting not allowed, group is not an array" : registerAddress
    else
      let regName = self.mRegisterGroupName.string + ":" + self.mRegisterName
      [!?ioInstructionGenerationList appendEnterRegisterAddress
         !?ioTemporaries
         ?registerAddress
         !baseAddress
         !addressOffset
         !regName
      ]
    end
  case arrayGroup (@lbigintlist baseAddresses) :
    switch self.mGroupIndex
    case noIndex :
      error self.mRegisterGroupName : "subscripting required, group is an array" : registerAddress
    case index (indexExpression endOfIndex checkIndexExpression) :
      handleArraySubscriptNew (
        !self: inSelfType
        !routineAttributes:inRoutineAttributes
        !context: inContext
        !mode: inMode
        !?temporary: ioTemporaries
        !?staticEntityMap:ioStaticEntityMap
        !?variableMap: ioUniversalMap
        !?alloca: ioAllocaList
        !indexExpression 
        !endOfIndex
        !checkIndexExpression
        !arraySize:[baseAddresses count]
        !elementType:outRegisterType
        !?ioInstructionGenerationList
        ?let @objectIR groupIndexIR
      )
      [!?ioInstructionGenerationList appendEnterRegisterGroupSubscriptedAddress
        !?ioTemporaries
        ?registerAddress
        !self.mRegisterGroupName.string
        ![baseAddresses count]
        !groupIndexIR
      ]
    end
  end
#-------------------- Analyze control register target
  switch self.mRegisterIndex
  case noIndex :
    if registerArraySize == 0 then
      outllvmRegisterAddressName = registerAddress
    else
      error self.mRegisterName : "the control register is an array" : outllvmRegisterAddressName
    end
  case index (indexExpression endOfIndex checkIndexExpression) :
    handleArraySubscriptNew (
      !self: inSelfType
      !routineAttributes:inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap:ioStaticEntityMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !indexExpression 
      !endOfIndex
      !checkIndexExpression
      !arraySize:registerArraySize
      !elementType:outRegisterType
      !?ioInstructionGenerationList
      ?let @objectIR indexIR
    )
    [!?ioInstructionGenerationList appendComputeSubscriptedVolatileRegisterAddress
      !?ioTemporaries
      ?outllvmRegisterAddressName
      !indexIR
      !registerAddress
      !registerElementSize
    ]
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
