#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @functionCallEffectiveParameterListAST {
  @lstring mSelector
  @expressionAST mExpression
}

#·······················································································································

shared class @constructorCallAST : @expressionAST {
  @lstring mTypeName
  @functionCallEffectiveParameterListAST mParameterList
  @location mErrorLocation
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  rule <primary> ?!@ast ioAST !@expressionAST outExpression {
    <type_definition> !?ioAST ?let typeName
    $($
    @functionCallEffectiveParameterListAST parameterList = {}
    repeat
    while
      $!selector:$ ?let selector
      <expression> !?ioAST ?let expression
      parameterList += !selector !expression
    end
    let errorLocation = @location.here
    $)$
    outExpression = @constructorCallAST.new {!typeName !parameterList !errorLocation}
  }

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH FROM EXPRESSION                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @constructorCallAST addDependenceEdgeForStaticExpression
  ?let @lstring inConstantName
  ?!@semanticTypePrecedenceGraph ioGraph
{
  [!?ioGraph addEdge !inConstantName !self.mTypeName]
  
  for () in self.mParameterList do
    [mExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @constructorCallAST noteExpressionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  [!?ioGraph noteNode !self.mTypeName]
  for () in self.mParameterList do
    [mExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @constructorCallAST analyzeExpression
  ?self:let @omnibusType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?optionalTargetType:let @omnibusType unused inOptionalTargetType
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !result:@objectIR outResult
{
#--- Analyze effective argument list
  var constructorKey = "("
  for (selector *) in self.mParameterList do
    constructorKey += "!" + selector
  end
  constructorKey += ")"
#--- Analyze constructor signature and init value
  let resultType = [@unifiedTypeMap-proxy.searchKey {!inContext.mTypeMap !self.mTypeName} type]
  let typeName = @lstring.new {!resultType.omnibusTypeDescriptionName !self.mErrorLocation}
  [inContext.mTypeConstructorMap searchKey !typeName ?let constructorMap]
#  let constructorMap = resultType.constructorMap2
  [constructorMap searchKey !.new {!constructorKey !self.mErrorLocation} ?let constructorSignature ?let initValue]
#--- Examine init value
  switch initValue
  case arrayValue (elementType size) :
    if constructorKey == "(!repeated)" then
      let expression = [self.mParameterList mExpressionAtIndex !0]
      [expression analyzeExpression
        !self:inSelfType
        !routineAttributes:inRoutineAttributes
        !optionalTargetType:elementType
        !context:inContext
        !mode:inMode
        !?temporary:ioTemporaries
        !?staticEntityMap:ioStaticEntityMap
        !?variableMap:ioUniversalMap
        !?alloca:ioAllocaList
        !?instructionListIR:ioInstructionGenerationList
        ?result:let @objectIR expressionResult
      ]
      let result = checkAssignmentCompatibility (
        !inContext.mAssignmentOperatorMap
        !source:expressionResult
        !targetAnnotationType:elementType
        !errorLocation:[self.mParameterList mSelectorAtIndex !0].location
        !staticTypeAllowed:false
      )
      if [result isStatic] then
        let idx = [ioStaticEntityMap.mGlobalStructuredConstantList  length]
        outResult = .llvmArrayRepeatedStaticValue {!type:resultType !arraySize:size !value:result !index:idx}
        @operandIRList operandIRList = {}
        for i in 0 ..< size do
          operandIRList += !result
        end
        ioStaticEntityMap.mGlobalStructuredConstantList += !resultType !operandIRList
      else
        outResult = .llvmArrayRepeatedDynamicValue {!type:resultType !arraySize:size !value:result}
      end
    else
      var individualConstructor = "("
      for i in 0 ..< size do
        individualConstructor += "!"
      end
      individualConstructor += ")"
      if constructorKey == individualConstructor then
        @operandIRList operandIRList = {}
        var resultIsStatic = true
        for () in self.mParameterList do
          [mExpression analyzeExpression
            !self:inSelfType
            !routineAttributes:inRoutineAttributes
            !optionalTargetType:elementType
            !context:inContext
            !mode:inMode
            !?temporary:ioTemporaries
            !?staticEntityMap:ioStaticEntityMap
            !?variableMap:ioUniversalMap
            !?alloca:ioAllocaList
            !?instructionListIR:ioInstructionGenerationList
            ?result:let @objectIR expressionResult
          ]
          let result = checkAssignmentCompatibility (
            !inContext.mAssignmentOperatorMap
            !source:expressionResult
            !targetAnnotationType:elementType
            !errorLocation:mSelector.location
            !staticTypeAllowed:false
          )
          operandIRList += !result
          resultIsStatic = resultIsStatic && [result isStatic]
        end
        if resultIsStatic then
          let idx = [ioStaticEntityMap.mGlobalStructuredConstantList  length]
          outResult = .llvmArrayStaticValues {!type:resultType !values:operandIRList !index:idx}
          ioStaticEntityMap.mGlobalStructuredConstantList += !resultType !operandIRList
        else
          outResult = .llvmArrayDynamicValues {!type:resultType !values:operandIRList}
        end
      else
        error self.mErrorLocation : "invalid constructor" : outResult
      end
    end
  case null :
    outResult = .null {!type: resultType}
  case simple (value) :
    if [self.mParameterList length] != 0 then
      error self.mErrorLocation : resultType.omnibusTypeDescriptionName + " constructor should have no parameter" : outResult
    else
      outResult = .literalInteger {!type: resultType !value:value}
    end
  case structure (sortedOperandList) :
    var sortedOperandIRList = sortedOperandList
    if [self.mParameterList length] != [constructorSignature length] then
      error self.mErrorLocation : "this constructor call should name " + [constructorSignature length] + " parameter"
        + if [constructorSignature length] > 1 then "s" else "" end
        + " instead of " + [self.mParameterList length]
      : outResult
    else
      for () formal_ in constructorSignature, () effective_ in self.mParameterList do
        if effective_mSelector.string != formal_mSelector  then
          error effective_mSelector : "the selector should be '!" + formal_mSelector + ":'"
        end
        [effective_mExpression analyzeExpression
          !self:inSelfType
          !routineAttributes:inRoutineAttributes
          !optionalTargetType:formal_mType
          !context:inContext
          !mode:inMode
          !?temporary:ioTemporaries
          !?staticEntityMap:ioStaticEntityMap
          !?variableMap:ioUniversalMap
          !?alloca:ioAllocaList
          !?instructionListIR:ioInstructionGenerationList
          ?result:let @objectIR expressionResult
        ]
        var result = checkAssignmentCompatibility (
          !inContext.mAssignmentOperatorMap
          !source:expressionResult
          !targetAnnotationType:formal_mType
          !errorLocation:effective_mSelector.location
          !staticTypeAllowed:false
        )
        [!?ioInstructionGenerationList appendLoadWhenReference !?ioTemporaries !?result]
        sortedOperandIRList += !result !formal_mFieldIndex
      end
      outResult = .llvmStructureValue {!type:resultType !values:sortedOperandIRList}
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
