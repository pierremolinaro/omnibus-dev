#----------------------------------------------------------------------------------------------------------------------*
#  AST                                                                                                                 *
#----------------------------------------------------------------------------------------------------------------------*

class @newIntegerRepresentationDeclaration : @abstractDeclaration {
  @lstring mBaseTypeName
  @lstring mCType
  @lbigint mSize
  @bool mIsSigned
}

#----------------------------------------------------------------------------------------------------------------------*

override getter @newIntegerRepresentationDeclaration location -> @location outLocation {
  outLocation = mBaseTypeName.location
}

#----------------------------------------------------------------------------------------------------------------------*
#  SYNTAX                                                                                                              *
#----------------------------------------------------------------------------------------------------------------------*

syntax extension plm_target_specific_syntax {

  #--------------------------------------------------------------------------------------------------------------------*

  rule <declaration> ?!@ast ioAST {
    $newSignedRepresentation$
    $attribute$ ?let newAttributeName
    $:$
    $literal_string$ ?let cType
    $integer$ ?let size
    ioAST.mDeclarationList += !@newIntegerRepresentationDeclaration.new {
      !newAttributeName
      !cType
      !size
      !true
    }
  }

  #--------------------------------------------------------------------------------------------------------------------*

  rule <declaration> ?!@ast ioAST {
    $newUnsignedRepresentation$
    $attribute$ ?let newAttributeName
    $:$
    $literal_string$ ?let cType
    $integer$ ?let size
    ioAST.mDeclarationList += !@newIntegerRepresentationDeclaration.new {
      !newAttributeName
      !cType
      !size
      !false
    }
  }

  #--------------------------------------------------------------------------------------------------------------------*

}

#----------------------------------------------------------------------------------------------------------------------*
#  PRECEDENCE GRAPH                                                                                                    *
#----------------------------------------------------------------------------------------------------------------------*

override method @newIntegerRepresentationDeclaration enterInPrecedenceGraph
  ?!@semanticTypePrecedenceGraph ioGraph
{
  [!?ioGraph addNode !.new {!"basetype " + mBaseTypeName !mBaseTypeName.location} !self]
}

#----------------------------------------------------------------------------------------------------------------------*

override getter @newIntegerRepresentationDeclaration keyRepresentation -> @string outRepresentation {
  outRepresentation = "bool " + mBaseTypeName 
}

#----------------------------------------------------------------------------------------------------------------------*
#  ENTER IN CONTEXT                                                                                                    *
#----------------------------------------------------------------------------------------------------------------------*

override method @newIntegerRepresentationDeclaration enterInContext
  ?procedureDeclarationList:let @procedureDeclarationListAST unused inProcedureListAST
  ?!context:@semanticContext ioContext
  ?!staticStringMap:@globalLiteralStringMap unused ioGlobalLiteralStringMap
{
#--- Check size
  if (mSize.bigint != 8G) && (mSize.bigint != 16G) && (mSize.bigint != 32G) && (mSize.bigint != 64G) then
    error mSize : "Only 8, 16, 32 or 64 are accepted here"
  end
#---
  [!?ioContext.mTargetBaseTypeMap insertKey
    !mBaseTypeName
    !mCType.string
    ![mSize.bigint uint]
    !mIsSigned
  ]
}


#----------------------------------------------------------------------------------------------------------------------*
#  SEMANTIC ANALYSIS                                                                                                   *
#----------------------------------------------------------------------------------------------------------------------*

override method @newIntegerRepresentationDeclaration initAnalysis ?!context:@semanticContext unused ioContext { }

#----------------------------------------------------------------------------------------------------------------------*

override method @newIntegerRepresentationDeclaration semanticAnalysis
  ?context:let @semanticContext unused inContext
  ?!intermediateCode:@intermediateCodeStruct unused ioIntermediateCodeStruct
{
}

#----------------------------------------------------------------------------------------------------------------------*
