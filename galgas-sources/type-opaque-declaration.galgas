#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

shared class @typeOpaqueDeclarationAST : @abstractDeclarationAST {
  @lstring mOpaqueTypeName
  @expressionAST mSizeExpression
  @location mSizeExpressionLocation
  @lstringlist mAttributeList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <declaration> ?!@ast ioAST {
    $opaque$
    $\$type$ ?let opaqueTypeName
    @lstringlist attributeList = {}
    repeat
    while
      $@attribute$ ?let attribute
      attributeList += !attribute
    end
    ${$
    <expression> !?ioAST ?let sizeExpression
    let sizeExpressionLocation = @location.here
    $}$
    ioAST.mDeclarationListAST += !@typeOpaqueDeclarationAST.new {
      !opaqueTypeName
      !sizeExpression
      !sizeExpressionLocation
      !attributeList
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  PRECEDENCE GRAPH FROM EXPRESSION                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @typeOpaqueDeclarationAST noteTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  [!?ioGraph noteNode !self.mOpaqueTypeName]
  [self.mSizeExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
}

#·······················································································································

override method @typeOpaqueDeclarationAST enterInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
#--- Add structure type name as node
  [!?ioGraph addNode !self.mOpaqueTypeName !self]
#--- Add size expression
  [self.mSizeExpression addDependenceEdgeForStaticExpression !self.mOpaqueTypeName !?ioGraph]
}

#·······················································································································

override getter @typeOpaqueDeclarationAST keyRepresentationForErrorSignaling -> @string outRepresentation {
  outRepresentation = "opaque " + self.mOpaqueTypeName 
}

#·······················································································································

override getter @typeOpaqueDeclarationAST locationForErrorSignaling -> @location outLocation {
  outLocation = self.mOpaqueTypeName.location
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  ENTER IN CONTEXT                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @typeOpaqueDeclarationAST enterInContext
  ?!context:@semanticContext ioContext
  ?!@declarationDecorationList unused ioDecoratedDeclarationList
  ?!@decoratedRegularRoutineList unused ioDecoratedRoutineList
  ?!@routineListIR unused ioRoutineListIR
  ?!staticlistValues:@staticListInitializationMap unused ioStaticListValueMap
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!registerUserAccess:@controlRegisterUserAccesMapAST unused ioControlRegisterUserAccesMapAST
  ?!userDefinedTypes:@userLLVMTypeDefinitionListIR unused ioUserLLVMTypeDefinitionListIR
{
  @omnibusTypeAttributes opaqueTypeAttributes = .none
  var copyable = false
  for (attribute) in self.mAttributeList do
    if attribute.string == instantiableAttribute () then
      if [opaqueTypeAttributes instanciable] then
        error attribute : "duplicated attribute"
      else
        opaqueTypeAttributes = opaqueTypeAttributes | .instanciable
      end
    elsif attribute.string == copyableAttribute () then
      if copyable then
        error attribute : "duplicated attribute"
      else
        copyable = true
        opaqueTypeAttributes = opaqueTypeAttributes | .copyable
      end
    else
      error attribute
      : "only @" + copyableAttribute () + " and @" + instantiableAttribute ()
        + " attributes are allowed here"
    end
  end
#--- Compute opaque type size
  computeStaticExpression (
    !?context: ioContext
    !?staticEntityMap:ioStaticEntityMap
    !expression:self.mSizeExpression
    !errorLocation:self.mSizeExpressionLocation
    !optionalContextualTypeName:["" nowhere]
    ?result:let @objectIR sizeExpressionIR
  )
  if not [sizeExpressionIR isLiteralInteger] then
    error self.mSizeExpressionLocation : "array size expression is not a literal integer"
  else
    @constructorMap constructorMap = {}
    if [opaqueTypeAttributes instanciable] then
      [!?constructorMap insertKey !["()" nowhere] !{} !.simple {!value:0G}]
      [!?ioContext.mTypeConstructorMap insertKey !self.mOpaqueTypeName !constructorMap]
    end
    [sizeExpressionIR literalInteger ?1* ?value:let bitSize]
    let opaqueType = @omnibusType.new {
#      !propertyGetterMap:{}
      !subscript:.noSubscript
      !attributes:opaqueTypeAttributes
      !omnibusTypeDescriptionName:self.mOpaqueTypeName
      !kind:.opaque {!bitCount:bitSize}
      !llvmBaseTypeName:self.mOpaqueTypeName
    }
    [!?ioContext.mTypeMap insertType
      !lkey:self.mOpaqueTypeName
      !type:opaqueType
    ]
  end
#--- Add Assignment ?
  if copyable then
    message "OPAQUE " + self.mOpaqueTypeName + " is copyable\n"
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
