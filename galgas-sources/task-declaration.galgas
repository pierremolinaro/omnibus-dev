#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @taskListAST {
  public var @lstring mTaskName
  public var @lstringlist mLowerPriorityTaskList
  public var @lbigint mStackSize
  public var @taskSetupListAST mTaskSetupListAST
  public var @taskSetupListAST mTaskActivateListAST
  public var @taskSetupListAST mTaskDeactivateListAST
  public var @syncInstructionBranchListAST mGuardedCommandList
  public var @location mEndOfTaskDeclaration
  public var @bool mAutoStart
}

#·······················································································································

list @taskSetupListAST {
  public var @lstring mName
  public var @lstringlist mDependanceList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <declaration> ?!@ast ioAST {
    $task$
    $identifier$ ?let taskName
    var @lstringlist higherPriorityTaskList = §[]
    select
    or
      $>$
      repeat
        $identifier$ ?let aTaskName
        higherPriorityTaskList += !aTaskName
      while
        $,$
      end
    end
    $@attribute$ ?let stackSizeAttribute
    if stackSizeAttribute.string != "stacksize" then
      error stackSizeAttribute : "this attribute should be @stacksize"
    end
    $integer$ ?let @lbigint stackSize
  #--- @autostart attribute
    var autoStart = false
    select
    or
      $@attribute$ ?let activateAttribute
      if activateAttribute.string != "autostart" then
        error activateAttribute : "this attribute should be @autostart"
      end
      autoStart = true
    end
    ${$
    var @structurePropertyListAST taskVarList = §[]
    var @taskSetupListAST taskSetupList = §[]
    var @taskSetupListAST taskActivateList = §[]
    var @taskSetupListAST taskDeactivateList = §[]
    var @syncInstructionBranchListAST branchList = §[]
    repeat
    while
      <private_struct_property_declaration> !?ioAST !?taskVarList
    while
      $func$
      $identifier$ ?let procName
      <procedure_formal_arguments> !?ioAST ?let @routineFormalArgumentListAST formalArgumentList
      let @lstring returnTypeName
      select
        returnTypeName = ["" here]
      or
        $->$
        <type_definition> !?ioAST ?returnTypeName
      end
      ${$
      <instructionList> !?ioAST ?let instructionList
      ioAST.mDeclarationListAST += !@functionDeclarationAST.init {
        !.userMode
        !false
        !taskName
        !procName
        !§[]
        !formalArgumentList
        !returnTypeName
        !instructionList
        !.here
      }
      $}$
    while
      <task_event> !?ioAST !taskName: taskName !?setup: taskSetupList !?activate: taskActivateList !?deactivate: taskDeactivateList
    while
      $while$
      <guarded_command> !?ioAST !false ?let @guardedCommandAST guardedCommand
      ${$
      <instructionList> !?ioAST ?let instructionList
      let endOfInstructions = @location.here
      $}$
      branchList += !guardedCommand !instructionList !endOfInstructions
#    while
#      $until$
#      <guarded_command> !?ioAST !true ?let @guardedCommandAST guardedCommand
#      ${$
#      <instructionList> !?ioAST ?let instructionList
#      let endOfInstructions = @location.here
#      $}$
#      branchList += !guardedCommand !instructionList !endOfInstructions
    end
    let endOfTask = @location.here
    $}$
    ioAST.mTaskListAST +=
      !taskName
      !higherPriorityTaskList
      !stackSize
      !taskSetupList
      !taskActivateList
      !taskDeactivateList
      !branchList
      !endOfTask
      !autoStart
    ioAST.mDeclarationListAST += !@structureDeclarationAST.init {
      !taskName
      !taskName
      !taskName
      !§[]
      !false # Generate assignment routine
      !taskVarList
      !false
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  PRECEDENCE GRAPH                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

method @taskListAST noteTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  for (* * * * * * mGuardedCommandList * *) in self do
    for guardedCommand in mGuardedCommandList do
      [guardedCommand.mInstructionList noteInstructionListTypesInPrecedenceGraph !?ioGraph]
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  ENTER IN CONTEXT                                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

map @taskMap {
  public var @omnibusType mTaskType
  insert insertKey error message "the '%K' task is already declared in %L"
  search searchKey error message "there is no '%K' task"
}

#·······················································································································

method @taskListAST enterInContext
  ?!context:@semanticContext ioContext
  ?!@declarationDecorationList ioDecoratedDeclarationList
  ?!@decoratedRegularRoutineList ioDecoratedRoutineList
  ?!@routineListIR ioRoutineListIR
  ?!staticEntityMap:@staticEntityMap unused ioStaticEntityMap
  ?!userDefinedTypes:@userLLVMTypeDefinitionListIR unused ioUserLLVMTypeDefinitionListIR
{
#------------- Sort tasks following their relative priority
  var @taskListAST taskList = self
  var @taskListAST sortedTaskList = §[]
  var @stringset handledTaskSet = §[]
  var continue = true
  loop ([taskList count]) while continue && ([taskList count] > 0) do
    continue = false
    var @taskListAST candidateTaskList = §[]
    var idx = 0
    for task in taskList do
      var allPrecedenceDefined = true
      for (precedenceName) in task.mLowerPriorityTaskList while allPrecedenceDefined do
        allPrecedenceDefined = [handledTaskSet hasKey !precedenceName.string]
      end
      if allPrecedenceDefined then
        candidateTaskList +=
          !task.mTaskName
          !task.mLowerPriorityTaskList
          !task.mStackSize
          !task.mTaskSetupListAST
          !task.mTaskActivateListAST
          !task.mTaskDeactivateListAST
          !task.mGuardedCommandList
          !task.mEndOfTaskDeclaration
          !task.mAutoStart
        continue = true
        [!?taskList removeAtIndex ?9* !idx]
      else
        idx = idx + 1
      end
    end
    if [option omnibus_options.taskStrictPriorityOrder value] && ([candidateTaskList count] > 1) then
      for task in candidateTaskList do
        error task.mTaskName : "this task does not respect a strict priority order"
      end
    end
    if [candidateTaskList count] > 0 then
      continue = true
      for task in candidateTaskList do
        handledTaskSet += !task.mTaskName.string
      end
      sortedTaskList += candidateTaskList
    end
  end
#--- Signal unsorted entries as error
  for (name 8*) in taskList do
    error name : "this task is out of priority order"
  end
#---
  var @decoratedTaskList decoratedTaskList = §[]
  for task in sortedTaskList do (taskIndex)
  #---- Add activate function
    [!?ioContext.mRoutineMap addFunctionWithoutArgument
      !omnibusTypeDescriptionName: task.mTaskName
      !llvmBaseTypeName: task.mTaskName
      !methodName:.init {!"activate" !task.mTaskName.location}
      !mode:.safeUserMode
      !resultTypeProxy:.null
      ?routineLLVMName:*
    ]
    let assemblerTaskName = [task.mTaskName assemblerRepresentation]
    let activateTaskCallName = llvmNameForServiceCall (!llvmNameForActivateTask ())
    ioRoutineListIR += !@taskActivateFunctionIR.init {
      !routineMangledNameFromAST (!task.mTaskName !["activate" nowhere] !§[])
      !isRequired:false
      !warnsIfUnused:false
      !assemblerTaskName
      !activateTaskCallName
      !taskIndex
    }
  #------------------------------------------- Analyze task guarded command
  #  Build a "task.loop" function that contains a sync instruction
    var @instructionListAST taskListInstructionList = §[]
    if [task.mGuardedCommandList count] > 0 then
      let syncInstruction = @syncInstructionAST.init {!task.mTaskName.location !task.mGuardedCommandList !task.mTaskName.location}
      taskListInstructionList += !syncInstruction
    end
    let routineMangledName = routineMangledNameFromAST (!task.mTaskName ![llvmNameForTaskLoopFunction () nowhere] ! §[])
    ioDecoratedRoutineList +=
      !receiverTypeName: task.mTaskName
      !mode:.userMode
      !isRequired:true
      !routineKind: .function
      !warnIfUnused: true
      !exportedRoutine: false
      !routineAttributes: .mutatingRoutine
      !routineMangledLLVMName: routineMangledName
      !formalArgumentList: §[]
      !warningOnUnusedArgs: true
      !taskListInstructionList
      !task.mTaskName.location
      !returnTypeName: ["" nowhere]
    [ioContext.mTypeMap searchType !lkey:task.mTaskName ?type:let taskType]
    [!?ioContext.mValuedObjectMap insertTask !task.mTaskName !taskType]
    var initialValueList = @sortedOperandIRList §[]
    let typeName = @lstring.init {!taskType.omnibusTypeDescriptionName !task.mTaskName.location}
    [ioContext.mTypeConstructorMap searchKey !typeName ?let constructorMap]
    for (* * initValue) in constructorMap do
#    for (* * initValue) in [taskType constructorMap2] do
      [initValue structure ?sortedOperandList: initialValueList]
    end
    ioContext.mGlobalTaskVariableList +=
      !task.mTaskName.string 
      !task.mTaskName.string
      !.llvmStructureValue{!type:taskType !values:initialValueList}
  #---
    decoratedTaskList += 
      !task.mTaskName
      !task.mStackSize
      !task.mTaskSetupListAST
      !task.mTaskActivateListAST
      !task.mTaskDeactivateListAST
      !task.mEndOfTaskDeclaration
      !task.mAutoStart
  end
#---------------- Add decorated declaration
  ioDecoratedDeclarationList += !@decoratedTaskListDeclaration.init {!decoratedTaskList}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  SEMANTIC ANALYSIS                                                                                                    
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

list @decoratedTaskList {
  public var @lstring mTaskName
  public var @lbigint mStackSize
  public var @taskSetupListAST mTaskSetupListAST
  public var @taskSetupListAST mTaskActivateListAST
  public var @taskSetupListAST mTaskDeactivateListAST
  public var @location mEndOfTaskDeclaration
  public var @bool mAutoStart
}

#·······················································································································

refclass @decoratedTaskListDeclaration : @abstractDecoratedDeclaration {
  public var @decoratedTaskList mTaskList
}

#·······················································································································

override method @decoratedTaskListDeclaration semanticAnalysis
  ?context:let @semanticContext inContext
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!intermediateCode:@intermediateCodeStruct ioIntermediateCodeStruct
{
  for task in self.mTaskList do (priority)
    [task taskSemanticAnalysis
      !context:inContext
      !priority:priority
      !?temporary:ioTemporaries
      !?intermediateCode:ioIntermediateCodeStruct
    ]
  end
}

#·······················································································································

private proc analyzeOrderedTaskRoutines
  ?let @taskSetupListAST inTaskRoutineLISTAST
  !@stringlist outSetupOrderedList
{
  outSetupOrderedList = §[]
  if [inTaskRoutineLISTAST count] > 0 then
  #--- Check there is exactly one entry without any precedence
    var @uint numberOfEntriesWithoutAnyPrecedence = 0
    for (* precedenceList) in inTaskRoutineLISTAST do
      if [precedenceList count] == 0 then
        numberOfEntriesWithoutAnyPrecedence += numberOfEntriesWithoutAnyPrecedence + 1
      end
    end
    if numberOfEntriesWithoutAnyPrecedence == 0 then
      for (name *) in inTaskRoutineLISTAST do
        error name : "Exactly one routine should have no precedence"
      end
    elsif numberOfEntriesWithoutAnyPrecedence > 1 then
      for (name precedenceList) in inTaskRoutineLISTAST do
        if [precedenceList count] == 0 then
          error name : "Exactly one routine should have no precedence"
        end
      end    
    end
  #--- Signal unsorted entries as error
    var @taskSetupListAST taskSetupListAST = inTaskRoutineLISTAST
    var @stringset precedenceSet = §[]
    var continue = true
    loop ([inTaskRoutineLISTAST count]) while continue && ([taskSetupListAST count] > 0) do
      continue = false
      var idx = 0
      for (name precedenceList) in taskSetupListAST do
        var allPrecedenceDefined = true
        for (precedenceName) in precedenceList while allPrecedenceDefined do
          allPrecedenceDefined = [precedenceSet hasKey !precedenceName.string]
        end
        if allPrecedenceDefined then
          outSetupOrderedList += !name.string
          precedenceSet += !name.string
          continue = true
          [!?taskSetupListAST removeAtIndex ?2* !idx]
        else
          idx = idx + 1
        end
      end
    end
  #--- Signal unsorted entries as error
    for (name *) in taskSetupListAST do
      error name : "this routine is out of execution order"
    end
  end
}

#·······················································································································

method @decoratedTaskList-element taskSemanticAnalysis
  ?context:let @semanticContext inContext
  ?priority:let @uint inPriority
  ?!temporary:@semanticTemporariesStruct unused ioTemporaries
  ?!intermediateCode:@intermediateCodeStruct ioIntermediateCodeStruct
{
  [inContext.mTypeMap searchType !lkey:self.mTaskName ?type: let taskType]
#------------------------------------------- Check stack size is >= 64, and a multiple of 8
  let stackSize = self.mStackSize.bigint
  if (stackSize < 64) || ((stackSize mod 8) != 0) then
    error self.mStackSize : "stack size should be a multiple of 8, and >= 64"
  end
#------------------------------------------- Analyze task setup routines
  analyzeOrderedTaskRoutines (
    !self.mTaskSetupListAST
    ?let setupOrderedList
  )
#------------------------------------------- Analyze task activate routines
  analyzeOrderedTaskRoutines (
    !self.mTaskActivateListAST
    ?let activateOrderedList
  )
#------------------------------------------- Analyze task deactivate routines
  analyzeOrderedTaskRoutines (
    !self.mTaskDeactivateListAST
    ?let deactivateOrderedList
  )
#------------------------------------------- Task Name string constant index
  [!?ioIntermediateCodeStruct.mStaticEntityMap findOrAddStaticString !self.mTaskName.string ?let taskNameStringIndex]
#------------------------------------------- Generate task
  [!?ioIntermediateCodeStruct.mTaskMapIR insertKey
    !self.mTaskName
    !taskType
    !inPriority
    !self.mStackSize.bigint
    !setupOrderedList
    !activateOrderedList
    !deactivateOrderedList
    !taskNameStringIndex
    !self.mAutoStart
  ]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  CODE GENERATION                                                                                                      
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

map @taskMapIR {
  public var @omnibusType mTaskType
  public var @uint mPriority
  public var @bigint mStackSize
  public var @stringlist mSetupOrderedList
  public var @stringlist mActivateOrderedList
  public var @stringlist mDeactivateOrderedList
  public var @uint mTaskNameStringIndex
  public var @bool mActivate

  insert insertKey error message "** internal error **"
  search searchKey error message "** internal error **"
  remove removeKey error message "** internal error **"
}

#·······················································································································

sortedlist @taskSortedListIR {
  public var @string mTaskName
  public var @uint mPriority
  public var @bigint mStackSize
  public var @stringlist mSetupOrderedList
  public var @stringlist mActivateOrderedList
  public var @stringlist mDeactivateOrderedList
  public var @uint mTaskNameStringIndex
  public var @bool mActivate
}{
  mPriority >
}

#·······················································································································

list @globalTaskVariableList {
  public var @string mTaskName
  public var @string mTaskTypeName
  public var @objectIR mInitialValue
}

#·······················································································································

method @taskMapIR generateCode
  ?!@string ioLLVMcode
  ?let @generationContext inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
  ?!@primitiveAndServiceIRlist ioServiceList
{
#---------- Declare "activate task" service
  ioLLVMcode += llvmTitleComment (!"Activate task service")
  let activateTaskImplementationName = llvmNameForServiceImplementation (!llvmNameForActivateTask ())
  let activateTaskCallName = llvmNameForServiceCall (!llvmNameForActivateTask ())
  ioServiceList += !activateTaskCallName !activateTaskImplementationName !false
  ioLLVMcode += "declare void @" + activateTaskCallName + " (i8 %inTaskIndex) nounwind\n\n"
#---------- Declare "wait for activation" service
  ioLLVMcode += llvmTitleComment (!"Wait for activation service")
  let waitForActivationImplementationName = llvmNameForServiceImplementation (!llvmNameForTaskWaitsForActivation ())
  let waitForActivationCallName = llvmNameForServiceCall (!llvmNameForTaskWaitsForActivation ())
  ioServiceList += !waitForActivationCallName !waitForActivationImplementationName !false
  ioLLVMcode += "declare void @" + waitForActivationCallName + " () nounwind\n\n"
#---------- Declare task variables
  ioLLVMcode += llvmTitleComment (!"Task variables")
  for (taskName taskTypeName initValueList) in inGenerationContext.mGlobalTaskVariableList do
    ioLLVMcode += llvmNameForGlobalVariable (!taskName)
    ioLLVMcode += " = internal global %"
    ioLLVMcode += [taskTypeName assemblerRepresentation]
    ioLLVMcode += " " + [initValueList llvmValue] + "\n"
  end
  ioLLVMcode += "\n"
#---------- Build ordered task list
  var @taskSortedListIR orderedTaskList = §[]
  for task in self do
    orderedTaskList +=
      !task.lkey.string
      !task.mPriority
      !task.mStackSize
      !task.mSetupOrderedList
      !task.mActivateOrderedList
      !task.mDeactivateOrderedList
      !task.mTaskNameStringIndex
      !task.mActivate
  end
#---------- Declare task stacks and main routine
  ioLLVMcode += llvmTitleComment (!"Create task extern routine")
  ioLLVMcode += "declare void @kernel_create_task (i32 %inTaskIndex, i8* %inTaskName, i32* %inStackBufferAddress, "
  ioLLVMcode += "i32 %inStackBufferSize, void ()* %inTaskRoutine) nounwind\n\n"
  for task in orderedTaskList do
    ioLLVMcode += llvmTitleComment (!"Task " + task.mTaskName)
    let assemblerTaskName = [task.mTaskName assemblerRepresentation]
    ioLLVMcode += stackNameForTask (!task.mTaskName) + " = global [" + (task.mStackSize / 4) + " x i32] zeroinitializer\n"
    ioLLVMcode += "\n"
    let effectiveParameter = "(%" + assemblerTaskName + "* " + llvmNameForGlobalVariable (!task.mTaskName) + ")"
    let formalArgument = " (%" + assemblerTaskName + "* " + llvmNameForSelf () + ")"
    ioLLVMcode += llvmSeparatorLine () + "\n"
  #---------- Setup
    ioLLVMcode += "define internal void " + setupNameForTaskType (!task.mTaskName) + formalArgument + llvmAttributeFunction () + "{\n"
    for (setupName) in task.mSetupOrderedList do
      let mangledName = "task.setup." + task.mTaskName + "." + setupName
      ioLLVMcode += "  call void @" + [mangledName assemblerRepresentation] + formalArgument + "\n"
    end
    ioLLVMcode += "  ret void\n"
    ioLLVMcode += "}\n\n"
    ioLLVMcode += llvmSeparatorLine ()
    ioLLVMcode += "\n"
  #---------- Activate
    ioLLVMcode += "define internal void @" + [activateNameForTaskType (!task.mTaskName) assemblerRepresentation]
    ioLLVMcode += formalArgument + llvmAttributeFunction () + "{\n"
    for (activateName) in task.mActivateOrderedList do
      let mangledName = "task.activate." + task.mTaskName + "." + activateName
      ioLLVMcode += "  call void @" + [mangledName assemblerRepresentation] + formalArgument + "\n"
    end
    ioLLVMcode += "  ret void\n"
    ioLLVMcode += "}\n\n"
    ioLLVMcode += llvmSeparatorLine ()
    ioLLVMcode += "\n"
  #---------- Deactivate
    ioLLVMcode += "define internal void " + deactivateNameForTaskType (!task.mTaskName) + formalArgument + llvmAttributeFunction () + "{\n"
    for (deactivateName) in task.mDeactivateOrderedList do
      let mangledName = "task.deactivate." + task.mTaskName + "." + deactivateName
      ioLLVMcode += "  call void @" + [mangledName assemblerRepresentation] + formalArgument + "\n"
    end
    ioLLVMcode += "  ret void\n"
    ioLLVMcode += "}\n\n"
    ioLLVMcode += llvmSeparatorLine ()
    ioLLVMcode += "\n"
  #---------- Task routine
    ioLLVMcode += "define internal void " + mainRoutineNameForTask (!task.mTaskName) + " ()" + llvmAttributeFunction () + "noreturn {\n"
    ioLLVMcode += "  call void " + setupNameForTaskType (!task.mTaskName) + " " + effectiveParameter + "\n"
    let taskLoopLabel = "loop." + [task.mTaskName assemblerRepresentation]
    ioLLVMcode += "  br label %" + taskLoopLabel + "\n\n"
    ioLLVMcode += taskLoopLabel + ":\n"
    if not task.mActivate then
      ioLLVMcode += "  call void @" + waitForActivationCallName + " ()\n"
    end
    ioLLVMcode += "  call void @" + [activateNameForTaskType (!task.mTaskName) assemblerRepresentation] + " " + effectiveParameter + "\n"
    ioLLVMcode += "  call void @func." + llvmNameForFunction (!task.mTaskName + "." + llvmNameForTaskLoopFunction () + "()") + " " + effectiveParameter + "\n"
    ioLLVMcode += "  call void " + deactivateNameForTaskType (!task.mTaskName) + " " + effectiveParameter + "\n"
    if task.mActivate then
      ioLLVMcode += "  call void @" + waitForActivationCallName + " ()\n"
    end
    ioLLVMcode += "  br label %" + taskLoopLabel + "\n"
    ioLLVMcode += "}\n\n"
  end
#------------------------------------- Start tasks
  ioLLVMcode += llvmTitleComment (!"Start tasks")
  ioLLVMcode += "define internal void @start.tasks ()" + llvmAttributeFunction () + "{\n"
  for task in orderedTaskList do (idx)
    ioLLVMcode += "  " + stackAddressForTask (!task.mTaskName)
    ioLLVMcode += " = getelementptr inbounds [" + (task.mStackSize / 4) + " x i32], "
    ioLLVMcode += "[" + (task.mStackSize / 4) + " x i32]* " + stackNameForTask (!task.mTaskName)
    ioLLVMcode += ", i32 0, i32 0\n"
    ioLLVMcode += "  %task.name." + idx + " = load i8*, i8** " + literalStringName (!task.mTaskNameStringIndex) + "\n"
    ioLLVMcode += "  call void @kernel_create_task ("
    ioLLVMcode += "i32 " + idx
    ioLLVMcode += ", i8* %task.name." + idx
    ioLLVMcode += ", i32* " + stackAddressForTask (!task.mTaskName)
    ioLLVMcode += ", i32 " + task.mStackSize
    ioLLVMcode += ", void ()* " + mainRoutineNameForTask (!task.mTaskName) + ")\n"
  end
  ioLLVMcode += "  ret void\n"
  ioLLVMcode += "}\n\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! CODE GENERATION: ACTIVATE FUNCTION
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @taskActivateFunctionIR : @abstractRoutineIR {
  public var @string mAssemblerTaskName
  public var @string mActivateServiceInvocationName
  public var @uint mTaskIndex
}

#·······················································································································

override method @taskActivateFunctionIR llvmCodeGeneration 
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  ioLLVMcode += "define internal void @" + [self.mRoutineMangledName assemblerRepresentation] + " "
  ioLLVMcode += "(%" + self.mAssemblerTaskName + "* " + llvmNameForSelf () + ")" + llvmAttributeFunction () + "{\n"
  ioLLVMcode += "  call void @" + self.mActivateServiceInvocationName + " (i8 " + self.mTaskIndex + ")\n"
  ioLLVMcode += "  ret void\n"
  ioLLVMcode += "}\n\n"
  ioLLVMcode += llvmSeparatorLine ()
  ioLLVMcode += "\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
