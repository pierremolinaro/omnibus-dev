
setter @instructionListIR appendLoadVolatileRegisterAddress
  ?let @objectInMemoryIR inTargetValue
  ?let @operandIR inIndexResult
  ?let @bigint inAddress
  ?let @bigint inElementArraySize
{
  self += !@loadRegisterAddressIR.new {!inTargetValue !inIndexResult !inAddress !inElementArraySize}
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @loadRegisterAddressIR : @abstractInstructionIR {
  @objectInMemoryIR mTargetValue
  @operandIR mIndexResult
  @bigint mRegisterAddress
  @bigint mElementArraySize
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @loadRegisterAddressIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds ioGenerationAdds
{
  let registerOffsetName = "%register.subscript." + ioGenerationAdds.mUniqueIndex
  let registerIndexName = "%register.index." + ioGenerationAdds.mUniqueIndex
  let scriptedRegisterAddressValueName = "%register.address." + ioGenerationAdds.mUniqueIndex
  ioGenerationAdds.mUniqueIndex ++
#--- Needs to perm an extend to i32 ?
  [[mIndexResult.mType kind] integer ?min:* ?max:* ?unsigned:* ?bitCount:let bitCount]
  if bitCount < 32 then
    ioLLVMcode += "  " + registerIndexName + " = zext " + [mIndexResult.mType  llvmTypeName]
               + " " + [mIndexResult.mValue llvmName] + " to i32\n"
  end
  ioLLVMcode += "  " + registerOffsetName + " = mul i32 "
             + if bitCount < 32 then registerIndexName else [mIndexResult.mValue llvmName] end
             + ", " + mElementArraySize + " ; " + [mElementArraySize hexString] + "\n"
  ioLLVMcode += "  " + scriptedRegisterAddressValueName + " = add i32 " + registerOffsetName
             + ", " + mRegisterAddress + " ; " + [mRegisterAddress hexString] + "\n"
  let llvmType = [[mTargetValue type] llvmTypeName]
  ioLLVMcode += "  " + [mTargetValue llvmName] + " = inttoptr i32 " + scriptedRegisterAddressValueName
             + " to " + llvmType + " *\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @loadRegisterAddressIR enterAccessibleEntities 
  ?!@accessibleEntities unused ioAccessibleEntities
  ?!@uint unused ioMaxBranchOfOnInstructions
{
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
