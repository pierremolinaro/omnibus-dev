#—————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @sizeofExpressionAST : @expressionAST {
  public var @LValueAST mLValue
}

#·······················································································································

refclass @sizeofTypeAST : @expressionAST {
  public var @lstring mTypeName
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#—————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <primary> ?!@ast ioAST !@expressionAST outExpression {
    $sizeof$
    $($
    <lvalue> !?ioAST ?let lvalue
    $)$
    outExpression = @sizeofExpressionAST.init {!lvalue}
  }

  #·····················································································································

  rule <primary> ?!@ast ioAST !@expressionAST outExpression {
    $sizeof$
    <type_definition> !?ioAST ?let typeName
    outExpression = @sizeofTypeAST.init {!typeName}
  }

  #·····················································································································

}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH FROM EXPRESSION                                                                                     
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @sizeofExpressionAST addDependenceEdgeForStaticExpression
  ?let @lstring unused inConstantName
  ?!@semanticTypePrecedenceGraph unused ioGraph
{
}

#·······················································································································

override method @sizeofTypeAST addDependenceEdgeForStaticExpression
  ?let @lstring unused inConstantName
  ?!@semanticTypePrecedenceGraph unused ioGraph
{
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @sizeofExpressionAST noteExpressionTypesInPrecedenceGraph
  ?!@semanticTypePrecedenceGraph unused ioGraph
{
}

#·······················································································································

override method @sizeofTypeAST noteExpressionTypesInPrecedenceGraph
  ?!@semanticTypePrecedenceGraph ioGraph
{
  [!?ioGraph noteNode !self.mTypeName]
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#—————————————————————————————————————————————————————————————————————————————————————————————————
#! @sizeofExpressionAST
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @sizeofExpressionAST analyzeExpression
  ?self:let @omnibusType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?optionalTargetType:let @omnibusType unused inOptionalTargetType
  ?context:let @semanticContext inContext
  ?mode:let @mode unused inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap unused ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList unused ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !result:@objectIR outResult
{
  if [inRoutineAttributes contains !.guard] then
    error self.mLValue.mIdentifier : "sizeof () cannot be used in guard" : outResult
  else
  #--- Analyze LValue
    [ioUniversalMap searchValuedObjectType !context:inContext !selfType:inSelfType !self.mLValue ?let type]
    let resultTypeProxy = [inContext.mTypeMap searchKey
      !["u" + inContext.mTargetParameters.mPointerSize nowhere]
    ]
    let resultType = [resultTypeProxy type]
    getNewTempValue (!resultType !?ioTemporaries ?outResult)
    ioInstructionGenerationList += !@sizeofInstructionIR.init {
      !outResult
      !type
    }
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#! @sizeofTypeAST
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @sizeofTypeAST analyzeExpression
  ?self:let @omnibusType unused inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?optionalTargetType:let @omnibusType unused inOptionalTargetType
  ?context:let @semanticContext inContext
  ?mode:let @mode unused inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap unused ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap unused  ioUniversalMap
  ?!alloca:@allocaList unused ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !result:@objectIR outResult
{
  if [inRoutineAttributes contains !.guard] then
    error self.mTypeName : "sizeof () cannot be used in guard" : outResult
  else
    let type = [[inContext.mTypeMap searchKey !self.mTypeName] type]
    let resultTypeProxy = [inContext.mTypeMap searchKey
      !["u" + inContext.mTargetParameters.mPointerSize nowhere]
    ]
    let resultType = [resultTypeProxy type]
    getNewTempValue (!resultType !?ioTemporaries ?outResult)
    ioInstructionGenerationList += !@sizeofInstructionIR.init {!outResult!type}
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  CODE GENERATION                                                                                                      
#—————————————————————————————————————————————————————————————————————————————————————————————————
#! @sizeofInstructionIR
#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @sizeofInstructionIR : @abstractInstructionIR {
  public var @objectIR mTarget
  public var @omnibusType mSourceType
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @sizeofInstructionIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  let typeName = [self.mSourceType llvmTypeName]
  ioLLVMcode += "  " + [self.mTarget llvmName] + ".asPtr = getelementptr inbounds " + typeName + ", " + typeName + "* null, i32 1\n"
  ioLLVMcode += "  " + [self.mTarget llvmName] + " = ptrtoint " + typeName + "* " + [self.mTarget llvmName] + ".asPtr to i32\n"
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
