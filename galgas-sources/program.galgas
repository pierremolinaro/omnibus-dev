#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

case . "plm-target"
message "a target definition text file with the .plm-target extension"
grammar plm_target_grammar
?sourceFilePath:@lstring unused inSourceFile {
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

case . "plm-import"
message "an imported text file with the .plm-import extension"
grammar plm_target_grammar
?sourceFilePath:@lstring unused inSourceFile {
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

case . "plm"
message "a source text file with the .plm extension"
grammar plm_grammar
?sourceFilePath:@lstring inSourceFile
{
  if ([option .mode value] == "") & not [option .outputConcreteSyntaxTree value] then
    importFilesAndCompile (!sourceFilePath:inSourceFile)
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

proc importFilesAndCompile ?sourceFilePath:let @lstring inSourceFile
{
#--- Parsing
  @ast ast = .default
  grammar plm_grammar in inSourceFile
    !?ast
    ?importClauseList:let importedFileList
    ?endOfSourceFile:let endOfSourceFile
#--- Recursively import files
  @stringset importedFileAbsolutePathSet = .setWithString {!inSourceFile.string}
  let currentDirectory = [inSourceFile stringByDeletingLastPathComponent]
  recursiveImportFiles (
    !?ast:ast
    !currentDirectory:currentDirectory
    !importClauseList:importedFileList
    !?importedFilePathSet:importedFileAbsolutePathSet
  )
#--- Check target names are not duplicated
  if @uint.errorCount == 0 then
    @stringset targetNameSet = {}
    for (targetName) in ast.mTargetListAST do
      if [targetNameSet hasKey !targetName.string] then
        error targetName : "Duplicated target"
      end
      targetNameSet += !targetName.string
    end
  end
#--- Compile project for every target
  if @uint.errorCount == 0 then
    if [ast.mTargetListAST length] == 0 then
      warning endOfSourceFile : "no target: only syntax analysis is performed"
    elsif [ast.mTargetListAST length] > 1 then
      warning endOfSourceFile : [[ast.mTargetListAST length] string] + " targets: only syntax analysis is performed"
    else
      for (targetName) in ast.mTargetListAST do
        compileProject (
          !sourceFile:inSourceFile
          !ast:ast
          !forTarget:targetName
          !endOfSourceFile:endOfSourceFile
          !currentDirectory:currentDirectory
          !importedFilePathSet:importedFileAbsolutePathSet
        )
      end
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

private proc compileProject
  ?sourceFile:let @lstring inSourceFile
  ?ast:let @ast inAST
  ?forTarget:let @lstring inTargetName
  ?endOfSourceFile:let @location inEndOfSourceFile
  ?currentDirectory:let @string inCurrentDirectory
  ?importedFilePathSet:let @stringset inImportedFileAbsolutePathSet
{
  let @timer parsingTime = .start 
  @ast ast = inAST
  @stringset sourceFileAbsolutePathSet = inImportedFileAbsolutePathSet
#------------------------------------------------------------ Add target specific files
  addTargetSpecificFiles (
    !forTarget:inTargetName
    !?toAST:ast
    !currentDirectory:inCurrentDirectory
    !?importedFilePathSet:sourceFileAbsolutePathSet
  )
  if @uint.errorCount == 0 then
  #------------------------------------------------------------ Add Predefined types
  #--- Add boolean type
    ast.mDeclarationListAST += !@booleanDeclarationAST.new
  #--- Literal integer type
    ast.mDeclarationListAST += !@literalIntegerDeclarationAST.new
  #--- Add String type
    ast.mDeclarationListAST += !@literalStringDeclarationAST.new
  end
#------------------------------------------------------------ Retain required drivers
  if @uint.errorCount == 0 then
    retainRequiredDrivers (!?ast)
  end
  if [option plm_options.timePasses value] then
    message "Parsing: " + [parsingTime string] + "\n" 
  end
#------------------------------------------------------------ Ordered declaration list
  @declarationListAST orderedDeclarationListAST = .default
  if @uint.errorCount == 0 then
    let @timer t = .start 
    buildOrderedDeclarationList (
      !ast:ast
      !sourceFile:inSourceFile.string
      !endOfSourceFile:inEndOfSourceFile
      ?orderedDeclarationList:orderedDeclarationListAST
    )
    if [option plm_options.timePasses value] then
      message "Declaration sorting: " + [t string] + "\n" 
    end
  end
#------------------------------------------------------------ Check "check target" declarations
  if @uint.errorCount == 0 then
    for (targetLocation acceptedTargetList) in ast.mCheckTargetListAST do
      var accepted = false
      for (possibleTarget) in acceptedTargetList while not accepted do
        accepted = possibleTarget.string == inTargetName
      end
      if not accepted then
        error targetLocation : "invalid target"
      end
    end
  end
#------------------------------------------------------------ Semantic context
  @semanticContext semanticContext = .default
  @staticEntityMap globalLiteralStringMap = .default
  @staticListInitializationMap staticListValueMap = {}
  ast.mDeclarationListAST = orderedDeclarationListAST
  @declarationDecorationList decoratedDeclarationList = {}
  @decoratedRegularRoutineList decoratedRoutineList = {}
  @globalVariableIRList globalVariableIRList = {}
  @routineListIR routineListIR = {}
  if @uint.errorCount == 0 then
    let @timer t = .start 
    buildSemanticContext (
      !sourceFile:inSourceFile
      !ast:ast
      !?staticEntityMap:globalLiteralStringMap
      !?staticlistValues:staticListValueMap
      ?context:semanticContext
      ?decoratedDeclarationList
      ?decoratedRoutineList
      ?routineListIR
      ?globalVariableIRList
    )
    if [option plm_options.timePasses value] then
      message "Context: " + [t string] + "\n" 
    end
  end
#------------------------------------------------------------ Semantic analysis
  @intermediateCodeStruct intermediateCodeStruct = .default
  if @uint.errorCount == 0 then
    let @timer t = .start 
    semanticAnalysis (
      !sourceFile:inSourceFile.string
      !ast:ast
      !decoratedDeclarationList:decoratedDeclarationList
      !decoratedRoutineList: decoratedRoutineList
      !routineListIR:routineListIR
      !context:semanticContext
      !endOfSourceFile:inEndOfSourceFile
      !staticEntityMap:globalLiteralStringMap
      ?intermediateCode:intermediateCodeStruct
    )
    if [option plm_options.timePasses value] then
      message "Semantic: " + [t string] + "\n" 
    end
  end
#------------------------------------------------------------ Code optimization
  if @uint.errorCount == 0 then
    let @timer tOpt = .start 
    codeOptimization (!inSourceFile.string !?intermediateCode:intermediateCodeStruct)
    if [option plm_options.timePasses value] then
      message "Optimization: " + [tOpt string] + "\n" 
    end
  end
#------------------------------------------------------------ Code generation
  if @uint.errorCount == 0 then
    codeGeneration (
      !currentDir:inCurrentDirectory
      !outputFile:inSourceFile.string
      !eof:inEndOfSourceFile
      !intermediateCode:intermediateCodeStruct
      !typeMap:semanticContext.mTypeMap
      !target:inTargetName
      !panicCodeType:semanticContext.mPanicCodeType
      !panicLineType:semanticContext.mPanicLineType
      !interrupts:semanticContext.mAvailableInterruptMap
      !staticlistValues:staticListValueMap
      !sourceFileSet:sourceFileAbsolutePathSet
      !targetParameters:semanticContext.mTargetParameters
      !globalVariables:globalVariableIRList
    )
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

private proc addTargetSpecificFiles
  ?forTarget:let @lstring inTargetName
  ?!toAST:@ast ioAST
  ?currentDirectory:let @string inCurrentDirectory
  ?!importedFilePathSet:@stringset ioImportedFileAbsolutePathSet
{
  if [option plm_options.useDirAsTargetDir value] != "" then # Target is defined in files
    let targetDirectory = [[option plm_options.useDirAsTargetDir value] absolutePathFromPath !inCurrentDirectory]
    if [targetDirectory directoryExists] then
      let targetDefinitionFullPath = targetDirectory + "/" + inTargetName.string + "/+config.plm-target"
      if [targetDefinitionFullPath fileExists] then
        grammar plm_target_grammar in [targetDefinitionFullPath nowhere]
          !?ioAST
          ?importClauseList:let importedFileList
          ?endOfSourceFile:*
        recursiveImportFileSystemTargetFiles (
          !?ast:ioAST
          !targetDirectory:targetDirectory + "/" + inTargetName.string
          !importClauseList:importedFileList
          !?importedFilePathSet:ioImportedFileAbsolutePathSet
        )
      else
        error inTargetName
          : "This target is not defined in '" + [option plm_options.useDirAsTargetDir value] + "' directory"
      end
    else
      error ["" nowhere]: "the '" + [option plm_options.useDirAsTargetDir value] + "' directory does not exist"
    end
  else # Target is defined in embedded files
  #--- Parse target definition file
    let @filewrapper fw = [filewrapper targetTemplates]
    if [fw fileExistsAtPath !inTargetName.string + "/+config.plm-target"] then
      let source = [fw textFileContentsAtPath !inTargetName.string + "/+config.plm-target"]
      grammar plm_target_grammar on (source, inTargetName.string)
        !?ioAST
        ?importClauseList:let importedFileList
        ?endOfSourceFile:*
      recursiveImportEmbeddedTargetFiles (
        !?ast:ioAST
        !currentDirectory:inTargetName.string
        !importClauseList:importedFileList
        !?importedFilePathSet:ioImportedFileAbsolutePathSet
      )
    else
      error inTargetName
        : "This target is not defined in embedded targets"
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

private proc recursiveImportFileSystemTargetFiles
  ?!ast:@ast ioAST
  ?targetDirectory:let @string inTargetDirectory
  ?importClauseList:@lstringlist inImportedClauseList
  ?!importedFilePathSet:@stringset ioImportedFileAbsolutePathSet
{
  for (path) in inImportedClauseList do
    let absolutePath = inTargetDirectory + "/" + path.string
    if [absolutePath fileExists] then
      if not [ioImportedFileAbsolutePathSet hasKey !absolutePath] then
        ioImportedFileAbsolutePathSet += !absolutePath
        if [absolutePath pathExtension] == "plm-import" then
          grammar plm_grammar in @lstring.new {!absolutePath !path.location}
            !?ioAST
            ?importClauseList:let importedFileList
            ?endOfSourceFile:*
          recursiveImportFileSystemTargetFiles (
            !?ast:ioAST
            !targetDirectory:inTargetDirectory
            !importClauseList:importedFileList
            !?importedFilePathSet:ioImportedFileAbsolutePathSet
          )
        elsif [absolutePath pathExtension] == "plm-target" then
          grammar plm_target_grammar in @lstring.new {!absolutePath !path.location}
            !?ioAST
            ?importClauseList:let importedFileList
            ?endOfSourceFile:*
          recursiveImportFileSystemTargetFiles (
            !?ast:ioAST
            !targetDirectory:inTargetDirectory
            !importClauseList:importedFileList
            !?importedFilePathSet:ioImportedFileAbsolutePathSet
          )
        else
          error path : "invalid extension (should be .plm-import or .plm-target)"
        end
      end
    else
      error path : "file does not exist in file system target definition"
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

private proc recursiveImportEmbeddedTargetFiles
  ?!ast:@ast ioAST
  ?currentDirectory:let @string inCurrentDirectory
  ?importClauseList:@lstringlist inImportedClauseList
  ?!importedFilePathSet:@stringset ioImportedFileAbsolutePathSet
{
  for (path) in inImportedClauseList do
    let fullPath = inCurrentDirectory + "/" + path
    let @filewrapper fw = [filewrapper targetTemplates]
    if [fw fileExistsAtPath !fullPath] then
      let embeddedPath = ":" + fullPath
      if not [ioImportedFileAbsolutePathSet hasKey !embeddedPath] then
        ioImportedFileAbsolutePathSet += !embeddedPath
        grammar plm_grammar on ([fw textFileContentsAtPath !fullPath], path.string)
          !?ioAST
          ?importClauseList:let importedFileList
          ?endOfSourceFile:*
        recursiveImportEmbeddedTargetFiles (
          !?ast:ioAST
          !currentDirectory:[fullPath stringByDeletingLastPathComponent]
          !importClauseList:importedFileList
          !?importedFilePathSet:ioImportedFileAbsolutePathSet
        )
      end
    else
      error path : "cannot find this file in embedded files"
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

private proc recursiveImportFiles
  ?!ast:@ast ioAST
  ?currentDirectory:@string inCurrentDirectory
  ?importClauseList:@lstringlist inImportedClauseList
  ?!importedFilePathSet:@stringset ioImportedFileAbsolutePathSet
{
  for (path) in inImportedClauseList do
    let absolutePath = [path.string absolutePathFromPath !inCurrentDirectory]
    if [absolutePath fileExists] then # Exists in file system ?
      if not [ioImportedFileAbsolutePathSet hasKey !absolutePath] then
        ioImportedFileAbsolutePathSet += !absolutePath
        if [absolutePath pathExtension] == "plm-import" then
          grammar plm_grammar in @lstring.new {!absolutePath !path.location}
            !?ioAST
            ?importClauseList:let importedFileList
            ?endOfSourceFile:*
          recursiveImportFiles (
            !?ast:ioAST
            !currentDirectory:inCurrentDirectory
            !importClauseList:importedFileList
            !?importedFilePathSet:ioImportedFileAbsolutePathSet
          )
        elsif [absolutePath pathExtension] == "plm-target" then
          grammar plm_target_grammar in @lstring.new {!absolutePath !path.location}
            !?ioAST
            ?importClauseList:let importedFileList
            ?endOfSourceFile:*
          recursiveImportFiles (
            !?ast:ioAST
            !currentDirectory:inCurrentDirectory
            !importClauseList:importedFileList
            !?importedFilePathSet:ioImportedFileAbsolutePathSet
          )
        else
          error path : "invalid extension (should be .plm-import or .plm-target)"
        end
      end
    else
      let @filewrapper fw = [filewrapper targetTemplates]
      if [fw fileExistsAtPath !path.string] then
        let embeddedPath = ":" + path
        if not [ioImportedFileAbsolutePathSet hasKey !embeddedPath] then
          ioImportedFileAbsolutePathSet += !embeddedPath
          if [path pathExtension] == "plm-import" then
            grammar plm_grammar on ([fw textFileContentsAtPath !path.string], path.string)
              !?ioAST
              ?importClauseList:let importedFileList
              ?endOfSourceFile:*
            recursiveImportFiles (
              !?ast:ioAST
              !currentDirectory:inCurrentDirectory
              !importClauseList:importedFileList
              !?importedFilePathSet:ioImportedFileAbsolutePathSet
            )
          elsif [path pathExtension] == "plm-target" then
            grammar plm_target_grammar on ([fw textFileContentsAtPath !path.string], path.string)
              !?ioAST
              ?importClauseList:let importedFileList
              ?endOfSourceFile:*
            recursiveImportFiles (
              !?ast:ioAST
              !currentDirectory:inCurrentDirectory
              !importClauseList:importedFileList
              !?importedFilePathSet:ioImportedFileAbsolutePathSet
            )
          else
            error path : "invalid extension (should be .plm-import or .plm-target)"
          end
        end
      else
        error path : "cannot find this file in file system and in embedded files"
      end
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
