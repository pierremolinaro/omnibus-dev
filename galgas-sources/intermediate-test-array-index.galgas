
setter @instructionListIR testArrayIndex
  ?let @objectIR inIndex
  ?let @location inErrorLocation
  ?let @bigint inSize
  !@bool outGeneratePanicInstruction
{
  [[inIndex type].kind extractInteger ?1* ?max:let max ?unsigned:let isUnsigned ?1*]
  outGeneratePanicInstruction = (not isUnsigned) || (max >= inSize)
  self += !@testArrayIndexIR.init {!inIndex !inErrorLocation !inSize}
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @testArrayIndexIR : @abstractInstructionIR {
  public var @objectIR mIndex
  public var @location mErrorLocation
  public var @bigint mSize
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @testArrayIndexIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext inGenerationContext
  ?!@generationAdds ioGenerationAdds
{
  [[self.mIndex type].kind extractInteger ?1* ?max:let max ?unsigned:let isUnsigned ?1*]
#--- Test index >= 0 (for signed index)
  if not isUnsigned then
    let testToZero = "%test.index.zero." + ioGenerationAdds.mUniqueIndex
    let testToZeroOk = "test.index.zero.ok." + ioGenerationAdds.mUniqueIndex
    let testToZeroFailure = "test.index.zero.fails." + ioGenerationAdds.mUniqueIndex
    ioGenerationAdds.mUniqueIndex += 1
    ioLLVMcode += "  " + testToZero + " = icmp sge " + [[self.mIndex type] llvmTypeName] + " " + [self.mIndex llvmName] + ", 0\n"
    ioLLVMcode += "  br i1 " + testToZero + ", label %" + testToZeroOk + ", label %" + testToZeroFailure + "\n\n"
  
    ioLLVMcode += testToZeroFailure + ":\n"
    ioLLVMcode += "  call void @" + [panicRoutineNameForLocationFile (!self.mErrorLocation) assemblerRepresentation] + " ("
    ioLLVMcode += [inGenerationContext.mPanicLineLLVMType llvmTypeName] + " " + [self.mErrorLocation endLine] + ", "
    ioLLVMcode += [inGenerationContext.mPanicCodeLLVMType llvmTypeName] + " " + panicCodeForNegativeArrayIndex () + ") noreturn \n"
    ioLLVMcode += "  unreachable\n\n"
  
    ioLLVMcode += testToZeroOk + ":\n"
  end
#--- Test index < size (only if index max value is >= size)
  if max >= self.mSize then
    let testToSize = "%test.index.size." + ioGenerationAdds.mUniqueIndex
    let testToSizeOk = "test.index.size.ok." + ioGenerationAdds.mUniqueIndex
    let testToSizeFailure = "test.index.size.fails." + ioGenerationAdds.mUniqueIndex
    ioLLVMcode += "  " + testToSize + " = icmp "
    ioLLVMcode += if isUnsigned then "ult" else "slt" end
    ioLLVMcode += " " + [[self.mIndex type] llvmTypeName] + " " + [self.mIndex llvmName] + ", " + self.mSize + "\n"
    ioLLVMcode += "  br i1 " + testToSize + ", label %" + testToSizeOk + ", label %" + testToSizeFailure + "\n\n"
  
    ioLLVMcode += testToSizeFailure + ":\n"
    ioLLVMcode += "  call void @" + [panicRoutineNameForLocationFile (!self.mErrorLocation) assemblerRepresentation] + " ("
    ioLLVMcode += [inGenerationContext.mPanicLineLLVMType llvmTypeName] + " " + [self.mErrorLocation endLine] + ", "
    ioLLVMcode += [inGenerationContext.mPanicCodeLLVMType llvmTypeName] + " " + panicCodeForTooLargeArrayIndex () + ") noreturn \n"
    ioLLVMcode += "  unreachable\n\n"
  
    ioLLVMcode += testToSizeOk + ":\n"
  end
#---
  ioGenerationAdds.mUniqueIndex += 1
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @testArrayIndexIR enterAccessibleEntities
  ?!@stringset ioInvokedRoutineSet
  ?!@uint unused ioMaxBranchOfOnInstructions
{
  if not [option omnibus_options.noPanicGeneration value] then
    ioInvokedRoutineSet += !panicRoutineNameForLocationFile (!self.mErrorLocation)
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
