#----------------------------------------------------------------------------------------------------------------------*
#  AST                                                                                                                 *
#----------------------------------------------------------------------------------------------------------------------*

class @literalStringDeclaration : @abstractDeclaration {
  @lstring mLiteralStringTypeName
}

#----------------------------------------------------------------------------------------------------------------------*

override getter @literalStringDeclaration location -> @location outLocation {
  outLocation = mLiteralStringTypeName.location
}

#----------------------------------------------------------------------------------------------------------------------*
#  PRECEDENCE GRAPH                                                                                                    *
#----------------------------------------------------------------------------------------------------------------------*

override method @literalStringDeclaration enterInPrecedenceGraph
  ?!@semanticTypePrecedenceGraph ioGraph
{
  [!?ioGraph addNode !mLiteralStringTypeName !self]
}

#----------------------------------------------------------------------------------------------------------------------*

override getter @literalStringDeclaration keyRepresentation -> @string outRepresentation {
  outRepresentation = "literal string " + mLiteralStringTypeName 
}

#----------------------------------------------------------------------------------------------------------------------*
#  ENTER IN CONTEXT                                                                                                    *
#----------------------------------------------------------------------------------------------------------------------*

override method @literalStringDeclaration enterInContext
  ?procedureDeclarationList:let @procedureDeclarationListAST unused inProcedureListAST
  ?!context:@semanticContext ioContext
  ?!staticStringMap:@globalLiteralStringMap unused ioGlobalLiteralStringMap
{
#--- Enter in type map
  [!?ioContext.mTypeMap insertKey
    !lkey:mLiteralStringTypeName
    !kind:.literalString
    !constructorMap:{}
    !procedureMap:{}
    !copiable:true
    !equatable:true
    !comparable:true
  ]
#--- Enter operators
  enterStaticStringOperators (
    !integerTypeName:mLiteralStringTypeName
    !?context:ioContext
  )
}

#----------------------------------------------------------------------------------------------------------------------*

proc enterStaticStringOperators
  ?integerTypeName:@lstring inBooleanTypeName
  ?!context:@semanticContext ioContext
{
  [@unifiedTypeMap-proxy makeProxy !?ioContext.mTypeMap !inBooleanTypeName ?let selfTypeProxy]
  [!?ioContext.mEqualOperatorMap insertKey
    !.new {!inBooleanTypeName.string + "." + inBooleanTypeName !inBooleanTypeName.location}
    !selfTypeProxy
    !.icmp_eq # TODO
  ]
  [!?ioContext.mNonEqualOperatorMap insertKey
    !.new {!inBooleanTypeName.string + "." + inBooleanTypeName !inBooleanTypeName.location}
    !selfTypeProxy
    !.icmp_ne # TODO
  ]
  [!?ioContext.mStrictInfOperatorMap insertKey
    !.new {!inBooleanTypeName.string + "." + inBooleanTypeName !inBooleanTypeName.location}
    !selfTypeProxy
    !.icmp_ult # TODO
  ]
  [!?ioContext.mInfEqualOperatorMap insertKey
    !.new {!inBooleanTypeName.string + "." + inBooleanTypeName !inBooleanTypeName.location}
    !selfTypeProxy
    !.icmp_ule # TODO
  ]
  [!?ioContext.mStrictSupOperatorMap insertKey
    !.new {!inBooleanTypeName.string + "." + inBooleanTypeName !inBooleanTypeName.location}
    !selfTypeProxy
    !.icmp_ugt # TODO
  ]
  [!?ioContext.mSupEqualOperatorMap insertKey
    !.new {!inBooleanTypeName.string + "." + inBooleanTypeName !inBooleanTypeName.location}
    !selfTypeProxy
    !.icmp_uge # TODO
  ]
}

#----------------------------------------------------------------------------------------------------------------------*
#  SEMANTIC ANALYSIS                                                                                                   *
#----------------------------------------------------------------------------------------------------------------------*

override method @literalStringDeclaration initAnalysis ?!context:@semanticContext unused ioContext { }

#----------------------------------------------------------------------------------------------------------------------*

override method @literalStringDeclaration semanticAnalysis
  ?context:let @semanticContext unused inContext
  ?!intermediateCode:@intermediateCodeStruct ioIntermediateCodeStruct
{
}

#----------------------------------------------------------------------------------------------------------------------*
#  CODE GENERATION                                                                                                     *
#----------------------------------------------------------------------------------------------------------------------*

class @literalStringTypeIR : @abstractTypeIR {
  @string mLiteralStringTypeName
}

#----------------------------------------------------------------------------------------------------------------------*

private filewrapper literalStringGenerationTemplate in "../generation-templates" {
}{
}{
  template declaration "literal-string-declaration.galgasTemplate"
    ?@string STATIC_STRING_NAME
}

#----------------------------------------------------------------------------------------------------------------------*

override getter @literalStringTypeIR headerCodeGenerationForType -> @string outCode {
  outCode = [filewrapper literalStringGenerationTemplate.declaration
    !mLiteralStringTypeName
  ]
}

#----------------------------------------------------------------------------------------------------------------------*
