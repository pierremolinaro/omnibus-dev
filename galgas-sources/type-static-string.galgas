#—————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @literalStringDeclarationAST : @abstractDeclarationAST {
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override getter @literalStringDeclarationAST locationForErrorSignaling -> @location outLocation {
  outLocation = .nowhere
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH                                                                                                     
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @literalStringDeclarationAST enterInPrecedenceGraph
  ?!@semanticTypePrecedenceGraph ioGraph
{
  let literalStringTypeName = [staticStringTypeName () nowhere]
  [!?ioGraph addNode !literalStringTypeName !self]
  [!?ioGraph addEdge !literalStringTypeName !["u8" nowhere]]
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override getter @literalStringDeclarationAST keyRepresentationForErrorSignaling -> @string outRepresentation {
  outRepresentation = "literal string " + staticStringTypeName () 
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  ENTER IN CONTEXT                                                                                                     
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @literalStringDeclarationAST enterInContext
  ?!context:@semanticContext ioContext
  ?!@declarationDecorationList unused ioDecoratedDeclarationList
  ?!@decoratedRegularRoutineList unused ioDecoratedRoutineList
  ?!@routineListIR unused ioRoutineListIR
  ?!staticlistValues:@staticListInitializationMap unused ioStaticListValueMap
  ?!staticEntityMap:@staticEntityMap unused ioStaticEntityMap
  ?!registerUserAccess:@controlRegisterUserAccesMapAST unused ioControlRegisterUserAccesMapAST
  ?!userDefinedTypes:@userLLVMTypeDefinitionListIR unused ioUserLLVMTypeDefinitionListIR
{
#--- Enter in type map
  let selfType = literalStringType ()
  [!?ioContext.mTypeMap insertType
    ![staticStringTypeName () nowhere]
    !type: selfType
  ]
#--- Enter assignment operator
  let key = assignmentOperatorKey (!selfType !.nowhere !selfType)
  [!?ioContext.mAssignmentOperatorMap insertKey !key !@simpleCopyAssignmentOperatorUsage.init]
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
