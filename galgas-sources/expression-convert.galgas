#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @convertExpressionAST : @expressionAST {
  @expressionAST mExpression
  @lstring mTypeName
  @location mEndOfExpression
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension plm_syntax {

  #·····················································································································

  rule <primary> ?!@ast ioAST !@expressionAST outExpression {
    $convert$
    @lstring typeName
    select
      typeName = ["" here]
    or
      <type_definition> !?ioAST ?typeName
    end
    $($
    <expression> !?ioAST ?outExpression
    let endOfExpression = @location.here
    $)$
    outExpression = @convertExpressionAST.new {
      !outExpression
      !typeName
      !endOfExpression
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH FROM EXPRESSION                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @convertExpressionAST addDependenceEdgeForStaticExpression
  ?let @lstring inConstantName
  ?!@semanticTypePrecedenceGraph ioGraph
{
  [mExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
  if mTypeName.string != "" then
    let typeName = llvmTypeNameFromPLMname (!mTypeName)
    [!?ioGraph noteNode !typeName]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @convertExpressionAST noteExpressionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  [mExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
  if mTypeName.string != "" then
    let typeName = llvmTypeNameFromPLMname (!mTypeName)
    [!?ioGraph noteNode !typeName]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! @convertExpressionAST
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @convertExpressionAST analyzeExpression
  ?self:let @PLMType inSelfType
  ?contextFlags:let @contextFlags inContextFlags
  ?routineNameForInvocationGraph:let @lstring inCallerNameForInvocationGraph
  ?optionalTargetType:let @PLMType inOptionalTargetType
  ?context:let @semanticContext inContext
  ?mode:let @mode inCurrentMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@staticStringMap ioGlobalLiteralStringMap
  ?!variableMap:@universalValuedObjectMapForContext ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !result:@objectIR outResult
{
#--- Analyze expression
  [mExpression analyzeExpression
    !self:inSelfType
    !contextFlags:inContextFlags
    !routineNameForInvocationGraph:inCallerNameForInvocationGraph
    !optionalTargetType:inOptionalTargetType
    !context:inContext
    !mode:inCurrentMode
    !?temporary:ioTemporaries
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:ioInstructionGenerationList
    ?result:@objectIR expressionResultPossibleReference
  ]
  [!?ioInstructionGenerationList appendLoadWhenReference
    !?ioTemporaries
    !expressionResultPossibleReference
    ?let expressionResult
  ]  
#--- 
  if not [inCurrentMode panicAllowed] then
    error mTypeName : "operations that can generate panic are not allowed in `" + panicModeName () + " mode"
  end
#--- Analyze type
  let @PLMType resultType = if mTypeName.string == ""
    then inOptionalTargetType
    else [@unifiedTypeMap-proxy.searchKey {!inContext.mTypeMap !mTypeName} type]
  end
#--- Check types
  if not [resultType.kind isInteger] then
    error mTypeName : "this type is not an integer type" : outResult
  elsif not [[expressionResult type].kind isInteger] then
    error mEndOfExpression : "expression type is not an integer type" : outResult
  else
    [[expressionResult type].kind integer ?min:let minSource ?max:let maxSource ?2*]
    [resultType.kind integer ?min:let minTarget ?max:let maxTarget ?2*]
    let alwaysPossible = (minTarget <= minSource) && (maxTarget >= maxSource)
    if alwaysPossible then
      error mTypeName:"useless explicit conversion"
    end
    getNewTempValue (!resultType !?ioTemporaries ?outResult)
    if [option plm_options.noPanicGeneration value] then
      ioInstructionGenerationList += !@truncateInstructionIR.new {
        !outResult
        !expressionResult
      }
    else
      ioInstructionGenerationList += !@convertInstructionIR.new {
        !outResult
        !expressionResult
        !mTypeName.location
      }
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  CODE GENERATION                                                                                                      
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#! @convertInstructionIR
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @convertInstructionIR : @abstractInstructionIR {
  @objectIR mTarget
  @objectIR mOperand
  @location mLocation
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @convertInstructionIR enterAccessibleEntities
  ?!@accessibleEntities unused ioAccessibleEntities
  ?!@uint unused ioMaxBranchOfOnInstructions
{
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @convertInstructionIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext inGenerationContext
  ?!@generationAdds ioGenerationAdds
{
  [!?ioGenerationAdds.mStaticStringMap findOrAddStaticString
    ![[[mLocation file] lastPathComponent] stringByDeletingPathExtension]
    ?let staticStringIndex
  ]
  let lbl = [[mLocation locationIndex] string]
  let convertMinOkLabel = "min." + lbl + ".ok"
  let convertMaxOkLabel = "max." + lbl + ".ok"
  let convertFailLabel  = "fail." + lbl
  let compareMinVar     = "cmp." + lbl + ".min"
  let compareMaxVar     = "cmp." + lbl + ".max"
  [[mTarget type].kind integer ?min:let minTarget ?max:let maxTarget ?2*]
  [[mOperand type].kind integer ?min:let minSource ?max:* ?unsigned:let operandIsUnsigned ?1*]
  let operandType = [mOperand llvmTypeName]
  if minTarget != minSource then
    ioLLVMcode += "  %" + compareMinVar + " = icmp "
    ioLLVMcode += if operandIsUnsigned then "uge" else "sge" end
    ioLLVMcode += " " + operandType + " " + [mOperand llvmName] + ", " + minTarget + "\n"
    ioLLVMcode += "  br i1 %" + compareMinVar + ", label %" + convertMinOkLabel + ", label %" + convertFailLabel + "\n\n"
    ioLLVMcode += convertMinOkLabel + ":\n"
  end
  ioLLVMcode += "  %" + compareMaxVar + " = icmp "
  ioLLVMcode += if operandIsUnsigned then "ule" else "sle" end
  ioLLVMcode += " " + operandType + " " + [mOperand llvmName] + ", " + maxTarget + "\n"
  ioLLVMcode += "  br i1 %" + compareMaxVar + ", label %" + convertMaxOkLabel + ", label %" + convertFailLabel + "\n\n"
  ioLLVMcode += convertFailLabel + ":\n"
  ioLLVMcode += "  call void @raise.panic." + staticStringIndex + " ("
  ioLLVMcode += inGenerationContext.mPanicLineLLVMType + " " + [mLocation line] + ", "
  ioLLVMcode += inGenerationContext.mPanicCodeLLVMType + " " + panicCodeForConvertOverflow () + ") ; File '" + [[mLocation file] lastPathComponent] + "'\n"
  ioLLVMcode += "  unreachable\n\n"
  ioLLVMcode += convertMaxOkLabel + ":\n"
  ioLLVMcode += "  " + [mTarget llvmName] + " = trunc " + operandType + " " + [mOperand llvmName] + " to " + [mTarget llvmTypeName] + "\n"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
