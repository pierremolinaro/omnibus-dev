#—————————————————————————————————————————————————————————————————————————————————————————————————
#!  AST                                                                                                                  
#—————————————————————————————————————————————————————————————————————————————————————————————————

enum @guardKind {
  case baseGuard
  case convenienceGuard (@callInstructionAST base)
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @guardDeclarationAST : @abstractDeclarationAST {
  public var @lstring mReceiverTypeName # Empty string if standalone
  public var @string mReceiverLLVMBaseTypeName
  public var @lstring mGuardName
  public var @bool mIsPublic
  public var @lstringlist mGuardAttributeList
  public var @routineFormalArgumentListAST mGuardFormalArgumentList
  public var @guardKind mGuardKind
  public var @instructionListAST mGuardInstructionList
  public var @location mEndOfGuardDeclaration
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#!  SYNTAX                                                                                                               
#—————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <guard> ?!@ast ioAST
               ?receiverTypeName: let @lstring inReceiverTypeName
               ?receiverLLVMBaseTypeName: let @string inReceiverLLVMBaseTypeName {
    let @bool publicAccess
    select
      publicAccess = false
    or
      $public$
      publicAccess = true
    end
    $guard$
    $identifier$ ?let guardName
    var @lstringlist attributeList = §[]
    repeat
    while
      $@attribute$ ?let attribute
      attributeList += !attribute
    end
    <procedure_formal_arguments> !?ioAST ?let guardFormalArgumentList
    let @guardKind guardKind
    select
      guardKind = .baseGuard
    or
      $:$
      <procedure_call> !?ioAST ?let instruction
      guardKind = .convenienceGuard {!base:instruction}
    end
    ${$
    <instructionList> !?ioAST ?let instructionList
    $}$
#     ioGuardList +=
   ioAST.mDeclarationListAST += !@guardDeclarationAST.init {
      !inReceiverTypeName
      !inReceiverLLVMBaseTypeName
      !guardName
      !publicAccess
      !attributeList
      !guardFormalArgumentList
      !guardKind
      !instructionList
      !.here
    }
  }

  #·····················································································································

}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES IN PRECEDENCE GRAPH                                                                                       
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @guardDeclarationAST enterInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  var s = "guard."
  if self.mReceiverTypeName.string != "" then
    s += self.mReceiverTypeName.string + "."
  end
  s += self.mGuardName.string + "("
  for (formalArgumentPassingMode selector * *) in self.mGuardFormalArgumentList do
    s += [formalArgumentPassingMode formalPassingModeString] + selector + ":"
  end
  s += ")"
  [!?ioGraph addNode !.init {!s !self.mGuardName.location} !self]
#---
  for (* * formalArgumentTypeName *) in self.mGuardFormalArgumentList do
    [!?ioGraph noteNode !formalArgumentTypeName]
  end
  [self.mGuardInstructionList noteInstructionListTypesInPrecedenceGraph !?ioGraph]
  if self.mReceiverTypeName.string != "" then
    [!?ioGraph noteNode !self.mReceiverTypeName]
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override getter @guardDeclarationAST keyRepresentationForErrorSignaling -> @string {
  result = "guard " + self.mReceiverTypeName + "." + self.mGuardName
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override getter @guardDeclarationAST locationForErrorSignaling -> @location {
  result = self.mGuardName.location
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  ENTER IN CONTEXT                                                                                                     
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @guardDeclarationAST enterInContext
  ?!context:@semanticContext ioContext
  ?!@declarationDecorationList ioDecoratedDeclarationList
  ?!@decoratedRegularRoutineList unused ioDecoratedRoutineList
  ?!@routineListIR unused ioRoutineListIR
  ?!staticlistValues:@staticListInitializationMap unused ioStaticListValueMap
  ?!staticEntityMap:@staticEntityMap unused ioStaticEntityMap
  ?!registerUserAccess:@controlRegisterUserAccesMapAST unused ioControlRegisterUserAccesMapAST
  ?!userDefinedTypes:@userLLVMTypeDefinitionListIR unused ioUserLLVMTypeDefinitionListIR
{
#--- Signature
  routineTypedSignature (!?ioContext.mTypeMap !self.mGuardFormalArgumentList ?let signature)
#--- Guard mangled name
  let guardMangledName = [signature mangledName !receiverTypeName: self.mReceiverTypeName !functionName: self.mGuardName]
#--- Guard user routine LLVM name
  let guardUserLLVMName = guardUserLLVMName (
    !self.mReceiverLLVMBaseTypeName
    !self.mGuardName
    !self.mGuardFormalArgumentList
  )
#--- Guard implementation routine LLVM name
  let guardImplementationLLVMName = guardImplementationLLVMName (
    !self.mReceiverLLVMBaseTypeName
    !self.mGuardName
    !self.mGuardFormalArgumentList
  )
  [!?ioContext.mGuardMap insertKey
    !guardMangledName
    !self.mIsPublic
    !signature
    !guardUserLLVMName
    !guardImplementationLLVMName
  ]
  ioDecoratedDeclarationList += !@decoratedGuardDeclaration.init {
    !self.mReceiverTypeName
    !self.mGuardName
    !self.mIsPublic
    !self.mGuardAttributeList
    !self.mGuardFormalArgumentList
    !self.mGuardKind
    !self.mGuardInstructionList
    !self.mEndOfGuardDeclaration
  }
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#!   UTILITIES                                                                                                            
#—————————————————————————————————————————————————————————————————————————————————————————————————

private func guardUserLLVMName ?let @string inReceiverLLVMBaseTypeName
                               ?let @lstring inGuardName
                               ?let @routineFormalArgumentListAST inFormalArgumentList -> @lstring
{
  var s = "guard.user."
  s += inReceiverLLVMBaseTypeName + "."
  s += inGuardName.string
  s += "("
  for (formalArgumentPassingMode selector * *) in inFormalArgumentList do
    s += [formalArgumentPassingMode formalPassingModeString] + selector + ":"
  end
  s += ")"
  result = .init {!s !inGuardName.location}
}

#·······················································································································

private func guardImplementationLLVMName ?let @string inReceiverLLVMBaseTypeName
                                         ?let @lstring inGuardName
                                         ?let @routineFormalArgumentListAST inFormalArgumentList -> @lstring
{
  var s = "guard.implementation."
  s += inReceiverLLVMBaseTypeName + "."
  s += inGuardName.string
  s += "("
  for (formalArgumentPassingMode selector * *) in inFormalArgumentList do
    s += [formalArgumentPassingMode formalPassingModeString] + selector + ":"
  end
  s += ")"
  result = .init {!s !inGuardName.location}
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#!  ENTER IN CONTEXT                                                                                                    
#—————————————————————————————————————————————————————————————————————————————————————————————————

map @guardMapForContext {
  public var @bool mIsPublic
  public var @routineTypedSignature mSignature
  public var @lstring mUserRoutineLLVMName
  public var @lstring mImplementationRoutineLLVMName

  insert insertKey error message "the '%K' guard is already declared in %L"
  search searchKey error message "there is no '%K' guard"
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#!  SEMANTIC ANALYSIS                                                                                                    
#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @decoratedGuardDeclaration : @abstractDecoratedDeclaration {
  public var @lstring mReceiverTypeName
  public var @lstring mGuardName
  public var @bool mIsPublic
  public var @lstringlist mGuardAttributeList
  public var @routineFormalArgumentListAST mGuardFormalArgumentList
  public var @guardKind mGuardKind
  public var @instructionListAST mGuardInstructionList
  public var @location mEndOfGuardDeclaration
}

#·······················································································································

override method @decoratedGuardDeclaration semanticAnalysis
  ?context:let @semanticContext inContext
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!intermediateCode:@intermediateCodeStruct ioIntermediateCodeStruct
{
  let receiverType = [[inContext.mTypeMap searchKey !self.mReceiverTypeName] type]
#----------- 
  var warnIfUnused = true
  for (attribute) in self.mGuardAttributeList do
    if attribute.string == noUnusedWarningAttribute () then
      if not warnIfUnused then
        error attribute : "duplicate @" + noUnusedWarningAttribute () + " attribute"
      end
      warnIfUnused = false    
    else
      error attribute : "unknown attribute; available attribute is @" + noUnusedWarningAttribute ()
    end
  end
#--- Variable map
  var universalMap = inContext.mValuedObjectMap
  [!?universalMap openOverrideForSelectBlock]
  [!?universalMap openBranch]
  var @routineFormalArgumentListIR formalArguments = §[]
  let @guardKindGenerationIR convenienceGuardGenerationIR
  var @allocaList allocaList = §[]
  var @instructionListIR instructionGenerationList = §[]
  switch self.mGuardKind
  case baseGuard :
  #--- Add 'accept" output variable
    let resultVarName = @lstring.init {!acceptVariableOmnibusName() !self.mGuardName.location}
#    let acceptableTypeList = [inContext.mAssignableTypesFromCompileTimeType_Map listForKey !ctBoolTypeName ()]
    let resultType = [inContext booleanType !resultVarName.location]
#    if [acceptableTypeList length] == 0 then
#      error resultVarName : "There is no type assignable from compile time bool type" : resultType
#    elsif [acceptableTypeList length] == 1 then
#      [acceptableTypeList first ?resultType]
#    else
#      error resultVarName : "There are several types assignable from compile time bool type" : resultType
#    end
#    [inContext.mTypeMap searchKey !lkey:[boolTypeName () nowhere] ?type:let resultType]
    allocaList += !llvmNameForLocalVariable (!resultVarName.string) !resultType !false
    [!?universalMap insertLocalVariable
      !resultVarName
      !resultType
      !resultVarName
      !.noValue
      !true
    ]
  #--- Analyse formal arguments
    enterFormalArguments (
      !context:inContext
      !self.mGuardFormalArgumentList
      !?universalMap
      !?formalArguments
      !warningOnUnusedArgs: true
    )
    convenienceGuardGenerationIR = .baseGuard
  #--- Analyze instruction list
    [self.mGuardInstructionList analyzeRoutineInstructionList
      !self:receiverType
      !routineAttributes:.mutatingRoutine
      !context:inContext
      !mode:.guardMode
      !?temporary:ioTemporaries
      !?staticEntityMap:ioIntermediateCodeStruct.mStaticEntityMap
      !?variableMap:universalMap
      !?alloca:allocaList
      !?generatedCode:instructionGenerationList
    ]
  case convenienceGuard (baseGuardInstruction):
  #--- Analyse formal arguments
    enterFormalArguments (
      !context:inContext
      !self.mGuardFormalArgumentList
      !?universalMap
      !?formalArguments
      !warningOnUnusedArgs: true
    )
  #--- Analyze base guard
    [baseGuardInstruction baseGuardAnalyze 
      !self:receiverType
      !routineAttributes:.mutatingRoutine
      !context:inContext
      !mode:.serviceMode # Convenience guards instructions are executed in service mode
      !?temporary:ioTemporaries
      !?staticEntityMap:ioIntermediateCodeStruct.mStaticEntityMap
      !?variableMap:universalMap
      ?convenienceGuardGenerationIR
    ]
  #--- Analyze instruction list
    [self.mGuardInstructionList analyzeRoutineInstructionList
      !self:receiverType
      !routineAttributes: .mutatingRoutine
      !context:inContext
      !mode:.serviceMode # Convenience guards instructions are executed in service mode
      !?temporary:ioTemporaries
      !?staticEntityMap:ioIntermediateCodeStruct.mStaticEntityMap
      !?variableMap:universalMap
      !?alloca:allocaList
      !?generatedCode:instructionGenerationList
    ]
  end 
#--- End of variableMap
  [universalMap checkLocalVariableFinalState !?instructionGenerationList]
  [!?universalMap closeBranch !self.mEndOfGuardDeclaration]
  [!?universalMap closeOverride !self.mEndOfGuardDeclaration]
#--- Guard mangled name
  let guardUserLLVMName = guardUserLLVMName (
    !receiverType.llvmBaseTypeName
    !self.mGuardName
    !self.mGuardFormalArgumentList
  )
  let guardImplementationLLVMName = guardImplementationLLVMName (
    !receiverType.llvmBaseTypeName
    !self.mGuardName
    !self.mGuardFormalArgumentList
  )
#--- Code generation
  ioIntermediateCodeStruct.mRoutineListIR += !@guardUserRoutineIR.init {
    !guardUserLLVMName
    !isRequired:false # Is not root
    !warnsIfUnused:warnIfUnused
    !guardImplementationLLVMName
    !formalArguments
    !receiverType
    !convenienceGuardGenerationIR
  }
  ioIntermediateCodeStruct.mRoutineListIR += !@guardImplementationRoutineIR.init {
    !guardImplementationLLVMName
    !isRequired:false # Is not root
    !warnsIfUnused:warnIfUnused
    !formalArguments
    !receiverType
    !convenienceGuardGenerationIR
    !allocaList
    !instructionGenerationList
  }
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#!  SEMANTIC ANALYSIS                                                                                                    
#—————————————————————————————————————————————————————————————————————————————————————————————————

abstract method @callInstructionAST baseGuardAnalyze
  ?self:let @omnibusType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  !@guardKindGenerationIR outConvenienceGuardGenerationIR

#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @standAloneProcedureCallInstructionAST baseGuardAnalyze
  ?self:let @omnibusType unused inSelfType
  ?routineAttributes:let @routineAttributes unused inRoutineAttributes
  ?context:let @semanticContext unused inContext
  ?mode:let @mode unused inMode
  ?!temporary:@semanticTemporariesStruct unused ioTemporaries
  ?!staticEntityMap:@staticEntityMap unused ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap unused ioUniversalMap
  !@guardKindGenerationIR outConvenienceGuardGenerationIR
{
  error self.mSandAloneRoutineName : "NOT HANDLED YET" : outConvenienceGuardGenerationIR
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @procedureCallInstructionAST baseGuardAnalyze
  ?self:let @omnibusType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  !@guardKindGenerationIR outConvenienceGuardGenerationIR
{
  var @allocaList baseGuardAllocaList = §[]
  var @instructionListIR baseGuardInstructionGenerationList = §[]
#--- Analyze header identifier : it a variable, as a standalone procedure call is handler otherwise
  var @objectIR currentObject
  if self.mIdentifier.string == "" then # Receiver is self
    if inSelfType.kind == .void then
      error self.mIdentifier : "self is not available in this context" : currentObject
    else
      currentObject = .reference {!type:inSelfType !llvmName:llvmNameForSelf ()}
    end
  else
    [ioUniversalMap searchValuedObject
      !self.mIdentifier
      !inMode
      !ioTemporaries.mInitializedDriverSet
      ?currentObject
    ]
  end
#--- Insulate last access
  var accessList = self.mAccessList
  [!?accessList popLast ?let lastAccess]
#---- Loop on access list
  for (access) in accessList do
    let currentType = [currentObject type]
    switch access
    case property (@lstring propertyName) :
#      let typeOmnibusName = @lstring.new {!currentType.omnibusTypeDescriptionName !propertyName.location}
      let propertyGetterMap = [inContext propertyGetterMap !currentType]
#      let @propertyGetterMap propertyGetterMap = currentType.propertyGetterMapp
      [propertyGetterMap searchKey !propertyName ?* ?let propertyAccess]
      switch propertyAccess
      case constantProperty (*) :
        error propertyName: "a constant property not available in this context" : currentObject
      case storedProperty (@omnibusType propertyType @uint propertyIndex) :
        [!?ioTemporaries newTempLLVMVar ?let property_llvmName]
        [!?baseGuardInstructionGenerationList appendGetUniversalPropertyReference
          !currentType
          ![currentObject llvmName]
          !property_llvmName
          !propertyIndex
          !propertyName
        ]
        currentObject = .reference {!type:propertyType !llvmName:property_llvmName}
      case computedProperty (2*) :
        error propertyName : "a computed property cannot be named in guard" : currentObject
      end
    case arrayAccess (* @location endOfIndex *) :
      error endOfIndex : "not handled yet" : currentObject
    end
  end
#--- Handle last access
  switch lastAccess
  case property (@lstring methodName) :
    let currentType = [currentObject type]
    let guardMangledName = [self.mArguments mangledName !receiverTypeName: currentType.omnibusTypeDescriptionName !functionName: methodName]
    [inContext.mGuardMap searchKey !guardMangledName ?* ?let formalSignature ?* ?let guardRoutineImplentationLLVMName]
  #--- Add receiver as first parameter
    var @procCallEffectiveParameterListIR effectiveParameterListIR = §[
      .init (.outputInput, .reference {!type:currentType !llvmName:[currentObject llvmName]})
    ]
  #--- Analyze effective parameters
    analyzeEffectiveParameters (
      !self:inSelfType
      !routineAttributes:inRoutineAttributes
      !formal:formalSignature
      !effective:self.mArguments
      !errorLocation:methodName.location
      !context:inContext
      !requiredMode:inMode
      !?temporary:ioTemporaries
      !?staticEntityMap:ioStaticEntityMap
      !?variableMap:ioUniversalMap
      !?alloca:baseGuardAllocaList
      !?instructionListIR:baseGuardInstructionGenerationList
      !?effectiveIR:effectiveParameterListIR
    )
  #--- Generate guards
    outConvenienceGuardGenerationIR = .convenienceGuard {
      !baseGuardAllocaList:baseGuardAllocaList
      !baseGuardInstructionGenerationList:baseGuardInstructionGenerationList
      !baseGuardMangledName:guardRoutineImplentationLLVMName.string
      !baseGuardEffectiveParameterList:effectiveParameterListIR
    }
  case arrayAccess (* @location endOfIndex *) :
    error endOfIndex : "a property access is required here" : outConvenienceGuardGenerationIR
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#!  CODE GENERATION                                                                                                      
#—————————————————————————————————————————————————————————————————————————————————————————————————

enum @guardKindGenerationIR {
  case baseGuard
  case convenienceGuard (@allocaList baseGuardAllocaList
                         @instructionListIR baseGuardInstructionGenerationList
                         @string baseGuardMangledName
                         @procCallEffectiveParameterListIR baseGuardEffectiveParameterList)
}

#·······················································································································

refclass @guardUserRoutineIR : @abstractRoutineIR {
  public var @string mMangledImplementationGuardName
  public var @routineFormalArgumentListIR mFormalArgumentListForGeneration
  public var @omnibusType mReceiverType
  public var @guardKindGenerationIR mGuardKindGenerationIR
}

#·······················································································································

override method @guardUserRoutineIR enterAccessibleEntities
   ?!@stringset ioInvokedRoutineSet
   ?!@uint unused ioMaxBranchOfOnInstructions
{
  ioInvokedRoutineSet += !self.mMangledImplementationGuardName
}

#·······················································································································

override method @guardUserRoutineIR svcDeclarationGeneration
  ?!@primitiveAndServiceIRlist ioPrimitiveAndServiceList
  ?!@sectionIRlist ioSectionList
  ?!@generationAdds unused ioGenerationAdds
{
  let userMangledName = [self.mRoutineMangledName assemblerRepresentation]
  let implementationMangledName = [self.mMangledImplementationGuardName assemblerRepresentation]
  switch self.mGuardKindGenerationIR
  case convenienceGuard (* * * *) :
    ioPrimitiveAndServiceList += !userMangledName !implementationMangledName !false
  case baseGuard :
    ioSectionList +=
      !userMangledName
      !implementationMangledName
      !invocationFromAnyMode: false
  end
}

#·······················································································································

override method @guardUserRoutineIR llvmCodeGeneration 
  ?!@string ioLLVMcode
  ?let @generationContext unused inGenerationContext
  ?!@generationAdds unused ioGenerationAdds
{
  let prototype = llvmFunctionPrototype (
    !"i1" # Boolean
    ![self.mRoutineMangledName assemblerRepresentation]
    !self.mReceiverType
    !self.mFormalArgumentListForGeneration
  )
  ioLLVMcode += "declare " + prototype + "\n\n"
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

refclass @guardImplementationRoutineIR : @abstractRoutineIR {
  public var @routineFormalArgumentListIR mFormalArgumentListForGeneration
  public var @omnibusType mReceiverType
  public var @guardKindGenerationIR mGuardKindGenerationIR
  public var @allocaList mAllocaList
  public var @instructionListIR mInstructionGenerationList
}

#·······················································································································

override method @guardImplementationRoutineIR llvmCodeGeneration 
  ?!@string ioLLVMcode
  ?let @generationContext inGenerationContext
  ?!@generationAdds ioGenerationAdds
{
  let guardRoutineName = [self.mRoutineMangledName assemblerRepresentation]
  ioLLVMcode += "define i1 @" + guardRoutineName + " ("
  let receiverLLVMTypeName = if self.mReceiverType.kind == .void then
    ""
  else
    [self.mReceiverType llvmTypeName]
  end
  var first = true
  if receiverLLVMTypeName != "" then
    ioLLVMcode += receiverLLVMTypeName + "* " + llvmNameForSelf ()
    first = false
  end
  for (mFormalArgumentKind mFormalArgumentType mFormalArgumentName) in self.mFormalArgumentListForGeneration do
    if first then
      first = false
    else
      ioLLVMcode += ", "
    end
    switch mFormalArgumentKind
    case input :
      ioLLVMcode += [mFormalArgumentType llvmTypeName] + " %in." + mFormalArgumentName
    case output :
      ioLLVMcode += [mFormalArgumentType llvmTypeName] + "* " + llvmNameForLocalVariable (!mFormalArgumentName)
   case inputOutput :
      ioLLVMcode += [mFormalArgumentType llvmTypeName] + "* " + llvmNameForLocalVariable (!mFormalArgumentName)
    end
  end
  ioLLVMcode += ")" + llvmAttributeFunction () + "{\n"
#--- Allocate local variables 
  var allocaList = self.mAllocaList
#--- Store input arguments in memory
  for (mFormalArgumentKind mFormalArgumentType mFormalArgumentName) in self.mFormalArgumentListForGeneration do
    switch mFormalArgumentKind
    case input :
      allocaList += !llvmNameForLocalVariable (!mFormalArgumentName) !mFormalArgumentType !true
    case output, inputOutput :
    end
  end
#--- Allocate local variables 
  [allocaList generateAllocaList !?ioLLVMcode]
#---
  for (mFormalArgumentKind mFormalArgumentType mFormalArgumentName) in self.mFormalArgumentListForGeneration do
    switch mFormalArgumentKind
    case input :
      let llvmType = [mFormalArgumentType llvmTypeName]
      ioLLVMcode += "  store " + llvmType + " %in." + mFormalArgumentName + ", "
         + llvmType + " * " + llvmNameForLocalVariable (!mFormalArgumentName) + "\n"
    case output, inputOutput :
    end
  end
#--- Declare "accept" variable 
  let accepted_llvmName = llvmNameForLocalVariable (!acceptVariableOmnibusName ())
  let accepted_llvmName_loaded = llvmNameForLocalVariable (!acceptVariableOmnibusName () + ".loaded")
#--- Base guard or convenience guard ?
  switch self.mGuardKindGenerationIR
  case baseGuard :
    let accepted_label = acceptVariableOmnibusName () + ".guard.accepted"
    let rejected_label = acceptVariableOmnibusName () + ".guard.rejected"
    [self.mInstructionGenerationList instructionListLLVMCode !?ioLLVMcode !inGenerationContext !?ioGenerationAdds]
    ioLLVMcode += "  " + accepted_llvmName_loaded + " = load i1, i1* " + accepted_llvmName + "\n"
    ioLLVMcode += "  br i1 " + accepted_llvmName_loaded + ", label %" + accepted_label + ", label %" + rejected_label + "\n\n"
    ioLLVMcode += accepted_label + ":\n"
    ioLLVMcode += "  call void @accept.guard ()\n"
    ioLLVMcode += "  ret i1 true\n\n"
    ioLLVMcode += rejected_label + ":\n"
    ioLLVMcode += "  ret i1 false\n"
    ioLLVMcode += "}\n\n"
  case convenienceGuard (baseGuardAllocaList baseGuardInstructionGenerationList baseGuardMangledName baseGuardEffectiveParameterList) :
    [baseGuardAllocaList generateAllocaList !?ioLLVMcode]
    [baseGuardInstructionGenerationList instructionListLLVMCode !?ioLLVMcode !inGenerationContext !?ioGenerationAdds]
    ioLLVMcode += "  %invoked.guard.result = call i1 @" + [baseGuardMangledName assemblerRepresentation]
    ioLLVMcode += " ("
    for (mEffectiveParameterPassingMode mParameter) in baseGuardEffectiveParameterList do
      switch mEffectiveParameterPassingMode
      case input:
        ioLLVMcode += [mParameter llvmTypeName] + "* " + [mParameter llvmName]
      case output:
        ioLLVMcode += [mParameter llvmTypeName] + " " + [mParameter llvmName]
      case outputInput:
        ioLLVMcode += [mParameter llvmTypeName] + "* " + [mParameter llvmName]
      end
    between
      ioLLVMcode += ", "
    end
    ioLLVMcode += ")\n"
    ioLLVMcode += "  br i1 %invoked.guard.result, label %invoked.guard.acceptation, label %invoked.guard.rejection\n\n"
    ioLLVMcode += "invoked.guard.acceptation:\n"
    [self.mInstructionGenerationList instructionListLLVMCode !?ioLLVMcode !inGenerationContext !?ioGenerationAdds]
    ioLLVMcode += "  ret i1 true\n\n"
    ioLLVMcode += "invoked.guard.rejection:\n"
    ioLLVMcode += "  ret i1 false\n"
    ioLLVMcode += "}\n\n"
  end
  ioGenerationAdds.mExternFunctionDeclarationSet += !"void @accept.guard ()"
  ioGenerationAdds.mExternFunctionDeclarationSet += !"i1 @xtr.user.result ()"
}

#·······················································································································

override method @guardImplementationRoutineIR enterAccessibleEntities
  ?!@stringset ioInvokedRoutineSet
  ?!@uint ioMaxBranchOfOnInstructions
{
  [self.mInstructionGenerationList enterAccessibleEntities !?ioInvokedRoutineSet !?ioMaxBranchOfOnInstructions]
  switch self.mGuardKindGenerationIR
  case baseGuard :
  case convenienceGuard (* baseGuardInstructionGenerationList baseGuardImplementationMangledName *) :
    ioInvokedRoutineSet += !baseGuardImplementationMangledName
    [baseGuardInstructionGenerationList enterAccessibleEntities !?ioInvokedRoutineSet !?ioMaxBranchOfOnInstructions]
  end
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
