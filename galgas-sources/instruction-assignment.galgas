#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @assignmentInstructionAST : @instructionAST {
  @LValueAST mTargetAST
  @expressionAST mSourceExpression
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension plm_syntax {

  #·····················································································································

  rule <instruction> ?!@ast ioAST ?!@instructionListAST ioInstructionList {
    <lvalue> !?ioAST ?let assignmentTargetAST
  #--- Source expression
    $=$
    <expression> !?ioAST ?let sourceExpression
  #--- AST
    ioInstructionList += !@assignmentInstructionAST.new {
      !assignmentTargetAST.mIdentifier.location
      !assignmentTargetAST
      !sourceExpression
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @assignmentInstructionAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  [mSourceExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
  [mTargetAST noteInstructionTypesInPrecedenceGraph !?ioGraph]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#!  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @assignmentInstructionAST instructionSemanticAnalysis
  ?self:let @PLMType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
  [ioUniversalMap searchValuedObjectType !selfType:inSelfType !mTargetAST ?let targetType]
#--- Analyze source expression
  [mSourceExpression analyzeExpression
    !self:inSelfType
    !routineAttributes:inRoutineAttributes
    !optionalTargetType:targetType
    !context:inContext
    !mode:inMode
    !?temporary:ioTemporaries
    !?staticEntityMap:ioStaticEntityMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:ioInstructionGenerationList
    ?result:@objectIR sourceOperandPossibleReference
  ]
#--- Analyze LValue
  @instructionListListIR pendingStoreComputedPropertyInstructionGenerationList = {}
  [mTargetAST analyzeLValueInAssignment
    !self: inSelfType
    !routineAttributes:inRoutineAttributes
    !context: inContext
    !mode: inMode
    !?temporary: ioTemporaries
    !?staticEntityMap:ioStaticEntityMap
    !?variableMap:ioUniversalMap
    !?alloca: ioAllocaList
    !?instructionListIR: ioInstructionGenerationList
    !?pendingStoreComputedPropertyList: pendingStoreComputedPropertyInstructionGenerationList
    ?let @LValueRepresentation lvalueIR
  ]
#--- If source is a value, transform it to a reference
  if not [sourceOperandPossibleReference isReference] then # Assignment from value
    let result = checkAssignmentCompatibility (
      !source:sourceOperandPossibleReference # loadedResultValue
      !targetAnnotationType:targetType
      !errorLocation:mTargetAST.mIdentifier
      !staticTypeAllowed:false
    )
    [!?ioInstructionGenerationList appendStoreToUniversalReference !lvalueIR.llvmName !targetType !result]
  else # Assignment from reference
  #--- Check assignment is defined
    let sourceOperand_llvmBaseTypeName = [sourceOperandPossibleReference type].llvmBaseTypeName
    if [targetType.assignmentSources hasKey !sourceOperand_llvmBaseTypeName] then
      [!?ioInstructionGenerationList appendCopyFromReferences
        !?ioTemporaries
        !errorLocation:mTargetAST.mIdentifier
        !source:sourceOperandPossibleReference
        !target:lvalueIR
      ]
    else
      var m = "no valid assignment to " + targetType.plmTypeDescriptionName
      m += " from " + [sourceOperandPossibleReference type].plmTypeDescriptionName
      error mTargetAST.mIdentifier : m
    end
  end
#--- Append pending computed property store (in reverse order)
  for > (instructionList) in pendingStoreComputedPropertyInstructionGenerationList do
    ioInstructionGenerationList += instructionList
  end
}

#·······················································································································

method @LValueAST analyzeLValueInAssignment
  ?self:let @PLMType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  ?!pendingStoreComputedPropertyList:@instructionListListIR ioPendingStoreComputedPropertyInstructionGenerationListList
  !@LValueRepresentation outInternalRepresentation
{
  if mIdentifier.string == "" then # Receiver is "self"
    if inSelfType.kind == .void then
      error mIdentifier : "self is not available in this context" : outInternalRepresentation
    else
      analyzeSelfLValueInAssignment (
        !self:inSelfType
        !routineAttributes:inRoutineAttributes
        !context:inContext
        !mode:inMode
        !?temporary: ioTemporaries
        !?staticEntityMap:ioStaticEntityMap
        !?variableMap: ioUniversalMap
        !?alloca: ioAllocaList
        !?instructionListIR: ioInstructionGenerationList
        !?pendingStoreComputedPropertyList: ioPendingStoreComputedPropertyInstructionGenerationListList
        !mOperand
        ?outInternalRepresentation
      )
    end
  else
    [ioUniversalMap searchEntity !mIdentifier ?let @valuedObject entity]
    switch entity
    case task (*) :
      error mIdentifier : "a task has no value" : outInternalRepresentation
    case driver (* *) :
      error mIdentifier : "a driver has no value" : outInternalRepresentation
    case globalConstant (*) :
      error mIdentifier : "a global constant cannot be written" : outInternalRepresentation
    case localConstant (3*) :
      error mIdentifier : "a local constant cannot be written" : outInternalRepresentation
    case globalSyncInstance (2*) :
      error mIdentifier : "a global sync instance is not allowed here" : outInternalRepresentation
    case localVariable (@PLMType variableType @lstring plmName) :
      analyzeVariableInLValueInAssignment (
        !self: inSelfType
        !routineAttributes:inRoutineAttributes
        !context: inContext
        !mode: inMode
        !?temporary: ioTemporaries
        !?staticEntityMap:ioStaticEntityMap
        !?variableMap: ioUniversalMap
        !?alloca: ioAllocaList
        !?instructionListIR: ioInstructionGenerationList
        !?pendingStoreComputedPropertyList: ioPendingStoreComputedPropertyInstructionGenerationListList
        !variableName: mIdentifier
        !llvmName:llvmNameForLocalVariable (!plmName)
        !type: variableType
        !mOperand
        ?outInternalRepresentation
      )
    end
  end
}

#·······················································································································

private proc analyzeVariableInLValueInAssignment
  ?self:let @PLMType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  ?!pendingStoreComputedPropertyList:@instructionListListIR ioPendingStoreComputedPropertyInstructionGenerationListList
  ?variableName:let @lstring inVariableName
  ?llvmName:let @string inLLVMName
  ?type:let @PLMType inVariableType
  ?let @LValueOperandAST inOperand
  !@LValueRepresentation outInternalRepresentation
{
  var currentType = inVariableType
  var currentLLVMName = inLLVMName
  switch inOperand
  case noOperand :
    [!?ioUniversalMap searchValuedObjectForWriteAccess !inVariableName ?*]
  case property (@lstring name @LValueOperandAST nextOperand) :
    [!?ioUniversalMap searchValuedObjectForReadWriteAccess !inVariableName ?*]
    handlePropertyAccessInAssignment (
      !self: inSelfType
      !?currentType
      !?currentLLVMName
      !routineAttributes: inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap: ioStaticEntityMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !?instructionListIR: ioInstructionGenerationList
      !?pendingStoreComputedPropertyList: ioPendingStoreComputedPropertyInstructionGenerationListList
      !name
      !nextOperand
    )
  case arrayAccess (indexExpression endOfIndex checkIndexExpression nextOperand) :
    [!?ioUniversalMap searchValuedObjectForReadWriteAccess !inVariableName ?*]
    handleArrayAccessInAssignment (
      !self: inSelfType
      !?currentType
      !?currentLLVMName
      !routineAttributes: inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap: ioStaticEntityMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !?instructionListIR: ioInstructionGenerationList
      !?pendingStoreComputedPropertyList: ioPendingStoreComputedPropertyInstructionGenerationListList
      !indexExpression
      !endOfIndex
      !checkIndexExpression
      !nextOperand
    )
  end
  outInternalRepresentation = .new {!currentType !currentLLVMName}
}

#·······················································································································

private proc handlePropertyAccessInAssignment
  ?self:let @PLMType inSelfType
  ?!@PLMType ioCurrentType
  ?!@string ioCurrentLLVMName
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  ?!pendingStoreComputedPropertyList:@instructionListListIR ioPendingStoreComputedPropertyInstructionGenerationListList
  ?let @lstring inPropertyName
  ?let @LValueOperandAST inNextOperand
{
  let @propertySetterMap propertySetterMap = ioCurrentType.propertySetterMap
  [propertySetterMap searchKey !inPropertyName ?* ?let @propertySetterKind propertyAccess]
  switch propertyAccess
  case computedProperty (@unifiedTypeMap-proxy computedPropertyType getterModeDictionary setterModeDictionary) :
  #--- Get property value
    let getterRoutineLLVMName = checkModeAndReturnsRoutineLLVMName (!getterModeDictionary !inMode !inPropertyName)
    [!?ioInstructionGenerationList appendGetComputedPropertyValue
      !?ioTemporaries
      !.reference {!type: ioCurrentType !llvmName: ioCurrentLLVMName}
      !getterRoutineLLVMName
      ![computedPropertyType type]
      ?@objectIR resultValueIR
    ]
  #--- Transform in reference
    [!?ioInstructionGenerationList referenceFromPossibleValue !?ioTemporaries !?ioAllocaList !?resultValueIR]
  #--- Append pending computed property store
    let setterRoutineLLVMName = checkModeAndReturnsRoutineLLVMName (!setterModeDictionary !inMode !inPropertyName)
    @instructionListIR pendingStoreComputedPropertyInstructionGenerationList = {}
    var tempValueIR = resultValueIR
    [!?pendingStoreComputedPropertyInstructionGenerationList appendLoadWhenReference !?ioTemporaries !?tempValueIR]
    [!?pendingStoreComputedPropertyInstructionGenerationList appendStoreComputedPropertyValue
      !.reference {!type: ioCurrentType !llvmName: ioCurrentLLVMName}
      !setterRoutineLLVMName
      !tempValueIR
    ]
    ioPendingStoreComputedPropertyInstructionGenerationListList += !pendingStoreComputedPropertyInstructionGenerationList
  #---
    ioCurrentType = [resultValueIR type]
    ioCurrentLLVMName = [resultValueIR llvmName]
    handleNextOperandInAssignment (
      !self: inSelfType
      !?ioCurrentType
      !?ioCurrentLLVMName
      !inNextOperand
      !routineAttributes: inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap: ioStaticEntityMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !?instructionListIR: ioInstructionGenerationList
      !?pendingStoreComputedPropertyList: ioPendingStoreComputedPropertyInstructionGenerationListList
    )
  case storedProperty (@PLMType propertyType @uint index) :
    [!?ioTemporaries newTempLLVMVar ?let newLLVMvariable]
    [!?ioInstructionGenerationList appendGetUniversalPropertyReference
      !ioCurrentType
      !ioCurrentLLVMName
      !newLLVMvariable
      !index
      !inPropertyName
    ]
    ioCurrentType = propertyType
    ioCurrentLLVMName = newLLVMvariable
    handleNextOperandInAssignment (
      !self: inSelfType
      !?ioCurrentType
      !?ioCurrentLLVMName
      !inNextOperand
      !routineAttributes: inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap: ioStaticEntityMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !?instructionListIR: ioInstructionGenerationList
      !?pendingStoreComputedPropertyList: ioPendingStoreComputedPropertyInstructionGenerationListList
    )
  end
}

#·······················································································································

private proc handleArrayAccessInAssignment
  ?self:let @PLMType inSelfType
  ?!@PLMType ioCurrentType
  ?!@string ioCurrentLLVMName
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  ?!pendingStoreComputedPropertyList:@instructionListListIR ioPendingStoreComputedPropertyInstructionGenerationListList
  ?let @expressionAST inIndexExpression
  ?let @location inEndOfIndex
  ?let @bool inCheckIndexExpression
  ?let @LValueOperandAST inNextOperand
{
  switch ioCurrentType.subscript
  case noSubscript :
    error inEndOfIndex
    : "the receiver type is " + [ioCurrentType plmTypeDescriptionName] + " and does not accept subscripting"
  case literalString :
    error inEndOfIndex
    : "the receiver type is literal string and does not accept subscripting"
    : ioCurrentType, ioCurrentLLVMName
  case staticSubscript (@PLMType elementType @bigint arraySize) :
    handleArraySubscriptNew (
      !self: inSelfType
      !routineAttributes:inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap:ioStaticEntityMap
      !?variableMap:ioUniversalMap
      !?alloca: ioAllocaList
      !inIndexExpression 
      !inEndOfIndex
      !inCheckIndexExpression
      !arraySize:arraySize
      !elementType:elementType
      !?ioInstructionGenerationList
      ?let @objectIR indexIR
    )
    [!?ioTemporaries newTempLLVMVar ?let newLLVMvariable]
    [!?ioInstructionGenerationList appendGetUniversalArrayElementReference
      !ioCurrentType
      !ioCurrentLLVMName
      !elementType
      !newLLVMvariable
      !indexIR
    ]
    ioCurrentType = elementType
    ioCurrentLLVMName = newLLVMvariable
    handleNextOperandInAssignment (
      !self: inSelfType
      !?ioCurrentType
      !?ioCurrentLLVMName
      !inNextOperand
      !routineAttributes: inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap: ioStaticEntityMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !?instructionListIR: ioInstructionGenerationList
      !?pendingStoreComputedPropertyList: ioPendingStoreComputedPropertyInstructionGenerationListList
    )
  end
}

#·······················································································································

private proc analyzeSelfLValueInAssignment
  ?self:let @PLMType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  ?!pendingStoreComputedPropertyList:@instructionListListIR ioPendingStoreComputedPropertyInstructionGenerationListList
  ?let @LValueOperandAST inOperand
  !@LValueRepresentation outInternalRepresentation
{
  var currentType = inSelfType
  var currentLLVMName = llvmNameForSelf ()
  switch inOperand
  case noOperand :
  case property (@lstring name @LValueOperandAST next) :
    handlePropertyAccessInAssignment (
      !self: inSelfType
      !?currentType
      !?currentLLVMName
      !routineAttributes: inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap: ioStaticEntityMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !?instructionListIR: ioInstructionGenerationList
      !?pendingStoreComputedPropertyList: ioPendingStoreComputedPropertyInstructionGenerationListList
      !name
      !next
    )
  case arrayAccess (indexExpression endOfIndex checkIndexExpression nextOperand) :
    handleArrayAccessInAssignment (
      !self: inSelfType
      !?currentType
      !?currentLLVMName
      !routineAttributes: inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap: ioStaticEntityMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !?instructionListIR: ioInstructionGenerationList
      !?pendingStoreComputedPropertyList: ioPendingStoreComputedPropertyInstructionGenerationListList
      !indexExpression
      !endOfIndex
      !checkIndexExpression
      !nextOperand
    )
  end
  outInternalRepresentation = .new {!currentType !currentLLVMName}
}

#·······················································································································

private proc handleNextOperandInAssignment
  ?self:let @PLMType inSelfType
  ?!@PLMType ioCurrentType
  ?!@string ioCurrentLLVMName
  ?let @LValueOperandAST inOperand
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  ?!pendingStoreComputedPropertyList:@instructionListListIR ioPendingStoreComputedPropertyInstructionGenerationListList
{
  switch inOperand
  case noOperand :
  case property (@lstring name @LValueOperandAST next) :
    handlePropertyAccessInAssignment (
      !self: inSelfType
      !?ioCurrentType
      !?ioCurrentLLVMName
      !routineAttributes: inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap: ioStaticEntityMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !?instructionListIR: ioInstructionGenerationList
      !?pendingStoreComputedPropertyList: ioPendingStoreComputedPropertyInstructionGenerationListList
      !name
      !next
    )
  case arrayAccess (indexExpression endOfIndex checkIndexExpression nextOperand) :
    handleArrayAccessInAssignment (
      !self: inSelfType
      !?ioCurrentType
      !?ioCurrentLLVMName
      !routineAttributes: inRoutineAttributes
      !context: inContext
      !mode: inMode
      !?temporary: ioTemporaries
      !?staticEntityMap: ioStaticEntityMap
      !?variableMap: ioUniversalMap
      !?alloca: ioAllocaList
      !?instructionListIR: ioInstructionGenerationList
      !?pendingStoreComputedPropertyList: ioPendingStoreComputedPropertyInstructionGenerationListList
      !indexExpression
      !endOfIndex
      !checkIndexExpression
      !nextOperand
    )
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
