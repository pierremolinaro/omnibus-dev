#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @selfVarAssignmentInstructionAST : @instructionAST {
  @lstring mTargetVarName
  @expressionAST mSourceExpression
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @varAssignmentInstructionAST : @instructionAST {
  @lstring mTargetVarName
  @expressionAST mSourceExpression
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension common_syntax {

  #·····················································································································

  rule <instruction> ?!@instructionListAST ioInstructionList ?!@labelMap unused ioLabelMap {
    $identifier$ ?let varName
    $=$
    <expression> ?let @expressionAST expression
    ioInstructionList += !@varAssignmentInstructionAST.new {
      !varName
      !expression
    }
  }

  #·····················································································································

  rule <instruction_beginning_by_self> ?!@instructionListAST ioInstructionList ?!@labelMap unused ioLabelMap {
    $identifier$ ?let varName
    $=$
    <expression> ?let @expressionAST expression
    ioInstructionList += !@selfVarAssignmentInstructionAST.new {
      !varName
      !expression
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @selfVarAssignmentInstructionAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  [mSourceExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @varAssignmentInstructionAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  [mSourceExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @varAssignmentInstructionAST analyze
  ?self:let @unifiedTypeMap-proxy inSelfType
  ?routineNameForInvocationGraph:let @lstring inRoutineNameForInvocationGraph
  ?context:let @semanticContext inContext
  ?modes:let @stringset inModeSet
  ?allowException:let @bool inAllowExceptions
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@staticStringMap ioGlobalLiteralStringMap
  ?!variableMap:@variableMap ioVariableMap
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
#--- Get target type
  [!?ioVariableMap neutralAccess
    !mTargetVarName
    ?@unifiedTypeMap-proxy targetType
    ?*
    ?@objectInMemoryIR target
    ?4*
  ]
#--- Analyze source expression
  [mSourceExpression analyzeExpression
    !self:inSelfType
    !routineNameForInvocationGraph:inRoutineNameForInvocationGraph
    !optionalTargetType:targetType
    !context:inContext
    !modes:inModeSet
    !allowException:inAllowExceptions
    !?temporary:ioTemporaries
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioVariableMap
    !?instructionListIR:ioInstructionGenerationList
    ?result:@operandIR sourceOperand
  ]
  let result = checkAssignmentCompatibility (
    !source:sourceOperand
    !targetAnnotationType:targetType
    !errorLocation:mTargetVarName.location
    !staticTypeAllowed:false
  )
#--- Write local variable
  [!?ioVariableMap searchForWriteAccess
    !mTargetVarName
    ?*
    ?let accessIsAllowed
    ?*
    ?let isCopiable
    ?2*
    ?let isConstant
  ]
  if isConstant then
    error mTargetVarName : "a constant cannot be modified"
  end
  if not accessIsAllowed then
    error mTargetVarName : "routine has no access right on the this variable"
  end
  if not isCopiable then
    error mTargetVarName : "the '" + mTargetVarName + "' variable is not copiable"
  end
  [target storeInMemory !value:result !?instructionListIR:ioInstructionGenerationList]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @selfVarAssignmentInstructionAST analyze
  ?self:let @unifiedTypeMap-proxy inSelfType
  ?routineNameForInvocationGraph:let @lstring inRoutineNameForInvocationGraph
  ?context:let @semanticContext inContext
  ?modes:let @stringset inModeSet
  ?allowException:let @bool inAllowExceptions
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@staticStringMap ioGlobalLiteralStringMap
  ?!variableMap:@variableMap ioVariableMap
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
  if inSelfType == .null then
    error mTargetVarName : "'self' is not available in this context"
  else
  #--- Get target type
    [!?ioVariableMap neutralAccess
      !mTargetVarName
      ?@unifiedTypeMap-proxy targetType
      ?*
      ?@objectInMemoryIR target
      ?4*
    ]
  #--- Analyze field name
    switch [inSelfType kind]
    case boolean :
      error mTargetVarName : "a boolean does not have fields"
    case boolset :
      error mTargetVarName : "a boolset does not have fields"
    case literalString :
      error mTargetVarName : "a static string does not have fields"
    case enumeration (*) :
      error mTargetVarName : "an enumeration does not have fields"
    case structure (@propertyMap propertyMap *) :
      [propertyMap searchKey !mTargetVarName ?targetType ?let idx]
      getNewTempObjectInMemory (!targetType !?ioTemporaries ?let result)
      [!?ioInstructionGenerationList appendGetElementPtr
        !result
        !target
        !idx
      ]
      target = result
    case integer (* * * *) :  
      error mTargetVarName : "an integer does not have fields"
    case staticInteger :  
      error mTargetVarName : "an integer does not have fields"
    case opaque (*) :  
      error mTargetVarName : "an opaque type does not have fields"
    end
  #--- Analyze source expression
    [mSourceExpression analyzeExpression
      !self:inSelfType
      !routineNameForInvocationGraph:inRoutineNameForInvocationGraph
      !optionalTargetType:targetType
      !context:inContext
      !modes:inModeSet
      !allowException:inAllowExceptions
      !?temporary:ioTemporaries
      !?staticStringMap:ioGlobalLiteralStringMap
      !?variableMap:ioVariableMap
      !?instructionListIR:ioInstructionGenerationList
      ?result:@operandIR sourceOperand
    ]
    let result = checkAssignmentCompatibility (
      !source:sourceOperand
      !targetAnnotationType:targetType
      !errorLocation:mTargetVarName.location
      !staticTypeAllowed:false
    )
  #--- Write local variable
    [!?ioVariableMap searchForWriteAccess
      !mTargetVarName
      ?*
      ?let accessIsAllowed
      ?*
      ?let isCopiable
      ?2*
      ?let isConstant
    ]
    if isConstant then
      error mTargetVarName : "a constant cannot be modified"
    end
    if not accessIsAllowed then
      error mTargetVarName : "routine has no access right on the this variable"
    end
    if not isCopiable then
      error mTargetVarName : "the '" + mTargetVarName + "' variable is not copiable"
    end
    [target storeInMemory
      !value:result
      !?instructionListIR:ioInstructionGenerationList
    ]
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

