#----------------------------------------------------------------------------------------------------------------------*
#  AST                                                                                                                 *
#----------------------------------------------------------------------------------------------------------------------*

enum @operatorAssignKind {
  case bitWiseOrAssign
  case bitWiseAndAssign
  case bitWiseXorAssign
}

#----------------------------------------------------------------------------------------------------------------------*

class @operatorAssignInstructionAST : @instructionAST {
  @lstring mTargetVarName
  @expressionAST mSourceExpression
  @operatorAssignKind mOperator
}

#----------------------------------------------------------------------------------------------------------------------*
#  SYNTAX                                                                                                              *
#----------------------------------------------------------------------------------------------------------------------*

syntax extension common_syntax {

  #--------------------------------------------------------------------------------------------------------------------*

  rule <instruction> ?!@instructionListAST ioInstructionList ?!@labelMap unused ioLabelMap {
    $identifier$ ?let varName
    @operatorAssignKind operatorAssign
    select
      $|=$
      operatorAssign = .bitWiseOrAssign
    or
      $&=$
      operatorAssign = .bitWiseAndAssign
    or
      $^=$
      operatorAssign = .bitWiseXorAssign
    end
    <expression> ?let @expressionAST expression
    ioInstructionList += !@operatorAssignInstructionAST.new {
      !varName
      !expression
      !operatorAssign
    }
  }

  #--------------------------------------------------------------------------------------------------------------------*

}

#----------------------------------------------------------------------------------------------------------------------*
#  SEMANTICS                                                                                                           *
#----------------------------------------------------------------------------------------------------------------------*

override method @operatorAssignInstructionAST analyze
  ?routineNameForInvocationGraph:let @lstring inRoutineNameForInvocationGraph
  ?optionalReceiverType:let @receiverType inReceiverType
  ?context:let @semanticContext inContext
  ?modes:let @stringset inModeSet
  ?allowException:let @bool inAllowExceptions
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@globalLiteralStringMap ioGlobalLiteralStringMap
  ?!variableMap:@variableMap ioVariableMap
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
#--- Get target type
  [!?ioVariableMap neutralAccess
    !mTargetVarName
    ?let targetType
    ?6*
  ]
#--- Analyze source expression
  [mSourceExpression analyzeExpression
    !routineNameForInvocationGraph:inRoutineNameForInvocationGraph
    !optionalReceiverType:inReceiverType
    !optionalTargetType:targetType
    !context:inContext
    !modes:inModeSet
    !allowException:inAllowExceptions
    !?temporary:ioTemporaries
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioVariableMap
    !?instructionListIR:ioInstructionGenerationList
    ?type:let rightExpressionType
    ?result:@operandIR sourceValueName
  ]
#--- Check inferred type
  if [targetType key] != [rightExpressionType key] then
    error mTargetVarName : "source expression type is '" + [rightExpressionType key]
      + "', and is not compatible with target type '" + [targetType key] + "'"
  end
#--- Write local variable
  [!?ioVariableMap searchForReadWriteAccess
    !mTargetVarName
    ?*
    ?let accessIsAllowed
    ?let unused targetVar
    ?let targetVariableKind
    ?let isCopiable
    ?2*
  ]
  if not accessIsAllowed then
    error mTargetVarName : "routine has no access right on the this variable"
  end
#--- Check operator is available for type
  @infixOperatorMap operatorMap
  @string generatedOperator
  switch mOperator
  case bitWiseAndAssign :
    operatorMap = inContext.mAndOperatorMap
    generatedOperator = "&="
  case bitWiseOrAssign :
    operatorMap = inContext.mOrOperatorMap
    generatedOperator = "|="
  case bitWiseXorAssign :
    operatorMap = inContext.mXorOperatorMap
    generatedOperator = "^="
  end
  [operatorMap checkWith
    !leftType:targetType
    !rightType:rightExpressionType
    !operatorLocation:mTargetVarName.location
    ?resultType:*
  ]
  if not isCopiable then
    error mTargetVarName : "the '" + mTargetVarName + "' variable is not copiable"
  end
#--- Code generation
  ioInstructionGenerationList += !@operatorAssignInstructionIR.new {
   !targetVariableKind
   !targetType
   !mOperator
   !sourceValueName
 }
}

#----------------------------------------------------------------------------------------------------------------------*
#  CODE GENERATION                                                                                                     *
#----------------------------------------------------------------------------------------------------------------------*

class @operatorAssignInstructionIR : @abstractInstructionIR {
  @variableKindIR mTargetVariable
  @unifiedTypeMap-proxy mTargetVarType
  @operatorAssignKind mOperator
  @operandIR mSourceValue
}

#----------------------------------------------------------------------------------------------------------------------*

override method @operatorAssignInstructionIR llvmInstructionCode ?!@string ioCode {
  switch mOperator
  case bitWiseOrAssign :
    ioCode += [mTargetVariable mangledName] + " |= " + mSourceValue + " ;\n"
  case bitWiseAndAssign :
    ioCode += [mTargetVariable mangledName] + " &= " + mSourceValue + " ;\n"
  case bitWiseXorAssign :
    ioCode += [mTargetVariable mangledName] + " ^= " + mSourceValue + " ;\n"
  end
}

#----------------------------------------------------------------------------------------------------------------------*

override method @operatorAssignInstructionIR enterAccessibleEntities ?!@accessibleEntities ioAccessibleEntities {
  [mTargetVariable enterAccessibleEntities !?ioAccessibleEntities]
#  [mSourceValue enterAccessibleEntities !?ioAccessibleEntities]
}

#----------------------------------------------------------------------------------------------------------------------*
