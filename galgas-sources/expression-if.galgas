#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @ifExpressionAST : @expressionAST {
  @expressionAST mIfExpression
  @location mIfExpressionEndLocation
  @expressionAST mThenExpression
  @location mThenExpressionEndLocation
  @expressionAST mElseExpression
  @location mElseExpressionEndLocation
}

#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension plm_syntax {

  #·····················································································································
  
  rule <primary> ?!@ast ioAST !@expressionAST outExpression {
    <expression_if> !?ioAST ?let if_expression
    outExpression = if_expression
  }
  
  #·····················································································································
  
  rule <expression_if> ?!@ast ioAST !@ifExpressionAST outExpression {
    $if$
    <expression> !?ioAST ?let ifExpression
    let @location ifExpressionEndLocation = .here
    ${$
    <expression> !?ioAST ?let thenExpression
    let @location thenExpressionEndLocation = .here
    $}$
    $else$
    @expressionAST elseExpression
    select
      ${$
      <expression> !?ioAST ?elseExpression
      $}$
    or
      <expression_if> !?ioAST ?let else_if_expression
      elseExpression = else_if_expression
    end
    let @location elseExpressionEndLocation = .here
    outExpression = @ifExpressionAST.new {
      !ifExpression !ifExpressionEndLocation
      !thenExpression !thenExpressionEndLocation
      !elseExpression !elseExpressionEndLocation
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH FROM EXPRESSION                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @ifExpressionAST addDependenceEdgeForStaticExpression
  ?let @lstring inConstantName
  ?!@semanticTypePrecedenceGraph ioGraph
{
  [mIfExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
  [mThenExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
  [mElseExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @ifExpressionAST noteExpressionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  [mIfExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
  [mThenExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
  [mElseExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @ifExpressionAST analyzeExpression
  ?self:let @PLMType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?routineNameForInvocationGraph:let @lstring inCallerNameForInvocationGraph
  ?optionalTargetType:let @PLMType inOptionalTargetType
  ?context:let @semanticContext inContext
  ?mode:let @mode inCurrentMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !result:@objectIR outResult
{
#--- Analyze if expression
  [mIfExpression analyzeExpression
    !self:inSelfType
    !routineAttributes:inRoutineAttributes
    !routineNameForInvocationGraph:inCallerNameForInvocationGraph
    !optionalTargetType:inOptionalTargetType
    !context:inContext
    !mode:inCurrentMode
    !?temporary:ioTemporaries
    !?staticEntityMap:ioStaticEntityMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:ioInstructionGenerationList
    ?result:@objectIR ifExpressionResult 
  ]
  [!?ioInstructionGenerationList appendLoadWhenReference
    !?ioTemporaries
    !?ifExpressionResult
  ]  
#--- Check "if" expression type is boolean
  if not [[ifExpressionResult type].kind isBoolean] then
    error mIfExpressionEndLocation : "'if' expression should be boolean"
  end
#--- Analyze 'then' expression
  [mThenExpression analyzeExpression
    !self:inSelfType
    !routineAttributes:inRoutineAttributes
    !routineNameForInvocationGraph:inCallerNameForInvocationGraph
    !optionalTargetType:inOptionalTargetType
    !context:inContext
    !mode:inCurrentMode
    !?temporary:ioTemporaries
    !?staticEntityMap:ioStaticEntityMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:ioInstructionGenerationList
    ?result:@objectIR thenExpressionTempResult
  ]
  [!?ioInstructionGenerationList appendLoadWhenReference
    !?ioTemporaries
    !?thenExpressionTempResult
  ]  
  let thenExpressionResult = checkAssignmentCompatibility (
    !source:thenExpressionTempResult
    !targetAnnotationType:inOptionalTargetType
    !errorLocation:mThenExpressionEndLocation
    !staticTypeAllowed:true
  )
#--- Analyze 'else' expression
  [mElseExpression analyzeExpression
    !self:inSelfType
    !routineAttributes:inRoutineAttributes
    !routineNameForInvocationGraph:inCallerNameForInvocationGraph
    !optionalTargetType:inOptionalTargetType
    !context:inContext
    !mode:inCurrentMode
    !?temporary:ioTemporaries
    !?staticEntityMap:ioStaticEntityMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:ioInstructionGenerationList
    ?result:@objectIR elseExpressionTempResult
  ]
  [!?ioInstructionGenerationList appendLoadWhenReference
    !?ioTemporaries
    !?elseExpressionTempResult
  ]  
  let elseExpressionResult = checkAssignmentCompatibility (
    !source:elseExpressionTempResult
    !targetAnnotationType:[thenExpressionResult type]
    !errorLocation:mElseExpressionEndLocation
    !staticTypeAllowed:true
  )
#--- 'if' expression is static ?
  if [ifExpressionResult isStatic] then # yes, perform 'if' statically
    [ifExpressionResult literalInteger ?1* ?value:let value]
    outResult = if value != 0G then thenExpressionResult else elseExpressionResult end
  else # No, generate 'select' LLVM instruction
    getNewTempValue (![elseExpressionResult type] !?ioTemporaries ?outResult)
    [!?ioInstructionGenerationList appendSelectOperation
      !outResult
      !ifExpressionResult
      !thenExpressionResult
      !elseExpressionResult
    ]      
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
