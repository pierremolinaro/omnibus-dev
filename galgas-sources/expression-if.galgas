#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @ifExpressionAST : @expressionAST {
  @expressionAST mIfExpression
  @location mIfExpressionEndLocation
  @expressionAST mThenExpression
  @location mThenExpressionEndLocation
  @expressionAST mElseExpression
  @location mElseExpressionEndLocation
}

#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension plm_syntax {

  #·····················································································································
  
  rule <primary> !@expressionAST outExpression {
    <expression_if> ?let if_expression
    outExpression = if_expression
  }
  
  #·····················································································································
  
  rule <expression_if> !@ifExpressionAST outExpression {
    $if$
    <expression> ?let ifExpression
    let @location ifExpressionEndLocation = .here
    ${$
    <expression> ?let thenExpression
    let @location thenExpressionEndLocation = .here
    $}$
    $else$
    @expressionAST elseExpression
    select
      ${$
      <expression> ?elseExpression
      $}$
    or
      <expression_if> ?let else_if_expression
      elseExpression = else_if_expression
    end
    let @location elseExpressionEndLocation = .here
    outExpression = @ifExpressionAST.new {
      !ifExpression !ifExpressionEndLocation
      !thenExpression !thenExpressionEndLocation
      !elseExpression !elseExpressionEndLocation
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  PRECEDENCE GRAPH FROM EXPRESSION                                                                                     
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @ifExpressionAST addDependenceEdgeForStaticExpression
  ?let @lstring inConstantName
  ?!@semanticTypePrecedenceGraph ioGraph
{
  [mIfExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
  [mThenExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
  [mElseExpression addDependenceEdgeForStaticExpression !inConstantName !?ioGraph]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @ifExpressionAST noteExpressionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  [mIfExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
  [mThenExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
  [mElseExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @ifExpressionAST analyzeExpression
  ?self:let @PLMType inSelfType
  ?inGuard:let @bool inGuard
  ?routineNameForInvocationGraph:let @lstring inCallerNameForInvocationGraph
  ?optionalTargetType:let @PLMType inOptionalTargetType
  ?context:let @semanticContext inContext
  ?mode:let @mode inCurrentMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@staticStringMap ioGlobalLiteralStringMap
  ?!variableMap:@universalValuedObjectMapForContext ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
  !result:@objectIR outResult
{
#--- Analyze if expression
  [mIfExpression analyzeExpression
    !self:inSelfType
    !inGuard:inGuard
    !routineNameForInvocationGraph:inCallerNameForInvocationGraph
    !optionalTargetType:inOptionalTargetType
    !context:inContext
    !mode:inCurrentMode
    !?temporary:ioTemporaries
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:ioInstructionGenerationList
    ?result:let @objectIR ifExpressionResultPossibleReference 
  ]
  [!?ioInstructionGenerationList appendLoadWhenReference
    !?ioTemporaries
    !ifExpressionResultPossibleReference
    ?let ifExpressionResult
  ]  
#--- Check "if" expression type is boolean
  if not [[[ifExpressionResult type] mKind] isBoolean] then
    error mIfExpressionEndLocation : "'if' expression should be boolean"
  end
#--- Analyze 'then' expression
  [mThenExpression analyzeExpression
    !self:inSelfType
    !inGuard:inGuard
    !routineNameForInvocationGraph:inCallerNameForInvocationGraph
    !optionalTargetType:inOptionalTargetType
    !context:inContext
    !mode:inCurrentMode
    !?temporary:ioTemporaries
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:ioInstructionGenerationList
    ?result:let @objectIR thenExpressionPossibleReferenceResult
  ]
  [!?ioInstructionGenerationList appendLoadWhenReference
    !?ioTemporaries
    !thenExpressionPossibleReferenceResult
    ?let thenExpressionTempResult
  ]  
  let thenExpressionResult = checkAssignmentCompatibility (
    !source:thenExpressionTempResult
    !targetAnnotationType:inOptionalTargetType
    !errorLocation:mThenExpressionEndLocation
    !staticTypeAllowed:true
  )
#--- Analyze 'else' expression
  [mElseExpression analyzeExpression
    !self:inSelfType
    !inGuard:inGuard
    !routineNameForInvocationGraph:inCallerNameForInvocationGraph
    !optionalTargetType:inOptionalTargetType
    !context:inContext
    !mode:inCurrentMode
    !?temporary:ioTemporaries
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:ioInstructionGenerationList
    ?result:let @objectIR elseExpressionPossibleReferenceResult
  ]
  [!?ioInstructionGenerationList appendLoadWhenReference
    !?ioTemporaries
    !elseExpressionPossibleReferenceResult
    ?let elseExpressionTempResult
  ]  
  let elseExpressionResult = checkAssignmentCompatibility (
    !source:elseExpressionTempResult
    !targetAnnotationType:[thenExpressionResult type]
    !errorLocation:mElseExpressionEndLocation
    !staticTypeAllowed:true
  )
#--- 'if' expression is static ?
  if [ifExpressionResult isStatic] then # yes, perform 'if' statically
    [ifExpressionResult literalInteger ?1* ?value:let value]
    outResult = if value != 0G then thenExpressionResult else elseExpressionResult end
  else # No, generate 'select' LLVM instruction
    getNewTempValue (![elseExpressionResult type] !?ioTemporaries ?outResult)
    [!?ioInstructionGenerationList appendSelectOperation
      !outResult
      !ifExpressionResult
      !thenExpressionResult
      !elseExpressionResult
    ]      
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
