#—————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#—————————————————————————————————————————————————————————————————————————————————————————————————

class @whileInstructionAST : @instructionAST {
  public var @expressionAST m_while_Expression
  public var @location mEndOf_test_expression
  public var @instructionListAST mWhileInstructionList
  public var @location mEndOf_while_instruction
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#—————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension omnibus_syntax {

  #·····················································································································

  rule <instruction> ?!@ast ioAST ?!@instructionListAST ioInstructionList  {
#    $do$
    $while$
    let instructionLocation = @location.here
    <expression> !?ioAST ?let @expressionAST testExpression
    let testExpressionEndLocation = @location.here
    ${$
    <instructionList> !?ioAST ?let instructionList
    let endOfInstruction = @location.here
    $}$
    ioInstructionList += !@whileInstructionAST {
      !instructionLocation
      !testExpression
      !testExpressionEndLocation
      !instructionList
      !endOfInstruction
    }
  }
  
  #·····················································································································

}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @whileInstructionAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  [self.mWhileInstructionList noteInstructionListTypesInPrecedenceGraph !?ioGraph]
  [self.m_while_Expression noteExpressionTypesInPrecedenceGraph !?ioGraph]
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @whileInstructionAST instructionSemanticAnalysis
  ?self:let @omnibusType inSelfType
  ?routineAttributes:let @routineAttributes inRoutineAttributes
  ?context:let @semanticContext inContext
  ?mode:let @mode inMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticEntityMap:@staticEntityMap ioStaticEntityMap
  ?!variableMap:@universalValuedObjectMap ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
#--- Analyze test expression
  var @instructionListIR testInstructionGenerationList = @[]
  [self.m_while_Expression analyzeExpression
    !self:inSelfType
    !routineAttributes:inRoutineAttributes
    !optionalTargetType:voidType ()
    !context:inContext
    !mode:inMode
    !?temporary:ioTemporaries
    !?staticEntityMap:ioStaticEntityMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:testInstructionGenerationList
    ?result:@objectIR testValue
  ]
  [!?testInstructionGenerationList appendLoadWhenReference
    !?ioTemporaries
    !?testValue
  ]  
#--- Check result is allowed for condition
  [inContext.mImplicitConversionToBooleanMap generateConvertToBooleanCode
    !testValue
    !self.mEndOf_test_expression
    !?ioTemporaries
    !?testInstructionGenerationList
    !?alloca: ioAllocaList
    ?let booleanResult
  ]
  switch booleanResult 
  case compileTime (*) :
    error self.mEndOf_test_expression : "test expression type should not be a compile time expression"
  case llvmVariable (*) :
  end

#--- Check inferred type
#  if [testValue type].kind != .boolean then
#    error mEndOf_test_expression : "test expression type is '" + [testValue omnibusTypeDescriptionName] + "', it should be boolean"
#  elsif [testValue isStatic] then
#    error mEndOf_test_expression : "test expression type should not be static"
#  end
#--- Analyze instruction list
  var @instructionListIR instructionGenerationList = @[]
  [!?ioUniversalMap openOverrideForRepeatBlock]
  [self.mWhileInstructionList analyzeBranchInstructionList
    !self:inSelfType
    !routineAttributes:inRoutineAttributes
    !endOfInstructionList:self.mEndOf_while_instruction
    !context:inContext
    !mode:inMode
    !?temporary:ioTemporaries
    !?staticEntityMap:ioStaticEntityMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:instructionGenerationList
  ]
  [!?ioUniversalMap closeOverride !self.mEndOf_while_instruction]
#--- Code generation
  ioInstructionGenerationList += !@whileInstructionIR {
    ![self.mEndOf_test_expression endLocationIndex]
    !testInstructionGenerationList
    ![booleanResult llvmName]
    !instructionGenerationList
 }
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
#  CODE GENERATION                                                                                                      
#—————————————————————————————————————————————————————————————————————————————————————————————————

class @whileInstructionIR : @abstractInstructionIR {
  public var @uint mLabelIndex
  public var @instructionListIR mTestInstructionGenerationList
  public var @string m_while_llvmName
  public var @instructionListIR mInstructionGenerationList
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @whileInstructionIR llvmInstructionCode
  ?!@string ioLLVMcode
  ?let @generationContext inGenerationContext
  ?!@generationAdds ioGenerationAdds
{
  let labelTest = "while." + self.mLabelIndex + ".test"
  let labelLoop = "while." + self.mLabelIndex + ".loop"
  let labelEnd  = "while." + self.mLabelIndex + ".end"
  ioLLVMcode += "  br label %" + labelTest + "\n\n"
  ioLLVMcode += labelTest + ":\n"
  [self.mTestInstructionGenerationList instructionListLLVMCode !?ioLLVMcode !inGenerationContext !?ioGenerationAdds]
  ioLLVMcode += "  br i1 " + self.m_while_llvmName + ", label %" + labelLoop + ", label %" + labelEnd + "\n\n"
  ioLLVMcode += labelLoop + ":\n"
  [self.mInstructionGenerationList instructionListLLVMCode !?ioLLVMcode !inGenerationContext !?ioGenerationAdds]
  ioLLVMcode += "  br label %" + labelTest + "\n\n"
  ioLLVMcode += labelEnd + ":\n"
}

#—————————————————————————————————————————————————————————————————————————————————————————————————

override method @whileInstructionIR enterAccessibleEntities
  ?!@stringset ioInvokedRoutineSet
  ?!@uint ioMaxBranchOfOnInstructions
{
  [self.mTestInstructionGenerationList enterAccessibleEntities !?ioInvokedRoutineSet !?ioMaxBranchOfOnInstructions]
  [self.mInstructionGenerationList enterAccessibleEntities !?ioInvokedRoutineSet !?ioMaxBranchOfOnInstructions]
}

#—————————————————————————————————————————————————————————————————————————————————————————————————
