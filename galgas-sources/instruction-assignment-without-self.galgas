#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @varAssignmentNoSelfInstructionAST : @instructionAST {
  @LValueWithoutSelfAST mTargetAST
  @expressionAST mSourceExpression
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension plm_syntax {

  #·····················································································································

  rule <instruction> ?!@instructionListAST ioInstructionList {
    <lvalue_without_self> ?let assignmentTargetAST
  #--- Source expression
    $=$
    <expression> ?let sourceExpression
  #--- AST
    ioInstructionList += !@varAssignmentNoSelfInstructionAST.new {
      !assignmentTargetAST.mIdentifier.location
      !assignmentTargetAST
      !sourceExpression
    }
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @varAssignmentNoSelfInstructionAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  [mSourceExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
  [mTargetAST noteInstructionTypesInPrecedenceGraph !?ioGraph]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @varAssignmentNoSelfInstructionAST analyze
  ?self:let @PLMType inSelfType
  ?propertiesAreMutable:let @bool inRoutineCanMutateProperties
  ?directAccessToPropertiesAllowed:let @bool unused inDirectAccessToPropertiesAllowed
  ?routineNameForInvocationGraph:let @lstring inCallerNameForInvocationGraph
  ?context:let @semanticContext inContext
  ?mode:let @mode inCurrentMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@staticStringMap ioGlobalLiteralStringMap
  ?!variableMap:@universalPropertyAndRoutineMapForContext ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
  [mTargetAST type !selfType:inSelfType !variableMap:ioUniversalMap ?let targetType]
#--- Analyze source expression
  [mSourceExpression analyzeExpression
    !self:inSelfType
    !inGuard:false
    !routineNameForInvocationGraph:inCallerNameForInvocationGraph
    !optionalTargetType:targetType
    !context:inContext
    !mode:inCurrentMode
    !?temporary:ioTemporaries
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:ioInstructionGenerationList
    ?result:@objectIR sourceOperandPossibleReference
  ]
#--- Analyze LValue
  [mTargetAST analyzeLValueNoSelf
    !self: inSelfType
    !propertiesAreMutable: inRoutineCanMutateProperties
    !routineNameForInvocationGraph: inCallerNameForInvocationGraph
    !context: inContext
    !mode: inCurrentMode
    !?temporary: ioTemporaries
    !?staticStringMap: ioGlobalLiteralStringMap
    !?variableMap:ioUniversalMap
    !?alloca: ioAllocaList
    !?instructionListIR: ioInstructionGenerationList
    ?let @internalRepresentation internalRepresentation
  ]
#---
  [!?ioInstructionGenerationList appendLoadFromReference
    !?ioTemporaries
    !sourceOperandPossibleReference
    ![mTargetAST mIdentifier]
    ?let sourceOperand
  ]  
  let result = checkAssignmentCompatibility (
    !source:sourceOperand
    !targetAnnotationType:targetType
    !errorLocation:mTargetAST.mIdentifier
    !staticTypeAllowed:false
  )
  let errorLocation = mTargetAST.mIdentifier
  switch internalRepresentation
  case standAloneIdentifier (identifier) :
    [!?ioUniversalMap searchValuedObjectForWriteAccess !identifier ?let objectIR]
    [!?ioInstructionGenerationList appendStoreFromReference !objectIR !errorLocation !result]
  case volatileAbsoluteReference (@PLMType type @uint address) :
    [!?ioInstructionGenerationList appendStoreVolatileRegister !type !address !result]
  case volatileIndirectReference (@PLMType type @string llvmName) :
    [!?ioInstructionGenerationList appendStoreIndirectVolatileRegister !type !llvmName !result]
  case universalReference (@PLMType type @string llvmName) :
    [!?ioInstructionGenerationList appendStoreToUniversalReference !llvmName !type !result]
  case structureMember (@lstring propertyName @objectIR currentPointerIR) :
    if [[currentPointerIR type] isStructure] then
      [[currentPointerIR type] structure ?1* ?propertyMap:let @propertyMap propertyMap ?5*]
      [propertyMap searchKey !propertyName ?2* ?let type ?*]
      let targetRef = @objectIR.temporaryReference {
         !type:type
         !llvmName:.new {!"%temp." + ioTemporaries.mTemporaryIndex !propertyName}
      }
      ioTemporaries.mTemporaryIndex ++
      [!?ioInstructionGenerationList appendGetPropertyReference !targetRef !currentPointerIR !propertyName]
      [!?ioInstructionGenerationList appendStoreFromReference !targetRef !errorLocation !result]
    else
      error [currentPointerIR location] : "this should be a structure instance"
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
