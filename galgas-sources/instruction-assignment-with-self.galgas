#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  AST                                                                                                                  
#-——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @varAssignmentSelfInstructionAST : @instructionAST {
  @LValueSelfAST mAssignmentTargetAST
  @expressionAST mSourceExpression
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SYNTAX                                                                                                               
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension plm_syntax {

  #·····················································································································

#  rule <instruction> ?!@instructionListAST ioInstructionList {
#    <lvalue_with_self> ?let assignmentTargetAST
#  #--- Source expression
#    $=$
#    <expression> ?let sourceExpression
#  #--- AST
#    ioInstructionList += !@varAssignmentSelfInstructionAST.new {
#      !assignmentTargetAST.mSelfLocation
#      !assignmentTargetAST
#      !sourceExpression
#    }
#  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  NOTE TYPES                                                                                                           
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @varAssignmentSelfInstructionAST noteInstructionTypesInPrecedenceGraph ?!@semanticTypePrecedenceGraph ioGraph {
  [mSourceExpression noteExpressionTypesInPrecedenceGraph !?ioGraph]
  [mAssignmentTargetAST noteInstructionTypesInPrecedenceGraph !?ioGraph]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#  SEMANTICS                                                                                                            
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @varAssignmentSelfInstructionAST analyze
  ?self:let @PLMType inSelfType
  ?propertiesAreMutable:let @bool inRoutineCanMutateProperties
  ?directAccessToPropertiesAllowed:let @bool unused inDirectAccessToPropertiesAllowed
  ?routineNameForInvocationGraph:let @lstring inCallerNameForInvocationGraph
  ?context:let @semanticContext inContext
  ?mode:let @mode inCurrentMode
  ?!temporary:@semanticTemporariesStruct ioTemporaries
  ?!staticStringMap:@staticStringMap ioGlobalLiteralStringMap
  ?!variableMap:@universalPropertyAndRoutineMapForContext ioUniversalMap
  ?!alloca:@allocaList ioAllocaList
  ?!instructionListIR:@instructionListIR ioInstructionGenerationList
{
  [mAssignmentTargetAST type !self: inSelfType ?let targetType]
#--- Analyze source expression
  [mSourceExpression analyzeExpression
    !self:inSelfType
    !inGuard:false
    !routineNameForInvocationGraph:inCallerNameForInvocationGraph
    !optionalTargetType:targetType # [currentObject type]
    !context:inContext
    !mode:inCurrentMode
    !?temporary:ioTemporaries
    !?staticStringMap:ioGlobalLiteralStringMap
    !?variableMap:ioUniversalMap
    !?alloca:ioAllocaList
    !?instructionListIR:ioInstructionGenerationList
    ?result:@objectIR sourceOperandPossibleReference
  ]
#--- Analyze target
  [mAssignmentTargetAST analyzeAssignmentTarget
    !self: inSelfType
    !targetAccessKind:.write
    !propertiesAreMutable: inRoutineCanMutateProperties
    !routineNameForInvocationGraph: inCallerNameForInvocationGraph
    !context: inContext
    !mode: inCurrentMode
    !?temporary: ioTemporaries
    !?staticStringMap: ioGlobalLiteralStringMap
    !?variableMap:ioUniversalMap
    !?alloca: ioAllocaList
    !?instructionListIR: ioInstructionGenerationList
    ?let @internalRepresentation internalRepresentation
  ]
  [!?ioInstructionGenerationList appendLoadFromReference
    !?ioTemporaries
    !sourceOperandPossibleReference
    ![mAssignmentTargetAST mIdentifier]
    ?let sourceOperand
  ]  
  let result = checkAssignmentCompatibility (
    !source:sourceOperand
    !targetAnnotationType:targetType
    !errorLocation:[mAssignmentTargetAST mIdentifier]
    !staticTypeAllowed:false
  )
  let errorLocation = mAssignmentTargetAST.mIdentifier
  switch internalRepresentation
  case standAloneIdentifier (*) :
    error errorLocation : "invalid assignment target"
  case volatileAbsoluteReference (@PLMType unused type @uint unused address) :
    error errorLocation : "temporaryReference 6"
  case volatileIndirectReference (@PLMType unused type @string unused llvmName) :
    error errorLocation : "volatileIndirectReference 6"
  case universalReference (@PLMType type @string llvmName) :
    [!?ioInstructionGenerationList appendStoreToUniversalReference !llvmName !type !result]
  case structureMember (@lstring propertyName @objectIR currentPointerIR) :
    if [[currentPointerIR type] isStructure] then
      [[currentPointerIR type] structure ?1* ?propertyMap:let @propertyMap propertyMap ?5*]
      [propertyMap searchKey !propertyName ?* ?let @objectIR masterPropertyIR ?2*]
      let targetRef = @objectIR.temporaryReference {
         !type:[masterPropertyIR type]
         !llvmName:.new {!"%temp." + ioTemporaries.mTemporaryIndex !propertyName}
      }
      ioTemporaries.mTemporaryIndex ++
      [!?ioInstructionGenerationList appendGetPropertyReference !targetRef !currentPointerIR !propertyName]
      [!?ioInstructionGenerationList appendStoreFromReference !targetRef !errorLocation !result]
    else
      error [currentPointerIR location] : "this should be a structure instance"
    end
  end
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
